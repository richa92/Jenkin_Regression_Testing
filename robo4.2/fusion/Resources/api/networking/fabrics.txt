*** Settings ***
Library				RoboGalaxyLibrary
Library				FusionLibrary
Library				Collections

*** Keywords ***
Update fabric reserved vlan-pool
    [Documentation]    Updates the reserved vlan-pool for the fabric and returns the task response
    ...  default values return the appliance to the default values
    [arguments]    ${start}=3967    ${len}=128
    ${resp} =      fusion api get fabric
    ${uri} =       get from dictionary   ${resp['members'][0]}    uri
    ${body} =      Create dictionary   start=${start}
    ...                                length=${len}
    ...                                type=vlan-pool
    ${task} =      fusion api update fabric reserved vlan range   uri=${uri}  body=${body}
    [Return]     ${task}

Refresh Fabric
    [Documentation]    Refresh the Fabric given by name and return the associated task.
    [Arguments]    ${fabric_name}
    ${fabric} =    Fusion Api Get Fabric    param=?filter="'name'=='${fabric_name}'"
    Should Be Equal As Integers    ${fabric["count"]}    1
    ${uri} =    Set Variable    ${fabric["members"][0]["uri"]}
    &{body} =    Create Dictionary    op=replace    path=/refreshState    value=RefreshPending
    @{body} =    Create List    ${body}

    ${task} =    Fusion Api Patch Fabric    uri=${uri}    body=${body}
    [Return]    ${task}

Reapply Fabric
    [Documentation]    Reapply the Fabric given by name and return the associated task.
    [Arguments]    ${fabric_name}
    ${fabric} =    Fusion Api Get Fabric    param=?filter="'name'=='${fabric_name}'"
    Should Be Equal As Integers    ${fabric["count"]}    1
    ${uri} =    Set Variable    ${fabric["members"][0]["uri"]}
    &{body} =    Create Dictionary    op=replace    path=/state    value=ConfigPending
    @{body} =    Create List    ${body}

    ${task} =    Fusion Api Patch Fabric    uri=${uri}    body=${body}
    [Return]    ${task}

Remove All Fabrics
    [Documentation]    Remove all Fabrics; This normally is for cleanup and will continue to delete all if error
    Log    Removing Fabrics    console=True
    ${fabrics} =    Fusion Api Get Fabric
    :FOR    ${fb}    IN    @{fabrics['members']}
    \    ${resp} =    Fusion Api Delete Fabric    uri=${fb['uri']}
    \    ${task} =    Wait For Task    ${resp}    4m    20s
    \    Run Keyword And Continue On Failure    Should Be Equal As Strings    ${task['taskState']}    Completed