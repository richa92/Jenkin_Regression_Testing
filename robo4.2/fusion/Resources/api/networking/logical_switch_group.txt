*** Settings ***
Library				RoboGalaxyLibrary
Library				FusionLibrary
Library				BuiltIn
Library				Collections

*** Keywords ***
Add Logical Switch Group from variable
	[Documentation]	Adds a Logical Switch Group to an appliance from a variable which contains a list of dicts with the entire payload
	...             scope process example:
    ...                 'initialScopeUris': ['Scope:Test', 'Scope:Production']
	[Arguments]		${lsg}
	Log    Adding LOGICAL SWITCH GROUPS    console=true
	log variables   level=DEBUG
	${swmap} = 	Get From Dictionary	   ${lsg['switchMapTemplate']}    switchMapEntryTemplates
    :FOR    ${sw}   IN  @{swmap}
    \       ${swuri} =   Get From Dictionary   ${sw}   permittedSwitchTypeUri
	\       ${swuri} =   Common URI lookup by name	${swuri}
    \       Set to dictionary   ${sw}  permittedSwitchTypeUri   ${swuri}
    Set to dictionary   ${lsg['switchMapTemplate']}   switchMapEntryTemplates    ${swmap}

    # -Initial Scope process.
    ${initialScopeUris}=  Pop From Dictionary  ${lsg}  initialScopeUris  default=${None}
    ${scopeUris}=       Run Keyword If    ${initialScopeUris} != ${None}
    ...                 Run Keyword for List  ${initialScopeUris}  Common URI lookup by name
    Run Keyword If      ${initialScopeUris} != ${None}
    ...                 Set To Dictionary    ${lsg}  initialScopeUris  ${scopeUris}

	${resp} =	Fusion Api Create LSG  	${lsg}
	${task} =	Wait For Task	${resp} 	2min	5s

Remove All LSGs
	[Documentation]	Querys the appliance for all LSGs and then removes them
	Log    Removing LSGS    console=true
	${lsgs} = 	Fusion Api Get LSG
	:FOR	${lsg}	IN	@{lsgs['members']}
	\		${resp} = 	Fusion Api Delete LSG		uri=${lsg['uri']}
	\		${task} =	Wait For Task 	${resp} 	240s	2s

Get LSG URI
	[Documentation]	   Querys the appliance for all LSGs and then returns URI with matching name
	[Arguments]    ${name}
	Log    Get LSG URI    console=true
	${lsgs} = 	Fusion Api Get LSG
	:FOR	${lsg}	IN	@{lsgs['members']}
	\		Return From Keyword If    '${lsg['name']}'=='${name}'    ${lsg['uri']}

    Log    LSG: ${name} Not found
    [Return]    LSG: ${name} Not found

Edit LSG
    [Documentation]    Edit Logical Switch Group from LSG DTO
    ...   The passed in lsg contains the changes required by the caller.  This Keyword will optain Switch Type uri.
    ...              Example:
    ...                Edit LSG  ${lsg}
    [Arguments]    ${lsg}
    ${name} =    Get From Dictionary    ${lsg}    name

    ${swmap} = 	  Get From Dictionary	   ${lsg['switchMapTemplate']}    switchMapEntryTemplates
    :FOR    ${sw}   IN  @{swmap}
    \       ${swuri} =   Get From Dictionary   ${sw}   permittedSwitchTypeUri
	\       ${swuri} =   Common URI lookup by name	${swuri}
    \       Set to dictionary   ${sw}  permittedSwitchTypeUri   ${swuri}
    Set to dictionary   ${lsg['switchMapTemplate']}   switchMapEntryTemplates    ${swmap}

    ${uri} =    Get LSG URI  ${name}

    ${resp} =  Fusion Api Edit Lsg    ${lsg}    ${uri}
    [Return]    ${resp}