*** Settings ***
Library				BuiltIn
Library		        SSHLibrary

*** Keywords ***
# -----------------------------------------------------------------------------
#   FUSION BASH SHELL INTERFACE KEYWORDS
# -----------------------------------------------------------------------------
Login to Fusion via SSH
    [Documentation]             Connect to Fusion VM Bash via SSH
    ...                         Example:\n| Login to Fusion Via SSH | 10.0.12.106 | Administrator | hpvse123 |
    [Arguments]                 ${IP}=${FUSION_IP}      ${USERNAME}=${FUSION_SSH_USERNAME}
    ...                         ${PASSWORD}=${FUSION_SSH_PASSWORD}    ${PROMPT}=${FUSION_PROMPT}
    ...                         ${TIMEOUT}=${FUSION_TIMEOUT}    ${ALIAS}=Fusion_SSH
    Log Many                    ${IP}                   ${USERNAME}     ${PASSWORD}     ${PROMPT}   ${TIMEOUT}
    Set Default Configuration   prompt=${PROMPT}        timeout=${TIMEOUT}
    ${Id}=                      Open Connection         ${IP}    alias=${ALIAS}
    ${Output}=                  Login                   ${USERNAME}     ${PASSWORD}
    [Return]                    ${Id}

Logout of Fusion Via SSH
    [Documentation]     Exits the current Bash SSH session
    ...                 Example:\n| Logout Of Fusion Via SSH |
    Close Connection

Execute SSH Command
    [Documentation]     Executes given command on the Fusion SSH shell
    ...                 Example:\n| Execute CLI Command | show enclosure list |
    [Arguments]         ${Command}      ${PROMPT}=${FUSION PROMPT}
    Login to Fusion via SSH
    SSHLibrary.Write    ${Command}
    ${Output}=          Read until      ${PROMPT}
    Logout of Fusion Via SSH
    [Return]            ${Output}


Run Command Via SSH
    [Documentation]             Connect to system via SSH, runs command, closes connection and returns output
    ...                         Example:\n| Run Command Via SSH  |10.0.12.106|  |root|  |hpvse123| |Command| |Prompt|timeout|
    [Arguments]                 ${ip}      ${username}       ${pswd}       ${command}		${prompt}       ${timeout}
    Log    Logging into system via CLI, runs command, logs out and returns output    console=true
    Set Default Configuration   prompt=${PROMPT}        timeout=${TIMEOUT}
    SSHLibrary.Open Connection         ${ip}     timeout=${timeout}
    SSHLibrary.Login      ${username}       ${pswd}
    SSHLibrary.Write    ${command}
    ${output}=          SSHLibrary.Read until     ${prompt}
    Close Connection
    [Return]                    ${output}

Get Appliance Bond0 IPv6 Address
    [Documentation]             Get appliance bond0 Ipv6 Address
    ...                         Example output of ip addr show bond0:
    ...                         inet6 fe80::25b:e735:d8d1:710a/64 scope link
    ...                         inet6 fe80::9eb6:54ff:fe97:d18/64 scope link deprecated
    ${ipv6} =  set variable  ${EMPTY}
    Log  Get Appliance Bond0 IPv6 Address  console=True
    Login to Fusion via SSH
    ${output} =  Execute SSH Command  ip addr show bond0
    ${output} =  Get lines matching pattern  ${output}  * inet6 *
    ${lines} =  set variable  ${output.split('\n')}
    :FOR	${line}	in	@{lines}
	\   ${fields} =  set variable  ${line.split()}
	\   ${len} =  get length  ${fields}
	\   continue for loop if  ${len}==5
	\   ${ipv6} =  set variable  ${fields[1]}
	\   ${ipv6} =  Get substring  ${ipv6}  0  -3
	log  Applinace IPv6 Address is ${ipv6}  console=True
    [Return]  ${ipv6}
