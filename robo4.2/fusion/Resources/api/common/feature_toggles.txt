*** Settings ***
Library		        SSHLibrary

*** Keywords ***
${enableOrDisable} feature toggle ${feature}
    [Documentation]    Enables\Disables the feature toggle, waits 10 min, then pings appliance
    ...   Currently, this keyword will only work for ONE toggle at a time
    ...   ${enableOrDisable} = 'enable' or 'disable'
    ...   ${feature}         = the value of the feature toggle in the feature_toggles.xml file. ex: OVF266_SmartLink
    ...   usage:             ex.   enable feature toggle OVF266_SmartLink
    should match regexp     ${enableOrDisable}    (enable|disable)
    ${Id} =         Open Connection    ${APPLIANCE_IP}
    ${Output} =     Login              root     hpvse1
    ${Command} =    set variable    /ci/bin/set-feature-toggles -v --${enableOrDisable} ${feature}
    ${stdout}    ${stderr}    ${rc}=    Execute Command    ${Command}    return_stderr=True    return_rc=True
    Log    ${stdout}    console=True
    Close Connection
    Sleep    10min
    Wait For Appliance To Become Pingable    ${APPLIANCE_IP}
    Wait For Appliance To Be Ready           ${APPLIANCE_IP}

Get feature toggle state
    [Documentation]    returns the current value of a feature toggle from the feature_toggles.xml file
    [Arguments]    ${feature}
    ${Id} =         Open Connection    ${APPLIANCE_IP}
    ${Output} =     Login              root     hpvse1
    #  grep for feature, then pass to re and ((?i)true|false)
    ${Command} =    set variable    grep ${feature} /ci/etc/feature-toggles.xml | sed -e 's/.*="//'
    ${stdout}    ${stderr}    ${rc}=    Execute Command    ${Command}    return_stderr=True    return_rc=True
    Log    ${stdout}    console=True
    Close Connection
    [Return]   ${stdout}
