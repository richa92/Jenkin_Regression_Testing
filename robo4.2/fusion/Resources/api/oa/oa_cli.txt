*** Settings ***
Library		        SSHLibrary
Library             String
Library             ./CompareCLP.py
Library             ./OaHelpers.py

*** Keywords ***
#######################################
# OA CLI Commands
#######################################
OA CLI EFUSE
    [Documentation]    issues an EFUSE Action to the given Device\Bay.
    ...   {DEVICE} = <BLADE|IOM|NETTRAY|SWM|FAN|OA|EM>
    ...   {BAY} = desired bay for given device
    ...   {ACTION} = <ON|OFF>
    [Arguments]	       ${OA_HOST}    ${OA_USER}		${OA_PASS}    ${DEVICE}    ${BAY}    ${ACTION}
    Open Connection     ${OA_HOST}     timeout=10s
    Login               ${OA_USER}     ${OA_PASS}
    Write    ++diag--
    Read Until    diag>
    Write    efuse ${DEVICE} ${BAY} ${ACTION}
    Read Until    diag>
    Write    q
    Close All Connections

OA CLI POWERON
    [Documentation]    issues an POWERON command to the given Device\Bay.
    ...   {DEVICE} = <SERVER | INTERCONNECT>
    ...   {BAY} = desired bay for given device  { ALL | <bay number> [{ , | - } <bay number>]}
    [Arguments]	       ${OA_HOST}    ${OA_USER}		${OA_PASS}    ${DEVICE}    ${BAY}
    Open Connection     ${OA_HOST}     timeout=10s
    Login               ${OA_USER}     ${OA_PASS}
    Write    POWERON ${DEVICE} ${BAY}
    ${output}=  Read Until    >
    Close All Connections
    [Return]  ${output}

OA CLI POWEROFF
    [Documentation]    issues an POWEROFF command to the given Device\Bay.
    ...   {DEVICE} = <SERVER | INTERCONNECT>
    ...   {BAY} = desired bay for given device  { ALL | <bay number> [{ , | - } <bay number>]}
    [Arguments]	       ${OA_HOST}    ${OA_USER}		${OA_PASS}    ${DEVICE}    ${BAY}
    Open Connection     ${OA_HOST}     timeout=10s
    Login               ${OA_USER}     ${OA_PASS}
    Write    POWEROFF ${DEVICE} ${BAY}
    ${output}=  Read Until    >
    Close All Connections
    [Return]  ${output}

OA CLI Verify Blade CLP
    [Documentation]    Get the CLP string for a blade and verify.
    ...                  Data File Example:
    ...                     clp = {
    ...                         "oa":"16.125.76.26",
    ...                         "username":"Administrator",
    ...                         "password":"hpvse14",
    ...                         "bay":"1",
    ...                         "validate":'set netport1 OEMHP_initiatorName=iqn.2015-02.com.hpe:oneview-wpst16-bay1'
    ...                     }
    [Arguments]  ${clp}  ${VERBOSE}=${FALSE}
    log  \nRun b_clp ${clp['bay']} on OA ${clp['oa']}    console=True
    Open Connection     ${clp['oa']}     timeout=20s
    Login               ${clp['username']}  ${clp['password']}
    Write    ++diag--
    Read Until    diag>
    Write    b_clp ${clp['bay']}
    ${output} =  Read Until    diag>
    Run Keyword If    '${VERBOSE}'=='True'    Log    run b_clp output is ${output}    console=true
    Write    q
    Close All Connections

    ${result} =    CompareCLP.Do    ${clp['validate']}    ${output}    ${verbose}
    Should Be Equal As Integers    ${result}    1
    log to console  \nThe b_clp output matched validate
    [return]  ${output}

Get OA CLI Blade CLP
    [Documentation]    Get the CLP string for a blade.
    ...                  Data File Example:
    ...                     clp = {
    ...                         "oa":"16.125.76.26",
    ...                         "username":"Administrator",
    ...                         "password":"hpvse14",
    ...                         "bay":"1",
    ...                     }
    [Arguments]  ${clp}
    log  \nRun b_clp ${clp['bay']} on OA ${clp['oa']}    console=True
    Open Connection     ${clp['oa']}     timeout=20s
    Login               ${clp['username']}  ${clp['password']}
    Write    ++diag--
    Read Until    diag>
    Write    b_clp ${clp['bay']}
    ${output} =  Read Until    diag>
    Log    The CLP settings for Blade ${clp['bay']} in OA ${clp['oa']} are ${output}  console-True
    Write    q
    Close All Connections

OA CLI Show SNMP
    [Documentation]    Get the SNMP settings on the OA
    [Arguments]  ${OA_HOST}  ${OA_USER}  ${OA_PASS}  ${VERBOSE}=False
    log  \nRun show SNMP on OA ${OA_HOST}    console=True
    Open Connection     ${OA_HOST}     timeout=20s
    Login               ${OA_USER}  ${OA_PASS}
    Write    show snmp
    ${output} =  Read Until    >
    Run Keyword If    '${VERBOSE}'=='True'    Log    show snmp output is ${output}    console=true
    [return]  ${output}

OA CLI Test SNMP
    [Documentation]    Test SNMP on the OA
    [Arguments]  ${OA_HOST}  ${OA_USER}  ${OA_PASS}  ${VERBOSE}=False
    log  \nRun test SNMP on OA ${OA_HOST}    console=True
    Open Connection     ${OA_HOST}     timeout=20s
    Login               ${OA_USER}  ${OA_PASS}
    Write    test snmp
    ${output} =  Read Until    >
    Run Keyword If    '${VERBOSE}'=='True'    Log    ${output}    console=true
    [return]  ${output}

OA CLI EFUSE All Server Bays
    [Documentation]     Issues an E-Fuse action on all server bays
    ...                 Example:
    ...                   | OA CLI EFUSE All Server Bays | 192.168.10.20 | Administrator | iwf01help
    [Arguments]         ${OA_HOST}    ${OA_USER}    ${OA_PASS}
    :FOR    ${bay}    IN RANGE    1    17
    \    Log             Efuse action OFF is being performed on server bay ${bay}
    \    OA CLI EFUSE    ${OA_HOST}    ${OA_USER}    ${OA_PASS}    BLADE    ${bay}    OFF

    :FOR    ${bay}    IN RANGE    1    17
    \    Log             Efuse action ON is being performed on server bay ${bay}
    \    OA CLI EFUSE    ${OA_HOST}    ${OA_USER}    ${OA_PASS}    BLADE    ${bay}    ON

    :FOR    ${bay}    IN RANGE    1    17
    \    Log                   Power off action is being performed on ${bay}
    \    ${r}=                 OaHelpers.Slot Populated    ${OA_HOST}    ${OA_USER}    ${OA_PASS}    BLADE    ${bay}
    \    Run Keyword Unless    ${r}    OA CLI EFUSE    ${OA_HOST}    ${OA_USER}    ${OA_PASS}    BLADE    ${bay}    OFF

OA CLI EFUSE All Interconnects
    [Documentation]     Issues an E-Fuse action on all interconnects
    ...                 Example:
    ...                   | OA CLI EFUSE All Interconnects | 192.168.10.20 | Administrator | iwf01help
    [Arguments]         ${OA_HOST}    ${OA_USER}    ${OA_PASS}
    :FOR    ${bay}    IN RANGE    1    9
    \    Log             Efuse action OFF is being performed on interconnect bay ${bay}
    \    OA CLI EFUSE    ${OA_HOST}    ${OA_USER}    ${OA_PASS}    IOM    ${bay}    OFF

    :FOR    ${bay}    IN RANGE    1    9
    \    Log             Efuse action ON is being performed on interconnect bay ${bay}
    \    OA CLI EFUSE    ${OA_HOST}    ${OA_USER}    ${OA_PASS}    IOM    ${bay}    ON
