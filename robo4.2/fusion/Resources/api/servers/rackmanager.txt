*** Settings ***
Library                RoboGalaxyLibrary
Library                FusionLibrary
Library                OperatingSystem



*** Keywords ***
Add RackManagers from variable
    [Documentation]    Adds Rackmanager to an appliance from a variable which contains a list of dicts with the entire payload
    [Arguments]        ${rack_mgrs}    ${timeout}=10min
    Log      Adding RackManager    console=True
    ${resp} =    Fusion Api Add Rack Manager    ${rack_mgrs}
    ${status_code}=    Get From Dictionary    ${resp}    status_code
    ${task_output} =    Run Keyword If      '${status_code}' == '202'    Wait For Task    ${resp}     ${timeout}    10s
    ...    ELSE    Run Keyword    FAIL    msg=${resp['message']}
    [return]    ${task_output}

Get RackManager URI
    [Documentation]  Get Rack Manager URI
    ${resp} =    Fusion Api Get Rack Manager
    ${uri} =    Get From Dictionary    ${resp['members'][0]}    uri
    Log    ${uri}
    [Return]    ${uri}

Refresh Rack Manager
    [Documentation]  Performs refresh  operation on Rack Manager
    [Arguments]        ${rack_mgr_uri}    
    ${dict} =    Create Dictionary    isforce=false    op=RefreshRackManagerOp
    ${payload}=    Create List  ${dict}
    ${resp} =    fusion api patch rack manager    ${payload}   ${rack_mgr_uri}
    ${status_code}=    Get From Dictionary    ${resp}    status_code
    ${task_output} =    Run Keyword If      '${status_code}' == '202'    Wait For Task    ${resp}     
    ...    ELSE    Run Keyword    FAIL    msg=${resp['message']}
    [return]    ${task_output}

Delete Rack Manager
    [Documentation]  Performs remove operation on Rack Manager
    [Arguments]        ${rack_mgr_uri}
    ${resp} =    Fusion Api Delete Rack Manager    uri=${rack_mgr_uri}
    ${status_code}=    Get From Dictionary    ${resp}    status_code
    Log     Removing Rack Manager     console=True
    ${task_output} =    Run Keyword If      '${status_code}' == '202'    Wait For Task    ${resp}
    ...    ELSE    Run Keyword    FAIL    msg=${resp['message']}
    [return]    ${task_output}

Force Delete Rack Manager
    [Documentation]  Performs force remove operation on Rack Manager
    [Arguments]        ${rack_mgr_uri}    ${param}=?force=True
    ${resp} =    Fusion Api Delete Rack Manager    uri=${rack_mgr_uri}    param=${param}
    ${status_code}=    Get From Dictionary    ${resp}    status_code
    Log     Removing Rack Manager     console=True
    ${task_output} =    Run Keyword If      '${status_code}' == '202'    Wait For Task    ${resp}
    ...    ELSE    Run Keyword    FAIL    msg=${resp['message']}
    [return]    ${task_output}

Get Rack Manager State
    [Documentation]    Get the Current State of RM
    [Arguments]        ${rack_mgr_uri}    ${timeout}=10min
    ${resp} =    Fusion Api Get Rack Manager    ${rack_mgr_uri}
    ${rmc_state} =    Get From Dictionary    ${resp}    state
    [return]    ${rmc_state}

