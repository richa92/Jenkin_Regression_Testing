*** Settings ***
Library				RoboGalaxyLibrary
Library				FusionLibrary
Library				OperatingSystem
Library				BuiltIn
Library				Collections
Library				XML
Library		        SSHLibrary
Library				String
Library				Dialogs

Resource         	../fusion_api_resource.txt

*** Keywords ***

Create ID Pools IPV4 Subnet
	[Documentation]	Adds IPV4 Subnet to an appliance from a variable which contains a list of dicts with the entire payload.
	...		Data File Example :
	...		ipv4_subnet = [{"type": "Subnet","networkId": "15.146.152.0","subnetmask": "255.255.248.0","gateway": "15.146.152.1",
	...						"dnsServers": ["16.110.135.51",	"16.110.135.52"],"domain": "ind.hp.com"}]
								
	[Arguments]		${ipv4_subnet}	${STATUS_CODE}=200
	Log    Registering IPV4 Subnet    console=true
	:FOR	${subnet}	IN	@{ipv4_subnet}
	\	${resp} = 	Fusion Api Create Ipv4 Subnet 		${subnet}
	\	should be equal as integers  ${resp['status_code']}  ${STATUS_CODE}

Get ID Pools IPV4 Subnet URI
    [Documentation]  Get ID Pools IPV4 Subnet URI
	${resp} =		Fusion Api Get Ipv4 Subnet
	${uri} = 	Get From Dictionary		${resp['members'][0]}	uri
	[Return]	${uri}
	
Get ID Pools IPV4 Subnet URI By Network ID
	[Documentation]	Get ID Pools IPV4 Subnet URI By passing Network ID as input
	[Arguments]	${net_id}
	${subnets} =		Fusion Api Get Ipv4 Subnet
	${missing_subnet_uri}	Set Variable	None
	:FOR	${subnet}	in	@{subnets['members']}
	\	${subnet_uri}	Set Variable	None
	\	${subnet_uri}=	Run keyword if	'${subnet['networkId']}'=='${net_id}'	Get From Dictionary	${subnet}	uri
	\	Return from keyword if	'${subnet_uri}'!='None'		${subnet_uri}
	[Return]	${missing_subnet_uri}

Remove all ID Pools IPV4 Subnet
	[Documentation]	Delets all ID Pools IPV4 Subnet on Appliance
	${resp}     Fusion Api Get Ipv4 Subnet
	:FOR    ${member}    in    @{resp['members']}
	\	${uri} = 	Get From Dictionary		${member}	uri
	\	${status}    fusion_api_delete_ipv4_subnet    uri=${uri}
