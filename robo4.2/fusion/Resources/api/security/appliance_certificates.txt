*** Settings ***
Library              FusionLibrary
Library              RoboGalaxyLibrary
Library              OperatingSystem
Library              BuiltIn
Library              copy
Library              Collections
Library              String
Library              json
Resource             ../activity/tasks.txt


*** Keywords ***
Generate Appliance CSR
    [Documentation]  Generate the appliance CSR and return the CSR base64data
    ...                ${is_cnsa} = ${True} or ${False}
    [Arguments]  ${is_cnsa}=${False}
    ${response}=    Fusion Api Get Appliance Interfaces
    ${hostname}=    Get From Dictionary  ${response['applianceNetworks'][0]}  hostname
    ${app1Ipv4Addr}=    Get From Dictionary  ${response['applianceNetworks'][0]}  app1Ipv4Addr
    ${csr_body}    Create Dictionary    type=CertificateSigningRequest                  country=US                  state=California    locality=Palo Alto
    ...                                 organization=Hewlett Packard Enterprise         commonName=${hostname}
    ...                                 alternativeName=${app1Ipv4Addr},${hostname}     cnsaCertRequested=${is_cnsa}
    ${response}=    Fusion Api Generate Certificate Signing Request  ${csr_body}
    ${csr}=    Get From Dictionary  ${response}  base64Data
    [Return]  ${csr}

Create Cert Body For Import CA
    [Documentation]  Purpose to create a request body for import ca certificate
    ...                ${cert_body_example} should be in your variables.py
    ...                    cert_body_example = {
    ...                        "members": [{
    ...                            "type": "CertificateAuthorityInfo",
    ...                            "certificateDetails": {"base64Data": "", "type": "CertificateDetailV2", "aliasName": ""}
    ...                        }],
    ...                        "type": "CertificateAuthorityInfoCollection"
    ...                    }
    ...                 ${cert_data}:   base64Data of the certificate
    ...                 ${aliasName}:   Aliasname of the certificate
    [Arguments]  ${cert_data}  ${aliasName}  ${cert_body_example}
    ${cert_body}=    Copy Dictionary  ${cert_body_example}
    Set To Dictionary  ${cert_body['members'][0]['certificateDetails']}  base64Data=${cert_data}
    Set To Dictionary  ${cert_body['members'][0]['certificateDetails']}  aliasName=${aliasName}
    [Return]  ${cert_body}

Import Appliance Certificate
    [Documentation]  Import Appliance Certificate by given base64 certificate
    [Arguments]  ${signed_cer}  ${type}=CertificateDtoV3
    ${cert_body}=    Create Dictionary          base64Data=${signed_cer}      type=${type}
    ${response}=    Fusion Api Import Appliance Certificate  ${cert_body}
    Wait For Task2  ${response}  timeout=120  interval=5
