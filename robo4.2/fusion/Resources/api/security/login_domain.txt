*** Settings ***
Library             RoboGalaxyLibrary
Library             FusionLibrary
#Library             Collections

*** Keywords ***
Add Active Directory
    [Documentation]    Add Active Directory to OV
    [Arguments]        ${active_directory}
    ${responses} =    Create List
    Log    Adding Active Direcotry    console=true
    :FOR    ${ad}    IN    @{active_directory}
    \    ${resp} =    Fusion Api Add Directory    ${ad}
    \    append to list    ${responses}    ${resp}
    [Return]    ${responses}

Remove Active Directories By Uris
    [Documentation]     Remove Active Direcotries By Uri
    [Arguments]   ${uris}
    Log    Removing Active Direcotries    console=true
    ${resps}=    Create List
    :FOR    ${uri}    IN     @{uris}
    \      ${resp}=  fusion api delete directory    uri=${uri}
    \      Append To List   ${resps}    ${resp}
    :FOR    ${resp}    IN    @{resps}
    \       Wait For Task2    ${resp}    timeout=60  interval=5
    [Return]     ${True}

Remove Active Directory By Name

    [Documentation]     Remove Active Directory By Name
    [Arguments]    ${name}
    Log    Removing Active Directory By Name    console=true
    ${resps}=    Fusion API Get Directory
    ${rsps} =  Get From Dictionary  ${resps}  members
    :For     ${resp}    IN    @{rsps}
    \      ${status}  ${error} =    Run Keyword And Ignore Error   Should Be Equal As Strings  ${name}   ${resp["name"]}
    \      Continue For Loop If   '${status}'=='FAIL'
    \      ${respone} =     Fusion Api Delete Directory   uri=${resp["uri"]}
    \      Wait for task2   ${respone}  timeout=60  interval=5
    \      Return From Keyword  ${True}
    [Return]     ${False}

Get All Added Directories Uris
    [Documentation]    Get All Added Directories Uris
    ${uri_list}     Create List
    ${response} =    Fusion Api Get Directory
    ${status_code} =    Get From Dictionary     ${response}    status_code
    Should Be Equal     ${status_code}     ${200}
    ${resps} =     Get From Dictionary     ${response}    members
    :FOR    ${resp}    IN    @{resps}
    \       ${uri} =    Get From Dictionary   ${resp}    uri
    \       Append To List    ${uri_list}    ${uri}
    [Return]     ${uri_list}

Get Directoy Logindomain Id By Name
    [Documentation]    Get All the Directories Added
    [Arguments]        ${directoryName}
    ${response} =    Fusion Api Get Directory
    ${status_code} =    Get From Dictionary     ${response}    status_code
    Should Be Equal     ${status_code}     ${200}
    ${resps} =     Get From Dictionary     ${response}    members
    :FOR    ${resp}    IN    @{resps}
    \       Return From Keyword If    '${resp['name']}' == '${directoryName}'     ${resp['loginDomain']}
    [Return]     ${None}

Remove All Directories
    [Documentation]    Remove All the Directories Added
    ${uri_list} =    Get All Added Directories Uris
    ${response} =    Create List
    :FOR    ${uri}     IN     @{uri_list}
    \       ${resp} =    Fusion Api Delete Directory     uri=${uri}
    \       Append To List    ${response}    ${resp}
    [Return]     ${response}

Remove All Directories Async
    [Documentation]    Querys The Appliance For All Directories And Then Removes Them
    ${response} =    Remove All Directories
    Wait For Task2    ${response}

Create Group With Permissions
    [Arguments]         ${group_profile}    ${permission_list}
    [Documentation]     Add Group And Assign Specify Permissions To This Group, Return The Response
    Set To Dictionary    ${group_profile['group2PermissionPerGroup']}     permissions    ${permission_list}
    ${resps} =    Fusion Api Assign Roles To Directory Group    ${group_profile}
    [Return]    ${resps}

Edit Group Role Assignment
    [Arguments]    ${bodyProfiles}
    [Documentation]    Edit Group Role Assignment
    ${response} =    Create List
    :FOR    ${body}    IN    @{bodyProfiles}
    \       ${resp} =    Fusion Api Update Group Role Assignment    body=${body}
    \       Append To List    ${response}    ${resp}
    [Return]    ${response}

Get All Added Groups
    [Documentation]    Get all the groups added
    ${response} =     Fusion Api Get Group Role Assignment
    ${status_code} =    Get From Dictionary     ${response}    status_code
    Should Be Equal     ${status_code}     ${200}    msg= Failed to get groups info
    ${lists} =    Get From Dictionary     ${response}    members
    ${group_uri_list} =    Create List
    :FOR    ${group}    IN    @{lists}
    \       ${uri} =    Get From Dictionary    ${group}    uri
    \       Append To List     ${group_uri_list}     ${uri}
    [Return]     @{group_uri_list}
