*** Settings ***
Documentation                   High Level Keywords to support /rest/restores
Library                         FusionLibrary
Library                         RoboGalaxyLibrary
Library                         OperatingSystem
Library                         BuiltIn
Library                         Collections
Library                         XML
Library                         String
Library                         json
Library							Process


*** Keywords ***
#List Restores
#    ${restores} =    Fusion Api Get Restores

Initiate Restore
    [Documentation]  Initiate Restore
    [Arguments]  ${bkp_file}
    ${body} =    Create Dictionary
    Set To Dictionary    ${body}    type=RESTORE    uriOfBackupToRestore=${bkp_file}
    ${resp} =    Fusion Api Restore Backup    ${body}
    [RETURN]    ${resp}


OneView Restore Complete
    [Documentation]     Checks to see if OneView has finished restoring from backup
    ${resp} =    Fusion Api Get Restore Status
    ${status} =    Get From Dictionary    ${resp['members'][0]}    status
    ${percentcomplete} =    Get From Dictionary    ${resp['members'][0]}    percentComplete
    Log    status: ${status} percentComplete:${percentcomplete}    console=true
    Should Be Equal As Strings    SUCCEEDED    ${status}
    [Return]    ${resp}

Restore Appliance
    [Documentation]  Starts a restore operation with the specified backup file.
    ...              The backup must be existing on the appliance, please provide the filename of the local file to upload
    ...              Example:
    ...                  Restore Appliance From Backup
    ...                  Restore Appliance From Backup  C:\\Users\\Downloads\\example_backup_2014-03-06_023131.bkp
    [Arguments]      ${upload_file}=${None}  ${timeout}=120m  ${upload_timeout}=10m  ${interval}=20s
    Log   Restore Appliance from backup    console=True
    Run Keyword If   '${upload_file}' != '${None}'
    ...              Upload Backup   ${upload_file}
    ${backup}=       Get Backup
    Should Not Be Equal As Strings   ${backup["uri"]}  /bad_backup_uri
    Should Be Equal As Strings    ${backup["status"]}  SUCCEEDED
    ${body}=         Create Dictionary   type=RESTORE  uriOfBackupToRestore=${backup["uri"]}
    ${resp}=         Fusion Api Restore Backup    ${body}
    Wait Until Keyword Succeeds    ${timeout}  ${interval}  Restore Reached Endstate  ${resp["uri"]}

Restore Reached Endstate
    [Documentation]     Gets the status of restore process should be success.
    [Arguments]         ${uri}
    ${resp}=       Fusion Api Get Restore Status  uri=${uri}
    Log   Waiting for restore completed, Now is: ${resp["status"]}
    Should Match Regexp  ${resp["status"]}  ((?i)SUCCEEDED)
    [Return]  ${resp["status"]}
