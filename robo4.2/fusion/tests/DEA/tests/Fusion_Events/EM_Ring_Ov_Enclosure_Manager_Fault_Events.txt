*** Settings ***
Documentation     Validate EM Ring OV Enclosure Manager events on OneView
...               = GENERIC USAGE =
...               | pybot | -v GIT_REPO_ROOT:<repo root path> | -v APPLIANCE_IP:<ipv4> |EM_Ring_Ov_Enclosure_Manager_Fault_Events.txt|
...               = Variables =
...               | GIT_REPO_ROOT | Required: Repo root path if NOT defined in environment variable|
...               | APPLIANCE_IP | Required: OneView IPv4 address |
Suite Setup       Configure Events Test Environment
Suite Teardown    Run Keywords    Logout of EM RIS    AND    Logout of Fusion Via REST
Variables         ${GIT_REPO_ROOT}/fusion/tests/DEA/variables/dea_variables.py
Variables         ${GIT_REPO_ROOT}/mgmtfw/tests/thunderbird/variables/tBird_variables.py
Resource          ${GIT_REPO_ROOT}${RESOURCE_ROOT}/resource_all.txt
Resource          ${GIT_REPO_ROOT}${EM_RESOURCE_ROOT}/thunderbird_all.txt
Library           RoboGalaxyLibrary
Library           MgmtFWLibrary
Library           json

*** Test Cases ***
TC 01: Validate EmPeerEmNeeded And EmPeerEmNeededCleared_ElinkRemoved
    [Documentation]     Verify EmPeerEmNeeded And EmPeerEmNeededCleared_ElinkRemoved Alert In Ring OV Enclosure Manager
    [Tags]    DualEM    FLM2.02    Automated
    ${last_fusion_event_id}=    Get Last Fusion Event Id
    ${active_em_bay}=    Get Diags EmClusterStatus Active Node
    ${standby_em_bay}=    Get Diags EmClusterStatus Standby Node
    # Remove Standby EM
    Efuse Standby FLM    EFuseOn    ${standby_em_bay}
    #Disable EM External Link switch port for Active EM Bay and verify that event generated
    Set EM RIS Faults    EmSwitchPortFaultInjection    External   Down    ${active_em_bay}
    #Enable EM External Link switch port for Active EM Bay
    Set EM RIS Faults    EmSwitchPortFaultInjection    External   Up    ${active_em_bay}
    Efuse Standby FLM    EFuseOff    ${standby_em_bay}
    Run Keywords    Login to Fusion via SSH    AND    Login to EM And Create Session
    ${diags_50}    ${diags_auditlog_file_path}=    Get Diags Audit Log 50 Recent Entries
    Set Test Variable    ${EM_Event_Logger}    ${diags_auditlog_file_path}
    Verify Audit Log Alert Event    ${EL_EmPeerEmNeededCleared_ElinkRemoved}    EnclosureManagerSwitchPorts    None    240
    Verify Fusion Event Sent By EM    ${EL_EmPeerEmNeededCleared_ElinkRemoved}    ${last_fusion_event_id}
    Verify Audit Log Alert Event    ${EL_EmPeerEmNeeded}    EnclosureManagerSwitchPorts    None    240
    Verify Fusion Event Sent By EM    ${EL_EmPeerEmNeeded}    ${last_fusion_event_id}
    [Teardown]    Run Keyword If Test Failed    Run Keywords    Set EM RIS Faults    EmSwitchPortFaultInjection    External   Up    ${active_em_bay}
    ...    AND    Efuse Standby FLM    EFuseOff    ${standby_em_bay}