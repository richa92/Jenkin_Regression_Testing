*** Settings ***
Documentation     Tests suite to validate FrontpanelThermalReadSensors and FrontPanelThermalReadSensorsCleared alerts 
...    It covers testing of:
...        FrontpanelThermalReadSensors and FrontPanelThermalReadSensorsCleared alerts
...    = GENERIC USAGE =
...    | pybot | -v ENC_SERIAL_NO:[enclosure serial #] | -v APPLIANCE_IP:<ipv4> |-v GIT_REPO_ROOT: | FP_Alerts.txt |
...    = Variables =
...    | GIT_REPO_ROOT |      Required: Repo root path if NOT defined in environment variable this is mgmtfw root location|
...    | ENC_SERIAL_NO |      Optional: If omitted, will use the 1st enclosure detected in OV |
...    | APPLIANCE_IP |       Required: OneView IPv4 address |

Variables         ${GIT_REPO_ROOT}/fusion/tests/DEA/variables/dea_variables.py
Variables         ${GIT_REPO_ROOT}/mgmtfw/tests/thunderbird/variables/tBird_variables.py
Resource          ${GIT_REPO_ROOT}${RESOURCE_ROOT}/resource_all.txt
Resource          ${GIT_REPO_ROOT}${EM_RESOURCE_ROOT}/thunderbird_all.txt
Library           RoboGalaxyLibrary
Library           MgmtFWLibrary
Library           json

Suite Setup       Configure Events Test Environment
Suite Teardown    Run Keywords  Logout of EM RIS   AND    Logout of Fusion Via REST

*** Test Cases ***
TC: 01: Verify Events For FrontPanelThermalReadSensors alert
    [Documentation]        Verify FrontPanelThermalReadSensors and FrontPanelThermalReadSensorsCleared alert
    [Tags]    FrontPanel    FLM3.00    Automated
    ${thermal_read_sensor_fault}=   Get FrontPanel Fault Status   1   ThermalReadSensors
    Should Be Equal As Strings  ${thermal_read_sensor_fault}  OK
    #inject the fault
    ${last_fusion_event_id}=    get last fusion event ID
    EM API Get Diags   FrontPanelThermalReadSensorsFaultInjection/true
    Sleep    360
    ${diags_50}    ${diags_auditlog_file_path}=    Get Diags Audit Log 50 Recent Entries
    Set Test Variable    ${EM_Event_Logger}    ${diags_auditlog_file_path}
    Verify Fusion Event Sent By EM    ${Front_Panel_Thermal_Read_Sensor}    ${last_fusion_event_id}    ${Enclosure_category}
    ${thermal_read_sensor_fault}=   Get FrontPanel Fault Status   1   ThermalReadSensors
    Should Be Equal As Strings  ${thermal_read_sensor_fault}  Warning
    # clear the fault
    EM API Get Diags   FrontPanelThermalReadSensorsFaultInjection/false
    Sleep    30
    ${diags_50}    ${diags_auditlog_file_path}=    Get Diags Audit Log 50 Recent Entries
    Set Test Variable    ${EM_Event_Logger}    ${diags_auditlog_file_path}
    Verify Fusion Event Sent By EM    ${Front_Panel_Thermal_Read_Sensor_Cleared}    ${last_fusion_event_id}    ${Enclosure_category}
    ${thermal_read_sensor_fault}=   Get FrontPanel Fault Status   1   ThermalReadSensors
    Should Be Equal As Strings  ${thermal_read_sensor_fault}  OK
    [Teardown]    Run Keyword If Test Failed    EM API Get Diags   FrontPanelThermalReadSensorsFaultInjection/false