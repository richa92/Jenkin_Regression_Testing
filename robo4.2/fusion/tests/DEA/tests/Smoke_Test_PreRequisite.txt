*** Settings ***
Documentation    Base script to PREPARE and INITIALIZED the Appliance that REQUIRED in order to SMOKE TEST SUITE
...                            Example Smoke Test Flow
...                                   1. RUN-> Smoke_Test_PreRequisite.txt for either HBA or CNA (Tags based)
...                                   2. RUN-> Smoke_Test_OS_Install_3PAR.txt
...                            = Usage =
...                                    | pybot | -L TRACE | -v GIT_REPO_ROOT: <repo path> | APPLIANCE_IP:<fusion_ipv4> | -v Enclosures:<Encl_name> | NIC_Type: <CNA|HBA> | Smoke_Test_PreRequisite.txt
...                            = Variables =
...                                    | GIT_REPO_ROOT | REQUIRED; Repo root path if NOT defined in environment variable|
...                                    | APPLIANCE IP |      REQUIRED; OneView IP address under test |
...                                    | Enclosures |           REQUIRED: OneView/Appliance Name |
...                                    | NIC_Type |              REQUIRED: CNA or HBA|

# CORE LIBRARIES
##############
Library           RoboGalaxyLibrary
Library           FusionLibrary
Library           MgmtFWLibrary
Library           AltairLibrary
Library           Collections
Library           OperatingSystem

# LOCAL LIBRARIES & RESOURCES
#########################
Variables         ${GIT_REPO_ROOT}/fusion/tests/DEA/variables/dea_variables.py
Variables         ${GIT_REPO_ROOT}/fusion/tests/DEA/variables/smoke_data_variables.py
Resource         ${GIT_REPO_ROOT}/fusion/tests/DEA/resource/resource_all.txt

# GLOBAL LIBRARIES & RESOURCES
##########################
#Resource         ${GIT_REPO_ROOT}/fusion/Resources/api/activity/tasks.txt
Resource         ${GIT_REPO_ROOT}/fusion/Resources/api/common/common.txt
Resource         ${GIT_REPO_ROOT}/fusion/Resources/api/storage/storage_system.txt
Resource         ${GIT_REPO_ROOT}/fusion/Resources/api/storage/storage_pool.txt
Resource         ${GIT_REPO_ROOT}/fusion/Resources/api/storage/storage_volume.txt
Resource         ${GIT_REPO_ROOT}/fusion/Resources/api/storage/storage_volume_template.txt
Resource         ${GIT_REPO_ROOT}${EM_RESOURCE_ROOT}/thunderbird_all.txt
Variables        ${GIT_REPO_ROOT}/mgmtfw/tests/thunderbird/variables/tBird_variables.py
Resource          ../resource/Altair_Resource.txt

Suite Setup           Login to Fusion Via REST
Suite Teardown    Logout of Fusion Via REST

*** Test Cases ***
TC 00: PreRequisite Appliance Setup For CNA or HBA Smoke Test Run
    [Documentation]    This TC is to initialize and prepare the  Appliance with Networks and Storages REQUIRED to run
    ...                                Smoke_Test_OS_Install_3PAR.txt test suite
    [Tags]    PE    DEA    API    Automated    CNA    HBA
    Clean UP Environment
    ${resp}=    Remove ALL Current Appliance Storage Volume Templates
    ${resp}=    Remove ALL Current Appliance Storage Volumes    param=?suppressDeviceUpdates=true
    Wait For Task By Polling Task Length    ${resp}    timeout=300    interval=10
    ${resp}=    Remove ALL Current Appliance Storage Systems
    Wait For Task By Polling Task Length    ${resp}    timeout=300    interval=10
    Run Keyword If    '${NIC_Type}' == 'CNA'    Config CNA FCoE Network And Storages For Smoke Test    ${storage_system_fcoe_310}
    ...    ELSE    Config HBA FC Network And Storages For Smoke Test    ${storage_system_fc_310}
    Edit List Of Storage Pools    ${storage_pools}
    Add List Of Storage Volume Templates    ${storage_volumes_templates}
    ${resp}=    Add List Of Storage Volumes    ${storage_volumes}
    Wait For Task By Polling Task Length    ${resp}    timeout=300    interval=10


*** Keywords ***
Config CNA FCoE Network And Storages For Smoke Test
    [Documentation]    LOCAL keyword to prepare the Appliance with FCoE and Ethernet networks and storages in order to support
    ...                               CNA/FCoE Smoke Test Suite run
    [Arguments]    ${storage_system}
    Setup Ethernet FCoE Appliance
    ${resp}=    Add List Of Storage Systems From Variable    ${storage_system}
    Wait For Task By Polling Task Length    ${resp}    timeout=300    interval=10
    ${resp}=    Edit and Update Appliance Current Storage Systems    ${storage_system}
    Wait For Task By Polling Task Length    ${resp}    timeout=300    interval=10

Config HBA FC Network And Storages For Smoke Test
    [Documentation]    LOCAL keyword to prepare the Appliance with FC and Ethernet networks and storages in order to support
    ...                               HBA/FC Smoke Test Suite run..
    [Arguments]    ${storage_system}
    Setup Ethernet FC Environment
    ${resp}=    Add List Of Storage Systems From Variable    ${storage_system}
    Wait For Task By Polling Task Length    ${resp}    timeout=300    interval=10
    ${resp}=    Edit and Update Appliance Current Storage Systems    ${storage_system}
    Wait For Task By Polling Task Length    ${resp}    timeout=300    interval=10

