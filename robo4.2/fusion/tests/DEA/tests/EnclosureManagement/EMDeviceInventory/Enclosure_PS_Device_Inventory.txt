*** Settings ***
Documentation    Test suite to validate Inventory of Enclosure hardware - Power Supply

...    = GENERIC USAGE =
...    | pybot | -v ENC_SERIAL_NO:[enclosure serial #] | -v APPLIANCE_IP:<ipv4> | -v CIM_Linux_IP:<ipv4> |-v GIT_REPO_ROOT: | Enclosure_PS_Device_Inventory.txt |
...    = Variables =
...    | GIT_REPO_ROOT | Required: Repo root path if NOT defined in environment variable this is mgmtfw root location
...    | ENC_SERIAL_NO | Optional: If omitted, will use the 1st enclosure detected in OV
...    | APPLIANCE_IP | Required: OneView IPv4 address

Library            RoboGalaxyLibrary
Library            MgmtFWLibrary
Library            FusionLibrary
Library            Collections
Library            Selenium2Library

Variables          ${GIT_REPO_ROOT}/fusion/tests/DEA/variables/dea_variables.py
Variables          ${GIT_REPO_ROOT}/fusion/tests/DEA/variables/data_variables.py
Variables          ${GIT_REPO_ROOT}/mgmtfw/tests/thunderbird/variables/tBird_variables.py
Resource           ${GIT_REPO_ROOT}${RESOURCE_ROOT}/resource_all.txt
Resource           ${GIT_REPO_ROOT}${EM_RESOURCE_ROOT}/thunderbird_all.txt

Suite Setup       Run Keywords    Login to Fusion via REST    AND    Login to Fusion via SSH    AND    Login to EM And Create Session
Suite Teardown    Run Keywords    Logout of Fusion Via REST    AND    Logout of Fusion Via SSH

***Test Cases***

TC 01: PowerSupply Device Inventory: Validate Power Supply Inventory Details Present Or Absent In The List Before and After Powersupply Re-seated
    [Documentation]    This test verifies if PowerSupply inventory details are present or absent in the list before and after powersupply re-seated
    [Tags]    PE    API    Automated    PowerSupply    EM
    #Pass the device name <PS> as argument
    ${present_ps_list}    ${absent_ps_list}=    Get Device Bay Present And Absent List    ${PSDevice_EM}
    ${initial_len}=    Get Length    ${present_ps_list}
    : FOR    ${ps_bay}    IN    @{present_ps_list}
    \    #Retrieve the Powersupply health status from Fusion
    \    ${health_before_efuse}=    Get Fusion Device Health Status    ${PSDevice_EM}    ${ps_bay}
    \    #Efuse-On each ps bay
    \    Efuse PowerSupply Device    EFuseOn    ${ps_bay}
    \    #Wait for the Powersupply absence state
    \    Wait For Device Status    ${PSDevice_EM}    ${ps_bay}    Absent
    \    #Retrieve the device present and absent list after efuse-on
    \    ${present_ps_list_after_efuse}    ${empty_ps_list_after_efuse}=    Get Device Bay Present And Absent List    ${PSDevice_EM}
    \    List Should Not Contain Value    ${present_ps_list_after_efuse}    ${ps_bay}
    \    List Should Contain Value    ${empty_ps_list_after_efuse}    ${ps_bay}
    \    #Efuse-Off each ps bay
    \    Efuse PowerSupply Device    EFuseOff    ${ps_bay}
    \    #Wait for the Powersupply to return to OK status
    \    Wait For Device Status    ${PSDevice_EM}    ${ps_bay}    OK
    \    #Compare the device present and absent list after efuse-off
    \    ${present_ps_list_after_efuseoff}    ${empty_ps_list_after_efuseoff}=    Get Device Bay Present And Absent List    ${PSDevice_EM}
    \    List Should Contain Value    ${present_ps_list_after_efuseoff}    ${ps_bay}
    \    List Should Not Contain Value    ${empty_ps_list_after_efuseoff}    ${ps_bay}
    \    #Retrieve the Powersupply health status after efuse and compare
    \    ${health_after_efuse}=    Get Fusion Device Health Status    ${PSDevice_EM}    ${ps_bay}
    \    Should Be Equal    ${health_before_efuse}    ${health_after_efuse}
    [Teardown]    Run Keyword If Test Failed    Run Keywords    Efuse PowerSupply Device    EFuseOff    ${ps_bay}    AND    Wait For Device Status    ${PSDevice_EM}    ${ps_bay}    OK