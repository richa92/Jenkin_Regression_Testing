*** Settings ***
Documentation    Test suite to validate Inventory of Enclosure - ICM

...    = GENERIC USAGE =
...    | pybot | -v ENC_SERIAL_NO:[enclosure serial #] | -v APPLIANCE_IP:<ipv4> |-v GIT_REPO_ROOT: | Enclosure_ICM_Device_Inventory.txt |
...    = Variables =
...    | GIT_REPO_ROOT |      Required: Repo root path if NOT defined in environment variable this is mgmtfw root location|
...    | ENC_SERIAL_NO |      Optional: If omitted, will use the 1st enclosure detected in OV |
...    | APPLIANCE_IP |       Required: OneView IPv4 address |

Variables         ${GIT_REPO_ROOT}/fusion/tests/DEA/variables/dea_variables.py
Variables         ${GIT_REPO_ROOT}/mgmtfw/tests/thunderbird/variables/tBird_variables.py
Resource          ${GIT_REPO_ROOT}${RESOURCE_ROOT}/resource_all.txt
Resource          ${GIT_REPO_ROOT}${EM_RESOURCE_ROOT}/thunderbird_all.txt
Library           RoboGalaxyLibrary
Library           MgmtFWLibrary
Library           FusionLibrary
Library           Collections
Library           Selenium2Library

Suite Setup       Run Keywords    Login to Fusion Via REST    AND     Login to Fusion via SSH    AND    Login to EM And Create Session
Suite Teardown    Run Keywords    Logout of Fusion Via REST    AND    Logout of Fusion Via SSH

*** Test Cases ***

TC 01: ICM Device Inventory: Validate ICM Inventory Details Present Or Absent In The List Before And After ICM Re-seated
    [Documentation]    This test checks on ICM Inventory Details Present Or Absent In The List Before And After ICM Re-seated
    [Tags]    PE    API    Automated    ICM    EM
    #Pass the device name <ICM> as argument
    ${present_icm_list}    ${absent_icm_list} =    Get Interconnect Bay Present And Absent List
    ${initial_len}=    Get Length    ${present_icm_list}
    : FOR    ${icm_bay}    IN    @{present_icm_list}
    \    #Retrieve the ICM health status from Fusion
    \    ${health_before_efuse}=    Get Fusion Device Health Status    ${ICMDevice_EM}    ${icm_bay}
    \    #Efuse On the ICM Device
    \    Efuse Interconnect Device    EFuseOn    ${icm_bay}
    \    #Wait for the ICM absence state
    \    Wait For Device Status    ${ICMDevice_EM}    ${icm_bay}    Absent
    \    #Retrieve the device present and absent list after efuse-on
    \    ${present_icm_list_after_efuse}   ${empty_icm_list_after_efuse} =  Get Interconnect Bay Present And Absent List
    \    List Should Not Contain Value    ${present_icm_list_after_efuse}    ${icm_bay}
    \    List Should Contain Value    ${empty_icm_list_after_efuse}    ${icm_bay}
    \    #Efuse Off the ICM Device
    \    Efuse Interconnect Device    EFuseOff     ${icm_bay}
    \    #Wait for the ICM to return to OK status
    \    Wait For Device Status    ${ICMDevice_EM}    ${icm_bay}    OK
    \    #Compare the device present and absent list after efuse-off
    \    ${present_icm_list_after_efuseoff}    ${empty_icm_list_after_efuseoff} =  Get Interconnect Bay Present And Absent List
    \    List Should Contain Value    ${present_icm_list_after_efuseoff}    ${icm_bay}
    \    List Should Not Contain Value    ${empty_icm_list_after_efuseoff}    ${icm_bay}
    \    #Retrieve the ICM health status from Fusion after efuse and verify
    \    ${health_after_efuse}=    Get Fusion Device Health Status    ${ICMDevice_EM}    ${icm_bay}
    \    Should Be Equal    ${health_before_efuse}    ${health_after_efuse}
    [Teardown]    Run Keyword If Test Failed    Run Keywords    Efuse Interconnect Device    EFuseOff    ${icm_bay}    AND    Wait For Device Status    ${ICMDevice_EM}    ${icm_bay}    OK