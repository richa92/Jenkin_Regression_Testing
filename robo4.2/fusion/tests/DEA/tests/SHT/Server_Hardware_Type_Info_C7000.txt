*** Settings ***
Documentation     Test Suite to verify the Server Hardware Type data of a specific adapter
...    = Usage =
...    | pybot | -L DEBUG | GIT_REPO_ROOT:<path> | APPLIANCE_IP:<fusion_ipv4> | Server_Bay:<server_bay> | Adapter_Mezz_Slot:<adapter_slot_number> | Adapter_Name:<adapter_name> |  Server_Hardware_Type_Info_C7000.txt |
...    = Variables =
...    | GIT_REPO_ROOT | Required; Repo root path if NOT defined in environment variable|
...    | APPLIANCE IP | Required; OneView IP address under test |
...    | Server_Bay | server blade bay number under test |
...    | Adapter_Mezz_Slot | adapter slot number under test |
...    | Adapter_Name | adapter name under test |

Variables         ${GIT_REPO_ROOT}/fusion/tests/DEA/variables/dea_variables.py
Variables         ${GIT_REPO_ROOT}/fusion/tests/DEA/variables/data_variables.py
Resource          ${GIT_REPO_ROOT}${RESOURCE_ROOT}/resource_all.txt
Resource          ${GIT_REPO_ROOT}${EM_RESOURCE_ROOT}/thunderbird_all.txt

Library           RoboGalaxyLibrary
Library           FusionLibrary
Library           Collections

Suite Setup       Run Keywords    Login to Fusion Via REST    AND    Validate User Inputs
Suite Teardown    Logout of Fusion Via REST

***Test Cases***

TC01: Verify The Adapter Model Name In The SHT Information Displayed In OneView Is Correct
    [Documentation]    This test case verifies that the adapter model name in the SHT information displayed in OneView
    ...    matches the model name specified by the vendor
    [Tags]    CNA    HBA    SHT
    Log to Console    Verifying the model name of ${Adapter_Name} in mezz slot ${Adapter_Mezz_Slot} of server bay ${Server_Bay}
    ${field_value}=    Get Adapter Field Information    ${Server_Bay}    ${Adapter_Mezz_Slot}    model
    Should Be Equal    ${field_value}    ${C7000_Adapters[0]['${Adapter_Name}']['model']}    msg= Invalid adapter model name
    [Teardown]    Run Keyword If Test Failed        Send Email Notify
    
TC02: Verify The Adapter Device Type In The SHT Information Displayed In OneView Is Correct
    [Documentation]    This test case verifies that the adapter device type in the SHT information displayed in OneView
    ...    matches the device type specified by the vendor
    [Tags]    CNA    HBA    SHT
    Log to Console    Verifying the device type of ${Adapter_Name} in mezz slot ${Adapter_Mezz_Slot} of server bay ${Server_Bay}
    ${field_value}=    Get Adapter Field Information    ${Server_Bay}    ${Adapter_Mezz_Slot}    deviceType
    Should Be Equal    ${field_value}    ${C7000_Adapters[0]['${Adapter_Name}']['deviceType']}    msg= Invalid adapter device type
    [Teardown]    Run Keyword If Test Failed        Send Email Notify

TC03: Verify The Adapter Max Port Speed In The SHT Information Displayed In OneView Is Correct
    [Documentation]    This test case verifies that the max port speed of adapter in the SHT information displayed in OneView
    ...    matches the max port speed specified by the vendor
    [Tags]    CNA    HBA    SHT
    Log to Console    Verifying the max port speed of ${Adapter_Name} in mezz slot ${Adapter_Mezz_Slot} of server bay ${Server_Bay}
    ${field_value}=    Get Adapter Field Information    ${Server_Bay}    ${Adapter_Mezz_Slot}    ports
    ${max_port_speed1}=    Get From Dictionary    ${field_value[0]}    maxSpeedMbps
    ${max_port_speed2}=    Get From Dictionary    ${field_value[1]}    maxSpeedMbps
    ${max_port_speed}=    Run Keyword If    ${max_port_speed1} == ${max_port_speed2}    Set Variable    ${max_port_speed1}
           ...    ELSE    Set Variable    0
    Should Be Equal    ${max_port_speed}    ${C7000_Adapters[0]['${Adapter_Name}']['maxPortSpeed']}    msg= Invalid adapter max port speed
    [Teardown]    Run Keyword If Test Failed        Send Email Notify
    
TC04: Verify The Adapter Physical Ports Count In The SHT Information Displayed In OneView Is Correct
    [Documentation]    This test case verifies that the physical ports of adapter in the SHT information displayed in OneView
    ...    matches the physical ports specified by the vendor
    [Tags]    CNA    HBA    SHT
    Log to Console    Verifying the physical ports of ${Adapter_Name} in mezz slot ${Adapter_Mezz_Slot} of server bay ${Server_Bay}
    ${field_value}=    Get Adapter Field Information    ${Server_Bay}    ${Adapter_Mezz_Slot}    ports
    ${port_count}=    Get Length    ${field_value}
    Should Be Equal    ${port_count}    ${C7000_Adapters[0]['${Adapter_Name}']['physicalPorts']}    msg= Invalid adapter physical ports
    [Teardown]    Run Keyword If Test Failed        Send Email Notify
    
TC05: Verify The Adapter Virtual Ports Count In The SHT Information Displayed In OneView Is Correct
    [Documentation]    This test case verifies that the virtual ports of adapter in the SHT information displayed in OneView
    ...    matches the adapter virtual ports specified by the vendor
    [Tags]    CNA    HBA    SHT
    Log to Console    Verifying the virtual ports of ${Adapter_Name} in mezz slot ${Adapter_Mezz_Slot} of server bay ${Server_Bay}
    ${field_value}=    Get Adapter Field Information    ${Server_Bay}    ${Adapter_Mezz_Slot}    ports
    ${virtual_ports1}=    Get Length    ${field_value[0]['virtualPorts']}
    ${virtual_ports2}=    Get Length    ${field_value[1]['virtualPorts']}
    ${virtual_ports}=    Evaluate    ${virtual_ports1}+${virtual_ports2}
    Should Be Equal    ${virtual_ports}    ${C7000_Adapters[0]['${Adapter_Name}']['virtualPorts']}    msg= Invalid adapter virtual ports
    [Teardown]    Run Keyword If Test Failed        Send Email Notify
    
TC06: Verify The Adapter Capabilities In The SHT Information Displayed In OneView Is Correct
    [Documentation]    This test case verifies the capabilities of adapter in the SHT information displayed in OneView
    ...    matches the capabilities specified by the vendor
    [Tags]    CNA    HBA    SHT
    Log to Console    Verifying the capabilities of ${Adapter_Name} in mezz slot ${Adapter_Mezz_Slot} of server bay ${Server_Bay}
    ${field_value}=    Get Adapter Field Information    ${Server_Bay}    ${Adapter_Mezz_Slot}    capabilities
    List Should Contain Sub List    ${field_value}    ${C7000_Adapters[0]['${Adapter_Name}']['capabilities']}    msg= Invalid adapter capabilities
    [Teardown]    Run Keyword If Test Failed        Send Email Notify
    
***Keywords***
Validate User Inputs
    [Documentation]    This keyword validates the user's inputs - server bay and adapter mezz slot
    ${server_bay_list}=    Get Adapter Server Bay List for C7000    ${Adapter_Name}
    Run Keyword And Ignore Error    List Should Contain Value     ${server_bay_list}    ${Server_Bay}    msg= Invalid input server bay ${Server_Bay}
    ${mezz_bay_list}=    Get Adapter Mezz Bay List for C7000    ${Server_Bay}    ${Adapter_Name}
    ${Adapter_Mezz_Slot}=    Convert To Integer    ${Adapter_Mezz_Slot}
    List Should Contain Value    ${mezz_bay_list}    ${Adapter_Mezz_Slot}    msg= Invalid input mezz bay ${Adapter_Mezz_Slot}
