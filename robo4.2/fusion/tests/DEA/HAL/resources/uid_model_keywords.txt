*** Settings ***
Documentation     Keywords for the Enclosure & EM UID Light model-based test
...               = Usage =
...               | Resource | ../resources/uid_model_keywords.txt |
Library           Collections
Library           String
Library           json
Library           SSHLibrary
Library           OperatingSystem
Library           RoboGalaxyLibrary            # DVTs Robot Framework extensions
Library           FusionLibrary                # DVTs Robot Framework extensions
Variables         variables.py    ${ENCLOSURE}

*** Variables ***


*** Keywords ***

Set Device UID
    [Documentation]    Change the status of the UID light on a particular device.
    [Arguments]    ${Device}    ${Action}    ${State}    ${Bay Number}=${EMPTY}
    ${UidState}    Create Dictionary
    Set To Dictionary    ${UidState}    UidState    ${State}
    Run Keyword Unless    '${Bay Number}' == '${EMPTY}'    Set To Dictionary    ${UidState}    BayNumber    ${Bay Number}
    ${Response}    HAL API Perform POST Action    ${FUSION_IP}    ${Action}    ${UidState}
    # Make sure the API call worked; no exceptions, status = SUCCESS, correct device
    ${CallStatus}    Find Errors In HAPI Response    ${Device}    ${Response}    performAction    ${Action}    ${UidState}
    Fail On Any Errors In HAPI Response    ${CallStatus}

Set Invalid Device UID
    [Documentation]    Hopefully does not change the status of the UID light on a particular device.
    [Arguments]    ${Device}    ${Action}    ${Bay Number}=${EMPTY}
    # Get various random values for input into the system
    ${Random Values}    Get Random Values For Input
    ${Keys}    Get Dictionary Keys    ${Random Values}
    # Send these values into the system as the desired UID State and make sure the system doesn't accept them
    : For    ${Key}    In    @{Keys}
    \    ${Value}    Get From Dictionary    ${Random Values}    ${Key}
    \    ${UidState}    Create Dictionary
    \    Set To Dictionary    ${UidState}    UidState    ${Value}
    \    Run Keyword Unless    '${Bay Number}' == '${EMPTY}'    Set To Dictionary    ${UidState}    BayNumber    ${Bay Number}
    \    ${Response}    HAL API Perform POST Action    ${FUSION_IP}    ${Action}    ${UidState}
    \    ${CallStatus}    Find Errors In HAPI Response    ${Device}    ${Response}    performAction    ${Action}    ${UidState}
    \    Run Keyword And Expect Error    *
    \    ...    Fail On Any Errors In HAPI Response    ${CallStatus}
    # Now, send these values into the system as the desired Bay Number (if applicable)
    Return From Keyword If    '${Bay Number}' == '${EMPTY}'
    : For    ${Key}    In    @{Keys}
    \    ${Value}    Get From Dictionary    ${Random Values}    ${Key}
    \    ${UidState}    Create Dictionary
    \    Set To Dictionary    ${UidState}    UidState    ${Value}
    \    Set To Dictionary    ${UidState}    BayNumber    ${Value}
    \    ${Response}    HAL API Perform POST Action    ${FUSION_IP}    ${Action}    ${UidState}
    \    ${CallStatus}    Find Errors In HAPI Response    ${Device}    ${Response}    performAction    ${Action}    ${UidState}
    \    Run Keyword And Expect Error    *
    \    ...    Fail On Any Errors In HAPI Response    ${CallStatus}

Webapp Shows Device UID Is In State
    [Documentation]    Uses the Webapp to make sure the right UID light is in the right state.
    [Documentation]    Currently this only works for the Chassis UID light.
    [Arguments]    ${Device}    ${Action}    ${State}
    # Perform the POST action to find out what state the device UID is showing
    ${Response}    HAL API Perform POST Action    ${FUSION_IP}    ${Action}
    # Make sure the API call worked; no exceptions, status = SUCCESS, correct device
    ${EmptyDict}    Create Dictionary
    ${CallStatus}    Find Errors In HAPI Response    ${Device}    ${Response}    performAction    ${Action}    ${EmptyDict}
    Fail On Any Errors In HAPI Response    ${CallStatus}
    # Validate the correct state
    ${Observed}    Get From Dictionary    ${Response}    OperationResult
    ${Observed}    Get From Dictionary    ${Observed}    UidState
    Should Be Equal    ${Observed}    ${State}    msg=The UID light of device "${Device}" was observed in the ${Observed} state, but expected to be in the ${State} state.

Fusion Shows Device UID Is In State
    [Documentation]    Uses the regular Fusion API to make sure the right UID light is in the right state.
    [Arguments]    ${Device}    ${State}    ${DevicePath}
    # Perform the GET action to find out what state the device UID is showing
    ${Response}    Execute DCS API Command Via CLI    GET    rest/v1/${DevicePath}
    # Validate the correct state
    ${Observed}    Run Keyword Unless    '${Device}' == 'Chassis'    Get From Dictionary    ${Response}    Oem
    ${Observed}    Run Keyword Unless    '${Device}' == 'Chassis'    Get From Dictionary    ${Observed}    Hp
    ${Observed}    Run Keyword Unless    '${Device}' == 'Chassis'    Get From Dictionary    ${Observed}    IndicatorLED
    ${Observed}    Run Keyword Unless    '${Device}' == 'Chassis'    Should Be Equal    ${Observed}    ${State}    msg=The UID light of device "${Device}" was observed in the ${Observed} state, but expected to be in the ${State} state.
    ${Observed}    Run Keyword If    '${Device}' == 'Chassis'    Get From Dictionary    ${Response}    IndicatorLED
    ${Observed}    Run Keyword If    '${Device}' == 'Chassis'    Should Be Equal    ${Observed}    ${State}    msg=The UID light of device "${Device}" was observed in the ${Observed} state, but expected to be in the ${State} state.


EM1 UID Is On
    [Documentation]    Make sure the UID light for Enclosure Manager 1 is on.
    Fusion Shows Device UID Is In State    EM1    Lit    EnclosureManagers/1

EM1 UID Is Off
    [Documentation]    Make sure the UID light for Enclosure Manager 1 is off.
    Fusion Shows Device UID Is In State    EM1    Off    EnclosureManagers/1

EM1 UID Is Blinking
    [Documentation]    Make sure the UID light for Enclosure Manager 1 is blinking.
    Fusion Shows Device UID Is In State    EM1    Blinking    EnclosureManagers/1

EM2 UID Is On
    [Documentation]    Make sure the UID light for Enclosure Manager 2 is on.
    Fusion Shows Device UID Is In State    EM2    Lit    EnclosureManagers/2

EM2 UID Is Off
    [Documentation]    Make sure the UID light for Enclosure Manager 2 is off.
    Fusion Shows Device UID Is In State    EM2    Off    EnclosureManagers/2

EM2 UID Is Blinking
    [Documentation]    Make sure the UID light for Enclosure Manager 2 is blinking.
    Fusion Shows Device UID Is In State    EM2    Blinking    EnclosureManagers/2

Enclosure UID Is On
    [Documentation]    Make sure the UID light for the enclosure is on.
    Webapp Shows Device UID Is In State    Chassis    ChassisUidState    on
    Fusion Shows Device UID Is In State    Chassis    Lit    Chassis/1

Enclosure UID Is Off
    [Documentation]    Make sure the UID light for the enclosure is on.
    Webapp Shows Device UID Is In State    Chassis    ChassisUidState    off
    Fusion Shows Device UID Is In State    Chassis    Off    Chassis/1

Enclosure UID Is Blinking
    [Documentation]    Make sure the UID light for the enclosure is on.
    Webapp Shows Device UID Is In State    Chassis    ChassisUidState    blink
    Fusion Shows Device UID Is In State    Chassis    Blinking    Chassis/1


Turn On EM1 UID
    [Documentation]    Activate the UID light for Enclosure Manager 1.
    Set Device UID    EM1    EmUidControl    on    ${1}

Turn Off EM1 UID
    [Documentation]    Deactivate the UID light for Enclosure Manager 1.
    Set Device UID    EM1    EmUidControl    off    ${1}

Blink EM1 UID
    [Documentation]    Make the UID light for Enclosure Manager 1 blink.
    Set Device UID    EM1    EmUidControl    blink    ${1}

Turn On EM2 UID
    [Documentation]    Activate the UID light for Enclosure Manager 2.
    Set Device UID    EM2    EmUidControl    on    ${2}

Turn Off EM2 UID
    [Documentation]    Deactivate the UID light for Enclosure Manager 2.
    Set Device UID    EM2    EmUidControl    off    ${2}

Blink EM2 UID
    [Documentation]    Make the UID light for Enclosure Manager 2 blink.
    Set Device UID    EM2    EmUidControl    blink    ${2}

Turn On Enclosure UID
    [Documentation]    Activate the UID light for the chassis.
    Set Device UID    Chassis    ChassisUidControl    on

Turn Off Enclosure UID
    [Documentation]    Deactivate the UID light for the chassis.
    Set Device UID    Chassis    ChassisUidControl    off

Blink Enclosure UID
    [Documentation]    Make the UID light for the chassis blink.
    Set Device UID    Chassis    ChassisUidControl    blink

Send EM1 Junk Data
    [Documentation]    Send an unsupported or illogical state to the EM.
    Set Invalid Device UID    EM1    EmUidControl    ${1}

Send EM2 Junk Data
    [Documentation]    Send an unsupported or illogical state to the EM.
    Set Invalid Device UID    EM2    EmUidControl    ${2}

Send Enc Junk Data
    [Documentation]    Send an unsupported or illogical state to the enclosure.
    Set Invalid Device UID    Chassis    ChassisUidControl
