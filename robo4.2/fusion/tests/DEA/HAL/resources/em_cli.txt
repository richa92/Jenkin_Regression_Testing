*** Settings ***
Documentation     EM CLI keywords
...               = Usage =
...               | Library | ../resources/em_cli.txt |

Library           String
Library           RoboGalaxyLibrary            # DVTs Robot Framework extensions
Library           FusionLibrary                # DVTs Robot Framework extensions
Library           OperatingSystem
Library           SSHLibrary
Resource          ../resources/fusion_cli.txt

*** Variables ***
${EM_REBOOT_TIMEOUT}=     300
${EM_SHUTDOWN_TIMEOUT}=    60

*** Keywords ***
Enable IPv4 on EM
    [Documentation]    Enable IPv4 networking on the EM

    ${powerShell}    ${status}=    Run Keyword And Ignore Error    Environment Variable Should Be Set    windir
    # If $windir is set, we are using PowerShell.
    ${countFlag}=    Set Variable If    '${powerShell}'=='PASS'    -n 5    -c 5

    Login to Fusion via SSH
    @{Commands}    Create List    rm -fr ~/.ssh
    ...    ssh -o StrictHostKeyChecking=no root\@${EM_IP}%${FUSION_NIC}} ip addr add ${EM_IPV4} dev ${EM_NIC}
    ...    ssh -o StrictHostKeyChecking=no root\@${EM_IP}%${FUSION_NIC}} ifconfig ${EM_NIC} netmask ${NETMASK_IP}
    ...    ssh -o StrictHostKeyChecking=no root\@${EM_IP}%${FUSION_NIC}} route add default gw ${GATEWAY_IP} ${EM_NIC}

    :FOR    ${Command}    IN    @{Commands}
    \    ${stdout}         ${stderr}    ${rc}=            Execute Command    ${Command}    return_stderr=True    return_rc=True
    \    Log               ${stdout}
    \    Should Be Equal As Integers    ${rc}        0    msg=non-zero return code ${rc}
    Logout of Fusion via SSH

    # Run Ping Test To Validate IPv4 Access
    ${rc}    ${stdout}=    Run And Return Rc and Output    ping ${countFlag} ${EM_IPV4}
    Log    ${stdout}
    Should Be Equal As Integers    ${rc}    0    IPv4 Address ${EM_IPV4} Ping Failed

Login to EM via SSH
    [Documentation]    Example:\n| Login to EM Via SSH | 10.0.12.106 | Administrator | hpvse123 |
    [Arguments]    ${IP}=${EM_IP}    ${USERNAME}=${EM_LOGIN}    ${PASSWORD}=${EM_PASSWORD}
    ...            ${PROMPT}=${EM_PROMPT}    ${TIMEOUT}=${EM_TIMEOUT}
    Log Many    ${IP}    ${USERNAME}    ${PASSWORD}    ${PROMPT}    ${TIMEOUT}
    Set Default Configuration    prompt=${PROMPT}    timeout=${TIMEOUT}
    ${Id}=    Open Connection    \[${IP}\]
    ${Output}=    Login    ${USERNAME}    ${PASSWORD}
    [Return]    ${Id}

Logout of EM via SSH
    [Documentation]    Disconnect from EM via SSH
    ...    Example:\n| Logout Of EM Via SSH |
    Close Connection

Reboot EM Over SSH
    [Documentation]            Reboot EM directly through SSH

    ${Command}=    catenate    ssh -o StrictHostKeyChecking=no root\@${EM_IP}%${FUSION_NIC}} /sbin/shutdown -yr now
    ${stdout}         ${stderr}    ${rc}=            Execute Command    ${Command}    return_stderr=True    return_rc=True
    Log               ${stdout}
    Should Be Equal As Integers    ${rc}        0    msg=non-zero return code ${rc}
