** Settings ***
Documentation    Deploy Fusion ISO onto CIM
...    = Usage =
...    pybot |-v ENCLOSURE:tesla |-v ISO_URL:<Fusion ISO Image> Deploy_CIM_Fusion.txt
...    = Variables =
...    | ENCLOSURE | (required) Name of the enclosure to Deploy upon. (values from variables.py) | 
...    | ISO_URL | (optional) URL of Fusion ISO Image To Install (defaulted) |

Library           String
Library           Collections
Library           SSHLibrary
Library           robot.api.logger
Library           RoboGalaxyLibrary
Library           FusionLibrary
Variables         ../resources/variables.py    ${ENCLOSURE} 
Resource          ../resources/fusion_cli.txt
Resource          ../resources/fusion_api.txt
Resource          ../resources/defaults.txt

*** Variables ***
${ISO_URL}=                    http://jenkins.rsn.hp.com:8080/userContent/fusion-install.iso
#${ISO_URL}=                   http://jenkins.rsn.hp.com:8080/userContent/latest-fusion.iso
${Installation_Time}           45 minutes
${STARTUP_PROGRESS_POLL}       60 seconds
${STARTUP_PROGRESS_RETRIES}    30
${DUMP_FILE}                   fusion_dump.sdmp


*** Test Cases ***
Collect Data
    Log Variables
    Console    \nDeploying from: ${ISO_URL} onto: ${ENCLOSURE}

Insert Virtual Media image
    ILO CONNECT     ${ILO_IP}    ${ILO_USERNAME}    ${ILO_PASSWORD}
    ILO INSERT VIRTUAL MEDIA    cdrom    ${ISO_URL}
    ILO SET VIRTUAL MEDIA STATUS
    ${Response}=    ILO GET VIRTUAL MEDIA STATUS
    ${Inserted}    Get From Dictionary    ${Response}    image_inserted
    Run Keyword If    "${Inserted}" != "YES"    
    ...    Fatal Error    msg=Virtual image not inserted properly ${Response}

Boot from Virtual Media
    ILO SET ONE TIME BOOT    CDROM
    ${Status}=    ILO GET ONE TIME BOOT    
    Run Keyword If    "${Status}" != "cdrom"   
    ...    Fatal Error    msg=One-time boot not set properly ${Status}

Reset Server
    ILO RESET SERVER
    Sleep    30
    ${Status}=    ILO GET HOST POWER STATUS
    Run Keyword If    "${Status}" != "ON"    
    ...    Fatal Error    msg=Server not restarted ${Status}

Wait For Fusion Install and Startup
    [Documentation]    Sleep for ${Installation_Time}.
    Sleep    ${Installation_Time}    reason=Waiting for installation, reboot and startup

Wait Until Fusion Responds
    [Documentation]    Loop here until Fusion API begins to respond to requests.
    ...    Ignore WARN messages while waiting for Fusion to respond.
    : FOR    ${INDEX}    IN RANGE    1    ${STARTUP_PROGRESS_RETRIES}
    \    ${Status}    Run Keyword and Return Status    Fusion Api Get Startup Progress   [${FUSION_IPV6}]    api=199
    \    Pass Execution If    '${Status}'=='True'    Fusion Api has responded
    \    Console    \nIndex: ${INDEX}
    \    Sleep    ${STARTUP_PROGRESS_POLL}
    Fatal Error    msg=Could not Get Fusion REST API to respond

Wait Until Fusion Responds Successful
    [Documentation]    With Fusion responding to REST API requests, it'll likely 
    ...    start by responding with 503 Service Unavailable. Loop here until we start
    ...    getting 'success' responses. 
    : FOR    ${INDEX}    IN RANGE    1    ${STARTUP_PROGRESS_RETRIES}
    \    ${Response}   Fusion Api Get Startup Progress   [${FUSION_IPV6}]    api=199
    \    ${Status}=       Get From Dictionary    ${Response}      status_code
    \    Pass Execution If    '${Status}'!='503'    Fusion API responded available
    \    Console    \nIndex: ${INDEX}
    \    Sleep    ${STARTUP_PROGRESS_POLL}
    Fatal Error    msg=Failed to get successful Fusion Api response

Wait Until Fusion Startup Completes
    [Documentation]    With successful responses, start polling for startup
    ...    completion. 
    : FOR    ${INDEX}    IN RANGE    1    ${STARTUP_PROGRESS_RETRIES}
    \    ${Response}   Fusion Api Get Startup Progress   [${FUSION_IPV6}]    api=199
    \    ${total}=       Get From Dictionary    ${Response}      total
    \    ${complete}=       Get From Dictionary    ${Response}      complete
    \    Console    \nIndex: ${INDEX} \tCompleted ${complete} of ${total}
    \    Pass Execution If    "${total}"=="${complete}"    Fusion Login Ready For First Time Setup
    \    Sleep    ${STARTUP_PROGRESS_POLL}
    Fatal Error    msg=Could not verify Fusion web services completed startup

Open Browser To Login Page After Startup
    Console    Opening Browser to https://[${FUSION_IPV6}]/#/login
    Open Browser    https://[${FUSION_IPV6}]/#/login    ${BROWSER}
    Sleep    5
    Capture Page Screenshot

EULA Agreement
    Wait Until Element Is Visible    id=hp-eula-agree-button
    ...                              timeout=120
    ...                              error=Not at EULA page
    Click Button    id=hp-eula-agree-button
    sleep    5
    Click Button    id=hp-eula-ok-button
    sleep    5
    Capture Page Screenshot

First Time Login
    Wait Until Element Is Visible    id=hp-login-user
    ...                              timeout=300
    ...                              error=Not at Login page
    Input Text    hp-login-user      ${FUSION_USERNAME}
    Input Text    hp-login-password    ${FUSION_FACTORY_PASSWORD}
    Click Button    hp-login-button
    sleep    5
    Capture Page Screenshot

Assign Administrator Password
    Wait Until Element Is Visible    id=hp-initial-password-new1
    ...                              timeout=300
    ...                              error=Not at Initial Password page
    Input Text    id=hp-initial-password-new1    ${FUSION_PASSWORD}
    Input Text    id=hp-initial-password-new2    ${FUSION_PASSWORD}
    Click Button    id=hp-initial-password-button-element
    sleep    5
    Capture Page Screenshot

Assign V4 Networking
    Wait Until Element Is Visible    id=cic-network-hostname
    ...                              timeout=300
    ...                              error=Not at initial networking page
    Maximize Browser Window
    Sleep    1
    Capture Page Screenshot
    # Add Hostname
    Input Text    id=cic-network-hostname         ${FUSION_FQDN}
    Capture Page Screenshot
    Input Text    id=cic-network-ip-ipv4          ${FUSION_IP}
    Input Text    id=cic-network-mask-ipv4        ${NETMASK_IP}
    Input Text    id=cic-network-gateway-ipv4     ${GATEWAY_IP}
    Input Text    id=cic-network-preferred-dns    ${PRIMARY_DNS}
    Input Text    id=cic-network-alternate-dns    ${ALTERNATE_DNS}
    # Set IPv6 to unassigned; for the time being.
    Focus    id=cic-network-ipv6-unassign
    Select Radio Button    addressAssignmentforv6    cic-network-ipv6-unassign
    Capture Page Screenshot
    # OK the changes
    Click Button    id=cic-network-ok
    Sleep    5
    Wait Until Page Contains    Applying network settings.    timeout=300
    Capture Page Screenshot

Wait for Fusion Ready
    Wait Until Keyword Succeeds    30 min    30 sec    Page Should not contain    Applying network settings.
    Capture Page Screenshot
    Page Should Not Contain    Unable to apply all the network settings correctly.
    ${Location}=    Get Location
    ${Page}=    Fetch From Right    ${Location}    \#
    Pass Execution If    '${Page}'=='/login'         msg=Found login page
    Pass Execution If    '${Page}'=='/dashboard'     msg=Found dashboard
    Pass Execution If    '${Page}'=='/activities'    msg=Found activities
    Pass Execution If    '${Page}'=='/activity'      msg=Found activity
    Pass Execution If    '${Page}'=='/settings/show/overview'    msg=Found Overview
    Capture Page Screenshot
    Fail    msg=Failed to apply network settings

Cleanup
    Close Browser

Set Fusion Version Metadata
    Set Suite Metadata     Deployed URL    ${ISO_URL}    top=True
    Set Suite Metadata     Fusion URL:       https://${Fusion_IP}    top=True
    Login to Fusion via REST    ${FUSION_IP}    ${FUSION_USERNAME}    ${FUSION_PASSWORD}
    ${Response}=    Fusion Api Get Appliance Version
    Logout of Fusion Via REST
    Log    ${Response}
    ${softwareVersion}    Get from Dictionary    ${Response}    softwareVersion
    Set Suite Variable    ${Fusion_Version}    ${softwareVersion}
    Set Suite Metadata    Fusion Version    ${Fusion_Version}       top=True

Write Fusion Properties
    Run    echo \# %JOB_NAME% %DATE% %TIME% > C:\\Jenkins\\Fusion-CIM.properties
    Run    echo FUSION_IP=${Fusion_IP} >> C:\\Jenkins\\Fusion-CIM.properties
    Run    echo FUSION_URL=https://${Fusion_IP} >> C:\\Jenkins\\Fusion-CIM.properties
    Run    echo FUSION_VERSION=${Fusion_Version} >> C:\\Jenkins\\Fusion-CIM.properties

PostInstall Steps
    Prevent CIM iLo Reset From Fusion Factory Reset

Generate Fusion Dump and Log After Network Initialization
    Login to Fusion Via SSH
    Run Keyword And Ignore Error    SSHLibrary.Get File    /ci/logs/ciDebug.*
    Logout of Fusion Via SSH

    Login to Fusion Via REST
    Create And Download Support Dump    ${DUMP_FILE}
    Logout of Fusion Via REST
