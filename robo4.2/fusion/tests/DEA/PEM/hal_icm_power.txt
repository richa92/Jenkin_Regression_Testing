*** Settings ***
Documentation   Verifies Interconnect Module Power Status reported by DCS and HAL
...             = Usage =
...             | pybot | -L DEBUG | hal_icm_power.txt |
...             = Variables =
...             | FUSION IP     | Required; IP address of the FusionVM to use |
...             | ENCLOSURE     | Required; Enclosure name to run on (dcs, tesla, etc) |
...             = Preconditions =
...             - Claimed EM is requierd.
...             - Populated ICM bays are required.
...             - One or More Empty ICM bays are required.

Library         RoboGalaxyLibrary           # DVTs Robot Framework extensions
Library         FusionLibrary               # DVTs Robot Framework extensions
Library         loginPEMClass
Resource        ../HAL/resources/fusion_api.txt                 # Comet-DVT fusion API extensions
Resource        ../HAL/resources/hal_webapp.txt                 # Comet-DVT HAL webapp extensions
Resource        ../HAL/resources/fusion_cli.txt                 # Comet-DVT fusion CLI extensions
Resource        ../HAL/resources/system_info.txt                # Comet-DVT fusion Enclosure extensions
Resource        ../HAL/resources/perm_api.txt    # AM-DVT PERM webapp extensions
Variables       ../variables/dea_variables.py
Resource        ../resource/fusion_api_resource.txt    # AM-DVT Fusion API Resource
Resource        ../HAL/resources/hal_resource.txt    # AM-DVT hal resource file 


Suite Setup     Run Keywords
...             Login to Fusion Via REST
...             Login to Fusion via SSH
...             Verify Claimed EM                           # Currently not available
...             Save Initial ICM Power Status
...             Initialize ICM Power State for Model

Suite Teardown      Run Keywords
...                 Restore Initial ICM Power Status
...                 Login to Fusion Via REST
...                 Logout of Fusion Via REST
...                 Logout of Fusion Via SSH


*** Test Cases ***
Set ICM Power State to Invalid Value
    [Documentation]    Test script to Validate Invalid value for Power State of ICM 
    [Tags]    DCS    HW    HAL    Automated
    ${auth}=    Get Trusted Token
    set_header_to_pem_token    ${FUSION_IP}    ${creds}    ${auth}
    ${Invalid Power State}=    Select Random Element from List     ${Invalid Power States}
    ${Bay}=                     Select Random Element from List     ${PopulatedBays}
    Run Keyword and Expect Error            ${Error Message}
    ...     Set ICM Power State via HAL     ${Bay}      ${Invalid Power State}

Set ICM Power State of Invalid Bay
    [Documentation]    Test script to Validate setting of Power State for Invalid bay
    [Tags]    DCS    HW    HAL    Automated
    ${Power State}=     Select Random Element from List     ${OFF}
    ${Invalid Bay}=     Select Random Element from List     ${Invalid Bays}
    Run Keyword and Expect Error            ${Error Message}
    ...     Set ICM Power State via HAL     ${Invalid Bay}      ${Power State}

Set ICM Power State of Empty Bay
    [Documentation]    Test script to Validate setting of Power State for Empty bay
    [Tags]    DCS    HW    HAL    Automated
    ${Power State}=     Select Random Element from List     ${OFF}
    ${Empty Bay}=       Select Random Element from List     ${EmptyBays}
    Run Keyword and Expect Error            ${Error Message}
    ...     Set ICM Power State via HAL     ${Empty Bay}    ${Power State}

Get ICM Power State of Invalid Bay
    [Documentation]    Test script to Validate getting of Power State for Invalid bay
    [Tags]    DCS    HW    HAL    Automated
    ${Invalid Bay}=     Select Random Element from List     ${Invalid Bays}
    Run Keyword and Expect Error                ${Error Message}
    ...     Get ICM Power State via HAL API     ${Invalid Bay}

Get ICM Power State of Empty Bay
    [Documentation]    Test script to Validate getting of Power State for Empty bay
    [Tags]    DCS    HW    HAL    Automated
    ${Empty Bay}=   Select Random Element from List     ${EmptyBays}
    Run Keyword and Expect Error                ${Error Message}
    ...     Get ICM Power State via HAL API     ${Empty Bay}

Verify Interconnect Module Power Status
    [Documentation]    Test script to Validate ICM power module status
    [Tags]    DCS    HW    HAL    Automated
    Run Keyword If      '${DCS}'=='${True}'
    ...                 Log     Issue #AM63: Updated HAL Actions do not work on DCS Sprint 59 yet. Continue using old action names.
    ...                 level=WARN
    Run Random Model    model=hal_icm_power_model.xml   transitions=10
