*** Settings ***
Documentation   Verifies Interconnect Module Configuration settings
...             = Usage =
...             | pybot | -L DEBUG | hal_icm_configuration.txt |
...             = Variables =
...             | FUSION IP     | Required; IP address of the FusionVM to use |
...             | ENCLOSURE     | Required; Enclosure name to run on (dcs, tesla, etc) |
...             = Preconditions =
...             None

Library             RoboGalaxyLibrary               # RoboGalaxy Robot Framework extensions
Library             loginPEMClass
Library             FusionLibrary                   # RoboGalaxy Fusion extensions
Resource            ../HAL/resources/fusion_api.txt     # AM-DVT fusion API extensions
Resource            ../HAL/resources/hal_webapp.txt     # AM-DVT HAL webapp extensions
Resource            ../HAL/resources/fusion_cli.txt     # AM-DVT fusion CLI extensions
Resource            ../HAL/resources/system_info.txt    # AM-DVT System Info extensions
Resource            ../HAL/resources/perm_api.txt       # AM-DVT PERM webapp extensions
Variables           ../variables/dea_variables.py
Resource            ../resource/fusion_api_resource.txt     # AM-DVT Fusion API Resource
Resource            ../HAL/resources/hal_resource.txt     # AM-DVT hal resource file 


Suite Setup         Run Keywords
...                 Login to Fusion Via REST
...                 Verify Claimed EM     # Keyword not available
...                 Select Random Populated ICM Bay
...                 Get Initial ICM Configuration
Suite Teardown      Run Keywords
#...                 Restore Initial ICM Configuration
...                 Login to Fusion Via REST
...                 Logout of Fusion Via REST


*** Test Cases ***
Verify ICM Properties
    [Documentation]    Test script to Validate ICM Properties
    [Tags]    DCS    HW    HAL    Automated
    ${Model}=    Set Variable If    '${DCS}'=='${TRUE}'    ${DCS Model}    ${HW Model}
    Run All Transitions Model    model=${Model}

#-----------------------------------------------------------------------
# NEGATIVE TEST CASES
#-----------------------------------------------------------------------
Set IPv4 DNS to a invalid IP address
    [Documentation]    Test script to Validate Invalid ICM IPv4 DNS Address
    [Tags]    DCS    HW    HAL    Automated
    # Build Payload
    # Should be a valid IP address
    # "DNS": [ { "IpAddress": "0.0.0.0" } ],
    ${IpAddress}=    Create Dictionary    IpAddress=256.255.255.255
    ${DNS}=    Create List    ${IpAddress}
    # Set ICM Configuration
    ${fields}=    Create Dictionary    DNS=${DNS}
    # On HW, Expect Error message when attempting to Set DNS IpAddress value to an invalid value
    Run Keyword If    '${DCS}'=='${False}'
    ...    Run Keyword and Expect Error    ${error_icm_message}
    ...    Set and Update Expected Configuration    IPv4NetworkConfiguration    ${fields}
    # On DCS, no error message is expected
    Run Keyword If    '${DCS}'=='${True}'
    ...    Set and Update Expected Configuration    IPv4NetworkConfiguration    ${fields}

Set IPv4 NTP to a invalid IP address
    [Documentation]    Test script to Validate Invalid ICM IPv4 NTP Address
    [Tags]    DCS    HW    HAL    Automated
    # Build Payload
    # Should be a valid IP address
    # "DNS": [ { "IpAddress": "0.0.0.0" } ],
    ${IpAddress}=    Create Dictionary    IpAddress=256.255.255.255
    ${NTP}=    Create List    ${IpAddress}
    # Set ICM Configuration
    ${fields}=    Create Dictionary    NTP=${NTP}
    # On HW, Expect Error message when attempting to Set NTP IpAddress value to an invalid value
    Run Keyword If    '${DCS}'=='${False}'
    ...    Run Keyword and Expect Error    ${error_icm_message}
    ...    Set and Update Expected Configuration    IPv4NetworkConfiguration    ${fields}
    # On DCS, no error message is expected
    Run Keyword If    '${DCS}'=='${True}'
    ...    Set and Update Expected Configuration    IPv4NetworkConfiguration    ${fields}

Get ICM Configuration for Empty Bay
    [Documentation]    Test script to Validate getting ICM Configuration for empty bay
    [Tags]    DCS    HW    HAL    Automated
    ${Bay}=    Select Random Empty Interconnect Bay
    Log To Console    \nTesting on Interconnect in Bay ${Bay}
    # Error message is received due to the empty response when attempting to get information from an empty bay
    Run Keyword and Expect Error    ${error_icm_message}
    ...    Get ICM Configuration    ${Bay}

Get ICM Configuration for Invalid Bay
    [Documentation]    Test script to Validate getting ICM Configuration for invalid bay
    [Tags]    DCS    HW    HAL    Automated
    Log To Console    \nTesting on Interconnect in Bay ${invalid_icm_bay}
    # Error message is received due to the empty response when attempting to get information from an invalid bay
    Run Keyword and Expect Error    ${error_icm_message}
    ...    Get ICM Configuration    ${invalid_icm_bay}

Set ICM Configuration for Empty Bay
    [Documentation]    Test script to Validate setting ICM Configuration for empty bay
    [Tags]    DCS    HW    HAL    Automated
    ${Bay}=    Select Random Empty Interconnect Bay
    Log To Console    \nTesting on Interconnect in Bay ${Bay}
    # Build Payload
    ${fields}=    Create Dictionary    DHCP=${True}
    ${Properties}=    Create Dictionary    IPv4NetworkConfiguration=${fields}
    # Error message is received due to the empty response when attempting to set information on an empty bay
    Run Keyword and Expect Error    ${error_icm_message}
    ...    Set ICM Configuration    ${Bay}    ${Properties}

Set ICM Configuration for Invalid Bay
    [Documentation]    Test script to Validate setting ICM Configuration for invalid bay
    [Tags]    DCS    HW    HAL    Automated
    Log To Console    \nTesting on Interconnect in Bay ${invalid_icm_bay}
    # Build Payload
    ${fields}=    Create Dictionary    DHCP=${True}
    ${Properties}=    Create Dictionary    IPv4NetworkConfiguration=${fields}
    # Error message is received due to the empty response when attempting to set information on an invalid bay
    Run Keyword and Expect Error    ${error_icm_message}
    ...    Set ICM Configuration    ${invalid_icm_bay}    ${Properties}

Set ICM Configuration with an Invalid Property
    [Documentation]    Test script to Validate setting ICM Configuration with invalid property
    [Tags]    DCS    HW    HAL    Automated
    ${Bay}=    Select Random Empty Interconnect Bay
    Log To Console    \nTesting on Interconnect in Bay ${invalid_icm_bay}
    # Build Payload
    ${fields}=    Create Dictionary    fakeField=${True}
    ${Properties}=    Create Dictionary    IPv4NetworkConfiguration=${fields}
    # Error message is received due to the empty response when attempting to set information on an invalid bay
    Run Keyword and Expect Error    ${error_icm_message}
    ...    Set ICM Configuration    ${invalid_icm_bay}    ${Properties}
