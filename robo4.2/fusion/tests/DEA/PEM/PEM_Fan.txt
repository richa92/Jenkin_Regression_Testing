*** Settings ***
Documentation     Testing ALL PEM COMMANDS that are applicable to Fan Module
...    = Usage =
...    | pybot | -L DEBUG | APPLIANCE_IP:<fusion_ipv4> | PEM_Fan.txt | 
...    = Variables =
...    | GIT_REPO_ROOT | Required; Repo root path if NOT defined in environment variable|
...    | RESOURCE_ROOT | Optional: Resource files path if NOT defined in environment variable|
...    | VARIABLE_ROOT | Optional: Variable files path if NOT defined in environment variable|
...    | APPLIANCE IP     | Required; OneView IP address under test | 

Library           RoboGalaxyLibrary            # DVTs Robot Framework extensions
Library           FusionLibrary                # DVTs Robot Framework extensions
Library           OperatingSystem
Library           MgmtFWLibrary
Library           loginPEMClass 
Library           json
Library           String
Library           Collections

Variables         ${GIT_REPO_ROOT}${VARIABLE_ROOT}/dea_variables.py
Variables         ${GIT_REPO_ROOT}${VARIABLE_ROOT}/data_variables.py
Variables         ${GIT_REPO_ROOT}/mgmtfw/tests/thunderbird/variables/tBird_variables.py
Resource          ${GIT_REPO_ROOT}${RESOURCE_ROOT}/resource_all.txt
Resource          ${GIT_REPO_ROOT}/fusion/tests/DEA/HAL/resources/hal_webapp.txt

Suite Setup       Run Keywords    Login to Fusion via REST    AND    Login to Fusion via SSH    AND    Login to EM And Create Session
Suite Teardown    Run Keywords    Logout of Fusion Via REST

*** Variables ***
${bad_bay_num}    99

*** Test Cases ***
TC 01: PEM Fan Bay Efuse: Test PEM Action Fan Bay Efuse Reset On Populated Fan Bay
    [Documentation]    Validating the PEM Action To EfuseFanBay-Reset on listed Fan Bay on the Enclosure
    [Tags]    Automated    Fan    DEA    API    PEM    OV3.10
    # Get PEM's token and establish session and fusion uuid
    ${auth}=   Get Trusted PEM Token
    set_header_to_pem_token    ${APPLIANCE_IP}    ${pem_cred}    ${auth}
    ${fusion_uuid}=    Check For Single Enclosure and Get UUID
    ${enc_serial_number}=    Fetch From Right    ${fusion_uuid}    000

    # Get the current fan list from the enclosure
    ${fan_list}    ${fan_count}=    Get Current Fan List
    Should Not Be Empty    ${fan_list}    msg=should contain Fan inserted in the enclosure
    :FOR    ${bay_number}    IN    @{fan_list}
    \    ${fan_bay}=    Convert To Integer    ${bay_number}
    # perform the PEM action Efuse Fan Bay Reset and validate the fan bay presence after efuse reset
    \    ${response}=    PEM Action To Efuse Fan Bay    ${auth}    ${APPLIANCE_IP}    ${enc_serial_number}    ${fan_bay}    EFuseReset
    \    Should Be Equal As Strings    ${response["CallStatus"]}    SUCCESS
    \    Sleep    5
    \    ${fan_list_after_efuse_reset}    ${fan_count}=    Get Current Fan List
    \    Should Not Contain    ${fan_list_after_efuse_reset}     ${fan_bay}
    \    Sleep    25
    \    ${fan_list_after_efuse_reset}    ${fan_count}=    Get Current Fan List
    \    Should Contain    ${fan_list_after_efuse_reset}    ${fan_bay}

TC 02:PEM Fan Bay Efuse: Test PEM Action Fan Bay Efuse On/Off On Populated Fan Bay
    [Documentation]    Validating the PEM Action To EfuseFanBayOn/Off on listed Fan Bay on the Enclosure
    [Tags]    Automated    Fan    DEA    API    PEM    OV3.10
    # Get PEM's token and establish session
    ${auth}=   Get Trusted PEM Token
    set_header_to_pem_token    ${APPLIANCE_IP}    ${pem_cred}    ${auth}
    ${fusion_uuid}=    Check For Single Enclosure and Get UUID
    ${enc_serial_number}=    Fetch From Right    ${fusion_uuid}    000

    # Get fan list from the enclosure
    ${fan_list}    ${fan_count}=    Get Current Fan List
    Should Not Be Empty    ${fan_list}    msg=should contain Fan inserted in the enclosure
    # Execute PEM Action Fan Bay EFuse ON and OFF on each populated Fan bay and validate bay presence after Efuse
    :FOR    ${bay_number}    IN    @{fan_list}
    \    ${fan_bay}=    Convert To Integer    ${bay_number}
    \    ${response}=    PEM Action To Efuse Fan Bay    ${auth}    ${APPLIANCE_IP}    ${enc_serial_number}    ${fan_bay}    EFuseOn
    \    Should Be Equal As Strings    ${response["CallStatus"]}    SUCCESS
    \    Sleep    10
    # Once the above mentioned QUIX in Tags is resolved this test case will be enhanced
    \    ${response}=    PEM Action To Efuse Fan Bay    ${auth}    ${APPLIANCE_IP}    ${enc_serial_number}    ${fan_bay}    EFuseOff
    \    Should Be Equal As Strings    ${response["CallStatus"]}    SUCCESS
    \    Sleep    30
    \    ${fan_list_after_efuse_off}    ${fan_count}=    Get Current Fan List
    \    Should Contain    ${fan_list_after_efuse_off}    ${fan_bay}
    [Teardown]    Run Keyword If Test Failed    Run Keywords    PEM Action To Efuse Fan Bay    ${auth}    ${APPLIANCE_IP}    ${enc_serial_number}    ${fan_bay}    EFuseOff    AND    Sleep    30

TC 03: PEM Fan Bay Efuse Negative: Test PEM Action Fan Bay Efuse Reset/On/Off On Invalid Fan Bay
    [Documentation]    Validate PEM Action To EfuseFanBay Reset On Invalid Fan Bay
    [Tags]    Automated    Fan    DEA    API    PEM    OV3.10
    # Get PEM's token and establish session
    ${auth}=   Get Trusted PEM Token
    set_header_to_pem_token    ${APPLIANCE_IP}    ${pem_cred}    ${auth}
    ${fusion_uuid}=    Check For Single Enclosure and Get UUID
    ${enc_serial_number}=    Fetch From Right    ${fusion_uuid}    000

    # Verify PEM Action Fan Bay EFuse Reset on Invalid Bay and validate for ERROR status
    ${response}=    PEM Action To Efuse Fan Bay    ${auth}    ${APPLIANCE_IP}    ${enc_serial_number}    ${bad_bay_num}    EFuseReset
    Should Be Equal As Strings    ${response["CallStatus"]}    ERROR

    # Verify PEM Action Fan Bay EFuseOn on Invalid Bay and validate for ERROR status
    ${response}=    PEM Action To Efuse Fan Bay    ${auth}    ${APPLIANCE_IP}    ${enc_serial_number}    ${bad_bay_num}    EFuseOn
    Should Be Equal As Strings    ${response["CallStatus"]}    ERROR

    # Verify PEM Action Fan Bay EFuseOff on Invalid Bay and validate for ERROR status
    ${response}=    PEM Action To Efuse Fan Bay    ${auth}    ${APPLIANCE_IP}    ${enc_serial_number}    ${bad_bay_num}    EFuseOff
    Should Be Equal As Strings    ${response["CallStatus"]}    ERROR

TC 04: PEM Fan FRU Read: Test PEM Action Fan FRU Read On Fan Bays
    [Documentation]    Validate PEM Action FanFruRead On Fan Bays
    [Tags]    Automated    Fan    DEA    API    PEM    OV3.10
    # Get PEM's token and establish session
    ${auth}=   Get Trusted PEM Token
    set_header_to_pem_token    ${APPLIANCE_IP}    ${pem_cred}    ${auth}
    ${fusion_uuid}=    Check For Single Enclosure and Get UUID
    ${enc_serial_number}=    Fetch From Right    ${fusion_uuid}    000

    # Get fan list from the enclosure
    ${fan_list}    ${fan_count}=    Get Current Fan List
    Should Not Be Empty    ${fan_list}    msg=should contain fans inserted in the enclosure
    # Get Fan information from Fusion
    ${resp}=   Fusion Api Get Enclosures
    ${fusion_fan_info}=    Get From Dictionary    ${resp['members'][0]}    fanBays
    :FOR    ${bay_number}    IN    @{fan_list}
    \    ${current_fan_bay}=    Convert To Integer    ${bay_number}
    # Execute PEM Action FanFruRead and validate PEM action reading Fan's FRU
    \    ${response}=    PEM Action To Get Fan FRU    ${auth}    ${APPLIANCE_IP}    ${enc_serial_number}    ${current_fan_bay}
    \    Should Be Equal As Strings    ${response["CallStatus"]}    SUCCESS
    \    ${graphics_dict}=    Set Variable    ${response["OperationResult"]["Parsed"]["GraphicsInfo"]}
    \    ${source_from_pem}=    Get From Dictionary    ${graphics_dict['Image'][0]}    Source
    \    Should Be Equal As Strings    ${source_from_pem}    FRU
    # Validate matching Serial number for Fan Bay number under test
    \    ${fan_serial_number_pem}=    Set Variable    ${response["OperationResult"]["Parsed"]["ProductInfo"]["SerialNumber"]}
    \    ${fusion_fan_serial_num}=    Get From Dictionary    ${fusion_fan_info[${current_fan_bay}-1]}    serialNumber
    \    Should Be Equal As Strings    ${fan_serial_number_pem}    ${fusion_fan_serial_num}

TC 05: PEM Fan FRU Read Negative: Test PEM Action Fan FRU Read On Invalid Fan Bay
    [Documentation]    Validate PEM Action FanFRURead On Invalid Fan Bay
    [Tags]    Automated    Fan    DEA    API    PEM    OV3.10
    # Get PEM's token and establish session
    ${auth}=   Get Trusted PEM Token
    set_header_to_pem_token    ${APPLIANCE_IP}    ${pem_cred}    ${auth}
    ${fusion_uuid}=    Check For Single Enclosure and Get UUID
    ${enc_serial_number}=    Fetch From Right    ${fusion_uuid}    000

    # Error message is received due to the empty response when attempting to get information from an invalid bay
    ${response}=    PEM Action To Get Fan FRU    ${auth}    ${APPLIANCE_IP}    ${enc_serial_number}    ${bad_bay_num}
    Should Be Equal As Strings    ${response["CallStatus"]}    ERROR
