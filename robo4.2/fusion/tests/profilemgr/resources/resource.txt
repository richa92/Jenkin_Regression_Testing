*** Settings ***
Library             RoboGalaxyLibrary
Library             FusionLibrary
Library             SSHLibrary
Library             OperatingSystem
Library             XML
Library             String
Variables           ../../../FusionLibrary/ui/servers/serverprofiles_elements.py

*** Variables ***
${DataFile}         ../data/test_data.xml
${SkipTest}         False
${FailedTest}       ""


*** Keywords ***

Load Test Data and Open Browser
    Set Log Level    TRACE
    Load Test Data  ${DataFile}
    Log Variables
    Log To Console      ${TestData.envconfigs[0].browser}
    Open Browser  ${TestData.envconfigs[0].appliance_ip}    ${TestData.envconfigs[0].browser}
    Run Keyword If  "${TestData.envconfigs[0].browser}" == "ie"   Go To  javascript:document.getElementById('overridelink').click()
    Set Selenium Speed  ${TestData.envconfigs[0].seleniumSpeed}

Logout and Close All Browsers
    Fusion UI Logout of Appliance
    Close All Browsers

Log into Fusion appliance as Administrator
    ${user} =  Get Data By Property  ${TestData.users}  name  Administrator
    ${status}=           Fusion UI Login to Appliance   ${user[0].name}
    Run Keyword If      '${status}' == 'False'       Fail                FAIL: Error logging in.

Login thru REST API
    Load Test Data  ${DataFile}
    ${user} =  Get Data By Property  ${TestData.users}  name  Administrator
    ${admin_credentials} =      Create Dictionary   userName=${user[0].name}
    ...                                             password=${user[0].password}
    ${address} =  Fetch From Right     ${TestData.envconfigs[0].appliance_ip}      /
    Fusion Api Login Appliance    host=${address}    creds=${admin_credentials}

Delete All Server Profiles
    Login thru REST API
    ${profiles} =   Fusion Api Get Server Profiles
    :FOR    ${profile}  IN  @{profiles['members']}
    \       Log To Console     Removing Server Profile: ${profile['name']}
    \       ${resp} =   Fusion Api Delete Server Profile        uri=${profile['uri']}
    \       Fusion Api Wait For Task To Complete                uri=${resp['uri']}      retries=${80}

Delete All Storage Volumes
    Login thru REST API
    ${volumes} =   Fusion Api Get Storage Volumes
    :FOR    ${volume}  IN  @{volumes['members']}
    \       Log To Console     Removing Storage Volume: ${volume['name']}
    \       Fusion Api Delete Storage Volume        uri=${volume['uri']}

Run Suite Setup
    Set Global Variable       ${SkipTest}       False
    Delete All Server Profiles
    Load Test Data and Open Browser
    Log into Fusion appliance as Administrator

Skip Suite If Failure
    [Arguments]       ${FailedTestName}
    Run Keyword If Test Failed       Set Global Variable       ${SkipTest}       True
    Run Keyword If Test Failed       Set Global Variable       ${FailedTest}     ${FailedTestName}

Validate Skip Conditions
    Run Keyword If       "${SkipTest}" == "True"       Fail       SKIPPED due to failure on test case [${FailedTest}].

PM SAN Pre Test
    Load Test Data  ${DataFile}
    ${user} =  Get Data By Property  ${TestData.users}  name  Administrator
    ${admin_credentials} =      Create Dictionary   userName=${user[0].name}
    ...                                             password=${user[0].password}
    ${address} =  Fetch From Right     ${TestData.envconfigs[0].appliance_ip}      /
    ${status}=    Fusion Ui Profilemgr With San Pretest   ${TestData.sanvolumes}  ${address}  ${user[0].name}  ${user[0].password}
    Run Keyword If      '${status}' == 'False'       Fail                FAIL: Error creating SAN volumes
