*** Settings ***
Documentation        TC71 - E2E - On-Site Converged System Mng (System Edit, Add/Remove rack from Mantra) (F49)
Resource             ../resources/resources.txt
Suite Teardown       Logout and Close All Browsers
Suite Setup          PreTest Open Browser and perform Login
Library              XML

*** Variables ***
${SeleniumSpeed}    0.1
${ComplianceTask}    Refresh ConvergedSystem compliance results
${SystemProfileType}    ConvergedSystemCS700V2


*** Test Cases ***
Test 1: Remove each Rack and wait for compliance task to run
    # Getting the Number of Racks from the XML
    @{ListOfRacks}=    Get Elements    ${DataFile}    .//${SystemProfileType}/SystemProfile/Rack
    ${numberOfRacks}=    Get Length    ${ListOfRacks}
    ${RackVar}=        Set Variable    ${numberOfRacks}
    ${Contents} =   Read Converged System From XML    ${TestData.ConvergedSystemCS700V2}
    Set Suite Variable    ${numberOfRacks}
    Set Suite Variable    ${RackVar}

    # Remove each Rack based on the XML
    :FOR  ${INDEX}    IN RANGE   1    ${numberOfRacks} + 1
        \  ${RackName}=    XML.Get Element Attribute    ${DataFile}    name    .//${SystemProfileType}/SystemProfile/Rack[${RackVar}]
        \  ${RackVar}=    Evaluate    ${RackVar} - 1
        \  Remove Rack From System Profile    ${RackName}
        \  ${Contents} =   Subtrack Rack Configuration   ${RackName}    ${Contents}
        \  Wait for System Profile Task    ${ComplianceTask}
        \  Verify System Profile Contents    ${Contents}
Test 2: Verify if the System Profile Content is empty
    Verify if System Profile Content Section is Empty
    
Test 3: Add each Rack and wait for compliance task to run
	${Contents} =  Create Empty Object
	${FullConfiguration} =  Read Converged System From XML    ${TestData.ConvergedSystemCS700V2}

    # Add each existent rack found on XML file
    :FOR  ${INDEX}    IN RANGE   1    ${numberOfRacks} + 1
        \  ${RackName}=    XML.Get Element Attribute    ${DataFile}    name    .//${SystemProfileType}/SystemProfile/Rack[${INDEX}]
        \  ${Contents} =   add rack configuration   ${RackName}   ${Contents}   ${FullConfiguration}
        \  Add Rack To System Profile    ${RackName}
        \  Wait for System Profile Task    ${ComplianceTask}
        \  Verify System Profile Contents    ${Contents}

Test 4: Verify if System Profile has the expected number of racks
    Validate Element in System Profile Contents    Racks    ${numberOfRacks}
    