*** Settings ***
Documentation    DD Deploy OV onto CIM
...    = Usage =
...    pybot |-v ENCLOSURE:tesla |-v DD_URL:<Fusion ISO Image> Deploy_CIM_Fusion.txt
...    = Variables =
...    | ENCLOSURE | (required) Name of the enclosure to Deploy upon. (values from variables.py) |
...    | DD_URL | (optional) URL of Fusion ISO Image To Install (defaulted) |

Library           String
Library           Collections
Library           DateTime
Library           SSHLibrary
Library           robot.api.logger
Library           RoboGalaxyLibrary
Library           FusionLibrary
Variables         variables_tbird.py    ${ENCLOSURE}
Resource          ./resources/em_api.txt
Resource          ./resources/fusion_api.txt
Resource          ./resources/defaults.txt
Resource          ./resources/fusion_ssh.txt


*** Variables ***
${DD_URL}                      http://10.109.0.10/tbird/GARFS21/HPOneView-SSH-3.00.00-0254645-withsig_PASS126.zip
${Installation_Time}           90 minutes
${ADMIN_PASSWORD_POLL}         60 seconds
${ADMIN_PASSWORD_RETRIES}      40
${DUMP_FILE}                   fusion_dump.sdmp


*** Keywords ***
Get Firmware Versions
    Login to Fusion via SSH
    Get Fusion Version
    Get iLO Version
    # TODO: Issue warning if Intelligent Provisioning is not installed. (N/A)
    Get NIC Version

Verify Claimed Enclosure
    ${Command}=    Set Variable    /ci/bin/tbird/appliance-hal.sh list-enclosures
    ${stdout}    ${stderr}    ${rc}=    Execute Command    ${Command}    return_stderr=True    return_rc=True
    Should Be Equal As Integers    ${rc}    0    msg=Failed to get claimed enclosures.
    Should Not Be Equal As Strings    ${stdout}     ${empty}    msg=Failed to get an enclosure name returned
    Log To Console    Found Claimed Enclosure: ${stdout}    console=True

Initiate DD Image Update
    [Arguments]                 ${IP}=${MAINTANENCE_IP}      ${USERNAME}=${FUSION_SSH_USERNAME}
    ...                         ${PASSWORD}=${FUSION_SSH_PASSWORD}    ${PROMPT}=${FUSION_PROMPT}
    ...                         ${TIMEOUT}=${FUSION_TIMEOUT}    ${ALIAS}=Fusion_SSH
    Log Many                    ${IP}                   ${USERNAME}     ${PASSWORD}     ${PROMPT}   ${TIMEOUT}
    Set Default Configuration   prompt=${PROMPT}        timeout=${TIMEOUT}
    ${Id}=                      Open Connection         ${IP}    alias=${ALIAS}
    ${Output}=                  Login                   ${USERNAME}     ${PASSWORD}

    ${OV_PATH}    ${LATEST_OV_IMAGE}=    Split String From Right    ${DD_URL}    /    1
    Console    Modfying permissions on developer_usb_reimage.sh
    Execute Command    chmod a+x /ci/etc/usb-reimage/developer_usb_reimage.sh

    # Initiate USB update
    # Set Client Configuration    timeout=80 minutes
    ${Command}=    Set Variable    /ci/etc/usb-reimage/developer_usb_reimage.sh -u ${OV_PATH} -f ${LATEST_OV_IMAGE} -E
    Console    Executing Command '${Command}'.
    SSHLibrary.Write    ${Command}
    Console    developer_usb_reimage.sh script is running...

    # Wait for script messages
    Wait Until Keyword Succeeds    20 minutes    2 minutes
    ...    Read Until    Script succeeded
    Console    developer_usb_reimage.sh script is succeeded. Waiting for reboot...
    Run Keyword And Ignore Error
    ...    Wait Until Keyword Succeeds    16 minutes    2 minutes
    ...    Read Until    rebooting now
    Console    Rebooting CIM now.

Wait For Fusion Install and Startup
    [Documentation]    Sleep for ${Installation_Time}.
    ${DateTime}=    Get Current Date
    Log    Time: ${DateTime}    console=True
    Console    \nSleep for ${Installation_Time}.
    Sleep    ${Installation_Time}    reason=Waiting for installation, reboot and startup.

Assign Administrator Password
    ${Creds}    Create Dictionary    newPassword    ${Fusion_Password}
    ...                              oldPassword    ${Fusion_Factory_Password}
    ...                              userName       ${Fusion_Username}
    :FOR    ${INDEX}    IN RANGE    1    ${ADMIN_PASSWORD_RETRIES}
    \    ${Status}    ${Response}    Run Keyword and Ignore Error    Fusion Api Change Administrator Password    ${FUSION_IP}    ${Creds}
    \    Run Keyword If    '${Status}'=='PASS'
    ...    Log    Assigned Administrator password ${FUSION_PASSWORD}    console=True
    \    Return From Keyword If    '${Status}'=='PASS'
   # should be able to use status code, but that does not work, getting 500
   # ...    Pass Execution If    ${Response['status_code']}==200
    \    Console    \nIndex: ${INDEX}
    \    Sleep    ${ADMIN_PASSWORD_POLL}
    Fatal Error    msg=Error Assigning Administrator Password

Appliance Login
    ${Creds}    Create Dictionary    userName    ${Fusion_Username}    password    ${Fusion_Password}
    ${Response}    ${SessionID}    Fusion Api Login Appliance    ${FUSION_IP}    ${Creds}
    Run Keyword If    ${Response['status_code']}==200    Log    Successful first-time login    console=True
    Return From Keyword If    ${Response['status_code']}==200
    Fail         msg=Error Login Administrator first-time ${Response['message']}

Write Fusion Properties
    Run    echo \# %JOB_NAME% %DATE% %TIME% > C:\\Jenkins\\Fusion-CIM.properties
    Run    echo FUSION_IP=${FUSION_IP} >> C:\\Jenkins\\Fusion-CIM.properties
    Run    echo FUSION_URL=https://${FUSION_IP} >> C:\\Jenkins\\Fusion-CIM.properties
    Run    echo FUSION_VERSION=${Fusion_Version} >> C:\\Jenkins\\Fusion-CIM.properties

Generate Fusion Dump and Log After Network Initialization
    Login to Fusion Via SSH
    Run Keyword And Ignore Error
    ...  SSHLibrary.Get File    /ci/logs/ciDebug.*
    Logout of Fusion Via SSH
    Login to Fusion Via REST
    ${Status}  ${R}=   Run Keyword And Ignore Error
    ...  Create And Download Support Dump    ${DUMP_FILE}
    Run Keyword If   "${Status}" == "FAIL"
    ...  Log    AM92: Fusion Dump Failed        level=Warn

Stop cluster service on Standby CIM
    [Arguments]    ${IP}=${STANDBY_CIM}    ${USERNAME}=${EM_LOGIN}    ${PASSWORD}=${EM_PASSWORD}
    ...            ${PROMPT}=${FUSION_PROMPT}    ${TIMEOUT}=${FUSION_TIMEOUT}
    Log Many    ${IP}    ${USERNAME}    ${PASSWORD}    ${PROMPT}    ${TIMEOUT}
    Set Default Configuration    prompt=${PROMPT}    timeout=${TIMEOUT}
    ${Id}=    Open Connection    ${IP}
    ${Output}=    Login    ${USERNAME}    ${PASSWORD}
    ${Command} =    Set Variable    service cluster stop && service jetty-cluster stop
    ${stdout}    ${stderr}    ${rc}=    Execute Command    ${Command}    return_stderr=True    return_rc=True
    Log    ${stdout}
     Should Be Equal As Integers        ${rc}    0    msg=non-zero return code ${rc}

Stop cluster service on Active CIM
    [Arguments]    ${IP}=${ACTIVE_CIM}    ${USERNAME}=${EM_LOGIN}    ${PASSWORD}=${EM_PASSWORD}
    ...            ${PROMPT}=${FUSION_PROMPT}    ${TIMEOUT}=${FUSION_TIMEOUT}
    Log Many    ${IP}    ${USERNAME}    ${PASSWORD}    ${PROMPT}    ${TIMEOUT}
    Set Default Configuration    prompt=${PROMPT}    timeout=${TIMEOUT}
    ${Id}=    Open Connection    ${IP}
    ${Output}=    Login    ${USERNAME}    ${PASSWORD}
    ${Command} =    Set Variable    service cluster stop && service jetty-cluster stop
    ${stdout}    ${stderr}    ${rc}=    Execute Command    ${Command}    return_stderr=True    return_rc=True
    Log    ${stdout}
     Should Be Equal As Integers        ${rc}    0    msg=non-zero return code ${rc}
    Sleep    180
