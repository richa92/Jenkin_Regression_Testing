*** Settings ***
Documentation    Deploy Fusion ISO onto CIM using Atlas USB Key
...    = Usage =
...    pybot |-v ENCLOSURE:tesla |-v ISO_URL:<Fusion ISO Image> Deploy_CIM_Fusion.txt
...    = Variables =
...    | ENCLOSURE | (required) Name of the enclosure to Deploy upon. (values from variables.py) |
...    | ISO_URL | (optional) URL of Fusion ISO Image To Install (defaulted) |
...
...     --Prepare for Firmware Update [Get Firmware Versions and Get Enclosure Manager Credentials]
...     --Stop cluster service on Standby and Active CIM
...     --Reset all EM's in the ring
...     --Deploy Firmware Updates [Initiate DD Image Update,
...     --Wait For Fusion Install and Startup and Assign Administrator Password]

Resource          ../resources/resource.txt

*** Test Cases ***
Prepare For Firmware Update
    [Tags]    Pre-Deploy
    [Documentation]    Set Log level, get firmware version details
    ...    Get firmware version
    Log Variables
    Set Log Level   TRACE
    Console    \nDeploying from: ${ISO_URL} onto: ${ENCLOSURE}
    Get Firmware Versions

Setup Primary CIM USB Key
    [Tags]    Pre-Deploy
    [Documentation]    Before initiating Reimage required to download two node install script,
    ...                Download remote reimgae script and JSON file for FTS.
    ...                Remove reimage logs, remove old JSON file.
    Setup CIM USB Key Before Reimage    ${ACTIVE_CIM}    ${FUSION_SSH_USERNAME}
    ...    ${FUSION_SSH_PASSWORD}    ${PRIMARY_JSON_PATH}
    ...    ${REIMAGE_SCRIPT_PATH}    ${REMOTE_SCRIPT_PATH}

Setup Standby CIM USB Key
    [Tags]    Pre-Deploy
    [Documentation]    Before initiating Reimage required to download two node install script,
    ...                Download remote reimgae script and JSON file for FTS.
    ...                Remove last reimage logs,
    ...                Remove old JSON file.
    Setup CIM USB Key Before Reimage    ${STANDBY_CIM}    ${FUSION_SSH_USERNAME}
    ...    ${FUSION_SSH_PASSWORD}    ${STANDBY_JSON_PATH}
    ...    ${REIMAGE_SCRIPT_PATH}    ${REMOTE_SCRIPT_PATH}

Deploy Firmware Update Two Node HA
    [Tags]    Pre-Deploy
    [Documentation]    Initiate DD Reimage script from primary CIM
    ...                Wait for primary appliance to reboot
    ...                Start 90 minutes sleep
    Initiate DD Reimage On Two Node HA       ${ACTIVE_CIM}
    Wait For Fusion Install and Startup
    Assign Administrator Password

Setup Fusion after Deploy
    [Tags]    Post-Deploy
    [Documentation]    Login to appliance, verify enclosures are claimed
    ...    Get firmware version
    Appliance Login
    Resource.Set Fusion Version Metadata
    # Write Fusion Properties
    Get Firmware Versions
    Run Keyword and Ignore Error
    ...    Verify Claimed Enclosure
