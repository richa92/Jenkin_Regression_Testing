*** Settings ***
Library                         FusionLibrary
Library                         RoboGalaxyLibrary
Library                         OperatingSystem
Library                         BuiltIn
Library                         Collections
Library                         XML
Library                         String
Library                         json
Library                         Dialogs
Library                         SSHLibrary

Resource                        ../../../../Resources/api/fusion_api_resource.txt
Variables                       data_variables.py
*** Variables ***
#${X-API-VERSION}                500
${APPLIANCE_IP}                 10.47.4.124
${one}                          1
${two}                          2
${three}                        3
${DATA}                         data_variables.py
${VERBOSE}                      False
${VERIFY}                       True
${spp}                          C:\\RoboGalaxy\\fusion\\tests\\epic\\MAT\\Add-Spp.ps1
${dhcp}                         C:\\RoboGalaxy\\fusion\\tests\\epic\\MAT\\Get-DHCPAddress.ps1
${del_spp}                      C:\\RoboGalaxy\\fusion\\tests\\epic\\MAT\\Remove-Spp.ps1

*** Keywords ***
MAT Suite Setup
    [Arguments]     ${credentials}
    [Documentation]     Suite Setup to ping appliance and Login to OV
    Set Log Level   TRACE
    Appliance is pingable   ${APPLIANCE_IP}
    Fusion Api Login Appliance  ${appliance_ip}     ${credentials}

MAT Suite Teardown
    [Documentation]     Suite Teardown to Logout from OV
    Fusion Api Logout Appliance

Upload SPP to Fusion
    [Arguments]     ${host}     ${user}     ${password}     ${spppath}
    [Documentation]     Upload SPP to OneView
    ${Output}=   Run and return RC and output    powershell.exe -ExecutionPolicy Unrestricted -file ${spp} ${host} ${user} ${password} ${spppath}

Delete SPP From Fusion
    [Arguments]     ${host}     ${user}     ${password}     ${spp}
    [Documentation]     Delete SPP from OneView
    ${Output}=   Run and return RC and output    powershell.exe -ExecutionPolicy Unrestricted ${del_spp} ${host} ${user} ${password} ${spp}

Get Ethernet Connections IPs from Server Profiles and Ping
    [Arguments]     ${profile}
    [Documentation]     Read connection IP from Server Profiles and Ping
    :FOR    ${connection}  IN  @{profile['connections']}
    \   Run Keyword Unless  '${connection['functionType']}' == 'Ethernet'     Continue For Loop
    \   ${uri} =    Get From Dictionary     ${connection}  networkUri
    \   ${net} =    Fusion Api Get Ethernet Networks    ${uri}
    \   ${vlan} =    Get From Dictionary     ${net}  vlanId
    \   ${dhcp_detail} =   Get DHCP Server     ${vlan}
    \   Run Keyword If  ${dhcp_detail} == []   Continue For Loop
    \   ${mac} =    Get From Dictionary     ${connection}  mac
    \   ${ip} =     Get IP From DHCP    ${mac}  ${dhcp_detail[0]}  ${dhcp_detail[1]}   ${dhcp_detail[2]}    ${dhcp_detail[3]}
    \   ${RC}   ${output} =     Run and return RC and Output    ping ${ip}
    \   Run Keyword and Continue on Failure     Should be equal     ${RC}   ${0}     Host ${ip} unreachable. Reason:\n${output}

Get DHCP Server
    [Arguments]     ${vlan}
    [Documentation]     Get DHCP Server details using Vlan
    ${dhcp_details} =   Create List
    :FOR    ${dhcpserver}   IN  @{dhcpservers}
    \   Run Keyword Unless  '${dhcpserver['vlanid']}' == '${vlan}'      Continue For Loop
    \   ${dhcpserver_ip} =  Get From Dictionary     ${dhcpserver}   ip
    \   Append To List  ${dhcp_details}     ${dhcpserver_ip}
    \   ${dhcpserver_scope} =  Get From Dictionary     ${dhcpserver}   scope
    \   Append To List  ${dhcp_details}     ${dhcpserver_scope}
    \   ${dhcpserver_user} =  Get From Dictionary     ${dhcpserver}   username
    \   Append To List  ${dhcp_details}     ${dhcpserver_user}
    \   ${dhcpserver_password} =  Get From Dictionary     ${dhcpserver}   password
    \   Append To List  ${dhcp_details}     ${dhcpserver_password}
    [Return]    ${dhcp_details}

Get IP From DHCP
    [Arguments]     ${mac}  ${dhcp_ip}  ${dhcp_scope}   ${dhcp_user}    ${dhcp_password}
    [Documentation]     Get IP Address from DHCP Server
    ${Output}=   Run    powershell.exe -ExecutionPolicy Unrestricted -file ${dhcp} ${mac} ${dhcp_ip} ${dhcp_scope} ${dhcp_user} ${dhcp_password}
    [Return]    ${Output}
