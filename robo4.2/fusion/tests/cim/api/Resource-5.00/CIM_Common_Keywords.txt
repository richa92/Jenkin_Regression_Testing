*** Settings ***
Library           RoboGalaxyLibrary
Library           FusionLibrary
Library           String
Library           Collections
Library           XML
Library           json

*** Keywords ***

Get Appliance Host Name
    [Documentation]    Get Appliance Host Name
    ...                Returns the appliance host name from appliance network interface
    ${response}=    Fusion Api Get Appliance Interfaces
    ${applianceHostName}=    Get From Dictionary    ${response["applianceNetworks"][0]}    hostname
    Log    Appliance hostname: ${applianceHostName}    console=True
    [Return]   ${applianceHostName}

Get Appliance IP Address
    [Documentation]    Get Appliance IP Address
    ...                Returns the appliance IP address from appliance network interface
    ${response}=    Fusion Api Get Appliance Interfaces
    ${applianceIPAddress}=  Get From Dictionary   ${response["applianceNetworks"][0]}  app1Ipv4Addr
    Log    Appliance IP address: ${applianceIPAddress}    console=True
    [Return]    ${applianceIPAddress}

Get Appliance Mac Address
    [Documentation]    Get Appliance Mac Address
    ...                Returns MAC address associated with the Appliance network interface
    Get sessionID    ${admin_credentials}    ${Appliance_IP}
    ${response}=    Fusion Api Get Appliance Interfaces
    ${macAddress}=    Get From Dictionary    ${response["applianceNetworks"][0]}    macAddress
    Log    ${macAddress}    console=True
    [Return]    ${macAddress}

Verify Task Details
    [Documentation]    This keyword verifies the task details for given task URI
    ...                Verification is done for task state, status, owner fields
    [Arguments]    ${taskURI}    ${expectedOwner}    ${expectedState}
    ${response} =    Get request    ${TaskURI}    ${X-API-VERSION}
    Should be Equal    ${response.status_code}    ${200}    msg=Operation failed
    ${resp}    Evaluate    json.loads('''${response.text}''')    json
    ${taskOwner} =    Get From Dictionary    ${resp}    owner
    ${taskState} =    Get From Dictionary    ${resp}    taskState
    Should Be Equal    ${taskOwner}    ${expectedOwner}    msg=Current task owner not as expected
    Should Be Equal    ${taskState}    ${expectedState}    msg=Current task State not as expected
