*** Settings ***
Documentation        Download BackUp
Resource            ../Resource/CIM_CommonResource.txt
Resource            ../Resource/CIM_Activity.txt
Resource            ../Resource/CIM_Settings.txt
Test Setup           Load Test Data and Open Browser
Test Teardown        Logout and close all browsers

*** Variables ***
${searchtext}                   A backup download was initiated
${activityname}                 Started downloading backup
${resource}                     Backup
${activitystate}                Completed
${activityowner}                Administrator

*** Test Cases ***
As an Administrator I want to Download Backup
    [Documentation]    Create and download backup as administrator
    Log into Fusion appliance as Administrator
    ${bkpfile} =    Create BackUp
    Create Folder If not Exists    ${DownloadPath}
    wait for element and click    ${FusionSettingsPage.ID_SETTINGS_LABEL}
    Fusion UI Download Backup    ${DownloadPath}

    Log    Download Backup is completed and check for BackUp File in tmp folder

    ${bkpfiles} =    OperatingSystem.List Files In Directory    ${DownloadPath}    *.bkp
    ${downloadedbkpfile} =    Get From List    ${bkpfiles}    0
    Log    ${downloadedbkpfile}
    Should Be Equal As Strings    ${bkpfile.strip()}    ${downloadedbkpfile.strip()}    msg=Created ${bkpfile} is not same as downloaded ${downloadedbkpfile}

    #Verify Download Backup activity in Activity Page
    Verify Activity Existence in Activity Page    ${activityname}    ${resource}    ${activitystate}    ${activityowner}

    #Verify entry in Auditlog file
    Download and verify entry in Audit log file    ${DownloadPath}    ${searchtext}
