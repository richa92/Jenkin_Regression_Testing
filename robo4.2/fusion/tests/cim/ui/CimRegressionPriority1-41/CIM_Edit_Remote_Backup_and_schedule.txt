*** Settings ***
Documentation        Edit Backup Settings and Schedule should be updated in CIM_TestData.xml
Resource            ../Resource/CIM_CommonResource.txt
Resource            ../Resource/CIM_Activity.txt
Resource            ../Resource-41/CIM_Settings.txt
Resource            ../Resource/CIM_Settings.txt
Library             String
Library             DateTime
Test Setup          Load Test Data and Open Browser
Test Teardown       Logout and close all browsers

*** Variables ***
${searchtext}        Edit backup settings operation completed successfully
${activityname}      Edit backup settings
${resource}          Backup
${activitystate}     Completed
${activityowner}     Administrator

*** Test Cases ***
As an Administrator I want to Edit Backup Settings And Schedule
    [Documentation]    Edit remote backup Settings and Schedule
    Log into Fusion appliance as Administrator

    #Navigate to Edit Backup Settings page
    Navigate to Edit Backup Settings page

    #Edit Remote Backup Location
    Edit Remote Backup Location Updated    ${TestData.RemoteBackpupSettings[0].protocol}
    ...    ${TestData.RemoteBackpupSettings[0].ipaddress}
    ...    ${TestData.RemoteBackpupSettings[0].backupdirectory}
    ...    ${TestData.RemoteBackpupSettings[0].username}
    ...    ${TestData.RemoteBackpupSettings[0].password}

    #Edit Schedule
    Select frequency Updated    ${TestData.RemoteBackpupSettings[0].schedule}
    ...    ${TestData.RemoteBackpupSettings[0].time}
    ...    ${TestData.RemoteBackpupSettings[0].day}

    #Click OK button
    wait for element and click    ${FusionSettingsPage.ID_BACKUP_EDIT_OK_BUTTON}
    Click Element    ${FusionSettingsPage.ID_BACKUP_EDIT_SSH_KEY_VERIFY_CHECKBOX}
    Click Element    ${FusionSettingsPage.ID_BACKUP_EDIT_SSH_OK_BUTTON}

    #Verify Notification bar
    Verify notification bar    ok    ${activityname}    ${activitystate}    30

    #Verify Backup page
    Verify Remote Location In Backup Page Updated    ${TestData.RemoteBackpupSettings[0].protocol}
    ...    ${TestData.RemoteBackpupSettings[0].ipaddress}
    ...    ${TestData.RemoteBackpupSettings[0].backupdirectory}
    ...    ${TestData.RemoteBackpupSettings[0].username}
    Verify Schedule In Backup Page    ${TestData.RemoteBackpupSettings[0].schedule}
    ...    ${TestData.RemoteBackpupSettings[0].time}
    ...    ${TestData.RemoteBackpupSettings[0].day}

    #Verify Activity page
    Verify Activity Existence in Activity Page    ${activityname}    ${resource}    ${activitystate}    ${activityowner}

    #Verify entry in auditlog file.
    Download and verify entry in Audit log file    ${DownloadPath}    ${searchtext}
