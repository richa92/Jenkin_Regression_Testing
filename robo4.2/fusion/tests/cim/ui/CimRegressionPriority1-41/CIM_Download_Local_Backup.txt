*** Settings ***
Documentation      Download backup on my computer
Resource           ../Resource/CIM_CommonResource.txt
Resource           ../Resource/CIM_Activity.txt
Resource           ../Resource/CIM_Settings.txt
Test Setup         Load Test Data and Open Browser
Test Teardown      Logout and close all browsers
Library            OperatingSystem
Library            String

*** Variables ***
${searchtext}          A backup download was initiated
${activityname}        Started downloading backup
${resource}            Backup
${activitystate}       Completed
${activityowner}       Administrator
${downloadlocation}    my computer

*** Test Cases ***
As an Administrator I want to Download backup on my computer
    [Documentation]    Download Backup on my computer
    Log into Fusion appliance as Administrator

    #Check if Remote backup server is configured
    Navigate to    Settings    Backup
    ${present} =    Run Keyword And Return Status    Element Should Be Visible    //*[@data-fs-backup-remote-show-when='RemoteNotSet NotEnabled']
    Run Keyword If    ${present}    Fail    Remote backup is not set, please set it before executing the testcase

    #Create Download folder
    Create Folder If not Exists    ${DownloadPath}

    #Get Backup ID to be download
    ${backupid} =    Get Backup ID

    #Download Backup on my computer
    Select action    Download backup
    wait for element and click    //input[@type='radio'][./following-sibling::*[contains(., '${downloadlocation}')]]
    Click Element    ${FusionSettingsPage.ID_BACKUP_DOWNLOAD_OK}

    #Verify Notification bar
    Verify notification bar    ok    ${activityname}    ${activitystate}    20

    #Verify Activity page
    Verify Activity Existence in Activity Page    ${activityname}    ${resource}    ${activitystate}    ${activityowner}

    #Verify Backup is downloaded in my computer
    ${DownloadBkpPath} =    Replace String    ${DownloadPath}    /    \\
    ${downloadbkpfile} =    Catenate    SEPARATOR=\\    ${DownloadBkpPath}    ${backupid}
    OperatingSystem.Wait Until Created    ${downloadbkpfile}    30

    #Verify entry in auditlog file.
    Download and verify entry in Audit log file    ${DownloadPath}    ${searchtext}
