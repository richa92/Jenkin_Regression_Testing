*** Settings ***

Resource            ../Resource/CIM_CommonResource.txt
Resource            ../Resource/CIM_Activity.txt
Library             FusionLibrary
Library             XML
Variables         ../../../../FusionLibrary/ui/business_logic/general/base_elements.py
Variables         ../../../../FusionLibrary/ui/business_logic/general/activity_elements.py
Variables         ../../../../FusionLibrary/ui/business_logic/settings/scopes_elements.py
Force Tags          Buildup
Suite Setup         Load Test Data and Open Browser
Suite Teardown      Logout and Close All Browsers

*** Variables ***

${count}            1
${warning}          Scope 3 cannot be deleted because it is used by an email notification filter.
${message}          Deleting Scope 3 will automatically unassign resources from the scope.

*** Test Cases ***


#################################Scenario############################################
Delete Scope Assigned To Alert Filter
    Log into Fusion appliance as Administrator
    ${scope} =    Get data by property    ${TestData.empty_scopes}    name    Scope 3
    ${alert} =    Get data by property    ${TestData.delete_alertFilter}    name    Alert3
    Fusion UI Create Scope    ${scope[0]}
    wait for element and click    ${GeneralScopesElements.ID_BTN_SETTING_SCOPE}
    Fusion UI Create Alert Filter With Advanced Criteria   @{TestData.alertFilter_with_advanced_criteria}
    Navigate to delete scope    ${scope[0].name}

    Validate Delete Message when scope is assigned to filter

    wait for element and click    ${GeneralScopesElements.ID_BTN_SETTING_SCOPE}
    Fusion UI Edit ALert Filter   @{TestData.edit_filter_remove_resource}
    Navigate to delete scope    ${scope[0].name}
    Validate Delete Message when scope is removed from filter
    wait for element and click    ${GeneralScopesElements.ID_BTN_SETTING_SCOPE}
    Fusion UI Delete Alert Filter    ${alert[0]}


*** Keywords ***

Navigate to delete scope
    [Arguments]    ${name}
    wait for element and click    xpath=.//*[@id='hp-settings-page']/nav/div[@class='hp-page-label hp-preserve']/h1/a
    wait for element and click    ${GeneralScopesElements.ID_LINK_SCOPES}
    wait for element and click    ${VerifyScopesElements.ID_VERIFY_DELETED_SCOPE_TITLE} ${name}
    wait for element and click    ${DeleteScopesElements.ID_ACTION_MAIN_BTN}
    wait for element and click    ${DeleteScopesElements.ID_ACTION_MENU_DELETE_SCOPE}

Validate Delete Message when scope is assigned to filter
    ${msg} =    get text    xpath=.//*[@id='delete-scope-invalid']//div[@class="hp-message"]
    Run Keyword If  '${msg}'=='${warning}'   Log    Remove Scope from filter and then delete scope
    Run Keyword Unless  '${msg}'=='${warning}'   Fail   Scope is not assigned to any filter
    wait for element and click    xpath=.//*[@id='delete-scope-invalid-close']

Validate Delete Message when scope is removed from filter
    ${msg} =    get text    xpath=.//*[@id='delete-scope-warning']
    Run Keyword If  '${msg}'=='${message}'   Log    Scope can now be deleted as it is removed from filter
    Run Keyword Unless  '${msg}'=='${message}'   Fail   Failed Error Message is not as expected
    wait for element and click    ${DeleteScopesElements.ID_BTN_DELETE_SCOPE}
