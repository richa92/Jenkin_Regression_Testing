*** Settings ***
Documentation		Restore BackUp
Resource			../Resource/CIM_CommonResource.txt
Resource			../Resource/CIM_Activity.txt
Resource			../Resource/CIM_Settings.txt
Test Setup			Load Test Data and Open Browser
Test Teardown		Logout and close all browsers
Variables			../../../../FusionLibrary/ui/settings/settings_elements.py
Variables			../../../../FusionLibrary/ui/business_logic/general/base_elements.py
Variables			../../../../FusionLibrary/ui/general/login_elements.py
Variables			../../../../../rg/RoboGalaxyLibrary/ui/common/PerfConstants_File.py

*** Variables ***
${searchtext}	The appliance was restored from a backup
${activityname}	Restore backup
${resource}	Backup
${activitystate}	Completed
${activityowner}	Administrator
${ID_DOWNLOADED_BACKUP_FILENAME_LABEL}	//*[@id='fs-settings-backup-saved']/div[contains(@class,'backup-saved-filename')]

*** Test Cases ***
As an Administrator I want to Restore Backup
	Log into Fusion appliance as Administrator
	Upload and restore backup

*** Keywords ***
Upload and restore backup
	${downloaded_backup_absolute_file_path} =	Get absolute file path of last downloaded backup
	Select action	Restore from backup
	wait for element and click	${FusionSettingsPage.ID_RADIOBTN_SELECT_A_BACKUP}
	Click Button	${FusionSettingsPage.ID_BTN_BROWSE_FILE}
	Select file from 'upload file' dialog	${downloaded_backup_absolute_file_path}

	#Initate upload and restore
	wait for element and click	${FusionSettingsPage.ID_BTN_UPLOAD_AND_RESTORE}
	Select Checkbox	${FusionSettingsPage.ID_CHECKBOX_RESTORE_CONFIRMATION}
	wait for element and click	${FusionSettingsPage.ID_BTN_YES_RESTORE_CONFIRMATION}
	wait for element and click	${FusionSettingsPage.ID_BTN_RESTORE_FROM_BACKUP}

	#Verify if 'Restoring' label is displayed
	wait for element visible	${FusionSettingsPage.ID_LABEL_RESTORING}	${PerfConstants.RESTORING_LABEL_VISIBLE}

	#Verify that restore operation has finished and login page is displayed
	wait for element visible	${FusionLoginPage.ID_BTN_LOGIN_BUTTON}	${PerfConstants.RESTORE_DATA_FROM_BACKUP_TIME}

	Log into Fusion appliance as Administrator

	#Verify Restore Backup activity in Activity Page
	Verify Activity Existence in Activity Page	${activityname}	 ${resource}	${activitystate}	${activityowner}

	#Verify entry in auditlog file
	Download and verify entry in Audit log file	${DownloadPath}	${searchtext}

Get absolute file path of last downloaded backup
	[Return]	${last_downloaded_backup_absolute_filepath}
	Navigate to	Settings	Backup
	${last_downloaded_backup_filename} =	Get name of the last downloaded backup from 'Backup' page
	${last downloaded_backup_absolute_filepath} =	Set Variable	${DownloadPath}/${last_downloaded_backup_filename}

Get name of the last downloaded backup from 'Backup' page
	[Documentation]	Returns the name of the last downloaded backup from Backup page
	[Return]	${last_downloaded_backup_filename}
	${file_info_in_last_downloaded_backup_field} =	Get Text	${ID_DOWNLOADED_BACKUP_FILENAME_LABEL}
	${last_downloaded_backup_filename} =	Fetch From Right	${file_info_in_last_downloaded_backup_field}	${SPACE}

Select file from 'upload file' dialog
	[Arguments]	${absolute_file_path}
	${BrowserUppercase}=	Convert To Uppercase	${Browser}
	${Browser_Specific_Window_Titles} =	Create Dictionary	CHROME=Open
											...				FIREFOX=File Upload
											...				INTERNET EXPLORER=Choose File to Upload
	${WindowTitle} =	Get From Dictionary	${Browser_Specific_Window_Titles}	${BrowserUppercase}
	${absolute_file_path} =	Replace String	${absolute_file_path}	/	\\
	${TAB_KEY} =	Set Variable	{TAB}
	${ENTER_KEY} =	Set Variable	{ENTER}
	Sleep	5
	send keys to native window	${WindowTitle}	"${absolute_file_path}"+${ENTER_KEY}
