*** Settings ***
Documentation			Run CHO 2-5 iteration test
Resource				../Resource/CIM_CommonResource.txt
Resource				../Resource/CIM_Appliance.txt
Test Setup				Load All Test Data
Test Teardown			Logout and close all browsers
Library    				RoboGalaxyLibrary
Library					FusionLibrary
Library					String
Library					DateTime
Library					OperatingSystem
Variables				../../../../FusionLibrary/ui/general/login_elements.py

*** Variables ***
${Results_Folder_Path}		C:/Users/Administrator/Desktop/CHO_Results/

*** Test Cases ***
Run CHO 2-5 iteration test
	:FOR	${iteration_count}	IN RANGE	0	${TestData.CHO[0].NumberOfIterations[0]}
	\	Log	Iteration#: ${iteration_count} started!	WARN
	\	Open Browser	${ApplianceUrl}
	\	${IsApplianceIDLE}=	Is appliance in IDLE state?	300	120
	\	${Iteration_Start_DateTime}=	Get Current Date
	\	${ip}=	Get appliance ip from url
	\	Run Keyword If	'${IsApplianceIDLE}'=='YES'	Log	Appliance is in IDLE state. Continuing execution. Rebooting appliance...	WARN
	\	Run Keyword If	'${IsApplianceIDLE}'=='YES'	Reboot appliance	${ip}	${TestData.RootUserCredentials[0].username}	${TestData.RootUserCredentials[0].password}
	\	Run Keyword Unless	'${IsApplianceIDLE}'=='YES'	Fail	Appliance not in IDLE state. Stopping execution...
	\	${IsApplianceStarting}=	Is Starting page displayed?	300	60
	\	Run Keyword If	'${IsApplianceStarting}'=='YES'	Log	Appliance reboot initiation successful. Continuing execution...	WARN
	\	Run Keyword Unless	'${IsApplianceStarting}'=='YES'	Fail	Appliance reboot initiation failed. Stopping execution...
	\	${IsApplianceIDLE}=	Is appliance in IDLE state?	1200	120
	\	Run Keyword If	'${IsApplianceIDLE}'=='YES'	Log	Appliance Reboot completed successfully.	WARN
	\	${Iteration_End_DateTime}=	Get Current Date
	\	${Iteration_Runtime}=	Get elapsed time in seconds_millis	${Iteration_Start_DateTime}	${Iteration_End_DateTime}
	\	Report test results	${Iteration_End_DateTime}	${Iteration_Runtime}	${iteration_count}
	\	Close All Browsers
	\	Log	Iteration#: ${iteration_count} completed!	WARN

*** Keywords ***
Report test results
	[Arguments]	${Iteration_End_DateTime}	${Iteration_Runtime}	${Iteration_Count}
	${Iteration_End_DateTime}=	Convert Date	${Iteration_End_DateTime}	date_format=%Y-%m-%d %H:%M:%S.%f
	${Iteration_End_DateTime_Custom_Format}=	Convert Date	${Iteration_End_DateTime}	result_format=%m/%d/%Y %I:%M:%S %p
	Write result to file	${Iteration_End_DateTime}	${Iteration_End_DateTime_Custom_Format}	${Iteration_End_DateTime_Custom_Format} : Browser Login Time *** ${Iteration_Runtime} Seconds ***CHO Iteration # ${Iteration_Count}${\n}

Write result to file
	[Arguments]	${Iteration_End_DateTime}	${Iteration_End_DateTime_Custom_Format}	${Result}
	${Iteration_End_Date_Custom_Format}=	Convert Date	${Iteration_End_DateTime}	result_format=%m%d%Y
	${Results_File_Name}=	Set Variable	CHOLogFile${Iteration_End_Date_Custom_Format}.txt
	${Results_File_Absolute_Path}=	Set Variable	${Results_Folder_Path}/${Results_File_Name}
	Append To File	${Results_File_Absolute_Path}	${Result}
	Append To File	${Results_File_Absolute_Path}	${Iteration_End_DateTime_Custom_Format} : --------------------------------------------------------------------------------------------------------------------------${\n}
