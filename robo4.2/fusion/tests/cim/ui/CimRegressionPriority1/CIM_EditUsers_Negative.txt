*** Settings ***
Documentation	Edit users - negative
Resource		../Resource/CIM_CommonResource.txt
Resource		../Resource/CIM_UsersAndGroups.txt
Test Teardown	Logout and close all browsers
Variables		../../../../FusionLibrary/ui/business_logic/usersandgroups/usersandgroups_elements.py

*** Variables ***
${User_Name}	edituser
${USERS_TABLE_ROW_CONTAINING_USERNAME}	//div[@id='fs-users-table_wrapper']//div[@class='dataTables_scrollBody']//table/tbody/tr/td[text()='${User_Name}']

*** Settings ***
Test Setup	Run Keywords	Load Test Data and Open Browser
...			AND	Add user	${User_Name}	${USERS_TABLE_ROW_CONTAINING_USERNAME}

*** Test Cases ***
Edit Users - Negative test case
	wait for element and click	${USERS_TABLE_ROW_CONTAINING_USERNAME}
	Select action	Edit
	Enter duplicate password in 'Initial/Confirm password' field and verify validation message
	Enter invalid value in 'Full name' field and verify validation message
	Enter a short password in 'Initial/Confirm password' field and verify validation message
	Enter invalid value in 'Email' field and verify validation message
	Skip selecting value in 'Role' field and verify validation message
	wait for element and click	${EditUserElements.ID_BTN_EDIT_CANCEL}

*** Keywords ***
Enter duplicate password in 'Initial/Confirm password' field and verify validation message
	Enter data in 'Initial password' field - Edit User page	hpvse123
	Enter data in 'Confirm password' field - Edit User page	hpvse123
	wait for element and click	${EditUserElements.ID_BTN_EDIT_OK}
	Verify validation error message	PASSWORD_DUPLICATE

Enter invalid value in 'Full name' field and verify validation message
	Enter data in 'Full name' field - Edit User page	Fullname! Invalid?@
	Verify validation error message	FULLNAME_INVALID

Enter a short password in 'Initial/Confirm password' field and verify validation message
	Enter data in 'Initial password' field - Edit User page	123
	Enter data in 'Confirm password' field - Edit User page	123
	Verify validation error message	PASSWORD_SHORT

Enter invalid value in 'Email' field and verify validation message
	Enter data in 'Email' field - Edit User page	invalidemail
	Verify validation error message	EMAIL_INVALID

Skip selecting value in 'Role' field and verify validation message
	Select option in 'Role' field - Edit User page	Specialized
	wait for element and click	${EditUserElements.ID_BTN_EDIT_OK}
	Verify validation error message	ROLE_REQUIRED

#-----------------------------------------------------------------------------------
#	Keywords to verify validation errors
#-----------------------------------------------------------------------------------
Verify validation error message
	[Arguments]	${Error_Message}
	Run Keyword If	'${Error_Message}'=='PASSWORD_DUPLICATE'	Wait Until Element is Visible	//div[@id='fs-user-edit-footer']//div[@class='hp-details' and contains(text(), 'New password is same as the current password.')]
	...	ELSE IF	'${Error_Message}'=='FULLNAME_INVALID'	Wait Until Element is Visible	//label[@for='fs-user-edit-fullname' and @class='hp-error' and text()="Full name should contain only letters, digits and the characters ' . - _ and space."]
	...	ELSE IF	'${Error_Message}'=='PASSWORD_SHORT'	Wait Until Element is Visible	//label[@for='fs-user-edit-newpasswd' and @class='hp-error' and text()='Password should be at least 8 characters long.']
	...	ELSE IF	'${Error_Message}'=='EMAIL_INVALID'	Wait Until Element is Visible	//label[@for='fs-user-edit-email' and @class='hp-error' and text()='Enter a valid email address.']
	...	ELSE IF	'${Error_Message}'=='ROLE_REQUIRED'	Wait Until Element is Visible	//label[@for='fs-user-edit-role-validator' and @class='hp-error' and text()='At least one role must be selected.']

#-----------------------------------------------------------------------------------
#	Setup pre-condition
#-----------------------------------------------------------------------------------
Add user
	[Arguments]	${User_Name}	${USERS_TABLE_ROW_CONTAINING_USERNAME}
	Log into Fusion appliance as Administrator
	Navigate to page	Users and Groups
	${UserPresent}	${error} =	Run keyword and Ignore Error	Page Should Contain Element	${USERS_TABLE_ROW_CONTAINING_USERNAME}
	Run keyword if	'${UserPresent}'=='PASS'	Remove user	${USERS_TABLE_ROW_CONTAINING_USERNAME}
	Select action	Add user
	Enter data in 'Login name' field - Add User page	${User_Name}
	Enter data in 'Full name' field - Add User page	${User_Name}
	Enter data in 'Initial password' field - Add User page	hpvse123
	Enter data in 'Confirm password' field - Add User page	hpvse123
	Select option in 'Role' field - Add User page	Full
	Enter data in 'Email' field - Add User page	${User_Name}@hpe.com
	Enter data in 'Office phone' field - Add User page	650-123-1234
	Enter data in 'Mobile phone' field - Add User page	408-123-1234
	wait for element and click	${CreateUserElements.ID_BTN_USER_ADD}

Remove user
	[Arguments]	${USERS_TABLE_ROW_CONTAINING_USERNAME}
	wait for element and click	${USERS_TABLE_ROW_CONTAINING_USERNAME}
	Select Action	Remove
	wait for element and click	${RemoveUserElements.ID_BTN_YES_CONFIRM_REMOVE}
