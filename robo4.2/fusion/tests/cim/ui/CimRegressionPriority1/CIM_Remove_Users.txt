*** Settings ***
Documentation		Delete users from Fusion
Resource			../Resource/CIM_CommonResource.txt
Resource			../Resource/CIM_UsersAndGroups.txt
Resource			../Resource/CIM_Activity.txt
Test Setup			Load Test Data and Open Browser
Test Teardown		Logout and close all browsers

*** Test Cases ***
As an Administrator I want to Remove Users
	Log into Fusion appliance as Administrator
	Fusion UI navigate to users and groups page
	Fusion UI Create User	@{TestData.users}
	${status}=	Fusion UI Remove User	@{TestData.users}
	Should Be True	${status}	msg=Failed remove users from appliance

	#Verify the Remove User Activity in Activity Page
	Navigate to Activity page
	:FOR	${user}	IN	@{TestData.users}
	\	${value} =	get variable value	${user.name}
	\	Run Keyword If	'${value.upper()}' == 'ADMINISTRATOR'	Continue For Loop
	\	${count}=	Get Matching XPath Count	//*[@id='hp-activities']/tbody//tr[//span[text()='Remove'] and td/a[text()='${value}']]
	\	Run Keyword If	${count} >= 1	Log	Given alert "Remove -- ${value}" is listed in activity page
	\	Run Keyword Unless	${count} >= 1	Fail	Given alert "Remove -- ${value}" is not present in the activity page

	#Verify entry in auditlog file
	${filepath}=	Download and unzip Audit log file	${DownloadPath}

	:FOR	${user}	IN	@{TestData.users}
	\	${value} =	get variable value	${user.name}
	\	Run Keyword If	'${value.upper()}' == 'ADMINISTRATOR'	Continue For Loop
	\	${searchtext} =	Set Variable	User with name: ${user.name} deleted successfully
	\	${bln_logentry_exists} =	file_contains	${filepath}	${searchtext}
	\	Should Be Equal As Strings	${bln_logentry_exists}	${blnExpected}	msg=Entry ${searchtext} not available in log file
