*** Settings ***
Documentation		Edit Network Happy Path for Static\DHCP Appliance
Resource			../Resource/CIM_CommonResource.txt
Resource			../Resource/CIM_Settings.txt
Test Setup			Load Test Data and Open Browser
Test Teardown		Logout and close all browsers
Variables			../../../../FusionLibrary/ui/business_logic/general/base_elements.py
Variables			../../../../FusionLibrary/ui/settings/settings_elements.py
Variables			../../../../FusionLibrary/ui/business_logic/settings/networking_elements.py
Variables			../../../../FusionLibrary/ui/general/login_elements.py
Variables			../../../../../rg/RoboGalaxyLibrary/ui/common/PerfConstants_File.py

*** Variables ***
${searchtext}		reconfigured the appliance network settings

*** Test Cases ***
Edit Network Happy Path
	Log into Fusion appliance as Administrator

#Navigate to Edit Networking page
	Fusion UI Navigate To Settings Page
	Navigate To Edit Networking Page

#Save 'Host Name' and 'IP' in variables
	Wait Until Element Is Visible	${C7000_EditNetworkingElements.ID_INPUT_APPLIANCE_HOST_NAME}	60
	${Appliance_Host_Name}=	Get Value	${C7000_EditNetworkingElements.ID_INPUT_APPLIANCE_HOST_NAME}
	Wait Until Element Is Visible	${C7000_EditNetworkingElements.ID_INPUT_IPV4_ADDRESS}	60
	${Appliance_IP}=	Get Value	${C7000_EditNetworkingElements.ID_INPUT_IPV4_ADDRESS}

#Check if hostname resolves to ip
	${HostnameAndIPResolveCorrectly}=	Does hostname resolve to IP address?	${Appliance_Host_Name}	${Appliance_IP}

#Apply network settings and wait until network settings are applied completely
	wait for element and click	${C7000_EditNetworkingElements.ID_BUTTON_OK}
	wait for element visible	${C7000_EditNetworkingElements.ID_PROGRESS_BAR_APPLYING_NETWORK_SETTINGS}
	wait for element not visible	${C7000_EditNetworkingElements.ID_PROGRESS_BAR_APPLYING_NETWORK_SETTINGS}	${PerfConstants.CONFIGURE_NETWORK_SETTINGS_FTW}

#Depending on whether or not hostname correctly resolves to IP address, choose the appropriate message to be verified in Activity page
	${Expected_Alert_Name_In_Activity_Page}=	Set Variable If	'${HostnameAndIPResolveCorrectly}'=='TRUE'	Network Configuration	${Appliance_Host_Name} does not resolve to ${Appliance_IP} address.
	Log	${Expected_Alert_Name_In_Activity_Page}	WARN

#Navigate to Activity page
	Navigate to page	Activity

#Verify message in Activity page
	wait for element visible	//*[@id='hp-activities']/tbody/tr[1]/td/a[1] 10
	${count}=	Get Matching XPath Count	//*[@id='hp-activities']/tbody//tr[1][//span[text()='${Expected_Alert_Name_In_Activity_Page}']]
	Run Keyword If	${count} >= ${activityCount}	Log	Alert is present in the activity page
	Run Keyword Unless	${count} >= ${activityCount}	Fail	Alert is not present in the activity page

#Verify entry in auditlog file
	Download and verify entry in Audit log file	${DownloadPath}	${searchtext}
