*** Settings ***
Documentation		Edit Backup Settings and Schedule
...	Remote server details like ipaddress, username, backupdirectory, protocol to be updated in CIM_TestData.xml file
...	Backup download schedule like DAILY and time, WEEKLY, time and day or NOT SET to be in CIM_TestData.xml
Resource			../Resource/CIM_CommonResource.txt
Resource			../Resource/CIM_Activity.txt
Resource			../Resource/CIM_Settings.txt
Library				String
Library				DateTime
Test Setup			Load Test Data and Open Browser
Test Teardown		Logout and close all browsers

*** Variables ***
${searchtext}		Edit backup settings operation completed successfully
${activityname}		Edit backup settings
${resource}			Backup
${activitystate}	Completed
${activityowner}	Administrator

*** Test Cases ***
As an Administrator I want to Edit Backup Settings and Schedule
	Log into Fusion appliance as Administrator

	#Navigate to Edit Backup Settings page
	Navigate to Edit Backup Settings page

	#Edit Remote Backup Location
	Edit Remote Backup Location	${TestData.RemoteBackpupSettings[0].protocol}
	...	${TestData.RemoteBackpupSettings[0].ipaddress}
	...	${TestData.RemoteBackpupSettings[0].backupdirectory}
	...	${TestData.RemoteBackpupSettings[0].username}
	...	${TestData.RemoteBackpupSettings[0].password}

	#Edit Schedule
	Select frequency	${TestData.RemoteBackpupSettings[0].schedule}
	...	${TestData.RemoteBackpupSettings[0].time}
	...	${TestData.RemoteBackpupSettings[0].day}

	#Click OK button
	Click Element	${FusionSettingsPage.ID_BACKUP_EDIT_OK_BUTTON}

	#Verify Notification bar
	Verify notification bar	ok	${activityname}	${activitystate}	30

	#Verify Backup page
	Verify Remote Location In Backup Page	${TestData.RemoteBackpupSettings[0].protocol}
	...	${TestData.RemoteBackpupSettings[0].ipaddress}
	...	${TestData.RemoteBackpupSettings[0].backupdirectory}
	...	${TestData.RemoteBackpupSettings[0].username}
	Verify Schedule In Backup Page	${TestData.RemoteBackpupSettings[0].schedule}
	...	${TestData.RemoteBackpupSettings[0].time}
	...	${TestData.RemoteBackpupSettings[0].day}

	#Verify Activity page
	Verify Activity Existence in Activity Page	${activityname}	${resource}	${activitystate}	${activityowner}

	#Verify entry in auditlog file.
	Download and verify entry in Audit log file	${DownloadPath}	${searchtext}
