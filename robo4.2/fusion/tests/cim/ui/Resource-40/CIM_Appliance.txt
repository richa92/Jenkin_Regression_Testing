*** Settings ***
Library           RoboGalaxyLibrary
Library           BuiltIn
Library           String
Library           Collections
Resource          ../Resource/CIM_Activity.txt
Resource          ../Resource-40/CIM_Appliance.txt
Resource          ../Resource-40/CIM_CommonResource.txt
Variables         ../../../../FusionLibrary/ui/business_logic/general/base_elements.py
Variables         ../../../../FusionLibrary/ui/settings/settings_elements.py

*** Variables ***
${data_count}    0

*** Keywords ***
Wait until VM shutdown is complete
    [Documentation]    Wait for the appliance shutdown to complete
    Wait until shutting down message is displayed
    Wait until unavailable message is displayed
    Wait until VM is powered off in v-center
    Log    Appliance was shutdown successfully    WARN

Wait until VM is powered off in v-center
    [Documentation]    Wait until VM is powered off in v-Center
    Wait Until Keyword Succeeds    15 min    15 sec    Is VM powered off?

Is VM powered off?
    [Documentation]    To check if VM is powered off in v-Center
    ${ApplianceExecutionState}=    Get VM Execution Status
    Log    ${ApplianceExecutionState}    WARN
    Run Keyword If    '${ApplianceExecutionState}'!='poweredOff'    Fail    Appliance is still not powered off in v-center!

Initiate Full Factory Reset
    [Documentation]    Perform appliance Full Factory Reset
    wait for element and click    ${FusionSettingsPage.ID_MENU_ACTION_MAIN_BTN}
    wait for element and click    ${FusionSettingsPage.ID_ELEMENT_FACTORY_RESET}
    wait for element visible    ${FusionSettingsPage.ID_CHECKBOX_PREVENT_NEWORK_SETTINGS}
    Unselect Checkbox    ${FusionSettingsPage.ID_CHECKBOX_PREVENT_NEWORK_SETTINGS}
    wait for element and click    ${FusionSettingsPage.ID_BTN_START_FACTORY_RESET}
    wait for element visible    ${FusionSettingsPage.ID_CHECKBOX_CONFIRM_FACTORY_RESET}
    Select Checkbox    ${FusionSettingsPage.ID_CHECKBOX_CONFIRM_FACTORY_RESET}
    wait for element and click    ${FusionSettingsPage.ID_BTN_CONFIRM_FACTORY_RESET}

Wait until appliance is assigned a new IP
    [Documentation]    Wait until appliance is assigned with new IP Address
    [Arguments]    ${IP_Before_Factory_Reset}    ${Time_Out}    ${Retry_Interval}
    Wait Until Keyword Succeeds    ${Time_Out}    ${Retry_Interval}    Check if new IP is assigned to appliance    ${IP_Before_Factory_Reset}

Check if new IP is assigned to appliance
    [Documentation]    To check if IP address is assigned to appliance
    [Arguments]    ${IP_Before_Factory_Reset}
    ${Is_New_IP_Assigned}=    Set Variable    False
    ${Current_IP}=    Get VM IPAddress
    Run Keyword And Return If    '${Current_IP}'=='${IP_Before_Factory_Reset}'    Fail    "New IP not assigned"
    ${Is_New_IP_Assigned}=    Set Variable    True
    [Return]    ${Is_New_IP_Assigned}
