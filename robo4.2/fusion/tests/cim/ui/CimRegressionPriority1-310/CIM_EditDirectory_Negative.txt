*** Settings ***
Documentation	Edit directory and directory server to validated error
Resource		../Resource/CIM_CommonResource.txt
Resource		../Resource/CIM_Security.txt
Test Setup		Load Test Data and Open Browser
Test Teardown	Logout and close all browsers
Variables		../../../../FusionLibrary/ui/settings/settings_elements.py
Variables		../../../../FusionLibrary/ui/business_logic/general/base_elements.py

*** Variables ***
${ID_DIRECTORY_EDIT}	//*[@id='security-directories-table']/tbody/tr[*]/td[contains(text(),'%s')]/parent::*/td[2]/div
${ID_DIRECTORY_SERVER_EDIT}	//table[@id='fs-edit-dir-servers-table']//div[@class='hp-edit'][1]

*** Test Cases ***
Edit directory and directory server to validate error
	Log into Fusion appliance as Administrator
	Navigate to	Settings	Security
	Select action	Edit
	${AD} =	Get test data for 'Active Directory'

	#Edit Directory server certificate with invalid format
	${editdirectory_button_locator} =	Replace String	${ID_DIRECTORY_EDIT}	%s	${AD.domainName}
	wait for element and click	${editdirectory_button_locator}
	wait for element and click	${ID_DIRECTORY_SERVER_EDIT}
	Enter incorrect certificate key in non Base x64 format and check for validation message
	wait for element and click	//*[@id='fs-cancel-edit-dir-server']

	#Update the directory name with a new value
	Update value in 'Directory' field - 'Edit Directory' page	${AD.domainName}	new_directory_name	${AD.userName}	${AD.userPswd}

*** Keywords ***
Enter incorrect certificate key in non Base x64 format and check for validation message
	Enter data in certificate field	INCORRECT PUBLIC KEY
	wait for element and click	//*[@id='edit-dir-server-ok']
	Verify validation error message	INCORRECT_CERTIFICATE_FORMAT

Update value in 'Directory' field - 'Edit Directory' page
	[Arguments]	${current_directory_name}	${new_directory_name}	${auth_username}	${auth_user_password}
	${value_in_directory_name_field_before_updating_with_new_value} =	Get Value	${FusionSettingsPage.ID_INPUT_DIR_NAME}
	Run Keyword If	'${value_in_directory_name_field_before_updating_with_new_value}'!='${current_directory_name}'	Fail	The current directory name is not as expected!

	#Update directory field with new value
	Enter data in 'Directory' field	${new_directory_name}
	${value_in_directory_name_field_after_updating_with_new_value}=	Get Value	${FusionSettingsPage.ID_INPUT_DIR_NAME}
	Run Keyword If	'${value_in_directory_name_field_after_updating_with_new_value}'!='${new_directory_name}'	Fail	The new directory name is not as expected
	wait for element and click	${FusionSettingsPage.ID_BTN_ADD_DIR}
	wait for element and input text	${FusionSettingsPage.ID_INPUT_AUTHN_USERNAME}	${auth_username}
	wait for element and input text	${FusionSettingsPage.ID_INPUT_AUTHN_PASSWORD}	${auth_user_password}
	wait for element and click	${FusionSettingsPage.ID_AD_OK_BUTTON}

	#Validate new directory name
	Wait for element visible	//*[@id='security-directories-table']/tbody/tr[*]/td[text()='${new_directory_name}']	30
