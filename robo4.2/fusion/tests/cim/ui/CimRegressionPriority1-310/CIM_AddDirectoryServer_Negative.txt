*** Settings ***
Documentation	Add directory server - Validating error cases
Resource		../Resource/CIM_CommonResource.txt
Resource		../Resource/CIM_Security.txt
Test Setup		Load Test Data and Open Browser
Test Teardown	Logout and close all browsers
Variables		../../../../FusionLibrary/ui/settings/settings_elements.py
Variables		../../../../FusionLibrary/ui/business_logic/general/base_elements.py

*** Test Cases ***
Add directory server - Validating error cases
	#Get testdata for Ad and LDAP
	${AD} =	Get test data for 'Active Directory'
	${LDAP} =	Get test data for 'LDAP'

	Log into Fusion appliance as Administrator
	Navigate to	Settings	Security
	Select action	Edit
	wait for element and click	${FusionSettingsPage.ID_BTN_ADD_DIRECTORY_SETTINGS}
	wait for element and click	${FusionSettingsPage.ID_BTN_ADD_DIRECTORY}

	#Validate Error for Blank values
	Enter data in 'IP address or host name' field	${EMPTY}
	Enter data in 'Directory server port' field	${EMPTY}
	Toggle 'Specify certificate' checkbox	True
	Enter data in certificate field	${EMPTY}
	wait for element and click	${FusionSettingsPage.ID_BTN_DIRECTORY_SERVER_ADD}
	Verify validation error message	IP_ADDRESS_OR_HOSTNAME_REQUIRED
	Verify validation error message	DIRECTORY_SERVER_PORT_REQUIRED
	Verify validation error message	CERTIFICATE_REQUIRED

	# Validate Error for incorrect certificate
	Enter incorrect certificate value and check for validation message	${AD}

	#Add 2 directory servers
	#Prerequisite: AD with same domain name should already be added to the appliance
	Add directory server	${AD}
	Add directory server	${LDAP}
	${dir_sever_count}=	Get Matching XPath Count	//*[@id='fs-dir-servers-table']/tbody/tr
	Run Keyword If	'${dir_sever_count}' < '2'	Fail	Fail to add two add directory servers

	# Close all open modules
	wait for element and click	${FusionSettingsPage.ID_BTN_DIR_SERVER_CANCEL}
	wait for element and click	${FusionSettingsPage.ID_BTN_SETTINGS_EDIT_CANCEL}

*** Keywords ***
Enter incorrect certificate value and check for validation message
	[Arguments]	${dir}
	Enter data in 'IP address or host name' field	${dir.server}
	Enter data in 'Directory server port' field	${dir.port}
	Toggle 'Specify certificate' checkbox	True
	Enter data in certificate field	INCORRECT PUBLIC KEY
	wait for element and click	${FusionSettingsPage.ID_BTN_DIRECTORY_SERVER_ADD}
	Verify validation error message	INCORRECT_CERTIFICATE_FORMAT
