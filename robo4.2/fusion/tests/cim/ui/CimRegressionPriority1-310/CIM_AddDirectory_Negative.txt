*** Settings ***
Documentation	Enter directory name with space and check validation error message
Resource		../Resource/CIM_CommonResource.txt
Resource		../Resource/CIM_Activity.txt
Resource		../Resource/CIM_Security.txt
Test Setup		Load Test Data and Open Browser
Test Teardown	Logout and close all browsers
Variables		../../../../FusionLibrary/ui/settings/settings_elements.py
Variables		../../../../FusionLibrary/ui/business_logic/general/base_elements.py

*** Variables ***
${activityname}		Add Directory
${resource}			Security
${activitystate}	Error
${activityowner}	Administrator

*** Test Cases ***
Verify error displayed in Add Directory page
	Log into Fusion appliance as Administrator
	Navigate to	Settings	Security
	Select action	Edit

	#Missing Mandatory fields
	wait for element and click	${FusionSettingsPage.ID_BTN_ADD_DIRECTORY_SETTINGS}
	wait for element and click	${FusionSettingsPage.ID_BTN_ADD_DIR}
	Verify validation error message	DIRECTORY_REQUIRED
	Verify validation error message	BASE_DN_REQUIRED
	Verify validation error message	DIRECTORY_SERVERS_REQUIRED

	#Directory Name containing space
	Enter data in 'Directory' field	AD 1
	wait for element and click	${FusionSettingsPage.ID_BTN_ADD_DIRECTORY_SETTINGS}
	Verify validation error message	INVALID_CHARACTERS_IN_DIRECTORY_NAME

	#Add Directory with Existing directory name
	#Pre-requisite: Directory with same name should already be added
	${dir} =	Get test data for 'Active Directory'
	Add Directory	${dir}
	Verify validation error message	SPECIFIED_DIRECTORY_ALREADY_CONFIGURED
	wait for element and click	${FusionSettingsPage.ID_BTN_DIR_SERVER_CANCEL}
	wait for element and click	${FusionSettingsPage.ID_BTN_SETTINGS_EDIT_CANCEL}

	#Verify Activity page
	Verify Activity Existence in Activity Page	${activityname}	${resource}	${activitystate}	${activityowner}
