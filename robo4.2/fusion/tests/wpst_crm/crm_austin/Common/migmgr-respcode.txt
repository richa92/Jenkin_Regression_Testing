*** Settings ***
Documentation     Migration Manager RESTAPI Negative Testing


Library           RoboGalaxyLibrary
Library           FusionLibrary
Library           BuiltIn
Library           ../lib/WPSTUtil.py

Variables                       ../resources/defaults.py
Variables                       ../resources/credentials_wpst.py   Austin3
Force Tags                      Austin3
Variables                       ../resources/DTOtemplates.py
Resource                        ../resources/keywords.txt
Resource                        ../resources/keywords_ovcli.txt
Suite Setup                     Suite Setup Tasks
#Test Setup                      Test Setup Tasks

*** Test Cases ***
TC0--Preparing the environment: Clean OV, Clean VC, import enclosure
    CleanOV.CleanVC
    @{commands}     Create List    import enclosure username=${OA_CREDENTIAL_DATA['oaUsername']} password=${OA_CREDENTIAL_DATA['oaPassword']}
    Run CLI In Interconnect From List    ${commands}

3.11.1a--Negative VC User has incorrect password
    [Documentation]   /rest/migrateable-vc-domains POST
    ...     Verify Migration Manager error handling when the specified VC User has incorrect password.
    ${OAVC_CREDENTIALS_badVCPassword}=    WPST Deep Copy      ${OAVC_CREDENTIALS}
    Set To Dictionary   ${OAVC_CREDENTIALS_badVCPassword}   vcmPassword    badVCPassword
    Log to console and log file    ${OAVC_CREDENTIALS_badVCPassword}
    ${resp}=   Build Create Compatiblity Report Against New EG    ${OAVC_CREDENTIALS_badVCPassword}
    ${resp} =   Run Keyword If  ${X-API-version} >= ${newMigrRespMinXAPIVersion}   Wait For MigratableVcDomainV300 Response Task   ${resp}
    ...         ELSE   Wait For Task   ${resp}
    ${validationTaskErrors}=    Create Dictionary    errorCode=FAIL_BAD_CREDENTIALS_FOR_VCM
    ${validation}=    Create Dictionary     taskState=Error
    ...                                     taskErrors=${validationTaskErrors}
    Should Match Dictionary in Response    ${resp}    ${validation}

3.11.1b--Negative VC IP is incorrect
    [Documentation]   /rest/migrateable-vc-domains POST
    ...     vcmIpAddress is read-only field but if a customer pushes a value in a read only option they get what they get.
    ...     This design is approved by Terry Martin and to support simulator
    ${OAVC_CREDENTIALS_badVCIp}=    WPST Deep Copy      ${OAVC_CREDENTIALS}
    Set To Dictionary   ${OAVC_CREDENTIALS_badVCIp}   vcmIpAddress    0.0.0.0
    Log to console and log file    ${OAVC_CREDENTIALS_badVCIp}
    ${resp}=   Build Create Compatiblity Report Against New EG    ${OAVC_CREDENTIALS_badVCIp}
    ${resp} =   Run Keyword If  ${X-API-version} >= ${newMigrRespMinXAPIVersion}   Wait For MigratableVcDomainV300 Response Task   ${resp}
    ...         ELSE   Wait For Task   ${resp}
    ${validationTaskErrors}=    Create Dictionary    errorCode=SERVICE_FAIL_TO_RETRIEVE_ENCLOSURE_CONFIGURATION
    ${validation}=    Create Dictionary     taskState=Error
    ...                                     taskErrors=${validationTaskErrors}
    Should Match Dictionary in Response    ${resp}    ${validation}

3.11.2a--Negative OA bad password
    [Documentation]      /rest/migrateable-vc-domains POST
    ...     Verify Migration Manager error handling when the specified OA User has incorrect password.
    ${OAVC_CREDENTIALS_badOAPassword}=    WPST Deep Copy      ${OAVC_CREDENTIALS}
    Set To Dictionary   ${OAVC_CREDENTIALS_badOAPassword}   oaPassword   badOAPassword
    Log to console and log file    ${OAVC_CREDENTIALS_badOAPassword}
    ${resp}=   Build Create Compatiblity Report Against New EG    ${OAVC_CREDENTIALS_badOAPassword}
    ${resp} =   Run Keyword If  ${X-API-version} >= ${newMigrRespMinXAPIVersion}   Wait For MigratableVcDomainV300 Response Task   ${resp}
    ...         ELSE   Wait For Task   ${resp}
    ${validationTaskErrors}=    Create Dictionary    errorCode=FAIL_BAD_CREDENTIALS_FOR_OA
    ${validation}=    Create Dictionary     taskState=Error
    ...                                     taskErrors=${validationTaskErrors}
    Should Match Dictionary in Response    ${resp}    ${validation}

3.11.2b--Negative OA bad IP
    [Documentation]      /rest/migrateable-vc-domains POST
    ...     Verify Migration Manager error handling when the specified OA IP is incorrect.
    ${OAVC_CREDENTIALS_badOAIp}=    WPST Deep Copy      ${OAVC_CREDENTIALS}
    Set To Dictionary   ${OAVC_CREDENTIALS_badOAIp}   oaIpAddress   0.0.0.0
    Log to console and log file    ${OAVC_CREDENTIALS_badOAIp}
    ${resp}=   Build Create Compatiblity Report Against New EG    ${OAVC_CREDENTIALS_badOAIp}
    ${resp} =   Run Keyword If  ${X-API-version} >= ${newMigrRespMinXAPIVersion}   Wait For MigratableVcDomainV300 Response Task   ${resp}
    ...         ELSE   Wait For Task   ${resp}
    ${validationTaskErrors}=    Create Dictionary    errorCode=SERVICE_FAIL_TO_RETRIEVE_ENCLOSURE_CONFIGURATION
    ${validation}=    Create Dictionary     taskState=Error
    ...                                     taskErrors=${validationTaskErrors}
    Should Match Dictionary in Response    ${resp}    ${validation}

3.11.2c--Negative OA bad permission
    [Documentation]      /rest/migrateable-vc-domains POST
    ...     Verify Migration Manager error handling when the specified OA User has insufficient permissions.
    ...     QXCR1001378104 OA credentials bug
    @{oa_commands}     Create List    add user oaUser1 compaq
    ...                               set user access oaUser1 user
    Run CLI In OA From List    ${oa_commands}
    ${OAVC_CREDENTIALS_backup}=    WPST Deep Copy       ${OAVC_CREDENTIALS}
    Set To Dictionary   ${OAVC_CREDENTIALS}   oaUsername   oaUser1
    ${validation}   Create Dictionary   errorCode=FEATURE_ISSUE_OA_USERNAME_WITHOUT_ADMIN_PRIVILEGE
    ...                                 level=Critical
    Validate Detail Report If General Issue Exists      incompatibilities/migrationGeneralIssues/issues   errorCode   ${validation}
    ${OAVC_CREDENTIALS}=    WPST Deep Copy       ${OAVC_CREDENTIALS_backup}
    Validate Detail Report If General Issues Removed       incompatibilities/migrationGeneralIssues/issues   ${validation}

3.11.3--Negative Insufficient OneView Permissions
    [Documentation]      /rest/migrateable-vc-domains POST
    ...     Verify Migration Manager error handling when the specified OneView User has insufficient permissions.
    ${ov_credential_BadUserPermission}=    Create Dictionary        userName=ovUser-ReadOnly
    ...                                                             password=hpvse123
    ${roles}=       Create List     Read only
    ${userBody}=    Create Dictionary       userName=${ov_credential_BadUserPermission['userName']}
    ...                                     password=${ov_credential_BadUserPermission['password']}
    ...                                     roles=${roles}
    ...                                     type=UserAndRoles
    Run Keyword If  ${LOGGED} == ${False}     Login to OneView Via REST
    ${resp}=    Fusion Api Add User    ${userBody}
    Should Match StatusCode in Response    ${resp}    ${200}
    Login to OneView Via REST      admin_credentials=${ov_credential_BadUserPermission}
    ${resp}=   Build Create Compatiblity Report Against New EG
    ${validation}=    Create Dictionary     status_code=${401}
    ...                                     message=Authorization error: User not authorized for this operation.
    Should Match Dictionary in Response    ${resp}    ${validation}
    Login to OneView Via REST
    ${resp}=    Fusion Api Remove User      uri=/rest/users/ovUser-ReadOnly
    Should Match StatusCode in Response    ${resp}    ${204}

3.11.11--Negative Testing: 3.11.11 Delete Report
    [Documentation]   Verify REST response code of 204 No Content
    Run Keyword If  ${LOGGED} == ${False}     Login to OneView Via REST
    ${resourceUri}   ${task_State}   ${task_ErrorCode}   ${task_ErrorMsg}=    Build Create Compatibility Report    ${OAVC_CREDENTIALS}
    ${response1} =   Fusion Api Delete Resource   ${resourceUri}
    ${response2} =   Fusion Api Delete Resource   ${resourceUri}
    Log   ${response2}
    Run Keyword If   ${response2['status_code']} != 400   Fail   msg=Unexpected status code returned where 400 was expected: ${response2['status_code']}
    Run Keyword If   '${response2['errorCode']}' != 'GENERAL_MIGRATION_REPORT_NOT_FOUND'   Fail   msg=Unexpected errorCode returned where GENERAL_MIGRATION_REPORT_NOT_FOUND was expected: ${response2['errorCode']}

3.11.12--Negative Testing: 3.11.12 Delete Report with invalid (malformed) URI
    [Documentation]   Verify REST response code of 400 Bad Request
    Run Keyword If  ${LOGGED} == ${False}     Login to OneView Via REST
    ${response} =   Fusion Api Delete Resource   /rest/migratable-vc-domains/somemalformedhashhere
    Log   ${response}
    Run Keyword If   ${response['status_code']} != 400   Fail   msg=Unexpected status code returned where 400 was expected: ${response['status_code']}

