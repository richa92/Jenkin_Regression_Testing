*** Settings ***

Documentation     OneView Migration Manager Test Suite for LLDP feature in 3.00
...	= Usage =
...		Enclosure Used: WSPT-Austin-1
...		pybot -vOV_IP:<OneView IP> -vBUG_IN_API:<True|False> LLDP-300.txt

Library           RoboGalaxyLibrary
Library           FusionLibrary
Library           BuiltIn
Library           ../lib/WPSTUtil.py

Variables                       ../resources/defaults.py
Variables                       ../resources/DTOtemplates.py
Variables                       ../resources/credentials_wpst.py   Austin1
Force Tags                      Austin1
Default Tags                    OV300
Resource                        ../resources/keywords.txt
Resource                        ../resources/keywords_ovcli.txt
Suite Setup                     Suite Setup Tasks
Test Setup                      Test Setup Tasks

*** Variables ***

@{delResList}                       server-profiles   enclosures
@{AllResourcesCommonList}           ethNets  lig   encGrp   encs   servers   profiles
${vc_config_file_lldpTrue}          config/LLDPblocking102.txt
${vc_config_file_lldpFalse}         config/LLDPblocking103-ov120.txt
${TC21520_Migrated}                 None

*** Test Cases ***
TC000--CleanOV
    [Tags]   CleanOV
    CleanOV

TC000--CleanVC
    [Tags]   CleanVC
    CleanVC

TC21517--Tagged LLDP True new LIG - Test Compatibility
    [Documentation]   Tagged LLDP True in VC to be migrated into a new LIG in OneView
    ...               Tagged LLDP is NOT supported in VC <4.45
    ...               Expected result is Migratable
    Import VC
    Config VC    ${vc_config_file_lldpTrue}
    Report.SelectCompare

TC21517--Tagged LLDP True new LIG - Migrate
    [Documentation]   Tagged LLDP True in VC to be migrated into a new LIG in OneView
    ...               Tagged LLDP is NOT supported in VC <4.45
    ...               Expected result is Migratable
    ...               Expected result is ethernetSettings has enableRichTLV: false, enableTaggedLldp: true, lldpIpv4Address: "", lldpIpv6Address: ""
    Migrate
    GetOVResources.Write.SelectCompare   ResourcesList=${AllResourcesCommonList}   ligSelectDataFile=../resources/private/LLDPtlv-200-US39378/lig.conf

TC21518--Tagged LLDP True existing LIG with match - Test Compatibility
    [Documentation]   Tagged LLDP True in VC to be migrated into an existing LIG in OneView with enableTaggedLldp true
    ...               Tagged LLDP is NOT supported in VC <4.45
    ...               This test case depends on TC21517
    ...               Expected result is Migratable
    Delete OneView Selected    ${delResList}
    Import VC
    Config VC    ${vc_config_file_lldpTrue}
    Report.SelectCompare   ${False}   ${OAVC_CREDENTIALS}   dataFiles/   ${False}   ${False}   EG_2S1408P9XC

TC21518--Tagged LLDP True existing LIG with match - Migrate
    [Documentation]   Tagged LLDP True in VC to be migrated into an existing LIG in OneView with enableTaggedLldp true
    ...               Tagged LLDP is NOT supported in VC <4.45
    ...               This test case depends on TC21518 Test Compatibility
    ...               Expected result is Migratable
    ...               Expected result is ethernetSettings has enableRichTLV: false, enableTaggedLldp: true, lldpIpv4Address: "", lldpIpv6Address: ""
    Migrate
    GetOVResources.Write.SelectCompare   ResourcesList=${AllResourcesCommonList}   ligSelectDataFile=../resources/private/LLDPtlv-200-US39378/lig.conf

TC21520--Tagged LLDP False existing LIG with mismatch - Test Compatibility.
    [Documentation]   Tagged LLDP False in VC to be migrated into an existing LIG in OneView with enableTaggedLldp true
    ...               Tagged LLDP is NOT supported in VC <4.45
    ...               This test case depends on TC21518 Migrate
    ...               Expected result is Not Migratable
    Delete OneView Selected    ${delResList}
    Import VC
    Config VC    ${vc_config_file_lldpFalse}
    Set Suite Variable   ${TC21520_Migrated}   ${False}
    Report.SelectCompare   ${False}   ${OAVC_CREDENTIALS}   dataFiles/   ${False}   ${False}   EG_2S1408P9XC

TC21520-Unblock--Tagged LLDP False existing LIG with mismatch corrected in OneView side - Migrate
    [Tags]   OV300-API201
    [Documentation]   Tagged LLDP False in VC to be migrated into an existing LIG in OneView with enableTaggedLldp set from True to False.
    ...               This has dependency to the other TC21520 Test Compatibility (Not Migratable test case).
    ...               Tagged LLDP is NOT supported in VC <4.45
    ...               Expected result is Migratable
    ...               Expected result is ethernetSettings has enableRichTLV: false, enableTaggedLldp: false, lldpIpv4Address: "", lldpIpv6Address: ""
    ${response} =   Set LIG    LIG_2S1408P9XC_1   ethernetSettings   enableTaggedLldp=False
    ${task} =   Wait For Task and Fail On Error  ${response}
    Report.SelectCompare   ${False}   ${OAVC_CREDENTIALS}   dataFiles/   ${False}   ${False}   EG_2S1408P9XC
    Migrate
    Set Suite Variable   ${TC21520_Migrated}   ${True}
    GetOVResources.Write.SelectCompare   ResourcesList=${AllResourcesCommonList}   ligSelectDataFile=../resources/private/LLDPtlv-200-US39378/lig.conf

TC21519--Tagged LLDP True existing LIG with mismatch - Test Compatibility
    [Documentation]   Tagged LLDP True in VC to be migrated into an existing LIG in OneView with enableTaggedLldp false
    ...               Tagged LLDP is NOT supported in VC <4.45
    ...               This test case depends on TC21520
    ...               Expected result is Not Migratable
    Run Keyword If   "${TC21520_Migrated}" == "${False}"   CleanOV.CleanVC.ConfigVC.Migrate   ${vc_config_file_lldpFalse}
    ...    ELSE IF   "${TC21520_Migrated}" == "${True}"   Log to console and logfile     \n Preparing to run against existing EG and LIG in OneView...
    ...    ELSE      Fail   msg=This test case depends on TC21520
    Delete OneView Selected    ${delResList}
    Import VC
    Config VC    ${vc_config_file_lldpTrue}
    Report.SelectCompare   ${False}   ${OAVC_CREDENTIALS}   dataFiles/   ${False}   ${False}   EG_2S1408P9XC

TC21519-Unblock--Tagged LLDP True existing LIG with mismatch corrected in OneView side - Migrate
    [Tags]   OV300-API201
    [Documentation]   Tagged LLDP True in VC to be migrated into an existing LIG in OneView with enableTaggedLldp set from False to True.
    ...               This has dependency to the other TC21519 Test Compatibility (Not Migratable test case).
    ...               Tagged LLDP is NOT supported in VC <4.45
    ...               Expected result is Migratable
    ...               Expected result is ethernetSettings has enableRichTLV: true, enableTaggedLldp: false, lldpIpv4Address: "", lldpIpv6Address: ""
    ${response} =   Set LIG    LIG_2S1408P9XC_1   ethernetSettings   enableTaggedLldp=True
    ${task} =   Wait For Task and Fail On Error  ${response}
    Report.SelectCompare   ${False}   ${OAVC_CREDENTIALS}   dataFiles/   ${False}   ${False}   EG_2S1408P9XC
    Migrate
    GetOVResources.Write.SelectCompare   ResourcesList=${AllResourcesCommonList}   ligSelectDataFile=../resources/private/LLDPtlv-200-US39378/lig.conf

