*** Settings ***
Documentation     Unsupported VC CLI which won't trigger any issues in report
...    Covers the following testcases:
...        3.1.1.25-ssl
...        3.1.2.2-banner
...        3.1.2.3-1-domain
...        3.1.2.15-ldap
...        3.1.2.16-ldap-group
...        3.1.2.19-log-target
...	   3.1.2.26-port-monitor
...        3.1.2.36-session-timeout
...        3.1.2.49-user-security
...        3.1.2.50-user
...        3.1.2.35-serverid
...        3.1.2.17 ldap-certificate
...        3.1.2.44 ssl-certificate
...        [1]3.1.2.24 network
...        [1]3.1.1.24 ssh
...        3.1.4.1 Stacking Link Escalation
...        [1]3.1.4.2 PacketBufferOverallocationRatio (Requires Starkey and Tyndall)
...        3.1.2.33--radius-group
...        3.5.2 Already Managed by OneView
...

Library           RoboGalaxyLibrary
Library           FusionLibrary
Library           ../lib/WPSTUtil.py

Variables                       ../resources/defaults.py
Variables                       ../resources/credentials_wpst.py   Austin4
Force Tags                      Austin4
Resource                        ../resources/keywords.txt
Suite Setup                     Suite Setup Tasks
Test Setup                      Test Setup Tasks

*** Variables ***
@{testResourcesList}     ethNets  fcNets  fcoeNets  networkset  lig  encGrp  profiles  users
@{bayNumbers}            3   4

*** Test Cases ***
TC0--Preparing the environment: Clean OV, Clean VC, import enclosure
    [Tags]   CleanOV.CleanVC
    CleanOV.CleanVC

#-----------VC CLI commands won't trigger any issue in report---------
# migrate to OV to verify not breaking anything
TC1--Unsupported but migratable configuration and compatibility report
    [Documentation]   Import Enclosure and Perform Testing for Unsupported but migratable config
    @{commands}     Create List    import enclosure username=${OA_CREDENTIAL_DATA['oaUsername']} password=${OA_CREDENTIAL_DATA['oaPassword']}
    Run CLI In Interconnect From List    ${commands}
    # Detect, reset and sleep to workaround QXCR1001467844: Utah module in No Comm state after import enclosure
    Check Interconnect In No Comm And Recover If Needed   ${bayNumbers}
    Report.Write.Compare

TC1--Unsupported but migratable configuration and compatibility report
    [Documentation]   Import Enclosure and Perform Testing for Unsupported but migratable config
    @{commands}    Create List    set ssl -quiet Strength=strong    #3.1.1.25-ssl
    ...    set ldap enabled=true serveraddress=192.168.3.1     #3.1.2.15-ldap
    ...    add banner text="WPST_VC VC Migration Manager"      # 3.1.2.2-banner, NOTE: We are getting High severity issue for OV-2.00 but NO issue for OV-1.20
    ...    set domain name=VC_MIGRATION                        #3.1.2.3-1-domain
    ...    set ldap enabled=true serveraddress=192.168.3.1
    ...    add ldap-group my_ldap_group Description=xxx Roles=domain
    ...    add log-target Destination=192.168.2.2 port=600 Format=ISO8601 State=enabled
    # 3.1.2.26-port-monitor
    ...    add port-monitor AnalyzerPort=enc0:1:X2 Speed=Auto Duplex=Full
    ...    add port-monitor MonitorPort=enc0:1:d1 Direction=Both
    ...    set port-monitor Enabled=true
    # 3.1.2.36-session-timeout
    ...    set session Timeout=0
    # 3.1.2.49-user-security
    ...    set user-security StrongPasswords=enabled minpasswordlength=10
    # 3.1.2.50-user
    ...    add user Storage Password=Compaq1234 Roles=Storage
    ...    set user Storage Password=Compaq12345 Roles=Storage
    # 3.1.2.35-serverid
    ...    set serverid type=vc-defined
    # 3.1.2.17 ldap-certificate
    # TODO: This started to fail now and I'm still checking with Anju what's
    # going on and if we have to move this or continue using it. -jason
    #...    load ldap-certificate address=ftp://test:root@15.186.27.24/ldap.cer
    # [1]3.1.1.24 ssh
    ...    load ssh address=sftp://testuser:compaq@15.178.211.147/upload/ssh_key.pub
    # [1]3.1.4.2 PacketBufferOverallocationRatio - this option is only
    # supported in Starkey and Tyndall. Deprecated in all other modules.
    ...    set advanced-networking -quiet PacketBufferOverallocationRatio=3
    # 3.1.2.33--radius-group
    ...    add radius-group MyGroup Description="Test Group" Roles=network,storage
    ...    set radius-group MyGroup Description="Test Group" Roles=network,storage
    Run CLI In Interconnect From List    ${commands}
    Sleep       10 sec

TC1--Stacking Link Escalation
    [Documentation]   The VC command in this test case requires <VC 4.30
    @{commands}    Create List    
    # 3.1.4.1 Stacking Link Escalation
    ...    set stackinglink StatusEscalation=true DomainStackingMode=Full
    Run CLI In Interconnect From List    ${commands}

TC1--VC below 4.45 version dependent
    [Tags]   BelowVC445
    [Documentation]   The VC command in this test case is already set by default in VC 4.45
    @{commands}    Create List    
    # 3.1.2.44 ssl-certificate
    ...    load ssl-certificate address=ftp://test:root@15.186.27.24/signedCA.crt
    Run CLI In Interconnect From List    ${commands}

TC1-WithoutBannerWarning--Generate Compatibility Report for OV 1.20
    [Tags]   OV120
    [Documentation]   Generate Compatibility Report
    @{commands}    Create List    
    ...    show config
    Run CLI In Interconnect From List    ${commands}
    Report.Write.Compare

TC1-WithBannerWarning--Generate Compatibility Report for OV 2.00
    [Tags]   OV200
    [Documentation]   Generate Compatibility Report
    @{commands}    Create List    
    ...    show config
    Run CLI In Interconnect From List    ${commands}
    Report.Write.Compare

TC2--Add some networks with color - Test Compatibility Report
    [Documentation]   Separate network config as this affect the network count
    ...               and will cause report json compare to fail if in TC1
    # [1]3.1.2.24 network
    @{commands}    Create List    add network NetRed -quiet Color=red Labels="Red"
    ...    add network NetGreen -quiet Color=green Labels="Green"
    ...    add network NetBlue -quiet Color=blue Labels="Blue"
    ...    add network NetOrange -quiet Color=orange Labels="Orange"
    ...    add network NetPurple -quiet Color=purple Labels="Purple"
    ...    show config
    Run CLI In Interconnect From List    ${commands}
    Report.Write.Compare

TC2--Add some networks with color - Migrate
    [Documentation]   Separate network config as this affect the network count
    ...               and will cause report json compare to fail if in TC1
    # [1]3.1.2.24 network
    Migrate
    GetOVResources.Write.Compare   ${testResourcesList}

TC2--Validate test case 3.1.2.49-user-security
    [Documentation]   Validate 3.1.2.49-user-security by creating user with
    ...               8-character password, it should pass to confirm that
    ...               minpasswordLength we set was ignored
    @{userRoles} =    Create List   Read only
    ${user} =    Create Dictionary   userName=Testuser  password=Testuser  fullName=Testuser  roles=${userRoles}  emailAddress=testuser\@hp.com  officePhone=1231231234  mobilePhone=2342342345  type=UserAndRoles
    ${resp} =    Fusion Api Add User   body=${user}

TC3-3.5.2--Already Managed by OneView
   [Documentation]   Verify migration of an enclosure already managed by OneView
   ...               The expected result is error during compatibility report of something like:
   ...               Unable to test the compatibility of migrating enclosure: 15.178.210.237 
   ...               Unable to retrieve the Virtual Connect configuration from the specified enclosure. No further validation is possible. Unable to read the Virtual Connect Manager configuration IP address 15.186.28.30.
   ...               Resolution: Provide a valid username, password, and IP address combination. Verify network connectivity and parameters before retrying the operation.
   Create Report and Validate Response with Retry   Error   FAIL_TO_READ_DATA_FROM_VCM_MANAGED_BY_ONEVIEW   None

