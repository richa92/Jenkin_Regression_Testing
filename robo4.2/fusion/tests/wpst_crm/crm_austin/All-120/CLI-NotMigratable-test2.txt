*** Settings ***
Documentation     Unsupported CLI that has HW requirements
...    Covers the following testcases:
...        3.1.2.51-uplinkport (NOTE: only supported in OV 2.0)
...        3.1.2.7-1--enet-vlan
...        3.1.2.9--fc-connection
...        3.1.2.10--fcoe-connection
...        [1]3.4.5 Gen9 Servers

#  Make sure the enclosure has the following setup and update the CLI commands  accordingly
#    enet uplink port: enc0:1:X1
#    enet uplink port: enc0:2:X1
#    FCoE SAN uplink port: enc0:1:X1
#    FCoE SAN uplink port: enc0:2:X1
#    FC uplink port: enc0:3 port 1
#    FC uplink port: enc0:4 port 1
#    Server bay:  9

######## IMPORTANT NOTES #######
# [1] When executing this test in OV 1.20, you have to use --exclude OV200
# [2] When executing this test in VC 4.4x, you have to use --exclude VC43X
# [3] When executing this test in OV 2.00, you have to use --exclude OV120

Library           RoboGalaxyLibrary
Library           FusionLibrary
Library           ../lib/WPSTUtil.py

Variables                       ../resources/defaults.py
Variables                       ../resources/credentials_wpst.py   Austin4
Force Tags                      Austin4
Resource                        ../resources/keywords.txt
Suite Setup                     Suite Setup Tasks
Test Setup                      Test Setup Tasks

*** Variables ***
@{bayNumbers}              3   4

*** Test Cases ***
TC0--CleanOV and CleanVC
    [Tags]  CleanOV.CleanVC
    CleanOV.CleanVC

TC0--Import Enclosure
    [Tags]  ImportEnclosure
    @{commands}     Create List    import enclosure username=${OA_CREDENTIAL_DATA['oaUsername']} password=${OA_CREDENTIAL_DATA['oaPassword']}
    Run CLI In Interconnect From List    ${commands}

3.1.2.51-uplinkport
    [Tags]  OV200
    [Documentation]    Migration Manager should report a Critical(later changed to High) issue for speed disabled on uplink ports.
    ...                QXCR1001378100 was raised and fixed in 2.0 Pass 25. Making this a OV200 test only.
    ...                It appear though that severity of issue remained at High instead of Critical per the CR requested. Sean(reviewer) confirmed this.
    @{commands_on}     Create List   add network net1
    ...                              add uplinkport enc0:1:X1 Network=net1 Speed=Disabled
    @{commands_off}    Create List   remove uplinkport enc0:1:X1 Network=net1
    ${validation}      Create Dictionary   errorCode=FEATURE_ISSUE_UPLINK_PORT_DISABLED_IN_UNSHARED_NETWORK
    ...                                    level=High
    ${filter}       Create Dictionary      name=net1
    Run CLI In Interconnect From List    ${commands_on}
    Validate Detail Report If Issues Exists with Filter     oneViewResourceCompatibility/networks/networks    ${validation}   ${filter}
    Run CLI In Interconnect From List    ${commands_off}
    Validate Detail Report If Issues Removed with Filter    oneViewResourceCompatibility/networks/networks    ${validation}   ${filter}

3.1.2.7-1--enet-vlan
    [Documentation]  set enet-vlan SharedServerVLanID=true
    @{commands_on}     Create List
    ...    poweroff server enc0:9
    ...    set enet-vlan SharedServerVLanID=false
    ...    add uplinkset SUS
    ...    add uplinkport enc0:2:X1 UplinkSet=SUS
    ...    add network-range -quiet UplinkSet=SUS nameprefix=Network_ vlanids=10,12
    ...    add network net20
    ...    add uplinkport enc0:1:X1 Network=net20
    ...    add profile Profile_1 -NoDefaultEnetConn -NoDefaultFcConn -NoDefaultFcoeConn -quiet
    ...    add enet-connection Profile_1 Network=net20
    ...    add enet-connection Profile_1 Network=Unassigned
    ...    add server-port-map Profile_1:2 Network_10 VLanID=80
    ...    add server-port-map Profile_1:2 Network_12 VLanID=82
    ...    assign profile Profile_1 enc0:9
    @{commands_off}     Create List
    ...     unassign profile Profile_1
    ...     remove profile Profile_1
    ...     remove network net20
    ...     remove network-range -quiet UplinkSet=SUS vlanids=10,12
    ...     remove UplinkSet SUS
    ...     set enet-vlan SharedServerVLanID=true
    ${validation}          Create Dictionary   errorCode=FEATURE_ISSUE_PROFILE_CONNECTION_MAPS_SERVER_VLANS_TO_ALTERNATE_NETWORKS_VLANS 
    ...                                        level=Critical
    ${filter}       Create Dictionary       name=Profile_1
    Run CLI In Interconnect From List    ${commands_on}
    Validate Detail Report If Issues Exists with Filter     oneViewResourceCompatibility/profiles/serverProfiles    ${validation}   ${filter}
    Run CLI In Interconnect From List    ${commands_off}
    Validate Detail Report If Issues Removed with Filter    oneViewResourceCompatibility/profiles/serverProfiles    ${validation}   ${filter}


#3.1.2.7-2-enet-vlan
# not automated because 4K VLAN causes "Test compatibility" to fail, it cannot
# generate a report.  I don't think RG libraries currently support this failure
# mode.
#        [Documentation]  set enet-vlan VlanCapacity=Expanded
#        Set Suite Variable              ${errorCode}    FEATURE_CODE_ISSUE_tbd
#        Set Suite Variable              ${level}        Critical
#        ${validation}          Create Dictionary   errorCode=${errorCode}
#        ...                                                     level=${level}
#        Run VC CLI Not Supported And Expect Issue  ${4K_CLI}   ${validation}
#        Run VC CLI And Expect Issue Resolved  ${4K_DIS_CLI}    ${validatio

#3.1.2.6-2-enet-connection
# not automated because enet-connection errors are in the profile section
#        [Documentation]  add enet-connection <profilename> pxeBootOrder=IPv4ThenIPv6
#        Set Suite Variable              ${errorCode}    FEATURE_ISSUE_PROFILE_WITH_BOOT_MODE_NOT_AUTO
#        Set Suite Variable              ${level}        Critical
#        ${validation}          Create Dictionary   errorCode=${errorCode}
#        ...                                                     level=${level}
#        Run VC CLI Not Supported And Expect Issue  ${ECON_PXE_CLI}   ${validation}
#        Run VC CLI And Expect Issue Resolved  ${ECON_PXE_DIS_CLI}    ${validation}

#3.1.2.6-3-enet-connection
# not automated because enet-connection errors are in the profile section
#        [Documentation]  add enet-connection <profilename> SpeedType=Disabled
#        Set Suite Variable              ${errorCode}    FEATURE_ISSUE_PROFILE_WITH_ETHERNET_CONNECTION_SPEED_TYPE_DISABLED
#        Set Suite Variable              ${level}        Critical
#        ${validation}          Create Dictionary   errorCode=${errorCode}
#        ...                                                     level=${level}
#        Run VC CLI Not Supported And Expect Issue  ${ECON_DIS_CLI}   ${validation}
#        Run VC CLI And Expect Issue Resolved  ${ECON_DIS_DIS_CLI}    ${validation}

#3.1.2.6-4-enet-connection
# not automated because it leaves the profile assigned to the server.
# need cleanup code.
#        [Documentation]  add enet-connection <profilename> iScsiMAC=
#        Run VC CLI Not Supported   ${ECON_ISCSI_MAC_CLI}

3.1.2.9--fc-connection - Below OV300
    [Tags]   BelowOV300
    [Documentation]  add fc-connection <ProfileName> Fabric=<FabricName> Speed=1Gb (aka 3.1.2.9-1-fc-connection.txt)
    @{commands_on}     Create List
    ...     poweroff server enc0:9
    ...     add fabric SAN3 Type=FabricAttach Bay=3 Ports=1 Speed=Auto LinkDist=Manual
    ...     add fabric SAN4 Type=FabricAttach Bay=4 Ports=1 Speed=Auto LinkDist=Manual
    ...     add profile Profile_1 -NoDefaultEnetConn -NoDefaultFcConn -NoDefaultFcoeConn
    ...     add fc-connection Profile_1 Fabric=SAN3 Speed=1Gb
    ...     add fc-connection Profile_1 Fabric=SAN4 Speed=1Gb
    ...     assign profile Profile_1 enc0:9
    @{commands_off}     Create List
    ...     unassign profile Profile_1
    ...     remove profile Profile_1
    ...     remove fabric SAN3
    ...     remove fabric SAN4
    ${validation}          Create Dictionary   errorCode=FEATURE_ISSUE_FC_CONNECTION_OP_SPEED_ONE
    ...                                        level=Low
    ${filter}       Create Dictionary       name=Profile_1
    # Detect, reset and sleep to workaround QXCR1001467844: Utah module in No Comm state after import enclosure
    Check Interconnect In No Comm And Recover If Needed   ${bayNumbers}
    Run CLI In Interconnect From List    ${commands_on}
    Validate Detail Report If Issues Exists with Filter     oneViewResourceCompatibility/profiles/serverProfiles    ${validation}   ${filter}
    Run CLI In Interconnect From List    ${commands_off}
    Validate Detail Report If Issues Removed with Filter    oneViewResourceCompatibility/profiles/serverProfiles    ${validation}   ${filter}

3.1.2.9--fc-connection - OV300
    [Tags]   OV300
    [Documentation]  add fc-connection <ProfileName> Fabric=<FabricName> Speed=1Gb (aka 3.1.2.9-1-fc-connection.txt)
    @{commands_on}     Create List
    ...     poweroff server enc0:9
    ...     add fabric SAN3 Type=FabricAttach Bay=3 Ports=1 Speed=Auto LinkDist=Manual
    ...     add fabric SAN4 Type=FabricAttach Bay=4 Ports=1 Speed=Auto LinkDist=Manual
    ...     add profile Profile_1 -NoDefaultEnetConn -NoDefaultFcConn -NoDefaultFcoeConn
    ...     add fc-connection Profile_1 Fabric=SAN3 Speed=1Gb
    ...     add fc-connection Profile_1 Fabric=SAN4 Speed=1Gb
    ...     assign profile Profile_1 enc0:9
    @{commands_off}     Create List
    ...     unassign profile Profile_1
    ...     remove profile Profile_1
    ...     remove fabric SAN3
    ...     remove fabric SAN4
    ${validation}          Create Dictionary   errorCode=FEATURE_ISSUE_PROFILE_WITH_FC_SPEED_NOT_SUPPORTED_BY_ADAPTER
    ...                                        level=Medium
    ${filter}       Create Dictionary       name=Profile_1
    # Detect, reset and sleep to workaround QXCR1001467844: Utah module in No Comm state after import enclosure
    Check Interconnect In No Comm And Recover If Needed   ${bayNumbers}
    Run CLI In Interconnect From List    ${commands_on}
    Validate Detail Report If Issues Exists with Filter     oneViewResourceCompatibility/profiles/serverProfiles    ${validation}   ${filter}
    Run CLI In Interconnect From List    ${commands_off}
    Validate Detail Report If Issues Removed with Filter    oneViewResourceCompatibility/profiles/serverProfiles    ${validation}   ${filter}

3.1.2.9-speedDisabled--fc-connection
    [Tags]  VC43X
    [Documentation]  add fc-connection <ProfileName> Fabric=<FabricName> Speed=Disabled (aka 3.1.2.9-2-fc-connection.txt)
    ...              This tescase has been tagged as VC43X because Utah, Ocho,
    ...              Sheppard, and Hill (well, Utah and Ocho in particular because those are the
    ...              ones in Austin4) doesn't allow Speed=Disabled starting VC 4.4X. Thus, unless we'll 
    ...              find setup with Omaha and use that for this testcase to run VC 4.4X we can't run this.
    ...              Consulted George about this and he agree that it's not worth it.
    ...              Just documenting out loud.
    @{commands_on}     Create List
    ...     poweroff server enc0:9
    ...     add fabric SAN3 Type=FabricAttach Bay=3 Ports=1 Speed=Auto LinkDist=Manual
    ...     add fabric SAN4 Type=FabricAttach Bay=4 Ports=1 Speed=Auto LinkDist=Manual
    ...     add profile Profile_1 -NoDefaultEnetConn -NoDefaultFcConn -NoDefaultFcoeConn
    ...     add fc-connection Profile_1 Fabric=SAN3 Speed=Disabled
    ...     add fc-connection Profile_1 Fabric=SAN4 Speed=Disabled
    ...     assign profile Profile_1 enc0:9
    @{commands_off}     Create List
    ...     unassign profile Profile_1
    ...     remove profile Profile_1
    ...     remove fabric SAN3
    ...     remove fabric SAN4
    ${validation}          Create Dictionary   errorCode=FEATURE_ISSUE_FC_CONNECTION_OP_SPEED_DISABLED
    ...                                        level=Critical
    ${filter}       Create Dictionary       name=Profile_1
    # Detect, reset and sleep to workaround QXCR1001467844: Utah module in No Comm state after import enclosure
    Check Interconnect In No Comm And Recover If Needed   ${bayNumbers}
    Run CLI In Interconnect From List    ${commands_on}
    Validate Detail Report If Issues Exists with Filter     oneViewResourceCompatibility/profiles/serverProfiles    ${validation}   ${filter}
    Run CLI In Interconnect From List    ${commands_off}
    Validate Detail Report If Issues Removed with Filter    oneViewResourceCompatibility/profiles/serverProfiles    ${validation}   ${filter}

3.1.2.10--fcoe-connection
    [Tags]    OV120
    [Documentation]  add fcoe-connection <ProfileName> FcoeNetwork=<FcoeNetName> (aka 3.1.2.10-1-fcoe-connection.txt)
    ...              This has been tagged as 120 because this config is supported in OV 2.00 and negative test will fail
    @{commands_on}     Create List
    ...     poweroff server enc0:9
    ...     add uplinkset Uplinkset_fcoe_1
    ...     add uplinkport enc0:1:X1 UplinkSet=Uplinkset_fcoe_1 Speed=Auto
    ...     add network net_fcoe_1 -fcoe UplinkSet=Uplinkset_fcoe_1 VLANID=100
    ...     add uplinkset Uplinkset_fcoe_2
    ...     add uplinkport enc0:2:X1 UplinkSet=Uplinkset_fcoe_2 Speed=Auto
    ...     add network net_fcoe_2 -fcoe UplinkSet=Uplinkset_fcoe_2 VLANID=200
    ...     add profile Profile_1 -NoDefaultEnetConn -NoDefaultFcConn -NoDefaultFcoeConn
    ...     add fcoe-connection Profile_1 FcoeNetwork=net_fcoe_1
    ...     add fcoe-connection Profile_1 FcoeNetwork=net_fcoe_2
    ...     assign profile Profile_1 enc0:9
    ${validation}          Create Dictionary   errorCode=FEATURE_CODE_ISSUE_DUAL_HOP_FCOE
    ...                                        level=Critical
    Run CLI In Interconnect From List    ${commands_on}
    Validate Detail Report If General Issue Exists    incompatibilities/migrationGeneralIssues/issues    errorCode   ${validation}

3.1.2.10--fcoe-connection-off
    [Tags]    OV120
    [Documentation]  Turn off configs we set in 3.1.2.10--fcoe-connection
    ...              Tagging this as 120 because this is meant to clean up the prior testcase which is tagged as 120 as well.
    @{commands_off}     Create List
    ...    unassign profile Profile_1
    ...    remove profile Profile_1
    ...    remove network net_fcoe_1
    ...    remove network net_fcoe_2
    ...    remove uplinkset Uplinkset_fcoe_1
    ...    remove uplinkset Uplinkset_fcoe_2
    ${validation}          Create Dictionary   errorCode=FEATURE_CODE_ISSUE_DUAL_HOP_FCOE
    ...                                        level=Critical
    Run CLI In Interconnect From List    ${commands_off}
    Validate Detail Report If General Issues Removed    incompatibilities/migrationGeneralIssues/issues    ${validation}

3.1.2.10--fcoe-connection-SpeedType-Disabled
    [Documentation]  fcoe-connection <ProfileName> SpeedType=Disabled (aka 3.1.2.10-3-fcoe-connection.txt)
    ...              This testcase is not limited to a certain OV version.
    @{commands_on}     Create List
    ...    poweroff server enc0:9
    ...    add fabric SAN1 Type=FabricAttach Bay=1 Ports=3 Speed=Auto LinkDist=Manual
    ...    add fabric SAN2 Type=FabricAttach Bay=2 Ports=3 Speed=Auto LinkDist=Manual
    ...    add profile Profile_1 -NoDefaultEnetConn -NoDefaultFcConn -NoDefaultFcoeConn
    ...    add fcoe-connection Profile_1 Fabric=SAN1 SpeedType=Disabled
    ...    add fcoe-connection Profile_1 Fabric=SAN2 SpeedType=Disabled
    ...    assign profile Profile_1 enc0:9
    @{commands_off}     Create List
    ...    unassign profile Profile_1
    ...    remove profile Profile_1
    ...    remove fabric SAN1
    ...    remove fabric SAN2
    ${validation}          Create Dictionary   errorCode=FEATURE_ISSUE_PROFILE_WITH_FCOE_CONNECTION_SPEED_TYPE_DISABLED
    ...                                        level=Critical
    ${filter}       Create Dictionary       name=Profile_1
    # Detect, reset and sleep to workaround QXCR1001467844: Utah module in No Comm state after import enclosure
    Check Interconnect In No Comm And Recover If Needed   ${bayNumbers}
    Run CLI In Interconnect From List    ${commands_on}
    Validate Detail Report If Issues Exists with Filter      oneViewResourceCompatibility/profiles/serverProfiles    ${validation}   ${filter}
    Run CLI In Interconnect From List    ${commands_off}
    Validate Detail Report If Issues Removed with Filter     oneViewResourceCompatibility/profiles/serverProfiles    ${validation}   ${filter}

3.4.5--Gen9 Servers
    [Tags]   OV120
    [Documentation]   US30243 IO Module and Server Validation: Unsupported server with profile assigned
    ...               This is a OneView 1.20 testcase only because 2.00 supports Gen9 server.
    @{commands_on}     Create List
    ...                add network gen9_net1
    ...                add network gen9_net2
    ...                add profile Profile_16 -NoDefaultEnetConn -NoDefaultFcConn -NoDefaultFcoeConn -quiet NAG=Default HideUnusedFlexNICs=true BootMode=UEFI
    ...                add enet-connection Profile_16 Network=gen9_net1
    ...                add enet-connection Profile_16 Network=gen9_net2
    ...                assign profile Profile_16 enc0:16
    @{commands_off}    Create List
    ...                unassign profile Profile_16
    ...                remove profile Profile_16
    ...                remove network gen9_net1
    ...                remove network gen9_net2
    ${validation1}          Create Dictionary   errorCode=FEATURE_ISSUE_THE_PROFILE_WITH_SERVER_NOT_SUPPORTED
    ...                                        level=Critical
    ${filter}       Create Dictionary       name=Profile_16
    Run CLI In Interconnect From List    ${commands_on}
    ${resp}  ${resp_state}      ReportDetail
    Validate Existing Detail Report If Issues Exists with Filter    ${resp}   oneViewResourceCompatibility/profiles/serverProfiles    ${validation1}   ${filter}

    ${validation2}          Create Dictionary   errorCode=FEATURE_ISSUE_PROFILE_WITH_BOOT_MODE_NOT_AUTO
    ...                                        level=Critical
    ${filter}       Create Dictionary       name=Profile_16
    Validate Existing Detail Report If Issues Exists with Filter    ${resp}   oneViewResourceCompatibility/profiles/serverProfiles    ${validation2}   ${filter}

    Run CLI In Interconnect From List    ${commands_off}
    ${resp}  ${resp_state}      ReportDetail
    Validate Existing Detail Report If Issues Removed with Filter   ${resp}   oneViewResourceCompatibility/profiles/serverProfiles    ${validation1}   ${filter}
    Validate Existing Detail Report If Issues Removed with Filter   ${resp}   oneViewResourceCompatibility/profiles/serverProfiles    ${validation2}   ${filter}

