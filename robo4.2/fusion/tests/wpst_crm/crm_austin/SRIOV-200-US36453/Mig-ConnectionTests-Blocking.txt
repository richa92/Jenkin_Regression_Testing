*** Settings ***
Documentation     US36453 Block SR-IOV phase 2 feature
...               Migration should be blocked when the
...               SR-IOV type is either custom or combination of customand all types.
...    Covers the following testcases:
...        17461--All Ethernet connections set to Custom
...        17462--Combination of the all connection types

#Make sure the enclosure has the following setup or update the Config files accordingly
#    enet uplink port: enc0:1:X1
#    enet uplink port: enc0:2:X1

Library           RoboGalaxyLibrary
Library           FusionLibrary
Library           ../lib/WPSTUtil.py

Variables                       ../resources/defaults.py
Variables                       ../resources/credentials_wpst.py   Austin3
Force Tags                      Austin3   SR-IOV   SRIOV
Resource                        ../resources/keywords.txt
Suite Setup                     Suite Setup Tasks
Test Setup                      Test Setup Tasks

*** Variables ***
${all_enet_sriov_vc_config_file_on}     config/CLI-Blocking-All-SRIOV-Enet-Connection-Types-On.txt
${all_enet_sriov_vc_config_file_off}     config/CLI-Blocking-All-SRIOV-Enet-Connection-Types-Off.txt

*** Test Cases ***
Test00--Preparing the environment: Clean OV, Clean VC
    [Tags]  CleanOV.CleanVC
    [Documentation]  System Reset and import enclosure
    CleanOV.CleanVC

Test00--Import Enclosure
    [Tags]  ImportEnclosure
    [Documentation]  Import Enclosure
    @{commands}     Create List    import enclosure username=${OA_CREDENTIAL_DATA['oaUsername']} password=${OA_CREDENTIAL_DATA['oaPassword']}
    Run CLI In Interconnect From List    ${commands}

Test01--Ethernet connections set to Custom--Combination of the all connection types
    [Documentation]     Testing the all Ethernet connections SRIOV-type set to Custom
    ...                 and  combination of the all Ethernet connection SR-IOV types.
    [Tags]  SRIOV
    ${validation}           Create Dictionary   errorCode=FEATURE_ISSUE_SRIOV_IS_CUSTOM
    ...                                         level=Critical
    ${filter1}       Create Dictionary       name=Profile_SRIOV_custom
    ${filter2}       Create Dictionary       name=Profile_SRIOV_all
    Config VC  ${all_enet_sriov_vc_config_file_on}
    ${resp}  ${resp_state}      ReportDetail
    Validate Existing Detail Report If Issues Exists with Filter    ${resp}    oneViewResourceCompatibility/profiles/serverProfiles    ${validation}   ${filter1}
    Validate Existing Detail Report If Issues Exists with Filter    ${resp}    oneViewResourceCompatibility/profiles/serverProfiles    ${validation}   ${filter2}
    Config VC  ${all_enet_sriov_vc_config_file_off}
    ${resp}  ${resp_state}      ReportDetail
    Validate Existing Detail Report If Issues Removed with Filter   ${resp}    oneViewResourceCompatibility/profiles/serverProfiles    ${validation}   ${filter1}
    Validate Existing Detail Report If Issues Removed with Filter   ${resp}    oneViewResourceCompatibility/profiles/serverProfiles    ${validation}   ${filter2}

