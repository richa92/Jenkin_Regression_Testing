*** Settings ***
Documentation     
...    Covers the following testcases:
...          3.4.4 Profile assigned to empty bay
...          3.4.5 Baremetal Enclosure
...          3.4.6 Null(Default) VC configuration

Library           RoboGalaxyLibrary
Library           FusionLibrary
Library           ../lib/WPSTUtil.py

Variables                       ../resources/defaults.py
Variables                       ../resources/credentials_wpst.py   Austin1
Force Tags                      Austin1
Resource                        ../resources/keywords.txt
Suite Setup                     Suite Setup Tasks
Test Setup                      Test Setup Tasks

*** Variables ***

*** Test Cases ***
Test00--Preparing the environment: Clean OV, Clean VC
    [Tags]  CleanOV.CleanVC
    CleanOV.CleanVC

Test00--Preparing the environment: import enclosure
    [Tags]  import
    @{commands}     Create List    import enclosure username=${OA_CREDENTIAL_DATA['oaUsername']} password=${OA_CREDENTIAL_DATA['oaPassword']}
    Run CLI In Interconnect From List    ${commands}

3.4.4--Profile assigned to empty bay
    [Documentation]     The Migration manager will ignore a profile assigned to an empty bay.
    ...                 The Migration Manager should report a High error.
    @{commands_on}     Create List   poweroff server *
    ...                              add profile Profile_EMPTYBAY -NoDefaultEnetConn -NoDefaultFcConn -NoDefaultFcoeConn -quiet
    ...                              assign profile Profile_EMPTYBAY enc0:7
    @{commands_off}     Create List  unassign profile Profile_EMPTYBAY
    ...                              remove profile Profile_EMPTYBAY
    ${validation}      Create Dictionary   errorCode=FEATURE_ISSUE_PROFILE_ASSIGNED_TO_EMPTY_BAY
    ...                              level=High
    ${filter}          Create Dictionary       name=Profile_EMPTYBAY
    Run CLI In Interconnect From List    ${commands_on}
    Validate Detail Report If Issues Exists with Filter      oneViewResourceCompatibility/profiles/serverProfiles    ${validation}   ${filter}
    Run CLI In Interconnect From List    ${commands_off}
    Validate Detail Report If Issues Removed with Filter     oneViewResourceCompatibility/profiles/serverProfiles    ${validation}   ${filter}

3.4.5--Baremetal Enclosure
    [Documentation]     US30242 : Requirement 5
    ...                 Verify migration of an enclosure which has not been imported into VC or OneView. 
    ...                 VC Migration Validation task should fail with message 
    ...                 "Unable to retrieve the Virtual Connect configuration from the specified enclosure. No further validation is possible. 
    ...                  Unable to read the Virtual Connect Manager configuration IP address {vcmip}. 
    ...                  No Virtual Connect domain exists. Add the enclosure directly to OneView."
    CleanOV.CleanVC
    Create Report and Validate Response     Error   SERVICE_FAIL_TO_RETRIEVE_ENCLOSURE_CONFIGURATION    None

3.4.6--Default VC configuration Test Compatibility
    [Documentation]     Verify migration of an enclosure which has been imported into VC, but has a default configuration.
    ...                 Migration should be successful.
    Import VC
    Report.Write.Compare

3.4.6--Default VC configuration Migrate
    [Documentation]     Verify migration of an enclosure which has been imported into VC, but has a default configuration.
    ...                 Migration should be successful.
    Migrate
    GetOVResources.Write.Compare

