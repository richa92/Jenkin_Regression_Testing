*** Settings ***
Documentation     This testsuite is intended to commonize applicable blocking ALM's Limitations Testcases.
...               = Usage =
...               cmdline: pybot -d ../logs  --timestampoutputs -t <target test case> <target test file>
...
...               This covers the following test cases in ALM:
...                     20806 - FC connection speeds supported by VC but not OV

Library               RoboGalaxyLibrary
Library               FusionLibrary
Library               ../lib/WPSTUtil.py

Variables             ../resources/defaults.py
Variables             ../resources/credentials_wpst.py   Austin3
Force Tags            Austin3

Resource              ../resources/keywords.txt

Suite Setup                     Suite Setup Tasks
Test Setup                      Test Setup Tasks

*** Variables ***


*** Test Cases ***
TC0--CleanOV and CleanVC
    [Tags]   CleanOV.CleanVC
    CleanOV
    CleanVC

TC0--Import Enclosure in VC
    [Tags]    ImportEnclosure
    @{commands}     Create List    import enclosure username=${OA_CREDENTIAL_DATA['oaUsername']} password=${OA_CREDENTIAL_DATA['oaPassword']}
    Run CLI In Interconnect From List    ${commands}

HP-LPe1605-16Gb-FC-HBA-Unsupported-Speed--FC connection speeds supported by VC but not OV
    [Documentation]    Test if errorCode FEATURE_ISSUE_PROFILE_WITH_FC_SPEED_NOT_SUPPORTED_BY_ADAPTER will be generated by having
    ...                HP LPe1605 16Gb FC HBA for BladeSystem c-Class with and FC connection mapping to this MEZZ with the port speed set to 4 Gbs. 
    ...                The AdapterSpeedSupport class shows the migration manager checks HBAs with "16Gb FC HBA" in the HBA name for speeds of Auto and 8 Gbs
    ...                and HBAs with "4Gb FC HBA " in the HBA name for speeds of Auto, 2 Gbs and 4 Gbs.
    ...                This issue will be generated for these HBAs if the VC configured FC connection port speed is something other than these listed values.

    @{configVCCmds}     Create List    add network NetA -quiet NAGs=Default VLanTunnel=Disabled LacpTimer=Domain-Default
    ...                                add uplinkport enc0:1:X1 Network=NetA Speed=Auto
    ...                                add network NetB -quiet NAGs=Default VLanTunnel=Disabled LacpTimer=Domain-Default
    ...                                add uplinkport enc0:2:X1 Network=NetB Speed=Auto
    ...                                add fabric FC1 Type=FabricAttach Bay=5 Ports=1 Speed=Auto LinkDist=Manual
    ...                                add profile Profile_1 -NoDefaultEnetConn -NoDefaultFcConn -NoDefaultFcoeConn -quiet NAG=Default HideUnusedFlexNICs=true BootMode=Auto SriovMode=Advanced
    ...                                add enet-connection Profile_1 Network=NetA PXE=UseBIOS SriovType=Disabled
    ...                                add enet-connection Profile_1 Network=NetB PXE=UseBIOS SriovType=Disabled
    ...                                add fc-connection Profile_1 Fabric=FC1 Speed=4Gb
    ...                                assign profile Profile_1 enc0:1

    ${validation}           Create Dictionary   errorCode=FEATURE_ISSUE_PROFILE_WITH_FC_SPEED_NOT_SUPPORTED_BY_ADAPTER
    ...                                         level=Medium
    ${filter}       Create Dictionary           name=Profile_1

    Run CLI In Interconnect From List    ${configVCCmds}
    Validate Detail Report If Issues Exists with Filter   oneViewResourceCompatibility/profiles/serverProfiles    ${validation}   ${filter}

    # Migrate into OV; get all resources in OV; write to Json file and compare with saved Json file
    Migrate.GetOVResources.Write.Compare

