*** Settings ***
Documentation     Verify Command History Rolls over
...    = Usage =
...    | pybot | -L DEBUG | -v SWITCH_IP:<Your-Switch-IP> | command_history_rollover.txt | 
...    = Variables =
...    | SWITCH IP | Required; IP address or hostname of the Comware Switch |
...    = Preconditions =
...    None

Library           RoboGalaxyLibrary                                     # DVTs Robot Framework extensions
Library           FusionLibrary                                         # DVTs Robot Framework extensions

Library           String
Library           Collections
Resource          ../resources/defaults.txt                             # Comet-DVT default variables
Resource          ../resources/switch.txt                               # Comet-DVT keyword extensions
Force Tags        switch                                                # All test cases get these tags
Suite Setup       Login to Switch via SSH
Suite Teardown    Logout of Switch via SSH

*** Variables ***
@{Commands}=    display ntp-service status
...             display power
...             display default-configuration
...             display device manuinfo
...             display current-configuration
...             display boot-loader
...             display cpu-usage
...             display ddns policy
...             display clock
#...             display ftp-user
${Overflow Command}    display install active

*** Test Cases ***
Verify Full history buffer
	Execute Switch Commands    ${Commands}
	# Add display history command to list
	Append to List    ${Commands}    display history
	Log List    ${Commands}
	
	Set Suite Variable    ${Commands}    ${Commands}
	
	# Get Switch Command History
	${History Commands}=    Get Command History
	Log List    ${History Commands}
	
	# Verify
	Lists Should Be Equal    ${Commands}    ${History Commands}
	
Verify History Buffer Rollover
	# Insert additional commands to history
	Execute Switch Command    ${Overflow Command}
	Remove From List    ${Commands}    0
	Append to List      ${Commands}    ${Overflow Command}
	
	# Add display history command to list
	Remove From List    ${Commands}    0
	Append to List      ${Commands}    display history
	
	Set Suite Variable    ${Commands}    ${Commands}
	Log List    ${Commands}
	
	# Get Switch Command History
	${History Commands}=    Get Command History
	Log List    ${History Commands}
	
	# Verify
	Lists Should Be Equal    ${Commands}    ${History Commands}
   