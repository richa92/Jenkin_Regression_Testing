*** Settings ***
Documentation     Parse Comware Command
...    = Usage =
...    | pybot | -L DEBUG | -v SWITCH_IP:<Your-Switch-IP> | parse_comware_command.txt | 
...    = Variables =
...    | SWITCH IP | Required; IP address or hostname of the Comware Switch |
...    = Preconditions =
...    None

Library           RoboGalaxyLibrary                                     # DVTs Robot Framework extensions
Library           FusionLibrary                                         # DVTs Robot Framework extensions
Library           String
Library           Collections
Resource          ../resources/defaults.txt                             # Comet-DVT default variables
Resource          ../resources/switch.txt                               # Comet-DVT keyword extensions
Force Tags        switch                                                # All test cases get these tags
Suite Setup       Login to Switch via SSH
Suite Teardown    Logout of Switch via SSH

*** Variables ***

*** Test Cases ***
Get NTP Service Status
	${Buffer}=    Execute Switch Command    display ntp-service status
    ${NTP Service}=    Parse Key Value Buffer    ${Buffer}
    Log Dictionary    ${NTP Service}

Get Power Status
	${Buffer}=    Execute Switch Command    display power
    ${Output}=    Create Dictionary
    @{Devices}=    Create List    Slot 1    Power 1    Power 2
    :FOR    ${Device}    IN     @{Devices}
    \   ${Device}=    Replace String    ${Device}    ${SPACE}    \\s+
    \   ${match}    ${group1}=    Should Match Regexp    ${Buffer}    (?is)${Device}(.*?)(?:Power|<)
    \   ${Device Output}=    Parse Key Value Buffer    ${group1}
    \   Set To Dictionary     ${Output}     ${Device}    ${Device Output}
    Log Dictionary    ${Output}
    

Get Device Manufacture information
    ${Buffer}=    Execute Switch Command    display device manuinfo
    ${Output}=    Create Dictionary
    @{Devices}=    Create List    Slot 1    Fan 1    Fan 2    Power 1    Power 2
    :FOR    ${Device}    IN     @{Devices}
    \   ${match}    ${group1}=    Should Match Regexp    ${Buffer}    (?is)${Device}(.*?)(?:Fan \\d|Power \\d|<)
    \   ${Device Output}=    Parse Key Value Buffer    ${group1}
    \   Set To Dictionary     ${Output}     ${Device}    ${Device Output}
    Log Dictionary    ${Output}

*** Keywords ***

Parse Key Value Buffer
	[Arguments]    ${Buffer}    ${PROMPT}=${SWITCH_PROMPT}
    ${Output}=    Create Dictionary
    ${Buffer}=    Get Lines Containing String    ${Buffer}    :
    @{lines}=     Split To Lines    ${Buffer}
    :FOR    ${line}    IN    @{lines}
    \    ${key}    ${value}=    Split String    ${line}    :     1
    \    Set To Dictionary     ${Output}    ${key.strip()}    ${value.strip()}
    Log Dictionary    ${Output}
    [Return]    ${Output}