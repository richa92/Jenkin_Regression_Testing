*** Settings ***
Documentation   EGp0004 - Delete Enclosure Groups
Library         robot.api.logger
Resource        ../resource.txt
Test Setup      Load Multi Test Data and Open Browser
Test Teardown   Pause And Close Browser

*** Variables ***
${user}     Administrator


*** Test Cases ***
As an Administrator I want to Delete Enclosure Groups
    Fusion UI Login To Appliance    ${user}
    # pre-condition, create an LIG for creating EGs
    Console     ${SPACE * 4}--1. create a LIG
    ${data}=                        Get Data By Xpath                                       //ligs/lig
    ${rc}=                          Fusion Ui Create Logical Interconnect Group             @{data.lig}
    # only print a log, not have to be True since some previous tests may have created this LIG.
    # so for both saving time and guaranting pre-condition, will not fail if Create LIG return False
    # but let following steps fail the test if the required LIG does not exist.
    Console                         <Result of creating LIG is: [ ${rc} ]>

    ${data}=                        Get Data By Xpath                           //EGp0004/encgroup
    Console     ${SPACE * 4}--2. create an EG
    # pre-condition, add an enclosure group for later deleting
    ${rc}=                          Fusion Ui Create Enclosure Group            @{data.encgroup}
    Should Be True                  ${rc}
    ...                             msg=Failed to prepare the pre-condition - creating an enclosure group

    Console     ${SPACE * 4}--3. delete EG
    # try to delete the enclosure group, should be deleted successfully
    ${rc}=                          Fusion UI Delete Enclosure Group            @{data.encgroup}
    Should Be True                  ${rc}
    ...                             msg=Failed to delete enclosure group



