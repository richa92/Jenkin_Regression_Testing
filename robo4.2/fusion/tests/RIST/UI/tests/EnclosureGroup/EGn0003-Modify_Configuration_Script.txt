*** Settings ***
Documentation   EGn0003 - Modify Configuration Script
Library         robot.api.logger
Resource        ../resource.txt
Test Setup      Load Multi Test Data and Open Browser
Test Teardown   Pause And Close Browser

*** Variables ***
${user}     Administrator


*** Test Cases ***
As an Administrator I want to Modify Configuration Script with command in blacklist
    Fusion UI Login To Appliance        ${user}
    # pre-condition, create an LIG for creating EGs
    Console     ${SPACE * 4}--1. create a LIG
    ${data}=                            Get Data By Xpath               //ligs/lig
    ${rc}=                              Fusion Ui Create Logical Interconnect Group             @{data.lig}
    # only print a log, not have to be True since some previous tests may have created this LIG.
    # so for both saving time and guaranting pre-condition, will not fail if Create LIG return False
    # but let following steps fail the test if the required LIG does not exist.
    Console                             <Result of Create LIG is: [ ${rc} ]>

    # Create enclosure group
    Console     ${SPACE * 4}--2. create an EG
    ${data}=                            Get Data By Xpath               //EGn0003/CreateEG/encgroup
    ${rc}=                              Fusion Ui Create Enclosure Group                        @{data.encgroup}
    Should Be True                      ${rc}
    ...                                 msg=Failed to prepare the pre-condition - creating an enclosure group

    # Add enclosure for later validating
    Console     ${SPACE * 4}--3. add an enclosure
    ${enc}=                             Get Data By Xpath               //EGn0003/enclosure
    ${rc}=                              Fusion UI Add Enclosure                                 @{enc.enclosure}
    Run Keyword If                      '${rc}'!='${True}'      Fusion UI Remove Enclosure      @{enc.enclosure}
    Should Be True                      ${rc}                           msg=Failed to add enclosures

    # Modify configuration script
    Console     ${SPACE * 4}--4. validate Configuration Script with blacklist command
    ${data}=                            Get Data By Xpath               //EGn0003/ValidateConfigurationScript/encgroup
    ${rc}=                              Fusion Ui Validate Cannot Update Configuration Script With Command In Blacklist
    ...                                 @{data.encgroup}
    Run Keyword If                      '${rc}'!='${True}'      Fusion UI Remove Enclosure      @{enc.enclosure}
    Should Be True                      ${rc}                           msg=Failed to modify configuration script with command in blacklist

    # Update Logical Enclosure Update From Group
    Console     ${SPACE * 4}--5. update LE from EG
    ${data}=                            Get Data By Xpath               //EGn0003/logicalenclosure
    ${rc}=                              Fusion UI Logical Enclosure Update From Group           @{data.logicalenclosure}
    Run Keyword If                      '${rc}'!='${True}'      Fusion UI Remove Enclosure      @{enc.enclosure}
    Should Be True                      ${rc}   Logical Enclosure Failed to Update From Group

    # remove enclosure for other tests to add it using different LIG/EG
    Console     ${SPACE * 4}--6. remove enclosure
    ${rc}=                              Fusion UI Remove Enclosure                              @{enc.enclosure}
    Console                             <Result of Remove Enclosure is: [ ${rc} ]>
