*** Settings ***
Documentation   EGn0002 - Try to delete enclosure group that is used by enclosure, and verify if the expected error message occurs
Library         robot.api.logger
Resource        ../resource.txt
Test Setup      Load Multi Test Data and Open Browser
Test Teardown   Pause And Close Browser



*** Variables ***
${user}     Administrator


*** Test Cases ***
As an Administrator I want to Delete Enclosure Groups
    Fusion UI Login To Appliance        ${user}

    # pre-condition, create an LIG for creating EGs
    Console     ${SPACE * 4}--1. create a LIG
    ${data}=                            Get Data By Xpath                                       //ligs/lig
    ${rc}=                              Fusion Ui Create Logical Interconnect Group             @{data.lig}
    # only print a log, not have to be True since some previous tests may have created this LIG.
    # so for both saving time and guaranting pre-condition, will not fail if Create LIG returns False
    # but let following steps fail the test if the required LIG does not exist.
    Console                             <Result of Create LIG is: [${rc}]>

    # pre-condition, create an enclosure group to for later validating
    Console     ${SPACE * 4}--2. create an EG
    ${data}=                            Get Data By Xpath                                       //EGn0002/encgroup
    ${rc}=                              Fusion Ui Create Enclosure Group                        @{data.encgroup}
    Should Be True                      ${rc}
    ...                                 msg=Failed to prepare the pre-condition - creating an enclosure group

    # pre-condition, add an enclosure for later validating
    Console     ${SPACE * 4}--3. add an enclosure
    ${enc}=                             Get Data By Xpath                                       //EGn0002/enclosure
    ${rc}=                              Fusion UI Add Enclosure                                 @{enc.enclosure}
    Run Keyword If                      '${rc}'!='${True}'      Fusion UI Remove Enclosure      @{enc.enclosure}
    Should be True                      ${rc}                                                   msg=Failed to add enclosures

    Console     ${SPACE * 4}--4. validate if fusion won't allow to delete EG that is used by enclosure
    ${data}=                            Get Data By Xpath                                       //EGn0002/encgroup
    ${rc}=                              Fusion UI Validate Cannot Delete Enclosure Group Used By Enclosure
    ...                                 @{data.encgroup}
    Run Keyword If                      '${rc}'!='${True}'      Fusion UI Remove Enclosure      @{enc.enclosure}
    Should Be True                      ${rc}                                                   msg=Failed to validate 'cannot delete enclosure group used by enclosure'

    # remove enclosure for other tests to add it using different LIG/EG
    Console     ${SPACE * 4}--5. remove the enclosure
    ${rc}=                              Fusion UI Remove Enclosure                              @{enc.enclosure}
    Console                             <Result of Remove Enclosure is: [ ${rc} ]>


