*** Settings ***
Documentation   EGn0001 - validate creating enclosure group using existing name is not allowed
Library         robot.api.logger
Resource        ../resource.txt
Test Setup      Load Multi Test Data and Open Browser
Test Teardown   Pause And Close Browser

*** Variables ***
${user}     Administrator


*** Test Cases ***
As an Administrator I want to validate creating enclosure group using existing name is not allowed
    Fusion UI Login To Appliance    ${user}
    # pre-condition, create an LIG for creating EGs
    Console     ${SPACE * 4}--1. create a LIG
    ${data}=                        Get Data By Xpath                                       //ligs/lig
    ${rc}=                          Fusion Ui Create Logical Interconnect Group             @{data.lig}
    # only print a log, not have to be True since some previous tests may have created this LIG.
    # so for both saving time and guaranting pre-condition, will not fail if Create LIG return False
    # but let following steps fail the test if the required LIG does not exist.
    Console                         <Result of Create LIG is: [ ${rc} ]>

    ${data}=                        Get Data By Xpath                           //EGn0001/encgroup
    Console     ${SPACE * 4}--2. create an EG
    # pre-condition, add an enclosure group to use its name for adding enclosure group again
    ${rc}=                          Fusion Ui Create Enclosure Group            @{data.encgroup}
    Should Be True                  ${rc}
    ...                             msg=Failed to prepare the pre-condition - creating an existing enclosure group

    # create enclosure group again using the created group's name, check if expected error message occurss
    Console     ${SPACE * 4}--3. validate if Fusion won't allow to create EG using an existing name
    ${rc}=                          Fusion UI Validate Cannot Create Enclosure Group Using Existing Name
    ...                             @{data.encgroup}
    Should Be True                  ${rc}
    ...                             msg=Failed to validate 'cannot create enclosure group using existing name'



