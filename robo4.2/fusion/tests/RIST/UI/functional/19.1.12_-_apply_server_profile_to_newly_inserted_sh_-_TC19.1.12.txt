*** Settings ***
Documentation     Inserting a blade into an empty bay server profile
Library           RoboGalaxyLibrary
Library           Collections

Resource          ../../../robogalaxy/RoboGalaxyLibrary/resources/config_manager/oa_ssh_keywords_resource.txt
Suite Setup       Setup System for Test Case
Suite Teardown    Clean Up and Close all connections
Force Tags        regression   OVA-Qual  DCS  1.10  enclosure-test  hw_specific
***Variables***
#${DataFile}		          dcs110_data.xml
${DataFile}               fwtc71_data.xml

# Fusion Variables
#${ApplianceUrl}		      https://16.125.69.173
${ApplianceUrl}           https://16.125.76.128
${Browser}		          firefox
${SeleniumSpeed}	      0.5
@{FUSION_ENTITIES}        Server Profiles

# OA Variables
${TIMEOUT}                1 minutes

*** Test Cases ***
1. Create Server Profile with an Empty Bay
    [Documentation]     Creating Server Profile with bay ${SH1 Bay} if required
    ${status}    ${value}=    Run Keyword and Ignore Error      Verify Server Profile Attribute    name    ${Server Profile Name}
    Run Keyword if    '${status}'=='FAIL'     Fusion UI Create Server Profile    @{TestData.profiles}[0]
    Verify Server Profile Attribute    server hardware   ${SH1}

2. Insert Non Matching SHT (SH1) into the Empty Bay. Server Profile not applied
    [Documentation]     Inserting blade into bay ${SH1 Bay}
    Switch E-Fuse on    Blade    ${SH1 Bay}
    # Server power is not applicable
    Verify Server Profile Attribute    associated server     (empty)

3. Remove Server Hardware from bay
    [Documentation]     Removing blade from bay ${SH1 Bay}
    Switch E-Fuse off    Blade    ${SH1 Bay}
    Verify Server Profile Attribute    associated server     (empty)

4. Edit Server Profile Bay. Stored Server Hardware information is cleared.
    [Documentation]     Modifying Server Profile Bay to Bay ${SH2 Bay}
    ${Power}=  Fusion UI Get Server Profile Attributes     ${Server Profile Name}    server power
    Run Keyword If    '${Power}'=='On'    Fusion UI Power Off server      ${SH1}
    Fusion UI Edit Server Profile  @{TestData.editprofilebays}[1]
    Verify Server Profile Attribute    server hardware   ${SH2}
    Verify Server Profile Attribute    associated server     (empty)

5. Insert Matching SHT Server Hardware (SH2) into the Empty bay. Server Profile is applied.
    [Documentation]     Inserting blade into bay ${SH2 Bay}
    Switch E-Fuse on    Blade    ${SH2 Bay}
    Verify Server Profile Attribute    server hardware   ${SH2}
    Verify Server Profile Attribute    associated server     ${SH2 Serial Number}

6. Remove Server Hardware from bay.
    [Documentation]     Removing blade from bay ${SH2 Bay}
    Switch E-Fuse off    Blade    ${SH2 Bay}
    Verify Server Profile Attribute    server hardware     ${SH2} (empty)
	Verify Server Profile Attribute    associated server     ${SH2 Serial Number}

# 7. [manual] Insert Non Matching SHT (SH1) into bay. Server Profile is not applied.
# 8. [manual] Remove Server Hardware from bay.
# 9.  [manual] Insert different Matching SHT (SH3) into bay. Server Profile is not applied.
# 10. [manual] Remove Server Hardware from bay.

11. Reinsert Matching SHT Server Hardware (SH2) into bay. Server Profile is applied.
    [Documentation]     Inserting blade into bay ${SH2 Bay}
    Switch E-Fuse on    Blade    ${SH2 Bay}
    Verify Server Profile Attribute    server hardware   ${SH2}
    Verify Server Profile Attribute    associated server     ${SH2 Serial Number}

12. Edit Profile Bay to an empty bay. Stored Server Hardware information is cleared.
    [Documentation]     Modifying Server Profile Bay to Bay ${SH3 Bay}
    ${Power}=  Fusion UI Get Server Profile Attributes     ${Server Profile Name}    server power
    Run Keyword If    '${Power}'=='On'    Fusion UI Power Off server      ${SH2}
    Fusion UI Edit Server Profile  @{TestData.editprofilebays}[2]
    Verify Server Profile Attribute    server hardware   ${SH3}
    Verify Server Profile Attribute    associated server     (empty)

13.1. Remove Server Hardware from bay.
    [Documentation]     Removing blade from bay ${SH2 Bay}
    Switch E-Fuse off    Blade    ${SH2 Bay}

13. Insert (Other) Matching Blade (SH3) into bay. Server Profile is applied.
    [Documentation]     Inserting blade into bay ${SH3 Bay}
    Switch E-Fuse on    Blade    ${SH3 Bay}
    Verify Server Profile Attribute	   server hardware       ${SH3}
    Verify Server Profile Attribute    associated server     ${SH3 Serial Number}

14. Remove Server hardware from bay.
    [Documentation]     Removing blade from bay ${SH3 Bay}
    Switch E-Fuse off    Blade    ${SH3 Bay}
    Verify Server Profile Attribute    server hardware       ${SH3} (empty)
    Verify Server Profile Attribute    associated server     ${SH3 Serial Number}	
    Sleep       5 minutes

# Currently being ommitted. These steps test a Rip-and-Replace bay behavior.
# 15. Edit Profile Bay to an empty bay. Stored Server Hardware infromation is cleared.
#     [Documentation]     Modifying Server Profile Bay to Bay ${SH2 Bay}
#     ${Power}=  Fusion UI Get Server Profile Attributes     ${Server Profile Name}    server power
#     Run Keyword If    '${Power}'=='On'    Fusion UI Power Off server      ${SH3}
#     Fusion UI Edit Server Profile  @{TestData.editprofilebays}[1]
#     Verify Server Profile Attribute    server hardware   ${SH2}
#     Verify Server Profile Attribute    associated server     (empty)

# 16. Insert Matching SHT Server Hardware (SH2) into bay. Server Profile is applied.
#     [Documentation]     Inserting blade into bay ${SH2 Bay}
#     Switch E-Fuse on    Blade    ${SH2 Bay}
#     Verify Server Profile Attribute	   server hardware       ${SH2}
#     Verify Server Profile Attribute    associated server     ${SH2 Serial Number}

*** Keywords ***

Verify Server Profile Attribute
    [Arguments]     ${attribute}    ${value}
    Capture Page Screenshot
    ${Got value}=  Fusion UI Get Server Profile Attributes     ${Server Profile Name}    ${attribute}
    Should be Equal     ${Got value}      ${value}    msg="${attribute} is not ${value}"

# SETUP Keywords

Setup System for Test Case
    Load Test Data and Open Browser
    Get Server Profile Test Info
    Log into Fusion appliance as Administrator
    Log into C7000 system
    Get Blade Serial Numbers
    Switch off all test blades
    
Load Test Data and Open Browser
    Set Log Level    TRACE
	Load Test Data  ${DataFile}
    Log Variables
	Open Browser  ${ApplianceUrl}  ${Browser}
	Run Keyword If  "${Browser}" == "ie"   Go To  javascript:document.getElementById('overridelink').click()
	Set Selenium Speed  ${SeleniumSpeed}

Get Server Profile Test Info
    # Bay of blade of specific type
    Set Suite Variable     ${SH1}       ${TestData.editprofilebays[0].newServerHardware}
    Set Suite Variable     ${SH1 Bay}   ${TestData.editprofilebays[0].serverHardwareBay}
    Log     "Invalid Server Hardware Type Blade in Bay: ${SH1 Bay}"

    # Bay of blade with a type different to SH1
    Set Suite Variable     ${SH2}       ${TestData.editprofilebays[1].newServerHardware}
    Set Suite Variable     ${SH2 Bay}   ${TestData.editprofilebays[1].serverHardwareBay}
    Log     "Valid Server Hardware Type Blade in Bay: ${SH2 Bay}"

    # Bay of blade with a same type as SH2
    Set Suite Variable     ${SH3}       ${TestData.editprofilebays[2].newServerHardware}
    Set Suite Variable     ${SH3 Bay}   ${TestData.editprofilebays[2].serverHardwareBay}
    Log     "Valid Server Hardware Type with different UUID in Bay: ${SH3 Bay}"

    # Set server profile name as a variable
    Set Suite Variable    ${Server Profile Name}   ${TestData.editprofilebays[1].profile}

Log into C7000 system
    Set Suite Variable     ${ADMIN DEFAULT PROMPT}         ${TestData.envconfigs.oa1[0].prompt}>
    Login to OA via SSH     ${TestData.envconfigs.oa1[0].ip}    ${TestData.envconfigs.oa1[0].username}    ${TestData.envconfigs.oa1[0].password}    ${TestData.envconfigs.oa1[0].prompt}>

    Set Default Configuration       timeout=${TIMEOUT}

Get Blade Serial Numbers
    # Blade 1
    Log         Get Blade ${SH1 Bay} Serial Number
    ${buffer}=      Execute OA CLI Command      Show Blade Info ${SH1 Bay}
    Should not Contain      ${buffer}    No Server Blade Installed    msg=No blade in bay ${SH1 Bay}
    ${lines}=       Get Lines Containing String     ${buffer}   Serial Number
    ${ret}=         Fetch from Right        ${lines.strip()}    ${SPACE}
    Set Suite Variable      ${SH1 Serial Number}    ${ret}

    # Blade 2
    Log         Get Blade ${SH2 Bay} Serial Number
    ${buffer}=      Execute OA CLI Command      Show Blade Info ${SH2 Bay}
    Should not Contain      ${buffer}    No Server Blade Installed    msg=No blade in bay ${SH2 Bay}
    ${lines}=       Get Lines Containing String     ${buffer}   Serial Number
    ${ret}=         Fetch from Right        ${lines.strip()}    ${SPACE}
    Set Suite Variable      ${SH2 Serial Number}    ${ret}

    # Blade 3   
    Log         Get Blade ${SH3 Bay} Serial Number
    ${buffer}=      Execute OA CLI Command      Show Blade Info ${SH3 Bay}
    Should not Contain      ${buffer}    No Server Blade Installed    msg=No blade in bay ${SH3 Bay}
    ${lines}=       Get Lines Containing String     ${buffer}   Serial Number
    ${ret}=         Fetch from Right        ${lines.strip()}    ${SPACE}
    Set Suite Variable      ${SH3 Serial Number}    ${ret}

Switch off all test blades
    Switch E-Fuse off    Blade    ${SH1 Bay}
    Switch E-Fuse off    Blade    ${SH2 Bay}
    Switch E-Fuse off    Blade    ${SH3 Bay}
  
# CLEANUP Keywords

Clean Up and Close all connections
    Restore Server Profile Bay
    Switch on all test blades
    Close SSH Connection
    Pause And Close Browser

Restore Server Profile Bay
    Fusion UI Edit Server Profile  @{TestData.editprofilebays}[0]
    Verify Server Profile Attribute    server hardware   ${TestData.editprofilebays[0].newServerHardware}

Switch on all test blades
    Switch E-Fuse on    Blade    ${SH1 Bay}
    Switch E-Fuse on    Blade    ${SH2 Bay}
    Switch E-Fuse on    Blade    ${SH3 Bay}

Pause And Close Browser
	Fusion UI Logout of Appliance
	Close All Browsers

# Fusion Keywords

Log into Fusion appliance as Administrator
    ${user} =  Get Data By Property  ${TestData.users}  name  Administrator
    Fusion UI Login to Appliance   ${user[0].name}