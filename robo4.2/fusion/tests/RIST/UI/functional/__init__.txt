*** Settings ***
Documentation     Fusion Functional Test Suite
Library           Collections
Library           String
Library           RoboGalaxyLibrary
Resource          ../resource.txt
Suite Setup       Suite Setup

*** Keywords ***
Suite Setup
    Load Test Data    ${DataFile}
    Set Suite Metadata     DataFile    ${DataFile}
    ${user}=    Get Data By Property    ${TestData.users}    name    Administrator
    ${IP}=      Fetch From Right    ${ApplianceUrl}    /
    Set Fusion Version Metadata    ${IP}    ${user[0].name}    ${user[0].password}
    Run Keyword and Ignore Error    Set Firefox Version Metadata

Set Firefox Version Metadata
    ${Version}=    Get Firefox Version
    Set Suite Metadata     Firefox Version    ${Version}


Set Fusion Version Metadata
    [Documentation]    Get the version ident string from Fusion
    [Arguments]    ${IP}    ${USERNAME}    ${PASSWORD}
    Login to Fusion via REST    ${IP}    ${USERNAME}    ${PASSWORD}
    ${Response}=    Fusion Api Get Appliance Version
    Logout of Fusion Via REST
    Log    ${Response}
    ${softwareVersion}=    Get from Dictionary    ${Response}    softwareVersion
    ${date}=               Get from Dictionary    ${Response}    date
    Set Suite Metadata     Fusion Version    ${softwareVersion} : ${date}    top=True

Login to Fusion Via REST
    [Documentation]    Connects to the Appliance and creates a session using the Username and Password.
    [Documentation]    Example:\n| Login to Fusion Via REST | 10.0.12.106 | Administrator | hpvse123 |
    [Arguments]    ${IP}    ${USERNAME}    ${PASSWORD}
    ${Response}    ${SessionId}    Fusion Api Login Appliance    ${IP}    ${USERNAME}    ${PASSWORD}
    ${Status}    Get From Dictionary    ${Response}    status
    Return From Keyword If    '${Status}' == '200'    ${Response}
    ${errorCode}    Get From Dictionary    ${Response}    errorCode
    ${message}      Get From Dictionary    ${Response}    message
    Fatal Error    msg=Invalid response returned ${Status} ${errorCode} ${message}

Logout of Fusion Via REST
    [Documentation]    Terminates a session with the REST API.
    [Documentation]    Example:\n| Logout Of Fusion Via REST |
    Fusion Api Logout Appliance
