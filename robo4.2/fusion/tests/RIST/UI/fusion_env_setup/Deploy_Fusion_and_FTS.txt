*** Settings ***
Documentation   Deploy latest Fusion OVA and perform 1st time setup
...             = USAGE =
...             | pybot | -v FUSION_NAME:MikeB-HPOneView | Deploy_Fusion.txt |
...             = Variables =
...             | FUSION_NAME | Required. VMware name for   the Fusion VM. |
...             | FUSION_DEPOT | (optional) URL specifying location of the OVA file |

Library     String
Library     Collections
Library     RoboGalaxyLibrary
Library     FusionLibrary
Library     robot.api.logger
Library     OperatingSystem
Library     SSHLibrary


*** Variables ***
${vSphere_IP}                   16.125.76.33
${vSphere_Username}             wpst-china
${vSphere_Password}             wpstchina1
${OVA_Type}                     DCS
${OVFtool}                      C:\\ovftool\\ovftool.exe    #for 64-bit ovftool
#${OVFtool}                      "C:\\Program Files\\VMware\\VMware OVF Tool\\ovftool.exe"   #for 64-bit ovftool
#${OVFtool}                      \\Progra~2\\VMware\\VMware~1\\ovftool.exe                   #for 32-bit ovftool
${Target_Locator}               vi://${vSphere_Username}:${vSphere_Password}@${vSphere_IP}/WPST/host/wpst-cluster
${VM_FOLDER}                    China_testheads/SHQA_OneView
${DATASTORE}                    OneView
${SITELAN_NETWORK}              hpcorpnet-16.114
#${Fusion_Depot}                 http://pyramid.vse.rdlabs.hpecorp.net/fusionOVF/Fusion1.10/DCS/PASSED/
#${Fusion_Depot}                 http://ci-nexus.vse.rdlabs.hpecorp.net/Fusion/rel/1.20/OVA/${OVA_Type}/
${Fusion_Depot_1.0}             http://pyramid.vse.rdlabs.hpecorp.net/fusionOVF/TrunkDCS/RELEASES/HPOneView-DCS_1.0.0.1_73791_PASSRC16/
${Fusion_Depot_1.01}            http://pyramid.vse.rdlabs.hpecorp.net/fusionOVF/Fusion1.01/${OVA_Type}/PASSED/
${Fusion_Depot_1.05}            http://pyramid.vse.rdlabs.hpecorp.net/fusionOVF/Fusion1.05/${OVA_Type}/PASSED/
${Fusion_Depot_1.10}            http://pyramid.vse.rdlabs.hpecorp.net/fusionOVF/Fusion1.10/${OVA_Type}/PASSED/
${Fusion_Depot_1.20}            http://ci-nexus.vse.rdlabs.hpecorp.net/Fusion/rel/1.20/OVA/${OVA_Type}/
${Fusion_Depot_1.20.00}         http://ci-nexus.vse.rdlabs.hpecorp.net/Fusion/release/1.20.00/OVA/${OVA_Type}/
${Fusion_Depot_1.20.01}         http://ci-nexus.vse.rdlabs.hpecorp.net/Fusion/release/1.20.01/OVA/${OVA_Type}/
${Fusion_Depot_1.20.02}         http://ci-nexus.vse.rdlabs.hpecorp.net/Fusion/release/1.20.02/OVA/${OVA_Type}/
${Fusion_Depot_1.20.03}         http://ci-nexus.vse.rdlabs.hpecorp.net/Fusion/release/1.20.03/OVA/${OVA_Type}/
${Fusion_Depot_1.20.04}         http://ci-nexus.vse.rdlabs.hpecorp.net/Fusion/release/1.20.04/OVA/${OVA_Type}/
${Fusion_Depot_1.20.05}         http://ci-nexus.vse.rdlabs.hpecorp.net/Fusion/release/1.20.05/OVA/${OVA_Type}/
${Fusion_Depot_1.20.06}         http://ci-nexus.vse.rdlabs.hpecorp.net/Fusion/release/1.20.06/OVA/${OVA_Type}/
${Fusion_Depot_2.0}             http://ci-nexus.vse.rdlabs.hpecorp.net/Fusion/master/OVA/${OVA_Type}/
${Fusion_Depot_2.0_release}     http://ci-nexus.vse.rdlabs.hpecorp.net/Fusion/release/2.00.00/OVA/${OVA_Type}/
${Fusion_Depot_2.00.00}         http://ci-nexus.vse.rdlabs.hpecorp.net/Fusion/release/2.00.00/OVA/${OVA_Type}/
${Fusion_Depot_2.00.01}         http://ci-nexus.vse.rdlabs.hpecorp.net/Fusion/release/2.00.01/OVA/${OVA_Type}/
${Fusion_Depot_2.00.02}         http://ci-nexus.vse.rdlabs.hpecorp.net/Fusion/release/2.00.02/OVA/${OVA_Type}/
${Fusion_Depot_3.0}             http://ci-nexus.vse.rdlabs.hpecorp.net/Fusion/rel/3.00/OVA/${OVA_Type}/
${initial}                      SHQA
${identifier}
${Fusion_URL}                   unknown
${Fusion_Password}              wpsthpvse1
${Non_Passed}                   False
${PASS_BUILD}                   True
${Schematic}                    demo
${FTS}                          True
${Build_Number_Filter}
${Debug_Mode}                   false
${Debug_Fusion_IP}              16.125.72.255
${Fusion_Version}               1.20
${Fusion_Depot}                 ${Fusion_Depot_${Fusion_Version}}
${Image_Ext}                    ova
${Image_Name}                   NONE
#Define when to create snapshot after deploying Fusion:
#                       No_snapshot                 - DO NOT create snapshot
#                       before_FTS                  - create snapshot before FTS (right before first time login)
#                       after_FTS                   - create snapshot after FTS done
#                       before_AND_after_FTS        - create snapshot both before and after FTS
${Snapshot_Timing}              after_FTS


*** Test Cases ***
Get Latest Fusion Build URL - OVA Type: ${OVA_Type}, PASS: ${PASS_BUILD}, Build Number Filter: ${Build_Number_Filter}
    Translate Pass Status   ${PASS_BUILD}
    Run Keyword If          '${Fusion_Version}${OVA_Type}'=='1.0DCS'        Set Suite Variable
    ...                     ${Image_Name}
    ...                     HPOneView-DCS_1.0.0.1_73791.ovf
    Run Keyword If          '${Fusion_Version}${OVA_Type}'=='1.0DCS'        Set Suite Variable
    ...                     ${Fusion_Depot}
    ...                     http://pyramid.vse.rdlabs.hpecorp.net/fusionOVF/TrunkDCS/RELEASES/HPOneView-DCS_1.0.0.1_73791_PASSRC16/
    Run Keyword If          '${Fusion_Version}${OVA_Type}'=='1.0SSH'        Set Suite Variable
    ...                     ${Image_Name}
    ...                     HPOneView-SSH_1.0.0.1_73575.ovf
    Run Keyword If          '${Fusion_Version}${OVA_Type}'=='1.0SSH'        Set Suite Variable
    ...                     ${Fusion_Depot}
    ...                     http://pyramid.vse.rdlabs.hpecorp.net/fusionOVF/Trunk/RELEASES/HPOneView-SSH_1.0.0.1_73575_PASSrc15/
    Run Keyword If          '${Fusion_Version}${OVA_Type}'=='1.0NoSSH'      Set Suite Variable
    ...                     ${Image_Name}
    ...                     HPOneView_1.0.0.1_73575.ovf
    Run Keyword If          '${Fusion_Version}${OVA_Type}'=='1.0NoSSH'      Set Suite Variable
    ...                     ${Fusion_Depot}
    ...                     http://pyramid.vse.rdlabs.hpecorp.net/fusionOVF/TrunkNoSSH/RELEASES/HPOneView_1.0.0.1_73575_PASSrc15/

    Console     \n[Fusion_Depot] = "${Fusion_Depot}"

    ${Image_Name}=      Run Keyword If          '${Image_Name}'=='NONE'
    ...                 Get LatestBuild Name    ${Fusion_Depot}     ''      ${Image_Ext}
    ...                 HPOneView.*${Build_Number_Filter}.*.%s$
    ...                 non_passed=${Non_Passed}
    ...                 ELSE
    ...                 Set Variable            ${Image_Name}

    Console                 \n[Image Name] = "${Image_Name}"
    Set Suite Variable      ${Deploy_URL}                   ${Fusion_Depot}${Image_Name}
    Console                 \nDeploying from: ${Deploy_URL}
    Log                     ${Deploy_URL}
    ${name}=                Get substring                   ${Image_Name}                   0   -4
    ${unique_identifier}=   Generate Unique Identifier      ${identifier}
    set Suite variable      ${Fusion_Name}                  ${initial}-${unique_identifier}-${name}
    Console                 \nThe Appliance name is: ${Fusion_Name}
Check OVFtool
    Pass Execution If       '${Debug_Mode}'=='true'         Skipped when DEBUG Mode.
    ${RC}                   ${Output}=                      Run and Return Rc and Output    dir ${OVFtool}
    Run Keyword Unless      '${RC}' == '0'                  Fatal Error
    ...                     msg=Could not find ovftool.exe. Is it installed? (See \\\\eml.usa.hp.com\\emgmt\\Tools\\WMARE)

Redeploy Fusion
    Pass Execution If       '${Debug_Mode}'=='true'     Skipped when DEBUG Mode.
    Console                 \nDeploying VM: ${Fusion_Name}
    ${Command}=             Catenate                    ${OVFtool}
    ...                     --skipManifestCheck
    ...                     --noSSLVerify
    ...                     --acceptAllEulas
    ...                     --machineOutput
    ...                     --powerOn
    ...                     --ipProtocol=IPv4
    ...                     --powerOffTarget
    ...                     --overwrite
    ...                     --name="${Fusion_Name}"
    ...                     --datastore="${DATASTORE}"
    ...                     --network="${SITELAN_NETWORK}"
    ...                     --diskMode=thin
    ...                     --vmFolder="${VM_FOLDER}"
    ...                     ${Deploy_URL}
    ...                     ${Target_Locator}
    Log                     ${Command}
    ${RC}                   ${Output}=                  Run and Return Rc and Output    ${Command}
    Run Keyword Unless      '${RC}' == '0'              Fatal Error                     msg=Could not deploy new VM ${Output}

Get Fusion IP Address
    Run Keyword If              '${Debug_Mode}'=='true'                         Set Suite Variable      ${Fusion_IP}
    ...                         ${Debug_Fusion_IP}
    Run Keyword If              '${Debug_Mode}'=='true'                         Set Suite Variable      ${Fusion_URL}
    ...                         https://${Fusion_IP}
    Run Keyword If              '${Debug_Mode}'=='true'                         Console
    ...                         \nYour FusionVM URL is: ${Fusion_URL}
    Pass Execution If           '${Debug_Mode}'=='true'                         Skipped when DEBUG Mode.
    Connect To VI Server        ${vSphere_IP}           ${vSphere_Username}     ${vSphere_Password}
    Wait Until Keyword Succeeds                         10 min                  60 sec                  Get VM IPv4 Addresses
    ...                         ${Fusion_Name}
    @{IPs}=                     Get VM IPv4 Addresses   ${Fusion_Name}
    ${IP}=                      Get From List           ${IPs}                  0
    Set Suite Variable          ${Fusion_IP}            ${IP}
    Set Suite Variable          ${Fusion_URL}           https://${Fusion_IP}
    Set Environment Variable    FUSION_URL              ${Fusion_Url}
    Set Environment Variable    FUSION_IP               ${Fusion_IP}
    Console                     \nYour FusionVM URL is: ${Fusion_URL}

Wait for VM Startup
    Pass Execution If   '${Debug_Mode}'=='true'     Skipped when DEBUG Mode.
    # It'going to take 15 to 20 minutes for Fusion to be login-able. A good time for a nap.
    Sleep               15 minutes                  Waiting for startup to complete
    Open Browser        ${Fusion_URL}               Firefox
    Sleep               15 seconds
    Capture Page Screenshot
    Wait Until Keyword Succeeds                     30 min      60 sec      Page Should not contain     Starting
    Capture Page Screenshot
    Close Browser

Create snapshot for VM before FTS
    Pass Execution If       '${Snapshot_Timing}'=='No_snapshot'     Skipped since Snapshot_Timing is set to No_snapshot.
    Pass Execution If       '${Snapshot_Timing}'=='after_FTS'       Skipped since Snapshot_Timing is set to after_FTS.
    Create VM Snapshot      ${Fusion_Name}                          snapshot_before_FTS     ${False}    ${True}

Open Browser To Login Page After Startup
    Pass Execution If   '${Debug_Mode}'=='true'     Skipped when DEBUG Mode.
    Open Browser        ${Fusion_URL}/#/login       Firefox
    # Click 'Continue to this website' link allowing test case to continue (for IE only).
    #Run Keyword If     "${BROWSER}" == "IE"        Go To   javascript:document.getElementById('overridelink').click()
    sleep               5
    Capture Page Screenshot

EULA Agreement
    Pass Execution If   '${Debug_Mode}'=='true'     Skipped when DEBUG Mode.
    Wait Until Page Contains Element                id=hp-eula-agree-button
    ...                 timeout=240
    ...                 error=Not at EULA page
    Pass Execution If   '${FTS}'=='false'           User skipped.
    Click Button        id=hp-eula-agree-button
    sleep               5
    Click Button        id=hp-eula-ok-button
    sleep               5
    Capture Page Screenshot

First Time Login
    Pass Execution If   '${Debug_Mode}'=='true'     Skipped when DEBUG Mode.
    Pass Execution If   '${FTS}'=='false'           User skipped.
    Wait Until Page Contains Element                id=hp-login-user
    ...                 timeout=120
    ...                 error=Not at Login page
    Input Text          hp-login-user               Administrator
    Input Text          hp-login-password           admin
    Click Button        hp-login-button
    sleep               10
    Capture Page Screenshot

Assign Administrator Password
    Pass Execution If   '${Debug_Mode}'=='true'         Skipped when DEBUG Mode.
    Pass Execution If   '${FTS}'=='false'               User skipped.
    Wait Until Page Contains Element                    id=hp-initial-password-new1
    ...                 timeout=240
    ...                 error=Not at InitialPassword page
    Input Text          id=hp-initial-password-new1     ${Fusion_Password}
    Input Text          id=hp-initial-password-new2     ${Fusion_Password}
    Click Button        id=hp-initial-password-button-element
    sleep               5
    Capture Page Screenshot

Assign DHCP Networking
    Pass Execution If           '${Debug_Mode}'=='true'     Skipped when DEBUG Mode.
    Pass Execution If           '${FTS}'=='false'           User skipped.
    Wait Until Page Contains Element                        id=cic-network-hostname
    ...                         timeout=120
    ...                         error=Not at Initial-network page
    Capture Page Screenshot
    Sleep                       5
    Capture Page Screenshot
    ${ci_name}=                 Get Value                   id=cic-network-hostname
    ${identifier_hostname}=     Replace String              ${identifier}   _           -
    ${identifier_hostname}=     Replace String              ${identifier_hostname}      .   -
    Input Text                  id=cic-network-hostname     ${ci_name}-${identifier_hostname}-${initial}.vse.rdlabs.hpecorp.net
    Sleep                       5
    Click Element               id=cic-fs-network-title
    Sleep                       5
    Click Button                id=cic-network-ipv4-dhcp
    Sleep                       5
    Click Button                id=cic-network-ipv4-dhcp
    Sleep                       5
    Click Button                id=cic-network-ipv4-dhcp
    Sleep                       5
    Click Button                id=cic-network-ok
    Sleep                       5
    Capture Page Screenshot
    Wait Until Page Contains    Applying network settings.                  timeout=300

Wait for Fusion Ready
    Pass Execution If   '${Debug_Mode}'=='true'     Skipped when DEBUG Mode.
    Pass Execution If   '${FTS}'=='false'           User skipped.
    Wait Until Keyword Succeeds                     30 min          30 sec      Page Should not contain
    ...                 Applying network settings.
    Wait Until Page Contains Element                id=hp-main-menu-control
    ...                 timeout=120
    ...                 error=Not at Dashboard page (cant find main-menu)
    ${Location}=        Get Location
    ${Page}=            Fetch From Right            ${Location}     ${Fusion_URL}
    Pass Execution If   '${Page}'=='/#/dashboard'   msg=Found dashboard
    Pass Execution If   '${Page}'=='/#/activity'    msg=Found activities
    Pass Execution If   '${Page}'=='/#/settings/show/overview'      msg=Found Overview
    Capture Page Screenshot
    Fail                msg=Failed to apply network settings

Change DCS Schematic to Others than DEMO If Required
    Pass Execution If   '${Debug_Mode}'=='true'     Skipped when DEBUG Mode.
    Pass Execution If   '${FTS}'=='false'           User skipped.
    Run Keyword If      "${Schematic}"!="demo"      Set Appliance Schematic     ${Schematic}    ${Fusion_IP}

Cleanup
    Pass Execution If   '${Debug_Mode}'=='true'     Skipped when DEBUG Mode.
    Close Browser

Create snapshot for VM after FTS
    Pass Execution If       '${Snapshot_Timing}'=='No_snapshot'     Skipped since Snapshot_Timing is set to No_snapshot.
    Pass Execution If       '${Snapshot_Timing}'=='before_FTS'      Skipped since Snapshot_Timing is set to before_FTS.
    Create VM Snapshot      ${Fusion_Name}                          snapshot_after_FTS      ${False}    ${True}

Write Fusion Properties
    Run     echo FUSION_IP=${Fusion_IP} > ${OUTPUT DIR}\\Fusion.properties
    Run     echo FUSION_URL=${Fusion_URL} >> ${OUTPUT DIR}\\Fusion.properties
    Run     echo FUSION_NAME=${Fusion_Name} >> ${OUTPUT DIR}\\Fusion.properties
    Run     echo DEPLOY_URL=${Deploy_URL} >> ${OUTPUT DIR}\\Fusion.properties


*** Keywords ***
Set Appliance Schematic
    [Arguments]         ${Schematic}    ${Fusion_IP}
    Open Connection     ${Fusion_IP}
    Login               root            hponeview

    ${RC}=                  Execute Command     dcs status
    Console                 \n${RC}
    Should Match Regexp     ${RC}               ^.*DCS.is.Running.*
    Should Match Regexp     ${RC}               .*Schematic.used\:.*/dcs/schematic/.*$

    ${RC}=                  Execute Command     dcs stop
    Console                 \n${RC}
    Should Match Regexp     ${RC}               .*DCS.Stopped.*Done.*

    Wait Until Keyword Succeeds     5 min   5 sec   Change DCS Schematic    ${Schematic}

    ${RC}=                  Execute Command     dcs status
    Console                 \n${RC}
    Should Match Regexp     ${RC}               ^.*DCS.is.Running.*
    Should Match Regexp     ${RC}               .*Schematic.used:.*/dcs/schematic/${Schematic}.*$

    Close Connection

Change DCS Schematic
    [Arguments]             ${Schematic}
    ${RC}=                  Execute Command     dcs start /dcs/schematic/${Schematic} cold
    Console                 \n${RC}
    Should Match Regexp     ${RC}               .*Schematic.location.*/dcs/schematic/${Schematic}.*
    Should Match Regexp     ${RC}               .*DCS.daemon.is.already.running.*

Translate Pass Status
    [Arguments]         ${PASS_BUILD}
    Run Keyword If      '${PASS_BUILD}'=='true'     Set Suite Variable      ${Non_Passed}   False
    Run Keyword If      '${PASS_BUILD}'=='false'    Set Suite Variable      ${Non_Passed}   True

Generate Unique Identifier
    [Arguments]             ${identifier}
    ${random number}=       Generate Random String      4                       0123456789
    ${identifier_length}=   Get Length                  ${identifier}
    Run Keyword If          ${identifier_length}==0     Return From Keyword     ${random number}
    ...                     ELSE                        Return From Keyword     ${identifier}-${random number}

