*** Settings ***
Library              FusionLibrary
Library              RoboGalaxyLibrary
Library              OperatingSystem
Library              BuiltIn
Library              Collections
Resource             ../resource.txt
Resource             ../../API/Fusion_Env_Setup/keywords.txt
Library              ../../../wpst_crm/crm_austin/lib/WPSTUtil.py

*** Keywords ***
Delete and Re-configure VC Domain
    [Documentation]  Remove and Initialize VC, then send command to add networks/fcs/server profiles etc.
    [Arguments]      ${vc_credentials}  ${oa_credentials}  ${vc_config_file}  ${waitTime}=2 min  ${doubleDense}=${False}
    WPST Delete Domain VC          ${vc_credentials}    ${oa_credentials}
    Log To Console     Success: Deleted VC Domain...
    Log To Console     Waiting ${waitTime} minutes...
    Sleep    ${waitTime}

    WPST Configure VC   vc_credentials=${vc_credentials}  oa_credentials=${oa_credentials}  config=${vc_config_file}  doubleDense=${doubleDense}
    Log To Console     Success: Configured VC Domain...

Clear Base Resources
    [Documentation]   Delete base resources
    Log To Console    \nCleaning test environment before test...
    Power off ALL servers          control=PressAndHold
    Remove All Server Profiles     force=${True}
    Remove All Server Profile Templates
    Wait For ALL Enclosures Complete Refresh
    Remove ALl Enclosures          param=?force=true
    Remove All Enclosure Groups    True
    Remove All LIGs
    Remove All Ethernet Networks
    Remove All FC Networks
    Remove All FCoE Networks
    Remove All Network Sets
    Remove ALl Server Hardware Types

Load Test Data and Prepare Environment
    [Arguments]    ${Ring}  ${FTS}  ${Add_Enclosure}  ${Add_Storage}  ${Team_Name}
    Load Test Data and Open Browser
    Log into Fusion appliance as Administrator
    Setup Env For C7000  ${Ring}  ${FTS}  ${Add_Enclosure}  ${Add_Storage}  ${Team_Name}

Remove All SSO Hp Sim Certificates and Snmp Trap Receivers For OA
    [Arguments]     ${enclosure_credentials}
    Fusion Srm OA Api User Login    ${enclosure_credentials.oa1hostname}    ${enclosure_credentials.oa1username}    ${enclosure_credentials.oa1password}
    Fusion Srm OA Api Remove All HP Sim Certificates
    Fusion Srm OA Api Remove All Snmp Trap Receiver

Pause And Close Browser And Clear Base Resources
    [Arguments]    ${appliance}    ${credentials}
    Pause And Close Browser
    Fusion Api Login Appliance    ${appliance}    ${credentials}
    Clear Base Resources
