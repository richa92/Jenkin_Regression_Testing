*** Settings ***
Library				RoboGalaxyLibrary
Library				FusionLibrary
Library				OperatingSystem
Library				BuiltIn
Library				Collections
Library				XML
Library		        SSHLibrary
Library				String
Library				Dialogs
Resource             ./../../../../Resources/api/fusion_api_resource.txt

*** Keywords ***
Power On ICM
    [Arguments]    ${name}
    [Documentation]    power on interconnect by its ICM name

    ${body}=                        Create Dictionary   path=/powerState
    ...                                                 value=On
    ...                                                 op=replace

    ${name} =                       replace string using regexp  ${name}  SH:  ${EMPTY}

    Log to console and logfile  	\nPoweron ICM
    ${uri} =                        Get IC URI      ${name}
    ${body_list}                    Create List     ${body}

    ${response}=                    fusion api patch interconnect       uri=${uri}     body=${body_list}
    Should Be Equal                 '${response["status_code"]}'    '202'    msg=REST API wrong, can't poweron interconnect!

    ${task}=                        wait for Task   ${response}     240s    5s
    Should Be Equal As Strings      ${task["taskState"]}     Completed    msg=task not finish well!
    Wait For Power State            240s    5s      ${uri}      On

Wait For Power State
    [Arguments]    ${timeout}=240s    ${interval}=2s     ${uri}=None        ${state}=Off
    [Documentation]    Waits for a task to reach an end-state
    Log to console    \n
    Wait Until Keyword Succeeds    ${timeout}    ${interval}    Task Reached Power Endstate      ${uri}  ${state}

Task Reached Power Endstate
    [Arguments]    ${uri}=None   ${state}=Off
    Set Log Level    TRACE
    ${resp}=                Fusion Api Get Interconnect     ${uri}

    Log to console and logfile      \t powerstate after power is ${resp['powerState']}
    ${productname}=         Get Variable Value      ${resp['productName']}
    ${partnumber}=          Get Variable Value      ${resp['partNumber']}
    ${powerState}=          Get Variable Value      ${resp['powerState']}
    Log to console and logfile    \t Task: powerState of [${productname}] is: ${powerState} for ${partnumber}
    Should Be Equal         ${powerState}    ${state}

Wait For PowerUID State
    [Arguments]    ${timeout}=240s    ${interval}=2s     ${uri}=None        ${state}=Off
    [Documentation]    Waits for a task to reach an end-state
    Log to console    \n
    Wait Until Keyword Succeeds    ${timeout}    ${interval}    Task Reached PowerUID Endstate      ${uri}  ${state}

Task Reached PowerUID Endstate
    [Arguments]    ${uri}=None   ${state}=Off
    Set Log Level    TRACE
    ${resp}=                Fusion Api Get Interconnect     ${uri}

    Log to console and logfile      \t PowerUID after power is ${resp['uidState']}
    ${productname}=         Get Variable Value      ${resp['productName']}
    ${partnumber}=          Get Variable Value      ${resp['partNumber']}
    ${uidState}=          Get Variable Value      ${resp['uidState']}
    Log to console and logfile    \t Task: PowerUID of [${productname}] is: ${uidState} for ${partnumber}
    Should Be Equal         ${uidState}    ${state}