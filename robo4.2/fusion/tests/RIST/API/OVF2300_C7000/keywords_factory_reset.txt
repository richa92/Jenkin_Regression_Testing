*** Settings ***
Library              FusionLibrary
Library              RoboGalaxyLibrary
Library              OperatingSystem
Library              BuiltIn
Library              copy
Library              Collections
Library              String
Library              json
Resource             ./keywords_mode_switch.txt
Resource             ../../../../Resources/api/fusion_api_resource.txt
Variables            ./Regression_Data.py

*** Variables ***
${APPLIANCE_IP}         unknown
${admin_username}       Administrator
${admin_password}       wpsthpvse1

*** Keywords ***
Factory Reset With PRESERVE NETWORK
    [Documentation]    Factory Reset With PRESERVE NETWORK, done util appliance is OK
    ${response} =    Fusion Api Appliance Factory Reset  mode=PRESERVE_NETWORK
    Wait For Appliance State To Be FACTORY_RESET
    Wait For Appliance State To Be OK

Factory Reset Without PRESERVE NETWORK
    [Documentation]    Factory Reset Without PRESERVE NETWORK, done util appliance is OK
    ${response} =    Fusion Api Appliance Factory Reset  mode=FULL
    Wait For Appliance State To Be FACTORY_RESET
    Wait For Appliance State To Be OK

Accept Appliance EULA
    [Documentation]    Accept Appliance EULA
    Log To Console        "Going to accept EULA"
    ${response}=    Fusion Api Save Eula    ${APPLIANCE_IP}    yes
    Should Be Equal As Numbers  ${response['status_code']}  200

Change Default OV Password
    [Documentation]    Change Default OV Password
    ${Creds}    Create Dictionary    newPassword=${admin_password}
    ...                              oldPassword=admin
    ...                              userName=${admin_username}
    Log To Console        "Going to change the password"
    ${response}=    Fusion Api Change Administrator Password  ${APPLIANCE_IP}  ${Creds}
    Should Be Equal As Numbers  ${response['status_code']}  200
    Log To Console        ${\n}    "Done change the password"

Get Appliance Host Name
    [Documentation]    Get Appliance Host Name
    ${response}=    Fusion Api Get Appliance Interfaces
    ${applianceNetworks}=    Get From Dictionary  ${response}  applianceNetworks
    Log To Console  hostname:${applianceNetworks[0]['hostname']}
    [Return]  ${applianceNetworks[0]['hostname']}

Get Appliance Mac Address
    [Documentation]    Get Appliance Mac Address
    ${response}=    Fusion Api Get Appliance Interfaces
    ${applianceNetworks}=    Get From Dictionary  ${response}  applianceNetworks
    Log To Console  mac address:${applianceNetworks[0]['macAddress']}
    [Return]  ${applianceNetworks[0]['macAddress']}

Configure Appliance Interfaces By DHCP
    [Documentation]    Configure Appliance Interfaces By DHCP
    ${hostname}=    Get Appliance Host Name
    ${mac_address}=    Get Appliance Mac Address
    ${dic}=    Create Dictionary  activeNode=1                unconfigure=${False}                 hostname=${hostname}.hp.cn
    ...                           macAddress=${mac_address}   confOneNode=${True}                  ipv4Type=DHCP
    ...                           ipv6Type=UNCONFIGURE        overrideIpv4DhcpDnsServers=${True}
    ...                           aliasDisabled=${True}
    ${applianceNetworks}=    Create List  ${dic}
    ${appliance_interface}=    Create Dictionary    type=ApplianceNetworkConfigurationV2
    ...                                             applianceNetworks=${applianceNetworks}
    ${response}=    Fusion Api Configure Appliance Interfaces  ${appliance_interface}
    Wait For Task2  ${response}  timeout=300  interval=20

############################################ High Leveal Keywords ############################################
Factory Reset Without PRESERVE NETWORK And Do FTS
    [Documentation]  will do factory reset and FTS, after that, appliance is ready to use
    Factory Reset Without PRESERVE NETWORK
    Accept Appliance EULA
    Change Default OV Password
    Login By IA User
    Configure Appliance Interfaces By DHCP
    Security Mode Check: Current Mode Is:  LEGACY
