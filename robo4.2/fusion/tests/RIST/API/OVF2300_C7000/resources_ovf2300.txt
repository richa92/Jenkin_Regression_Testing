*** Settings ***
Library              FusionLibrary
Library              RoboGalaxyLibrary
Library              OperatingSystem
Library              BuiltIn
Library              copy
Library              Collections
Library              String
Resource             ./keywords_certificates.txt
Resource             ./keywords_compatibility_report.txt
Resource             ./keywords_compliance_validator.txt
Resource             ./keywords_factory_reset.txt
Resource             ./keywords_mode_switch.txt
Resource             ./keywords_ssh_commands.txt
Resource             ./keywords_tbird_ha.txt
Resource             ../../../../Resources/api/fusion_api_resource.txt


*** Variables ***
${ssh_server_ip}                15.114.112.61
${APPLIANCE_IP}                 unknown
${APPLIANCE_TYPE}               unknown
${IS_HA_ENABLED}                unknown
${TBIRD_M1}                     unknown
${TBIRD_M2}                     unknown

*** Keywords ***
Precondition ENV Setup
    [Documentation]  Prepare the test environment by the following steps:
    ...                 1. Set Global Variable  ${APPLIANCE_TYPE}
    ...                 2. Set Global Variable  ${IS_HA_ENABLED}
    ...                 3. switch mode to legacy
    ...                 4. wait for HA synchronized if appliance is TBIRD HA mode.

    Set Log Level       TRACE
    Fusion Api Login Appliance  ${APPLIANCE_IP}  ${admin_credentials}
    ${resp}=    Fusion Api Get Appliance Version
    Set Global Variable  ${APPLIANCE_TYPE}  ${resp["modelNumber"]}
    ${HA_MODE}=    Set Variable If    "${APPLIANCE_TYPE}"=="Synergy Composer" and "${TBIRD_M1}"!="unknown" and "${TBIRD_M2}"!="unknown"  ${True}  ${False}
    Set Global Variable  ${IS_HA_ENABLED}  ${HA_MODE}
    Log To Console  ${\n}*********** Appliance Type: ${APPLIANCE_TYPE} ***********
    Run Keyword If  "${APPLIANCE_TYPE}"=="Synergy Composer"    Log To Console  *********** HA Mode: ${IS_HA_ENABLED} ***********
    Run Keyword If  "${IS_HA_ENABLED}"=="${True}"    Log To Console  Node1:${TBIRD_M1} ${\n}Node2:${TBIRD_M2}

    Login By IA User
    Switch Security Mode To:  LEGACY
    Login By IA User
    Wait Until HA Nodes Are Synchronized If Appliance Has 2 Nodes




