*** Settings ***
Documentation        F444_API_test cases automation.

Library              RoboGalaxyLibrary
Library              FusionLibrary
Library              OperatingSystem
Library              BuiltIn
Library              Collections
Library              XML
Library              SSHLibrary
Library              String
Library              json
Library  			 Dialogs

Resource             ../../../../Resources/api/fusion_api_resource.txt
Resource             ../global_variables.robot

*** Keywords ***
Verify Virtual Port of deviceSlots
    [Documentation]     Validate the virtual ports of device slots for server hardware. For 650/660 adapters, 8 VPs should be available.
    ...                 Example:   Verify Virtual Port of deviceSlots       <deviceslots>     <name>
    [Arguments]         ${deviceslots}  ${name}
    Log    Validating the virtual ports of device slots for server hardware ${name}    console=true
    :FOR     ${device}    IN    @{deviceslots}
    \        ${name}=              Get From Dictionary        ${device}     deviceName
    \        ${ret}     ${value}=  Run Keyword and Ignore Error    should match regexp  '${name}'     6[5|6]0
    \        Log    value is: ${value}...    console=true
    \        Run Keyword If      '${ret}' == 'FAIL'
    \        ...                 Continue For Loop
    \        ...                 ELSE    Verify Virtual Port of device   ${device}


Verify Virtual Port of device
    [Documentation]     Validate the virtual ports of device with name, location, and slot number. For 650/660 adapters, 8 VPs should be available.
    ...                 Example:   Verify Virtual Port of device    {device}
    ...                 <name> --- Example: "deviceName": "HP FlexFabric 20Gb 2-port 650FLB Adapter",
    ...                 <location> --- Example: "location": "Flb" / "mezz",
    ...                 <slotnumber> --- Example:   "slotNumber": 1
    [Arguments]         ${device}
    ${name} =           Get From Dictionary    ${device}     deviceName
    ${location} =       Get From Dictionary    ${device}     location
    ${slotnumber} =     Get From Dictionary    ${device}     slotNumber
    Log    Validating the virtual ports of device with name: ${name}, location: ${location}, slot number: ${slotnumber}    console=true
    ${pportslist} =     Get From Dictionary    ${device}   physicalPorts
    :FOR    ${pport}    IN    @{pportslist}
    \       ${vportlist} =     Get From Dictionary        ${pport}     virtualPorts
    \       Count virtual port of physical port     ${vportlist}


Count virtual port of physical port
    [Documentation]     Count the total number of virtual ports in a list and format them with <portnumber>-<portfunction> pairs.
    ...                 For 650/660 adapters, 8 VPs should be available.
    ...                 Example:   Verify virtual port of physical port    <vportlist>
    ...                 <vportlist> - A list for virtual ports. usually gets from physical ports.
    [Arguments]         ${vportlist}
    Log    Counting the virtual port number of physical port...    console=true
    ${count} =  get length  ${vportlist}
    Log    total virtual port number of physical port is ${count}    console=true
    :FOR    ${vport}    IN    @{vportlist}
    \       ${vportnumber} =       Get From Dictionary    ${vport}     portNumber
    \       ${vportfunction} =     Get From Dictionary    ${vport}     portFunction
    \       Log    number-function mapping: '${vportnumber}-${vportfunction}'    console=true
    Length Should Be    ${vportlist}    8


Verify connections in server profile
    [Documentation]     Verify whether there are connections with virtual ports(5-8, e-h) in server profile <sp> and list them with port id.
    ...                 For 650/660 adapters, 8 VPs should be available.
    ...                 Example:   Verify connections in server profile    <sp>
    ...                 <sp> - A server profile which is supposed to be created with 1 or more connections with virtual ports(5-8, e-h).
    [Arguments]         ${sp}
    ${name} =               get from dictionary  ${sp}   name
    Log    Verifying whether there are connections with virtual ports(5-8, e-h) in server profile ${name}...    console=true
    set test variable       ${F444p003_verified}     True
    ${portlist} =           create list
    ${connectionlist} =     get from dictionary  ${sp["connectionSettings"]}   connections
    should not be empty     ${connectionlist}    F444p003 - API - There is no connection in server profile ${name} created at all.
    :FOR    ${connection}   IN    @{connectionlist}
    \       ${name} =       Get From Dictionary    ${connection}     name
    \       ${portid} =     Get From Dictionary    ${connection}     portId
    \       Log    connection name to portid mapping: '${name}-${portid}'    console=true
    \       append to list  ${portlist}     ${portid}
    should match regexp     '${portlist}'   -[e|f|g|h]    F444p003 - API - No connection with virtual port(e-f) is found in server profile ${name}. Set case to fail.


Verify connections in server profile template
    [Documentation]     Verify whether there are connections with virtual ports(5-8, e-h) in server profile template <spt> and list them with port id.
    ...                 For 650/660 adapters, 8 VPs should be available.
    ...                 Example:   Verify connections in server profile template    <spt>
    ...                 <spt> - A server profile template which is supposed to be created with 1 or more connections with virtual ports(5-8, e-h).
    [Arguments]         ${spt}
    ${name} =               get from dictionary  ${spt}   name
    Log    Verifying whether there are connections with virtual ports(5-8, e-h) in server profile template ${name}...    console=true
    set test variable       ${F444p005_verified}     True
    ${portlist} =           create list
    ${connectionlist} =     get from dictionary  ${spt["connectionSettings"]}   connections
    should not be empty     ${connectionlist}    F444p005 - API - There is no connection in server profile template ${name} created at all.
    :FOR    ${connection}   IN    @{connectionlist}
    \       ${name} =       Get From Dictionary    ${connection}     name
    \       ${portid} =     Get From Dictionary    ${connection}     portId
    \       Log    connection name to portid mapping: '${name}-${portid}'    console=true
    \       append to list  ${portlist}     ${portid}
    should match regexp     '${portlist}'   -[e|f|g|h]    F444p005 - API - No connection with virtual port(e-f) is found in server profile template ${name}. Set case to fail.

Clear Test Environment
    [Documentation]  Clear test environment
    Fusion Api Login Appliance    ${APPLIANCE_IP}    ${admin_credentials}
    Log    Cleaning test environment before test...    console=true
    Power off ALL servers          control=PressAndHold
    Remove All Server Profiles     force=${True}
    Remove All Server Profile Templates
    Remove ALl Enclosures          param=?force=true
    Remove All Enclosure Groups    True
    Remove All LIGs
    Remove All Ethernet Networks
    Remove All FC Networks
    Remove All FCoE Networks
    Remove All Network Sets
    Remove ALl Server Hardware Types
    Remove ALL San Managers Async