*** Keywords ***
Open Browser and Login to OneView
    [Documentation]    Open a browser (if not already open) and Log into OneView (if not already logged in
    Set Log Level                   TRACE
    Run Keyword If  '${BROWSER_OPENED}' == 'False'  Run Keywords
    ...     Open Browser    ${URL}    ${BROWSER}                    AND
    ...     Set Global Variable     ${BROWSER_OPENED}    True
    Set Selenium Implicit Wait    ${SELENIUM_IMPLICIT_WAIT}
    Run Keyword If  '${LOGGED_IN}' == 'False'  Login To OneView  ${USERNAME}  ${PASSWORD}


Login To OneView
    [Arguments]    ${username}    ${password}
    [Documentation]    Log in to the appliance
    Wait Until Page Contains Element    css=#hp-login-button.hp-primary.hp-disabled    timeout=20s
    Maximize Browser Window
    Input Text                          id=hp-login-user    ${username}
    Input Text                          id=hp-login-password    ${password}
    Click Button                        id=hp-login-button
    Wait Until Page Contains Element    id=hp-main-menu-control
    Set Selenium Implicit Wait    0.5s
    ${tutorial}=  Is Element Visible    id=hp-tutorial-step
    Run Keyword If  '${tutorial}' == 'True'    Wait for Element Location     id=hp-tutorial-step
    Run Keyword If    ${tutorial}==True    Click Element    id=hp-tutorial-close
    Element Should Not Be Visible    id=hp-tutorial-step
    Close Guided Setup If Open
    Set Selenium Implicit Wait    ${SELENIUM_IMPLICIT_WAIT}
    Set Global Variable     ${LOGGED_IN}    True


Logout and Close Browser
    [Documentation]  Log out of OneView and close the browser
    Click Element   id=hp-session-control
    Wait Until Page Contains Element    css=#hp-session-logout    timeout=10s
    Click Link      Log out
    Wait Until Page Contains Element    id=hp-login-user    timeout=10s
    Set Global Variable     ${LOGGED_IN}    False
    Close Browser
    Set Global Variable     ${BROWSER_OPENED}    False
