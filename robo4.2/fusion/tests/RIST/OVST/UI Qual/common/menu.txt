*** Keywords ***
Open Main Menu
    [Documentation]  Click the main menu and ensure it opens
    Set Selenium Implicit Wait  0.5
    ${visible}=  Is Element Visible  css=.ui-resizable-handle
    Set Selenium Implicit Wait  ${SELENIUM_IMPLICIT_WAIT}
    # If the screen is resizing, we need to wait before attempting to activate the main menu
    Run Keyword If  '${visible}' == 'True'    Wait for Element Location     css=.ui-resizable-handle
    Click Element  id=hp-main-menu-control
    Element Should Be Visible  css=#hp-main-menu > #hp-main-menu-control.hp-active.hp-selected

Open Sub Menu
    [Arguments]   ${title}   ${item}
    [Documentation]  Click the main menu and then Sub Menu ensure it opens
    Open Main Menu
    Click Element   //label[contains(text(),'${title}')]
    Element Should Be Visible   //div[@class='hp-sections-container']//a[text()='${item}']
    #Click Element  id=hp-main-menu-control

Close Main Menu
    [Documentation]  Click the main menu and ensure it closes
    Click Element  id=hp-main-menu-control
    Set Selenium Implicit Wait  1
    Element Should Not Be Visible  css=#hp-main-menu > #hp-main-menu-control.hp-active.hp-selected
    Set Selenium Implicit Wait  ${SELENIUM_IMPLICIT_WAIT}

Click Main Menu Item
    [Arguments]  ${link}
    [Documentation]  Click a link on the main menu
    Set Selenium Implicit Wait  0.5
    ${visible}=  Is Element Visible  css=#hp-main-menu > #hp-main-menu-control.hp-active.hp-selected
    Set Selenium Implicit Wait  ${SELENIUM_IMPLICIT_WAIT}
    Run Keyword If  '${visible}' == 'False'    Open Main Menu
    Element Should Be Visible  css=#hp-main-menu > #hp-main-menu-control.hp-active.hp-selected
    Element Should Be Visible  xpath=//div[@class='hp-sections-container']//a[text()='${link}']
    Click Element   xpath=//div[@class='hp-sections-container']//a[text()='${link}']

Create List From Action or Panel Drop Menu
    [Documentation]  Create list from action or dialog header Drop Down
    [Arguments]      ${menu_id}
    ${items}=  Create List
    Click Element  xpath=//div[@id='${menu_id}' and div]
    ${item_list} =  Get WebElements    xpath=//div[@id='${menu_id}']//a
    :FOR  ${item}  in  @{item_list}
    \    ${item_text}=  Get Text  ${item}
    \    Append To List  ${items}  ${item_text}
    Click Element  xpath=//div[@id='${menu_id}' and div]
    [return]  ${items}

Select Item From Action or Panel Drop Menu
    [Documentation]  Select item from action or dialog header drop down
    [Arguments]      ${menu_id}  ${item}
    Wait Until Element Is Visible   ${menu_id}
    Click Element  xpath=//div[@id='${menu_id}' and div]
    Click Element  xpath=//div[@id='${menu_id}']//a[contains(text(), '${item}')]

Panel Should Be Visible
    [Documentation]   Ensure a panel is visible
    [Arguments]  ${panel}
    Element Should Be Visible    xpath=//li[label[contains(text(), '${panel}')]/following-sibling::fieldset]

Create List From Drop Down Menu
    [Documentation]   Create a list from the elements in a drop down menu
    [Arguments]        ${menu_id}
    ${items}=  Get List Items  xpath=//select[@id='${menu_id}']
    [return]   ${items}

Select Item From Drop Down Menu
    [Documentation]    Select an item from a list of items in a drop down menu
    [Arguments]    ${dropdown_id}    ${item}
    Click Element  xpath=//li[label[@data-localize='${dropdown_id}']]//div[@class='hp-value' and attribute::data-id]
    Click Element  xpath=//li[label[@data-localize='${dropdown_id}']]//*[@class='hp-name' and text()='${item}']

