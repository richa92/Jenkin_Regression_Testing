*** Keywords ***
Select Dialog Panel
    [Documentation]  Select a panel from the drop list and verify that it scrolls into view
    [Arguments]    ${menu_id}    ${item}

    Wait Until Element Is Visible   ${menu_id}
    Click Element     xpath=//div[@id='${menu_id}']/div
    Click Element     xpath=//div[@id='${menu_id}']//a[contains(text(), '${item}')]
    Element Should Be Visible    xpath=//li[label[contains(text(), '${item}')]/following-sibling::fieldset]

Close Guided Setup If Open
    [Documentation]    If the OneView guided setup pane is open then cloes it
    Set Selenium Implicit Wait    0.5s
    ${setup_pane}=  Is Element Visible    css=#cic-setup-checklist-flyout.hp-active
    Set Selenium Implicit Wait    ${SELENIUM_IMPLICIT_WAIT}
    Run Keyword If   '${setup_pane}' == 'True'    Close Guided Setup

Close Guided Setup
    [Documentation]   Close the guided setup panel
    Set Selenium Implicit Wait    0.5s
    Element Should Be Visible    css=#cic-setup-checklist-flyout.hp-active
    Click Element   css=#cic-setup-checklist-control
    Element Should Not Be Visible    css=#cic-setup-checklist-flyout.hp-active
    Set Selenium Implicit Wait    ${SELENIUM_IMPLICIT_WAIT}

Open Guided Setup
    [Documentation]   Open the guided setup panel
    Set Selenium Implicit Wait    0.5s
    Element Should Not Be Visible    css=#cic-setup-checklist-flyout.hp-active
    Click Element   css=#cic-setup-checklist-control
    Element Should Be Visible    css=#cic-setup-checklist-flyout.hp-active
    Set Selenium Implicit Wait    ${SELENIUM_IMPLICIT_WAIT}