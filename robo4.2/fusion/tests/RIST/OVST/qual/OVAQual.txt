*** Settings ***
Documentation    OVA Qualification Test Suite
...
...    = Usage =
...    | pybot | -v ApplianceUrl:http://<ip> | -v DataFile:OVAData.xml | OVAQual.txt |
...    = Variables =
...    | ApplianceUrl | Required; URL of the Fusion appliance to test |
...    | DataFile | (optional) XML data file. Defaults to ./OVAData.xml | 
...    | SeleniumSpeed | (optional) Sets selenium speed. Defaults to 0.1 |
...    | Browser | (optional) Browser to use. Defaults to FireFox |

Library           Collections
Library           String
Library           RoboGalaxyLibrary
Library           FusionLibrary
Force Tags        OVAQual
Suite Setup       Qual Setup    
Suite Teardown    Logout And Close All Browsers

*** Variables ***
${DataFile}        ./OVAData.xml
${ApplianceUrl}    https://unknown
${Browser}         FireFox
${SeleniumSpeed}    0.1

*** Test Cases ***
Create User
    [Tags]    USR
    Fusion UI Create User    @{TestData.users}
    
Create Ethernet Network
    [Tags]    ENW
    Fusion UI Create Ethernet Network  @{TestData.networks}
    
Create FC Network
    [Tags]    FCN
    Fusion UI create FC Network    @{TestData.fcnetworks}

Create Network Set
    [Tags]    NWS
    Fusion UI Create Network Set    @{TestData.networksets}

Create LIG
    [Tags]    LIG
    Fusion UI Create Logical Interconnect Group    @{TestData.ligs}

Create Enclosure Group
    [Tags]    EG
    Fusion UI Create Enclosure Group    @{TestData.encgroups}

Add Enclosure
    [Tags]    Encl
    Fusion UI Add Enclosure    @{TestData.enclosures}

Create Server Profile
    [Tags]    SP
    Fusion UI create Server Profile    @{TestData.profiles}

*** Keywords ***
Qual Setup
    Set Log Level    TRACE
    Load Test Data    ${DataFile}
    Log Variables
    Open Browser    ${ApplianceUrl}    ${Browser}
    Set Selenium Speed    ${SeleniumSpeed}
    ${user}=    Get Data By Property    ${TestData.users}    name    Administrator
    ${IP}=      Fetch From Right    ${ApplianceUrl}    /
    Log    ${IP}
    Set Fusion Version Metadata    ${IP}    ${user[0].name}    ${user[0].password}
    Fusion UI Login to Appliance   ${user[0].name}

Logout and Close All Browsers
    Fusion UI Logout of Appliance
    Close All Browsers

Set Fusion Version Metadata
    [Documentation]    Get the version ident string from Fusion
    [Arguments]    ${IP}    ${USERNAME}    ${PASSWORD}
    Login to Fusion via REST    ${IP}    ${USERNAME}    ${PASSWORD}
    ${Response}=    Fusion Api Get Appliance Version
    Logout of Fusion Via REST
    Log    ${Response}
    ${softwareVersion}=    Get from Dictionary    ${Response}    softwareVersion
    ${date}=               Get from Dictionary    ${Response}    date
    Set Suite Metadata     Fusion Version    ${softwareVersion} : ${date}    top=True

Login to Fusion Via REST
    [Documentation]    Connects to the Appliance and creates a session using the Username and Password.
    [Documentation]    Example:\n| Login to Fusion Via REST | 10.0.12.106 | Administrator | hpvse123 |
    [Arguments]    ${IP}    ${USERNAME}    ${PASSWORD}
    ${Response}    ${SessionId}    Fusion Api Login Appliance    ${IP}    ${USERNAME}    ${PASSWORD}
    ${Status}    Get From Dictionary    ${Response}    status
    Return From Keyword If    '${Status}' == '200'    ${Response}
    ${errorCode}    Get From Dictionary    ${Response}    errorCode
    ${message}      Get From Dictionary    ${Response}    message
    Fatal Error    msg=Invalid response returned ${Status} ${errorCode} ${message}

Logout of Fusion Via REST
    [Documentation]    Terminates a session with the REST API.
    [Documentation]    Example:\n| Logout Of Fusion Via REST |
    Fusion Api Logout Appliance

