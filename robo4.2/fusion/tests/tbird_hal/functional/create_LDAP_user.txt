*** Settings ***
Documentation     Create a LDAP user sarah (password: serveradmin) for Fusion Authentication Test
...               SSH login: ssh wpstLDAP\sarah@<FUSION_IP>
...               
...    = USAGE =
...    | pybot | -v FUSION_IP:<IP> or -v ENCLOSURE:<name>  | create_LDAP_user.txt |
...    = Variables =
...    | ENCLOSURE | Enclosure name from variable.py |
...    | FUSION_IP | Fusion IP Address |

Force Tags           CLI    DCS     HW              # Force tags on all test cases

Variables            ../resources/variables.py    ${ENCLOSURE}

Library              json
Library              SSHLibrary
Library              FusionLibrary
Library              OperatingSystem
Resource             ../resources/defaults.txt
Resource             ../resources/fusion_cli.txt
Resource             ../resources/fusion_api.txt

Suite Setup          Login To Fusion Via REST
Suite Teardown	     Logout Of Fusion Via REST

*** Test Cases ***

Add LDAP Servers
    ${GroupInfo}=    OperatingSystem.Get File   ../data/Groups_Directory.json
    ${JSON}=	     json.loads   ${GroupInfo}
    ${LDAP}=	     Get From Dictionary	${JSON}	   LDAP
    ${Response}=     Fusion Api Add Directory   ${LDAP}
    ${Status}=	     Get From Dictionary	${Response}	status_code
    Should Be Equal  ${Status}			${201}
    Set Suite Variable	    ${GroupInfo}

Add AD Servers
    ${JSON}=	     json.loads                 ${GroupInfo}
    ${AD}=	     Get From Dictionary	${JSON}	   AD
    ${Response}=     Fusion Api Add Directory   ${AD}
    ${Status}=	     Get From Dictionary	${Response}	status_code
    Should Be Equal  ${Status}			${201}

Add LDAP Group User
    ${creds}=        Create Dictionary          userName   sarah    password   serveradmin
    ${roles}=        Create List    	        Server administrator
    ${group2role}=   Create Dictionary          egroup  FUSION_SVR_ADMIN  loginDomain  wpstLDAP   roles  ${roles}  type   Group2RolesMappingPerGroupDto
    ${request}=      Create Dictionary          credentials  ${creds}   group2rolesPerGroup   ${group2role}   type  Group2RolesMappingPerGroupValidationDto
    ${Response}=     Fusion API Add Role to Group    ${request}
    ${Status}=	     Get From Dictionary	${Response}	status_code
    Should Be Equal  ${Status}			${201}

Verify the SSH login with LDAP user 
    Set Default Configuration  prompt=${CLI_PROMPT}  timeout=10
    ${Id}=    	     Open Connection      ${FUSION_IP}
    Should Be True   ${Id}    	     	  msg=Failed to open SSH connection 
    ${Output}=       Login    	     	  wpstLDAP\\sarah    serveradmin
    Should contain   ${Output}     	  main-view>	     msg=Faile to get CLI prompt
