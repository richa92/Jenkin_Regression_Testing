*** Settings ***
Documentation    ps_health.txt - Power Supply Health test
...    = USAGE =
...    | pybot | -v ENCLOSURE:dcs |-v FUSION_IP:<IP> | ps_health.txt |
...    = Variables =
...    | ENCLOSURE | Required; Enclosure to use |
...    | FUSION_IP | Required when enclosure is DCS; IP address of the FusionVM to use |
*** Settings ***
Library           json
Library           robot.api.logger
Variables         ../resources/variables.py    ${ENCLOSURE}
Resource          ../resources/defaults.txt
Resource          ../resources/fusion_api.txt
Resource          ../resources/em_api.txt
Resource          ../resources/fusion_ssh.txt
Force Tags        HW
Suite Setup       Suite Setup
Suite Teardown    Suite Teardown

*** Variables ***
${EFuse_Sleep}    30

*** Test Case ***
###############################################################################
EFuseOn Power Supply Bay
    [Tags]    EFuseOn    EM    Alert    Status
    EFuse Power Supply Bay    EFuseOn    ${bayNumber}
    Sleep    ${EFuse_Sleep}

EFuseOn Power Supply EM Event Validation
    [Tags]    EFuseOn    EM
    ${Event_List}    ${Last_EventID}=    EM Diags Get Audit Log Events
    ...    Starting_EventID=${Last_EventID}    Origin_Filter=.*Power.*

    ${Expected_Events}    Create List    emRegistry.1.0.PowerSupplyRemoved
    :FOR    ${Event}    IN    @{Event_List}
    \    Log    ${Event['MessageID']}
    \    Remove Values From List    ${Expected_Events}    ${Event['MessageID']}
    Should Be Empty    ${Expected_Events}    Event(s) not found: ${Expected_Events}

EFuseOn Power Supply Activity Alert Validation
    [Tags]    EFuseOn    Alert
    ${Alert_list}    ${Last_AlertID}    Fusion Api Get Activity Alerts
    ...    Starting_AlertID=${${Last_AlertId}}    Resource_Filter=.*Power.*

    ${Expected_Alerts}    Create List    hpris.emRegistry.1.0.PowerSupplyRemoved
    :FOR    ${Alert}    IN    @{Alert_List}
    \    Remove Values From List    ${Expected_Alerts}    ${Alert['alertTypeID']}
    Should Be Empty    ${Expected_Alerts}    Event(s) not found: ${Expected_Alerts}

EFuseOn Power Supply Device Status Validation
    [Tags]    EFuseOn    Status
    ${DeviceInfo}    Fusion Api Get Device    BayNumber=${bayNumber}    
    # Verify Power Supply state and status are as expected
    Should Be Equal    ${DeviceInfo['devicePresence']}    Absent
    #Should Be Equal    ${DeviceInfo['state']}             ${None}
    Should Be Equal    ${DeviceInfo['status']}            ${None}

###############################################################################

EFuseOff Power Supply Bay
    [Tags]    EFuseOff    EM    Alert    Status
    EFuse Power Supply Bay    EFuseOff    ${bayNumber}
    Sleep    ${EFuse_Sleep}

EFuseOff Power Supply EM Event Validation
    [Tags]    EFuseOff    EM
    ${Event_List}    ${Last_EventID}=    EM Diags Get Audit Log Events
    ...    Starting_EventID=${Last_EventID}    Origin_Filter=.*Power.*
    
    ${Expected_Events}    Create List    emRegistry.1.0.PowerSupplyInserted
    ...                                  emRegistry.1.0.PowerSupplyOk
    :FOR    ${Event}    IN    @{Event_List}
    \    Log    ${Event['MessageID']}
    \    Remove Values From List    ${Expected_Events}    ${Event['MessageID']}
    Should Be Empty    ${Expected_Events}    Event(s) not found: ${Expected_Events}

EFuseOff Power Supply Activity Alert Validation
    [Tags]    EFuseOff    Alert
    ${Alert_List}    ${Last_AlertID}    Fusion Api Get Activity Alerts
    ...    Starting_AlertID=${${Last_AlertId}}    Resource_Filter=.*Power.*

    ${Expected_Alerts}    Create List     hpris.emRegistry.1.0.PowerSupplyInserted
    :FOR    ${Alert}    IN    @{Alert_List}
    \    Remove Values From List    ${Expected_Alerts}    ${Alert['alertTypeID']}
    Should Be Empty    ${Expected_Alerts}    Event(s) not found: ${Expected_Alerts}

EFuseOff Power Supply Device Status Validation
    [Tags]    EFuseOff    Status
    ${DeviceInfo}    Fusion Api Get Device    BayNumber=${bayNumber}    
    # Verify Power Supply state and status are as expected
    Should Be Equal    ${DeviceInfo['devicePresence']}    Present
    #Should Be Equal    ${DeviceInfo['state']}            OK
    Should Be Equal    ${DeviceInfo['status']}            OK

###############################################################################

Inject Power Supply Fault
    [Tags]    Fault    EM    Alert    Status
    EM Diags Fault Injection    PSFaultInjection    ${bayNumber}    PS_OK    true
    Sleep    ${EFuse_Sleep}

Inject Power Supply Fault EM Event Validation
    [Tags]    Fault    EM
    ${Event_List}    ${Last_EventID}=    EM Diags Get Audit Log Events
    ...    Starting_EventID=${Last_EventID}    Origin_Filter=.*Power.*

    ${Expected_Events}    Create List    emRegistry.1.0.PowerSupplyWarning
    ...                                  emRegistry.1.0.PowerSupplyFault
    :FOR    ${Event}    IN    @{Event_List}
    \    Log    ${Event['MessageID']}
    \    Remove Values From List    ${Expected_Events}    ${Event['MessageID']}
    Should Be Empty    ${Expected_Events}    Event(s) not found: ${Expected_Events}

Inject Power Supply Fault Activity Alert Validation
    [Tags]    Fault    Alert
    ${Alert_list}    ${Last_AlertID}    Fusion Api Get Activity Alerts
    ...    Starting_AlertID=${${Last_AlertId}}    Resource_Filter=.*Power.*

    ${Expected_Alerts}    Create List    hpris.emRegistry.1.0.PowerSupplyFault
    :FOR    ${Alert}    IN    @{Alert_List}
    \    Remove Values From List    ${Expected_Alerts}    ${Alert['alertTypeID']}
    Should Be Empty    ${Expected_Alerts}    Event(s) not found: ${Expected_Alerts}
    
Inject Power Supply Fault Device Status Validation
    [Tags]    Fault    Status
    ${DeviceInfo}    Fusion Api Get Device    BayNumber=${bayNumber}
    # Verify Power Supply state and status are as expected
    Should Be Equal    ${DeviceInfo['status']}    Warning

###############################################################################

Clear Power Supply Fault
    [Tags]    Clear    EM    Alert    Status
    EM Diags Fault Injection    PSFaultInjection    ${bayNumber}    PS_OK    false
    Sleep    ${EFuse_Sleep}

Clear Power Supply Fault EM Event Validation
    [Tags]    Clear    EM
    ${Event_List}    ${Last_EventID}=    EM Diags Get Audit Log Events
    ...    Starting_EventID=${Last_EventID}    Origin_Filter=.*Power.*

    ${Expected_Events}    Create List    emRegistry.1.0.PowerSupplyOk
    ...                                  emRegistry.1.0.PowerSupplyFaultCleared
    :FOR    ${Event}    IN    @{Event_List}
    \    Log    ${Event['MessageID']}
    \    Remove Values From List    ${Expected_Events}    ${Event['MessageID']}
    Should Be Empty    ${Expected_Events}    Event(s) not found: ${Expected_Events}

Clear Power Supply Fault Activity Alert Validation
    [Tags]    Clear    Alert
    ${Alert_List}    ${Last_AlertID}    Fusion Api Get Activity Alerts
    ...    Starting_AlertID=${${Last_AlertId}}    Resource_Filter=.*Power.*

    ${Expected_Alerts}    Create List      hpris.emRegistry.1.0.PowerSupplyFaultCleared
    :FOR    ${Alert}    IN    @{Alert_List}
    \    Remove Values From List    ${Expected_Alerts}    ${Alert['alertTypeID']}
    Should Be Empty    ${Expected_Alerts}    Event(s) not found: ${Expected_Alerts}

Clear Power Supply Fault Device Status Validation
    [Tags]    Clear    Status
    ${DeviceInfo}    Fusion Api Get Device    BayNumber=${bayNumber}
    # Verify Fan state and status are as expected
    #Should Be Equal    ${DeviceInfo['state']}     OK
    Should Be Equal    ${DeviceInfo['status']}    OK

###############################################################################

#Cleanup Power Supply Faults
#    [tags]    Cleanup
#    :FOR    ${bayNumber}    IN RANGE    1    6
#    \    EFuse Power Supply Bay    EFuseOff    ${bayNumber}
#    \    EM Diags Fault Injection    PSFaultInjection    ${bayNumber}    PS_OK    false

###############################################################################

*** Keywords ***
Suite Setup
    Login to Fusion via REST
    Login to Fusion via SSH

    # Get EM Session ID
    ${EM_IP}    ${EMSessionID}=    EM Api Create Session
    Set Suite Variable    ${Header}    "X-Auth-Token":"${EMSessionID}"

    # Select a random Power Supply Bay
    ${bayNumber}    Evaluate    random.randint(1, 6)    random
    Set Suite Variable    ${bayNumber}    1

    # Find Last Audit Event IDs
    Run Keyword and Ignore Error    EM Diags Get Audit Log Events
    Console    Last EM Event found: ${Last_EventID}
    Run Keyword and Ignore Error    Fusion Api Get Activity Alerts 
    Console    Last Alert found: ${Last_AlertID}
    
Suite Teardown
    Logout of Fusion via REST
    Logout of Fusion via SSH

###############################################################################

EFuse Power Supply Bay
    [Documentation]    Perform an efuse action on a power supply bay. Action = EFuseOff | EFuseOn
    [Arguments]    ${Action}    ${BayNumber}
    ${Data}      Set Variable    {"Action":"${Action}"}
    ${Output}    Execute SSH Command
    ...    curl -ikX POST -H ${Header} --data-ascii '${data}' https://${EM_IP}%${fusion_nic}/rest/v1/PowerSupplyBays/${BayNumber}
    Should Contain    ${Output}    { "Action": "${Action}" }
    ...    msg=EFuse action failed \n${Output}


###############################################################################

EM Diags Get Audit Log Events
    [Documentation]    Fetch Diag Audit Log Events.
    ...    (Assumes an SSH connection with curl is currently open)
    ...    Returns a list of (dictionary) entries and EventID of the latest event encountered.
    [Arguments]    ${IP}=${EM_IP}    ${Nic}=${FUSION_NIC}
    ...            ${Origin_Filter}=/rest/v1    ${Starting_EventID}=1-1
    Set Suite Variable    ${Last_EventID}    ${Starting_EventID}
    ${EM_Audit_Events_List}    Create List

    # Get the most recent entries from the audit (RIS events) log.
    ${Output}    Execute SSH Command
    ...       curl -ksS -H ${Header} https://${EM_IP}%${FUSION_NIC}/rest/v1/Diags/AuditLog/50
    # Remove CLI prompt from buffer.  It messes up loads.
    ${Output}    String.Fetch From Left    ${Output}    [root@

    # Break the output into an (easier to handle) list of lines. One line per event.
    @{Lines}    Split To Lines    ${Output}
    : FOR    ${Line}    IN    @{Lines}
    \    # Reformat event/line (string) into a json dictionary    
    \    ${Event}    json.loads    ${Line}    

    \    # Select only the most recent events - skip any before that.
    \    ${EventID}    Get From Dictionary    ${Event}    EventID
    \    # EventID's are of the form n-nnn.  Only compare at the portion after the dash.
    \    ${tmpEventID}             Fetch From Right    ${EventID}    -
    \    ${tmpStarting_EventID}    Fetch From Right    ${Starting_EventID}    -
    \    Run Keyword If    ${${tmpStarting_EventID}}>=${${tmpEventID}}    Continue For Loop

    \    # Remember the last EventID encountered
    \    ${tmpLast_EventID}    Fetch From Right    ${Last_EventID}    -
    \    Run Keyword If    ${${tmpEventID}}>=${${tmpLast_EventID}}    Set Suite Variable    ${Last_EventID}    ${EventID}

    \    # Filter out any unrelated events (e.g., I'm only interested in power supply events)
    \    # Example: ${Origin_Filter} = '/rest/v1/PowerSupplyBays/${bayNumber}'
    \    ${OriginOfCondition}    Get From Dictionary    ${Event}    OriginOfCondition
    \    ${Found}    Run Keyword And Return Status
    \    ...    Should Match Regexp    ${OriginOfCondition}    ${Origin_Filter}
    \    Run Keyword If    '${Found}'=='False'    Continue For Loop

    \    # Add event (dictionary) into 'List of EM Audit Events'
    \    Append to List    ${EM_Audit_Events_list}    ${Event}
    [Return]    ${EM_Audit_Events_List}    ${Last_EventID}

EM Diags Fault Injection
    [Documentation]    Inject faults. Where InjectionType is one of: FruFaultInjection, 
    ...    EmSwitchFaultInjection, EmSwitchPortFaultInjection, BladeManagerFaultInjection, 
    ...    CommFaultInjection, FanFaultInjection, PSFaultInjection.
    ...    usage for FanFaultInjection:
    ...    URI format: /rest/v1/Diags/FanFaultInjection/<slotID>/<faultType>[/<bool>]
    ...    Where:
    ...      slotID    = Valid power supply slot number.
    ...      faultType = [Fan1 | Fan2 | EFuse]
    ...      bool      = true to inject fault, false to clear fault
    ...                    NOTE: Boolean is not valid for EFuse faultType.
    ...    usage for PSFaultInjection:
    ...    URI format: /rest/v1/Diags/PSFaultInjection/<slotID>/<faultType>/<bool>
    ...    Where:
    ...      slotID    = Valid Power Supply slot number.
    ...      faultType = [PS_OK | AC_OK ]
    ...      bool      = true to inject fault, false to clear fault    
    [Arguments]    ${InjectionType}    ${slotID}    ${faultType}    ${inject}=''
    ${Output}    Execute SSH Command
    ...    curl -ksS -H ${Header} https://${EM_IP}%${fusion_nic}/rest/v1/Diags/${InjectionType}/${slotID}/${faultType}/${inject}
    Should Contain    ${Output}    successfully
    [Return]    ${Output}

###############################################################################

Fusion Api Get Activity Alerts
    [Documentation]    Fetch recent Fusion Activity Alerts
    ...    (Assumes a Fusion REST Api connection is open and logged in).
    ...    Returns a list of Alerts.
    [Arguments]    ${Starting_AlertID}=1    ${Resource_Filter}=.*
    ${Alert_List}    Create List
    # Get the entire list of Alerts
    ${Response}    Fusion Api Get Alerts    /rest/alerts?sort=created:ascending    
    ${Count}    Get From Dictionary    ${Response}    count
    Run Keyword if            '${Count}'=='0'    Console    \nNo alert events found
    Return From Keyword If    '${Count}'=='0'    msg=No alert events found

    ${Members}    Get From Dictionary    ${Response}    members
    :FOR    ${Index}    IN RANGE    0    ${Count}
    \    ${Alert}    Get From List    ${Members}    ${Index}

    \    # Select only the most recent events - skip any before that
    \    ${uri}    Get From Dictionary    ${Alert}    uri
    \    ${id}     Fetch From Right    ${uri}    /
    \    Run Keyword If    ${Starting_AlertID}>=${id}    Continue For Loop

    \    # Remember the last Alert ID encountered
    \    Set Suite Variable    ${Last_AlertID}    ${id}

    \    # Filter out any unrelated alerts
    \    ${resourceID}    Get From Dictionary    ${Alert}    resourceID
    \    ${Found}    Run Keyword And Return Status
    \    ...    Should Match Regexp    ${resourceID}    ${Resource_Filter}
    \    Run Keyword If    '${Found}'=='False'    Continue For Loop

    \    # Include this alert into the 'List of Activity Events'
    \    Append to List    ${Alert_List}    ${Alert}
    Should Not Be Empty    ${Alert_List}    No Activity Events Found
    [Return]    ${Alert_List}    ${Last_AlertID}

Fusion Api Get Device
    [Documentation]    Get a specific device from the enclosure information 
    [Arguments]    ${DeviceType}=powerSupplyBays    ${BayNumber}=1
    ${Response}    Fusion Api Get Enclosures
    ${Status}      Get From Dictionary    ${Response}    status_code
    Should be Equal    ${Status}    ${200}    msg=Get Enclosures failed ${Response}    

    # Make sure we get (more than zero) enclosures listed
    ${Count}       Get From Dictionary    ${Response}    count
    Run Keyword If    '${Count}'=='0'    Fatal Error    msg=No enclosures found

    # Fetch a pointer to the first enclosure - Member0 
    ${Members}     Get From Dictionary    ${Response}    members
    ${Member0}     Get From List          ${Members}      0

    # Extract selected device bay information
    ${DeviceBays}        Get From Dictionary    ${Member0}      ${DeviceType}
    ${BayIndex}     Evaluate    ${BayNumber}-1
    ${DeviceInfo}    Get From List    ${DeviceBays}    ${BayIndex}
    [Return]    ${DeviceInfo}
