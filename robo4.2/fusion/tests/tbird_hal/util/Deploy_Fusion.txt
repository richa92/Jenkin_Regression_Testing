*** Settings ***
Documentation     Deploy latest Fusion OVA and perform 1st time setup
...    = USAGE =
...    | pybot | -v FUSION_NAME:<name> | Deploy_Fusion.txt |
...    | pybot | -v FUSION_NAME:<name> | [ -i FTS  -e FTS ] | Deploy_Fusion.txt |
...    = Variables =
...    | FUSION_NAME | Required. VMware name for  the Fusion VM. |
...    | FUSION_DEPOT | URL specifying location of the OVA files. |
...    | | (default http://ci-nexus.vse.adapps.hp.com/Fusion/master/OVA/SSH/ ). |
...    | VSPHERE_DATASTORE | vShpere device to hold deployed VM (default fusionvm-1) | 
...    = Tags =
...    | -e FTS | Deploys without performing First-Time Setup |
...    | -i FTS | Performs First-Time Setup without re-deploying. Requires -v FUSION_IP:nn.nn.nn.nn |

Library     String
Library     Collections
Library     OperatingSystem
Library     robot.api.logger
Library     RoboGalaxyLibrary
Library     FusionLibrary
Resource    ../resources/fusion_api.txt
Resource    ../resources/defaults.txt

*** Variables ***
${vSphere_IP}          vety.rsn.hp.com
${vSphere_Username}    Administrator
${vSphere_Password}    HPvse123
${OVFtool}             \\Progra~1\\VMware\\VMware~2\\ovftool.exe    #for 64-bit ovftool
#${OVFtool}            \\Progra~2\\VMware\\VMware~1\\ovftool.exe    #for 32-bit ovftool
#${Target_Locator}      vi://${vSphere_Username}:${vSphere_Password}@${vSphere_IP}/WPST/host/wpst-cluster
${Target_Locator}      "vi://Administrator:HPvse123@vety.rsn.hp.com/AM-DVT Datacenter/host/AM-DVT Cluster"
#${Fusion_Depot}        http://ci-nexus.vse.adapps.hp.com/Fusion/master/OVA/DCS/
${Fusion_Depot}        http://ci-nexus.vse.adapps.hp.com/Fusion/master/OVA/SSH/
${vSphere_Datastore}    "autovm-cluster"
#${vSphere_Datastore}    "fusionvm-1"

${Fusion_Name}         Noname-HPOneView
${Fusion_URL}          unknown
${Fusion_Password}     hpvse123

*** Test Cases ***
Get Latest Fusion DCS PASS Build URL
    ${OVA}=    Get LatestBuild Name        ${Fusion_Depot}    ''    ova
    Set Suite Variable    ${Deploy_URL}    ${Fusion_Depot}${OVA}
    Console    \nDeploying from: ${Deploy_URL}
    Log    ${Deploy_URL}
    Set Suite Metadata     Deployed URL    ${Deploy_URL}    top=True

Extract Build Id Number
    ${Match}    ${Id}=    Should Match Regexp    ${Deploy_URL}    (\\d{5}).ova$
    Log    ${Id}
    Set Suite Variable    ${Fusion_Id}    ${Id}
    Set Suite Metadata     Fusion Build Id    ${Fusion_Id}    top=True
    Set Suite Variable    ${Fusion_Name}    ${Fusion_Name}${Fusion_Id}
    Set Suite Metadata     Fusion VM Name    ${Fusion_Name}    top=True

Check OVFtool
    ${RC}    ${Output}=    Run and Return Rc and Output    dir ${OVFtool}
    Run Keyword Unless    '${RC}' == '0'    Fatal Error
    ...    msg=Could not find ovftool.exe. Is it installed? (See \\\\eml.usa.hp.com\\emgmt\\Tools\\WMARE)

Redeploy Fusion
    Console    \nDeploying VM: ${Fusion_Name}
    ${Command}=    Catenate    ${OVFtool}
    ...    --skipManifestCheck
    ...    --noSSLVerify
    ...    --acceptAllEulas
    ...    --machineOutput
    ...    --powerOn
    ...    --ipProtocol=IPv4
    ...    --powerOffTarget
    ...    --overwrite
    ...    --name="${Fusion_Name}"
    ...    --datastore=${vSphere_Datastore}
    ...    --network="VM Network"
    ...    --diskMode=thin 
    #...    --vmFolder="UI_Automation"
    ...    ${Deploy_URL}
    ...    ${Target_Locator}    
    Log    ${Command}
    Console    ${Command}
    ${RC}    ${Output}=    Run and Return Rc and Output    ${Command}
    Run Keyword Unless    '${RC}' == '0'    Fatal Error    msg=Could not deploy new VM ${Output}

Get Fusion IP Address
    Connect To VI Server    ${vSphere_IP}    ${vSphere_Username}    ${vSphere_Password}
    Wait Until Keyword Succeeds    10 min    60 sec    Get VM IPv4 Addresses    ${Fusion_Name}
    @{IPs}=    Get VM IPv4 Addresses    ${Fusion_Name}
    ${Count}=    Get Length    ${IPs}
    Run Keyword If    ${Count} == 0    Fatal Error    msg=No IP address returned from vSphere
    ${IP}=     Get From List    ${IPs}     0
    Should Match RegExp    ${IP}    16\.[0-9]{1,3}\.[0-9]{1,3}\.[0-9]{1,3}
    ...    msg=No valid IP address returned from vSphere
    Set Suite Variable    ${Fusion_IP}     ${IP}
    Set Suite Variable    ${Fusion_URL}    https://${Fusion_IP}
    Set Environment Variable    FUSION_URL    ${Fusion_Url}
    Set Environment Variable    FUSION_IP    ${Fusion_IP}
    Console    \nYour FusionVM URL is: ${Fusion_URL}
    Set Suite Metadata     Fusion URL:    ${Fusion_URL}    top=True

Wait for VM Startup
    # It'going to take 15 to 20 minutes for Fusion to be login-able. A good time for a nap.
    Sleep    15 minutes    Waiting for startup to complete
    Open Browser    ${Fusion_URL}    firefox
    Sleep    15 seconds
    Capture Page Screenshot
    Wait Until Keyword Succeeds    30 min    60 sec    Page Should not contain    Starting
    Capture Page Screenshot
    Close Browser
    
Open Browser To Login Page After Startup
    [Tags]    FTS
    Open Browser    ${Fusion_URL}/#/login    firefox
    # Click 'Continue to this website' link allowing test case to continue (for IE only).
    # Run Keyword If  "${BROWSER}" == "IE"   Go To  javascript:document.getElementById('overridelink').click()
    sleep    5
    Capture Page Screenshot

EULA Agreement
    [Tags]    FTS
    Wait Until Element Is Visible    id=hp-eula-agree-button
    ...                              timeout=120
    ...                              error=Not at EULA page 
    Click Button    id=hp-eula-agree-button
    sleep    5
    Click Button    id=hp-eula-ok-button
    sleep    5
    Capture Page Screenshot

First Time Login
    [Tags]    FTS
    Wait Until Element Is Visible    id=hp-login-user
    ...                              timeout=300
    ...                              error=Not at Login page
    Input Text    hp-login-user    Administrator
    Input Text    hp-login-password    admin
    Click Button    hp-login-button
    sleep    5
    Capture Page Screenshot

Assign Administrator Password
    [Tags]    FTS
    Wait Until Element Is Visible    id=hp-initial-password-new1
    ...                              timeout=300
    ...                              error=Not at InitialPassword page 
    Input Text    id=hp-initial-password-new1    ${Fusion_Password}
    Input Text    id=hp-initial-password-new2    ${Fusion_Password}
    Click Button    id=hp-initial-password-button-element
    sleep    5
    Capture Page Screenshot

Assign DHCP Networking
    [Tags]    FTS
    Wait Until Element Is Visible    id=cic-network-hostname
    ...                              timeout=300
    ...                              error=Not at Initial-network page
    Input Text    id=cic-network-hostname    ${Fusion_Name}.rsn.hp.com
    Sleep    5
    Capture Page Screenshot
    Click Button    id=cic-network-ipv4-dhcp
    Sleep    5
    Capture Page Screenshot
    # Workaround: Doesn't set button until the second try
    Click Button    id=cic-network-ipv4-dhcp
    Sleep    5
    Capture Page Screenshot
    Click Button    id=cic-network-ok
    Sleep    5
    Capture Page Screenshot
    Wait Until Page Contains    Applying network settings.    timeout=300
    
Wait for Fusion Ready
    [Tags]    FTS
    Wait Until Keyword Succeeds    30 min    30 sec    Page Should not contain    Applying network settings.
    Wait Until Element Is Visible    id=hp-main-menu-control
    ...                              timeout=300
    ...                              error=Not at Dashboard page (cant find main-menu)
    ${Location}=    Get Location
    ${Page}=    Fetch From Right    ${Location}    ${Fusion_URL}
    Pass Execution If    '${Page}'=='/#/dashboard'    msg=Found dashboard
    Pass Execution If    '${Page}'=='/#/activities'    msg=Found activities
    Pass Execution If    '${Page}'=='/#/activity'    msg=Found activity
    Pass Execution If    '${Page}'=='/#/settings/show/overview'    msg=Found Overview
    Capture Page Screenshot
    Fail    msg=Failed to apply network settings

Cleanup
    [Tags]    FTS
    Close Browser

Set Fusion Version Metadata
    Login to Fusion via REST    ${FUSION_IP}    ${FUSION_USERNAME}    ${FUSION_PASSWORD}
    ${Response}=    Fusion Api Get Appliance Version
    Logout of Fusion Via REST
    Log    ${Response}
    ${softwareVersion}=    Get from Dictionary    ${Response}    softwareVersion
    Set Suite Variable    ${Fusion_Version}    ${softwareVersion}
    Set Suite Metadata     Fusion Version    ${Fusion_Version}    top=True

Write Fusion Properties
    [Tags]    PROPERTIES
    Run    echo \# %JOB_NAME% %DATE% %TIME% > C:\\Jenkins\\Fusion.properties
    Run    echo FUSION_ID=${Fusion_Id} >> C:\\Jenkins\\Fusion.properties
    Run    echo FUSION_NAME=${Fusion_Name} >> C:\\Jenkins\\Fusion.properties
    Run    echo FUSION_IP=${Fusion_IP} >> C:\\Jenkins\\Fusion.properties
    Run    echo FUSION_URL=${Fusion_URL} >> C:\\Jenkins\\Fusion.properties
    Run    echo FUSION_VERSION=${Fusion_Version} >> C:\\Jenkins\\Fusion.properties

Take VM Snapshot
    [Tags]    DCS
    Connect to VI Server    ${vSphere HostName}     ${vSphere Username}     ${vSphere Password}
    ${Fusion_Name}=    Get VM Name by IP    ${FUSION IP}
    Create VM Snapshot      ${Fusion_Name}      VM_Deployed
    Disconnect from VI Server
