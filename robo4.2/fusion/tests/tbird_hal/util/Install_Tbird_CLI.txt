*** Settings ***
Documentation     Install Thunderbird CLI
...    = USAGE =
...    | pybot | -v FUSION_IP:<IP> | -v FUSION_VERSION:<version> | Install_Tbird_CLI.txt |
...    | pybot | -v FUSION_IP:16.114.191.80 | -v FUSION_VERSION:2.00.00-0016118 | Install_Tbird_CLI.txt |
...    = Variables =
...    | FUSION_IP | Fusion IP Address |
...    | FUSION_VERSION | Fusion Build Version |

Library           Collections
Library           RoboGalaxyLibrary            # DVTs Robot Framework extensions
Library           FusionLibrary                # DVTs Fusion extensions
Resource          ../resources/fusion_cli.txt
Resource          ../resources/defaults.txt

*** Variables ***
${REPO}                  /etc/yum.repos.d/fusion200.repo
${Install_CLI}           ${FALSE}
${FUSION_VERSION}        Latest

*** Test Cases ***
Install Tbird CLI
    [Tags]    DCS  HW
    [Documentation]    Install the Thunderbird CLI packages
    Login to Fusion via SSH
    Set Client Configuration    timeout=300

    # Check if CLI is already installed.
    ${output}=    Execute SSH Command    rpm -qa | grep -i cli-package
    ${status}=    Run Keyword and Return Status    Should Contain    ${output}    cli-package

    # Pass Test Case if already installed
    Pass Execution If    '${status}'=='${TRUE}'    CLI package is already installed.

    # Set Install variable to true
    Set Suite Variable    ${Install_CLI}    ${TRUE}

    #Create a fusion200 repository 
    @{Commands}    Create List      name=Fusion 2.00 repository
    ...                baseurl=http://ci-nexus.vse.adapps.hp.com/Fusion/master/YUM/Builds/${FUSION_VERSION}/
    ...                failovermethod=priority
    ...                enabled=1
    ...                gpgcheck=0
    ...                gpgkey=file:///etc/pki/rpm-gpg/RPM-GPG-KEY-fusion
    Execute Command    echo [fusion200] > ${REPO}

    :FOR    ${Command}    IN    @{Commands}
    \    ${stdout}    ${stderr}    ${rc}=    Execute Command    echo '${Command}' >> ${REPO}    return_stderr=True    return_rc=True
    \    Log    ${stdout}
    \    Should Be Empty    ${stderr}                 msg=Error returned: ${stderr}

    # Install Devmode package
    ${stdout}    ${stderr}    ${rc}=    Execute Command      yum install -y cli-package     return_stderr=True    return_rc=True
    Log    ${stdout}
    Log    ${stderr}
    Should Be Equal As Integers    ${rc}    0

    Logout of Fusion Via SSH

Take VM Snapshot
    [Tags]    DCS  SNAPSHOT
    # Skip Snapshot if CLI was not installed.
    Pass Execution If    '${Install_CLI}'=='${FALSE}'    CLI package was already installed. Skipping VM Snapshot.

    # Take Snapshot
    Connect to VI Server    ${vSphere HostName}     ${vSphere Username}     ${vSphere Password}
    ${VM Name}=    Get VM Name by IP    ${FUSION IP}
    Create VM Snapshot      ${VM Name}      CLI_Installed
    Disconnect from VI Server
