*** Settings ***
Documentation     CleanUp Fusion
...    = USAGE =
...    | pybot | -v FUSION_IP:<ip> | Cleanup_Fusion.txt |
...    = Variables =
...    | FUSION_IP | Required; IP address of the Fusion to use |

Library           Collections
Library           String 
Library           robot.api.logger
Library           RoboGalaxyLibrary    # DVTs Robot Framework extensions
Library           FusionLibrary
Force Tags        CometUtil            # All test cases in sub test suites get these tags
Suite Setup       Login to Fusion Via REST    
Suite Teardown    Logout of Fusion Via REST

*** Variables ***
${FUSION_IP}          unknown
${FUSION_USERNAME}    Administrator
${FUSION_PASSWORD}    hpvse123

@{FUSION_ENTITIES}    Server Profiles
    ...               Enclosures
    ...               Enclosure Groups
    ...               Uplink Sets
    ...               Ethernet Networks
    ...               FC Networks
    ...               Network Sets
    #...              Logical Interconnects
    ...               Logical Interconnect Groups
    ...               Power Devices
    ...               Server Hardware
    ...               Server Hardware Types
    ...               Racks

*** Test Cases ***
CleanUp Fusion
    [Documentation]    Remove all entities from a Fusion instance
    Delete Fusion Entities    @{Fusion_Entities}
    Delete Users
    Delete Login Domains

Clean Check
    [Documentation]    Check for remaining Fusion entities
    Check Fusion Entities    @{Fusion_Entities}

*** Keywords ***
Login to Fusion Via REST
    [Documentation]    Connects to the Appliance and creates a session using the Username and Password.
    [Arguments]    ${IP}=${FUSION_IP}    ${USERNAME}=${FUSION_USERNAME}    ${PASSWORD}=${FUSION_PASSWORD}
    Should Not Be Equal    ${IP}    'unknown'    msg=Please specify a valid Fusion IP address or hostname
    #${Response}    ${SessionId}    Fusion Api Login Appliance    ${IP}    ${USERNAME}    ${PASSWORD}
    #${Status}    Get From Dictionary    ${Response}    status
    #Return From Keyword If    '${Status}' == '200'    ${Response}
    #${errorCode}    Get From Dictionary    ${Response}    errorCode
    #${message}      Get From Dictionary    ${Response}    message
    #Fatal Error    msg=Invalid response returned ${Status} ${errorCode} ${message}
    
    ${Cred}=    Create Dictionary    userName    ${USERNAME}    password    ${PASSWORD}
    ${SessionId}    Fusion Api Login Appliance    ${IP}    ${Cred}
    Log    ${SessionId}

Logout of Fusion Via REST
    [Documentation]    Terminates a session with the REST API.
    Fusion Api Logout Appliance

Delete Fusion Entities
    [Documentation]    Remove entities from a Fusion VM
    [Arguments]    @{Fusion_Entities}
    : For     ${Entity}     in    @{Fusion_Entities}
    \    ${Response}=    Run Keyword    Fusion Api Get ${Entity}
    \    ${Members}    Get From Dictionary    ${Response}    members
    \    ${Count}      Get From Dictionary    ${Response}    count
    \    Continue For Loop If    '${Count}'=='0'
    \    Console    \nRemoving ${Entity}
    \    Delete Fusion Entity    ${Entity}    ${Count}      ${Members}

Delete Fusion Entity 
    [Documentation]    Removes specified entity (LIG, LI, etc).
    [Arguments]    ${Entity}    ${Count}    ${Members}
    : FOR    ${Index}    IN RANGE    0    ${Count}
    \    ${Member}=      Get From Dictionary    ${Members}    ${Index}
    \    ${Name}=        Get From Dictionary    ${Member}    name
    \    Console         .... ${Name}
    \    ${Uri}=         Get From Dictionary    ${Member}    uri
    \    ${ID}=          Fetch From Right    ${Uri}    /
    \    ${Response}     Run Keyword    Fusion Api Delete ${Entity} Id     ${ID}
    \    ${Status}       Get From Dictionary    ${Response}    status
    \    Continue For Loop If    '${Status}' == '200'
    \    Continue For Loop If    '${Status}' == '204'
    \    ${errorCode}    Get From Dictionary    ${Response}    errorCode
    \    ${message}      Get From Dictionary    ${Response}    message
    \    Fatal Error     msg=Invalid response returned deleting ${Entity} ${Status} ${errorCode} ${message}

Check Fusion Entities
    [Documentation]    Verify no remaining entities in a Fusion VM
    [Arguments]    @{Fusion_Entities}
    : For     ${Entity}     in    @{Fusion_Entities}
    \    Console    Checking for ${Entity}
    \    ${Response}=    Run Keyword    Fusion Api Get ${entity}
    \    ${Count}      Get From Dictionary    ${Response}    count
    \    Should Be Equal As Integers    ${Count}    0    msg="Found ${Entity} after deletion"

Delete Users
    [Documentation]    Remove all user accounts from a Fusion VM
    Console    \nRemoving Users
    ${Response}=    Fusion Api Get Users
    ${Members}      Get From Dictionary    ${Response}    members
    ${Count}        Get From Dictionary    ${Response}    count
    Delete User     ${Count}    ${Members}

Delete User 
    [Arguments]    ${Count}    ${Members}
    : FOR    ${Index}    IN RANGE    0    ${Count}
    \    ${Member}=      Get From Dictionary    ${Members}    ${Index}
    \    ${Name}=        Get From Dictionary    ${Member}    userName
    \    Continue For Loop If    '${Name}' == 'administrator'
    \    Console         .... ${Name}
    \    ${Response}     Run Keyword    Fusion Api Delete Users    ${Name}
    \    ${Status}       Get From Dictionary    ${Response}    status
    \    Continue For Loop If    '${Status}' == '204'
    \    ${errorCode}    Get From Dictionary    ${Response}    errorCode
    \    ${message}      Get From Dictionary    ${Response}    message
    \    Fatal Error     msg=Invalid response returned deleting Users ${Status} ${errorCode} ${message}

Delete Login Domains
    Console    \nRemoving Login Domains
    ${Response}=    Fusion Api Get Login Domains
    ${Members}      Get From Dictionary    ${Response}    members
    ${Count}        Get From Dictionary    ${Response}    count
    Delete Login Domain    ${Count}    ${Members}
    
Delete Login Domain
    [Arguments]    ${Count}    ${Members}
    : FOR    ${Index}    IN RANGE    0    ${Count}
    \    ${Member}=      Get From Dictionary    ${Members}    ${Index}
    \    ${Name}=        Get From Dictionary    ${Member}    name
    \    Console         .... ${Name}
    \    ${loginDomain}=    Get From Dictionary    ${Member}    loginDomain
    \    ${Response}     Run Keyword    Fusion Api Delete Login Domain    ${loginDomain}
    \    ${Status}       Get From Dictionary    ${Response}    status
    \    Continue For Loop If    '${Status}' == '204'
    \    ${errorCode}    Get From Dictionary    ${Response}    errorCode
    \    ${message}      Get From Dictionary    ${Response}    message
    \    Fatal Error     msg=Invalid response returned deleting Users ${Status} ${errorCode} ${message}
 