*** Settings ***
Documentation   Verifies Serial connections displayed in the Fusion CLI.
...             Requires a claimed enclosure.
...             = Usage =
...             | pybot | -L DEBUG | -v FUSION_IP:<IP> | -v ENCLOSURE:<name> | -i [ DCS | HW ] | CLI_serial_connections.txt |
...             = Variables =
...             | FUSION IP     | Required; IP address of the FusionVM to use |
...    | ENCLOSURE        | Required; Enclosure name to run on (dcs, tesla, etc) |
...             = Tags =
...             | -i DCS | Executes Test Cases applicable on DCS |
...             | -i HW  | Executes Test Cases applicable on Hardware |
...             = Preconditions =
...             None

Library             RoboGalaxyLibrary               # DVTs Robot Framework extensions
Library             FusionLibrary                   # DVTs Robot Framework extensions
Variables           ../resources/variables.py    ${ENCLOSURE}  # Variables file
Resource            ../resources/system_info.txt    # Comet-DVT System Info extensions
Resource            ../resources/fusion_cli.txt     # Comet-DVT fusion CLI extensions
Resource            ../resources/defaults.txt       # Comet-DVT default variables
Suite Setup         Run Keywords
...                     Login to Fusion CLI
...                     Enter CLI Console View
#...                     Verify EM is not Claimed
Suite Teardown      Run Keywords
...                     Logout of Fusion CLI
Force Tags          CLI	       HW                           # Force tags on all test cases


*** Variables ***
@{Expected Error Message}    No enclosure found with specified name or UUID.
...                          Verify enclosure identifier specified and retry operation.
...                          Usage: show enclosure (list | sessions) \[<enclosure name|uuid>\]
...                          ${SPACE * 7}show (interconnect | blade) (list | sessions) \[<enclosure name|uuid> \[<bay number>\]\]


*** Test Cases ***
# -----------------------------------------------------------------------
# NEGATIVE UNCLAIMED TEST CASES
# -----------------------------------------------------------------------
Verify Show Enclosure List without claimed enclosure
  [Tags]    DCS
  ${Buffer}=                      Execute CLI Show Command    enclosure list
  Verify Unclaimed Enclosure Error    ${Buffer}                   show enclosure list

Verify Show Blade List without claimed enclosure
  [Tags]    DCS
  ${Buffer}=                      Execute CLI Show Command    blade list
  Verify Unclaimed Enclosure Error    ${Buffer}                   show blade list

Verify Show Interconnect List without claimed enclosure
  [Tags]    DCS
  ${Buffer}=                      Execute CLI Show Command    interconnect list
  Verify Unclaimed Enclosure Error    ${Buffer}                   show interconnect list

Verify Show Enclosure Sessions without claimed enclosure
  [Tags]    DCS
  ${Buffer}=                      Execute CLI Show Command    enclosure sessions
  Verify Unclaimed Enclosure Error   ${Buffer}                   show enclosure sessions

Verify Show Blade Sessions without claimed enclosure
  [Tags]    DCS
  # show blade sessions
  ${Buffer}=                      Execute CLI Show Command    blade sessions
  Verify Unclaimed Enclosure Error    ${Buffer}                   show blade sessions

Verify Show Interconnect Sessions without claimed enclosure
  [Tags]    DCS
  # show interconnect sessions
  ${Buffer}=                      Execute CLI Show Command    interconnect sessions
  Verify Unclaimed Enclosure Error    ${Buffer}                   show interconnect sessions

*** Keywords ***
Verify Unclaimed Enclosure Error
    [Arguments]     ${Buffer}   ${Command}
    # Check for Internal Errors
    Should Not Contain      ${Buffer}   Internal error.     msg=Error message found in ${Command} output.

    # Verify Data
    Log List    ${Expected Error Message}
    Log List    ${Buffer}    
	Lists Should Be Equal    ${Buffer}    ${Expected Error Message}    msg=Failed to verify output for '${Command}'.

Execute CLI Show Command
    [Documentation]     Executes "Show ..." CLI command on the console view and returns buffer as an list.
    [Arguments]         ${params}
    ${output}=          Execute CLI Command     show ${params}      ${CLI CONSOLE PROMPT}

    # Covert to lines and remove control characters
    ${lines}=           Split to Lines      ${output}
    Remove From List    ${lines}            0   # Remove first line: \x1b[?1h\x1b
    Log List            ${lines}

    # Remove empty lines in list
    ${index}=           Get Length          ${lines}
    ${index}=           Evaluate            ${index}-1
    :FOR                ${line}             IN              @{lines}
    \                   ${value}=           Get From List   ${lines}            ${index}
    \                   ${size}=            Get length      ${value.strip()}
    \                   Run Keyword If      ${size}==0      Remove From List    ${lines}    ${index}
    \                   ${index}=           Evaluate        ${index}-1
    Remove From List    ${lines}            -1              # Remove last line: console-view>
    Log List            ${lines}

    [Return]    ${lines}

