*** Settings ***
Documentation     Reset ICM Manager from the Webapp
...    = USAGE =
...    | pybot | -v FUSION_IP:my_fusion_ip | icm_mgr_reset.txt |
...    | pybot | -v FUSION_IP:my_fusion_ip | -v EM_IP:16.114.179.116 | icm_mgr_reset.txt |
...    = Variables =
...    | FUSION_IP | Required. IP address or hostname of the Fusion 2.0 appliance. |
...    | EM_IP | Optional. IP address or hostname of the Enclosure Manager to which the enclosure belongs.  Default value is for DCS. |

Library           RoboGalaxyLibrary                    # DVTs Robot Framework extensions
Library           FusionLibrary                        # DVTs Robot Framework extensions
Library           OperatingSystem                      # For opening files
Library           json
Variables         ../resources/variables.py    ${ENCLOSURE}    # Variables file
Resource          ../resources/compare_json.txt        # Comet-DVT JSON comparison extensions
Resource          ../resources/defaults.txt            # Comet-DVT default variables
Resource          ../resources/fusion_api.txt          # Comet-DVT Fusion API extensions
Resource          ../resources/fusion_cli.txt          # Comet-DVT Fusion CLI extensions
Resource          ../resources/hal_webapp.txt          # Comet-DVT HAL Webapp extensions

Suite Setup       Login to Fusion via SSH
Suite Teardown    Logout of Fusion Via SSH

*** Variables ***
${HW_DESCRIPTION}    ../data/${HW_DESCRIPTION_FILE}

*** Keywords ***

Get ICM Populated Bays
    [Documentation]    Return a list of populated bays

    # Load the ICM bays expected in the system
    Set Library Search Order    OperatingSystem
    ${File}    Get File    ${HW_DESCRIPTION}
    ${Machine Schema}    loads    ${File}
    Log    ${Machine Schema}
    @{Icms}    Get From Dictionary    ${Machine Schema}    interconnectBays
    ${populated}=    Create List
    : For    ${Icm}    In    @{Icms}
    \    Log    ${Icm}
    \    ${status}    ${model}=    Run Keyword and Ignore Error    Get From Dictionary    ${Icm}    model
    \    ${bay number}=    Get From Dictionary    ${Icm}    bayNumber
    \    Run Keyword If    '${status}'=='PASS'    Append To List    ${populated}    ${bayNumber}

    [Return]    ${populated}

Get ICM Unpopulated Bays
    [Documentation]    Return a list of unpopulated bays

    # Load the ICM bays expected in the system
    Set Library Search Order    OperatingSystem
    ${File}    Get File    ${HW_DESCRIPTION}
    ${Machine Schema}    loads    ${File}
    Log    ${Machine Schema}
    @{Icms}    Get From Dictionary    ${Machine Schema}    interconnectBays
    ${unpopulated}=    Create List
    : For    ${Icm}    In    @{Icms}
    \    Log    ${Icm}
    \    ${status}    ${model}=    Run Keyword and Ignore Error    Get From Dictionary    ${Icm}    model
    \    ${bay number}=    Get From Dictionary    ${Icm}    bayNumber
    \    Run Keyword If    '${status}'=='FAIL'    Append To List    ${unpopulated}    ${bayNumber}

    [Return]    ${unpopulated}

Get ICM Bad Bays
    [Documentation]    Return a list of bad bays that we do not expect to be available on a tbird

    ${bad}=    Create List
    : For    ${bay}    In Range    -5    1
    \    Append To List    ${bad}    ${bay}
    : For    ${bay}    In Range    100    105
    \    Append To List    ${bad}    ${bay}
    [Return]    ${bad}

*** Test Cases ***

ICM Manager Reset Of Populated Bays
    # TODO:  Will add verification steps when testing with ICM hardware
    [Documentation]    Test the Reset Action of all ICM Managers

    ${populated}=    Get ICM Populated Bays
    @{pop}=    Convert To List    ${populated}

    # Happy Path
    : For    ${Bay Number}    In    @{pop}
    \    Log    ${Bay Number}
    \    ${ParamDict}    Create Dictionary    BayNumber    ${Bay Number}
    \    ${Response}    HAL API Perform POST Action    ${FUSION_IP}    IcmManagerReset    ${ParamDict}
    \    ${CallStatus}    Find Errors In HAPI Response    ICM Manager ${Bay Number}    ${Response}    performAction    IcmManagerReset    ${ParamDict}
    \    Fail On Any Errors In HAPI Response    ${CallStatus}


ICM Manager Reset Of Unpopulated Bays
    # TODO:  Will add verification steps when testing with ICM hardware
    [Documentation]    Test the Reset Action of all ICM Managers

    ${unpopulated}=    Get ICM Unpopulated Bays
    @{unpop}=    Convert To List    ${unpopulated}

    # Unhappy Path
    : For    ${Bay Number}    In    @{unpop}
    \    Log    ${Bay Number}
    \    ${ParamDict}    Create Dictionary    BayNumber    ${Bay Number}
    \    ${Response}    HAL API Perform POST Action    ${FUSION_IP}    IcmManagerReset    ${ParamDict}
    \    ${CallStatus}    Find Errors In HAPI Response    ICM Manager ${Bay Number}    ${Response}    performAction    IcmManagerReset    ${ParamDict}
    \    Run Keyword And Expect Error    *
    \    ...    Fail On Any Errors In HAPI Response    ${CallStatus}

ICM Manager Reset Of Bad Bays
    # TODO:  Will add verification steps when testing with ICM hardware
    [Documentation]    Test the Reset Action of all ICM Managers

    ${badb}=    Get ICM Bad Bays
    @{bad}=    Convert To List    ${badb}

    # Unhappy Path
    : For    ${Bay Number}    In    @{bad}
    \    Log    ${Bay Number}
    \    ${ParamDict}    Create Dictionary    BayNumber    ${Bay Number}
    \    ${Response}    HAL API Perform POST Action    ${FUSION_IP}    IcmManagerReset    ${ParamDict}
    \    ${CallStatus}    Find Errors In HAPI Response    ICM Manager ${Bay Number}    ${Response}    performAction    IcmManagerReset    ${ParamDict}
    \    Run Keyword And Expect Error    *
    \    ...    Fail On Any Errors In HAPI Response    ${CallStatus}
