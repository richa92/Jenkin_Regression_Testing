*** Settings ***
Documentation     Verifies EM Midplane fru information reported by DCS and HAL
...    = Usage =
...    | pybot | -L DEBUG | midplane_fru.txt | 
...    = Variables =
...    | FUSION IP        | Required; IP address of the FusionVM to use | 
...    | SUT              | Required; IP address of the FusionVM to use |
...    = Preconditions =
...    None

Library           RoboGalaxyLibrary            # DVTs Robot Framework extensions
Library           FusionLibrary                # DVTs Robot Framework extensions
Library           OperatingSystem
Variables         ../resources/variables.py    ${ENCLOSURE}
Resource          ../resources/compare_json.txt
Resource          ../resources/fusion_api.txt  # Comet-DVT fusion API extensions
Resource          ../resources/hal_webapp.txt  # Comet-DVT HAL webapp extensions
Resource          ../resources/defaults.txt    # Comet-DVT default variables
Suite Setup       Initialize
Suite Teardown    Logout of Fusion Via REST
Force Tags        DCS  HW  HAL

*** Variables ***
${SUT}    dcs

*** Test Cases ***
Get RIS Midplane FRU Data
	[Tags]    RIS
	${Response}=    Get EM MidPlane FRU Resource    1 
	Log     ${Response}

	# Verify Return Code
    ${Status}       Get From Dictionary    ${Response}    status_code
    Should be Equal As Strings    ${Status}   200    msg=Failed to perform REST call

    ${Resource}=    Get Resource from Response    ${Response}
    Log Dictionary    ${Response}
    Set Suite Variable    ${EM Midplane Fru Resource}    ${Resource}

Get HAL Midplane FRU Resource
	[Tags]    HAL
    ${Parameters}=    Create Dictionary
    Log Dictionary    ${Parameters}
    ${Response}=    HAL API Perform Post Action    ${FUSION IP}    ChassisMidplaneFruRead    ${Parameters}
    Log     ${Response}

    # Verify Return Code
    ${Status}       Get From Dictionary    ${Response}    status_code
    Should be Equal As Strings    ${Status}    200    msg=Failed to perform Webapp REST call
    
    # Verify Call Status
    ${Call Status}       Get From Dictionary    ${Response}    CallStatus
    Should be Equal As Strings    ${Call Status}    SUCCESS        msg=Error found while executing HAL ChassisMidplaneFruRead Rest Call.

    # Remove REST header info and save
    ${Response}=    Get From Dictionary    ${Response}    OperationResult
    Log Dictionary    ${Response}
    Set Suite Variable    ${HAL Midplane Fru Resource}    ${Response}

Compare MidPlane FRU Resource
	[Tags]    RIS  HAL
    Dictionaries Should Be Equal    ${EM Midplane Fru Resource}    ${HAL Midplane Fru Resource}    msg=EM and HAL Midplane fru Resources do not match.

Verify MidPlane Fru Preamble
	[Tags]    HAL
	
    # Expected Resource
    ${Expected}=    Get From Dictionary    ${JSON}    Parsed
    ${Expected}=    Get From Dictionary    ${Expected}    Preamble
    Log Dictionary    ${Expected}

    # HAL Resource
    ${Parsed}=      Get From Dictionary    ${HAL Midplane Fru Resource}    Parsed
    ${Preamble}=    Get From Dictionary    ${Parsed}    Preamble
    Log Dictionary    ${Preamble}

    # Remove fields which are not verified
    Remove From Dictionary    ${Preamble}    FactoryTimeStamp
    ...                                      LastModified

    # Compare
    Check Large JSON Structure    ${Expected}    ${Preamble}

Verify MidPlane Fru Product Info
	[Tags]    HAL
    # Expected Resource
    ${Expected}=    Get From Dictionary    ${JSON}    Parsed
    ${Expected}=    Get From Dictionary    ${Expected}    ProductInfo
    Log Dictionary    ${Expected}

    # HAL Resource
    ${Parsed}=    Get From Dictionary    ${HAL Midplane Fru Resource}    Parsed
    ${ProductInfo}=    Get From Dictionary    ${Parsed}    ProductInfo
    Log Dictionary    ${ProductInfo}

    # Compare
    ${Ignored Keys}=    Create List
    Check Large JSON Structure    ${Expected}    ${ProductInfo}    ${Ignored Keys}


*** Keywords ***
Initialize
    [Documentation]    TBD
    Login to Fusion Via REST
    #Verify WebApp is Active
    Load Resource

Verify WebApp is Active
    [Documentation]    TBD
    ${Status}=    Get HAL Webapp Status
    Should be Equal    ${Status}    ${TRUE}    msg=HAL Webapp is not active

Load Resource
    [Documentation]    TBD
    [Arguments]    ${filename}=${SUT}
    ${output}=    OperatingSystem.Get File    ../data/${filename}_fru.json
    ${JSON}=    Evaluate    json.loads('''${output}''')    json
    Log    ${JSON}
    Set Suite Variable    ${JSON}    ${JSON}