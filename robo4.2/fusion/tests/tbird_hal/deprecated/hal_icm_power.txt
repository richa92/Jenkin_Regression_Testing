*** Settings ***
Documentation   Verifies Interconnect Module Power Status reported by DCS and HAL
...             = Usage =
...             | pybot | -L DEBUG | hal_icm_power.txt |
...             = Variables =
...             | FUSION IP     | Required; IP address of the FusionVM to use |
...             | ENCLOSURE     | Required; Enclosure name to run on (dcs, tesla, etc) |
...             = Preconditions =
...             - Claimed EM is requierd.
...             - Populated ICM bays are required.
...             - One or More Empty ICM bays are required.

Library         RoboGalaxyLibrary           # DVTs Robot Framework extensions
Library         FusionLibrary               # DVTs Robot Framework extensions
Resource        ../resources/fusion_api.txt                 # Comet-DVT fusion API extensions
Resource        ../resources/hal_webapp.txt                 # Comet-DVT HAL webapp extensions
Resource        ../resources/fusion_cli.txt                 # Comet-DVT fusion CLI extensions
Resource        ../resources/system_info.txt                # Comet-DVT fusion Enclosure extensions
Resource        ../resources/defaults.txt   # Comet-DVT default variables
Variables       ../resources/variables.py   ${ENCLOSURE}    # Variables file
Force Tags      HAL  DCS  HW

Suite Setup     Run Keywords
...             Login to Fusion Via REST
...             Login to Fusion via SSH
#...            Verify Claimed EM                           # Currently not available
...             Save Initial ICM Power Status
...             Initialize ICM Power State for Model

Suite Teardown      Run Keywords
...                 Restore Initial ICM Power Status
...                 Logout of Fusion Via REST
...                 Logout of Fusion Via SSH


*** Variables ***
@{Invalid Bays}             abc     -5      100     7       0
@{Invalid Power States}     on      off     ON      OFF     bad     3
${Error Message}            HAL API Rest Call Failed.: ERROR != SUCCESS

${ON}                       On
${OFF}                      Off

${GetPowerStateAction}      IcmManagerGetPowerState
${SetPowerStateAction}      IcmManagerSetPowerState

# Issue #AM63: Updated HAL Actions do not work on DCS Sprint 59 yet. Continue using old action names.
${Deprecated GetPowerStateAction}   IcmPowerStateStatus
${Deprecated SetPowerStateAction}   IcmPowerStateControl


*** Test Cases ***
Set ICM Power State to Invalid Value
    ${Invalid Power State}=     Select Random Element from List     ${Invalid Power States}
    ${Bay}=                     Select Random Element from List     ${PopulatedBays}

    Run Keyword and Expect Error            ${Error Message}
    ...     Set ICM Power State via HAL     ${Bay}      ${Invalid Power State}

Set ICM Power State of Invalid Bay
    ${Power State}=     Select Random Element from List     ${OFF}
    ${Invalid Bay}=     Select Random Element from List     ${Invalid Bays}

    Run Keyword and Expect Error            ${Error Message}
    ...     Set ICM Power State via HAL     ${Invalid Bay}      ${Power State}

Set ICM Power State of Empty Bay
    ${Power State}=     Select Random Element from List     ${OFF}
    ${Empty Bay}=       Select Random Element from List     ${EmptyBays}

    Run Keyword and Expect Error            ${Error Message}
    ...     Set ICM Power State via HAL     ${Empty Bay}    ${Power State}

Get ICM Power State of Invalid Bay
    ${Invalid Bay}=     Select Random Element from List     ${Invalid Bays}

    Run Keyword and Expect Error                ${Error Message}
    ...     Get ICM Power State via HAL API     ${Invalid Bay}

Get ICM Power State of Empty Bay
    ${Empty Bay}=   Select Random Element from List     ${EmptyBays}

    Run Keyword and Expect Error                ${Error Message}
    ...     Get ICM Power State via HAL API     ${Empty Bay}

Verify Interconnect Module Power Status
    Run Keyword If      '${DCS}'=='${True}'
    ...                 Log     Issue #AM63: Updated HAL Actions do not work on DCS Sprint 59 yet. Continue using old action names.
    ...                 level=WARN
    Run Random Model    model=hal_icm_power_model.xml   transitions=10


*** Keywords ***
#-----------------------------------------------------------------------
# MODEL BASED TESTING KEYWORDS
#-----------------------------------------------------------------------
ICM 1 Powered ON
    Verify Interconnect Bay Power State     1   ${ON}

ICM 2 Powered ON
    Verify Interconnect Bay Power State     2   ${ON}

ICM 3 Powered ON
    Verify Interconnect Bay Power State     3   ${ON}

ICM 4 Powered ON
    Verify Interconnect Bay Power State     4   ${ON}

ICM 5 Powered ON
    Verify Interconnect Bay Power State     5   ${ON}

ICM 6 Powered ON
    Verify Interconnect Bay Power State     6   ${ON}

ICM 1 Powered OFF
    Verify Interconnect Bay Power State     1   ${OFF}

ICM 2 Powered OFF
    Verify Interconnect Bay Power State     2   ${OFF}

ICM 3 Powered OFF
    Verify Interconnect Bay Power State     3   ${OFF}

ICM 4 Powered OFF
    Verify Interconnect Bay Power State     4   ${OFF}

ICM 5 Powered OFF
    Verify Interconnect Bay Power State     5   ${OFF}

ICM 6 Powered OFF
    Verify Interconnect Bay Power State     6   ${OFF}

Power Off ICM 1 via HAL
    Set Interconnect Bay Power State    1   ${OFF}

Power Off ICM 2 via HAL
    Set Interconnect Bay Power State    2   ${OFF}

Power Off ICM 3 via HAL
    Set Interconnect Bay Power State    3   ${OFF}

Power Off ICM 4 via HAL
    Set Interconnect Bay Power State    4   ${OFF}

Power Off ICM 5 via HAL
    Set Interconnect Bay Power State    5   ${OFF}

Power Off ICM 6 via HAL
    Set Interconnect Bay Power State    6   ${OFF}

Power On ICM 1 via HAL
    Set Interconnect Bay Power State    1   ${ON}

Power On ICM 2 via HAL
    Set Interconnect Bay Power State    2   ${ON}

Power On ICM 3 via HAL
    Set Interconnect Bay Power State    3   ${ON}

Power On ICM 4 via HAL
    Set Interconnect Bay Power State    4   ${ON}

Power On ICM 5 via HAL
    Set Interconnect Bay Power State    5   ${ON}

Power On ICM 6 via HAL
    Set Interconnect Bay Power State    6   ${ON}

#-----------------------------------------------------------------------
# HAL API ICM Power KEYWORDS
#-----------------------------------------------------------------------
Get ICM Power State via HAL API
    [Arguments]         ${Bay}
    [Documentation]     Returns Power Status for given ICM bay

    # Build Parameters
    ${rc}               ${Bay}=             Run Keyword and Ignore Error    Convert To Integer      ${Bay}
    ${Parameters}=      Create Dictionary   BayNumber                       ${Bay}
    Log Dictionary      ${Parameters}

    # Get ICM Power State
    # Issue #AM63: Updated HAL Actions do not work on DCS Sprint 59 yet. Continue using old action names.
    ${HAL Action}=      Set Variable If                 '${DCS}'=='${True}'     ${Deprecated GetPowerStateAction}
    ...                 ${GetPowerStateAction}
    ${Response}=        HAL API Perform Post Action     ${FUSION IP}            ${HAL Action}
    ...                 ${Parameters}

    # Verify Return Code
    Dictionary Should Contain Key   ${Response}             status_code     msg=HAL API call has failed (No status_code).
    ${status_code}=                 Get From Dictionary     ${Response}     status_code
    Should be Equal As Strings      ${status_code}          200             msg=Failed to perform REST call

    # Verify Call Status
    ${CallStatus}=                  Get From Dictionary     ${Response}     CallStatus
    Should be Equal As Strings      ${CallStatus}           SUCCESS         msg=HAL API Rest Call Failed.

    # Verify No Errors
    Dictionary Should Not Contain Key   ${Response}     Error
    ...     msg=Error found while executing HAL IcmPowerStateStatus Rest Call.

    # Parse Power state from Response
    ${Response}=                    Get From Dictionary     ${Response}     OperationResult
    Dictionary Should Contain Key   ${Response}             PowerState
    ${Power State}=                 Get From Dictionary     ${Response}     PowerState

    [Return]    ${Power State}

Verify Interconnect Bay Power State
    [Arguments]         ${Bay}      ${State}
    [Documentation]     Verify ICM Power State of specified Bay

    # TODO: Run Get Power on Empty Bay and expect Error
    # Ignore and Pass if Empty Bay
    ${BayNo}=                   Convert To Integer              ${Bay}
    ${EmptyBay}=                Run Keyword and Return Status   List Should Not Contain value   ${PopulatedBays}    ${BayNo}
    ...                         msg=Bay ${Bay} is an Empty Bay.
    Return From Keyword If      '${EmptyBay}'=='${True}'

    # Get Power State
    ${Power State}=     Get ICM Power State via HAL API     ${Bay}

    # Verify PowerState is as expected
    Should be Equal as Strings      ${Power State}      ${State}    msg=Power State on ICM is not "${State}".

Set ICM Power State via HAL
    [Arguments]         ${Bay}      ${State}
    [Documentation]     Sets the ICM Power State based on parameters specified

    # Build Parameters
    ${Bay Int}=         Run Keyword and Return Status                   Convert To Integer      ${Bay}
    ${Bay}=             Run Keyword If      '${Bay Int}'=='${True}'     Convert To Integer      ${Bay}
    ...                 ELSE                Set Test Variable           \${Bay}
    ${Parameters}=      Create Dictionary   BayNumber=${Bay}
    ...                 PowerState=${State}
    Log Dictionary      ${Parameters}

    # Set ICM Power State
    # Issue #AM63: Updated HAL Actions do not work on DCS Sprint 59 yet. Continue using old action names.
    ${HAL Action}=      Set Variable If                 '${DCS}'=='${True}'     ${Deprecated SetPowerStateAction}
    ...                 ${SetPowerStateAction}
    ${Response}=        HAL API Perform Post Action     ${FUSION IP}            ${HAL Action}
    ...                 ${Parameters}

    # Verify Return Code
    Dictionary Should Contain Key   ${Response}             status_code     msg=HAL API call has failed (No status_code).
    ${status_code}=                 Get From Dictionary     ${Response}     status_code
    Should be Equal As Strings      ${status_code}          200             msg=Failed to perform REST call

    # Verify Call Status
    ${CallStatus}=                  Get From Dictionary     ${Response}     CallStatus
    Should be Equal As Strings      ${CallStatus}           SUCCESS         msg=HAL API Rest Call Failed.

    # Verify No Errors
    Dictionary Should Not Contain Key   ${Response}     Error
    ...     msg=Error found while executing HAL IcmPowerStateControl Rest Call.

    [Return]    ${Response}

Set Interconnect Bay Power State
    [Arguments]         ${Bay}      ${State}
    [Documentation]     Sets the ICM Power State if ICM Exists in bay.

    # TODO: Run Set Power on Empty Bay and expect Error.
    # Ignore and Pass if Empty Bay
    ${BayNo}=                   Convert To Integer              ${Bay}
    ${EmptyBay}=                Run Keyword and Return Status   List Should Not Contain value   ${PopulatedBays}    ${BayNo}
    ...                         msg=Bay ${Bay} is an Empty Bay.
    Return From Keyword If      '${EmptyBay}'=='${True}'

    ${Response}=    Set ICM Power State via HAL     ${Bay}      ${State}

#-----------------------------------------------------------------------
# ICM Configuration Resource Keywords
#-----------------------------------------------------------------------
Save Initial ICM Power Status
    [Documentation]     Store initial ICM Power state for each bay
    # Get Available Interconnects
    ${RIS InterconnectBays Resource}=   Set Variable If     '${DCS}'=='${True}'     /rest/v1/Chassis/1/InterconnectBays
    ...                 /rest/v1/InterconnectBays
    ${PopulatedBays}    ${EmptyBays}=   Find Filled Bays    ${RIS InterconnectBays Resource}

    # Set Suite Variables
    Set Suite Variable      ${PopulatedBays}    ${PopulatedBays}
    Set Suite Variable      ${EmptyBays}        ${EmptyBays}

    # Save Power Status of each Interconnect
    ${Initial PowerStates}=                     Create Dictionary
    : For                   ${Bay}              In                          @{PopulatedBays}
    \                       # Get Power State
    \                       ${PowerState}=      Get ICM Power State via HAL API         ${Bay}
    \                       Set To Dictionary   ${Initial PowerStates}      ${Bay}      ${PowerState}
    Set Suite Variable      ${Initial PowerStates}                          ${Initial PowerStates}

    ${FormattedDictionary}      Evaluate    json.dumps(${Initial PowerStates}, indent=${4}, sort_keys=${True})      json
    Log                         ${FormattedDictionary}

Initialize ICM Power State for Model
    [Documentation]     Set ICM Power State to on for each of the ICM Bays to match initial state of the Model.
    # Restore Power Status of each Interconnect
    : For               ${Bay}                  In          @{PopulatedBays}
    \                   # Get Current Power State of Bay
    \                   ${PowerState}=          Get ICM Power State via HAL API     ${Bay}
    \                   # If Powered On, Do Nothing
    \                   Continue For Loop If    '${PowerState}'=='${ON}'
    \                   # If Powered Off, Power On ICM
    \                   Set Interconnect Bay Power State    ${Bay}                  ${ON}

Restore Initial ICM Power Status
    [Documentation]             Restore ICM Power States for each ICM bay based on Initial stored states.
    # Display Initial power state
    ${FormattedDictionary}      Evaluate    json.dumps(${Initial PowerStates}, indent=${4}, sort_keys=${True})      json
    Log                         ${FormattedDictionary}

    # Restore Power Status of each Interconnect
    : For   ${Bay}                      In                      @{PopulatedBays}
    \       # Get Initial Power State of Bay
    \       ${Expected PowerState}=     Get From Dictionary     ${Initial Power States}     ${Bay}
    \       # Set Power State
    \       Set Interconnect Bay Power State                    ${Bay}                      ${Expected PowerState}
    \       # Get Power State and verify
    \       ${PowerState}=              Get ICM Power State via HAL API                     ${Bay}
    \       Should be Equal as Strings                          ${PowerState}               ${Expected PowerState}
    \       ...                         msg=Failed to restore ICM ${Bay} Power State to '${Expected PowerState}'.

