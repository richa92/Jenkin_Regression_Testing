*** Settings ***
Documentation     Validate appearance of detailed Tbird enclosure properties in the UI as per spec
...    FS6 020    Monitored Tbird Details View
...    FS6 030    Tbird EM Details
...    FS6 040    Tbird CI Manager Details
...    http://piano.usa.hp.com/mocks/Fusion/Release_2_00/F56%20Manually%20discover%20and%20view%20monitored%20Tbird%20enclosure
...    = USAGE =
...    | pybot | -v FUSION_IP:<appliance IP> | ui_enclosure_details.txt |
...    = Variables =
...    | FUSION_IP | Required. Hostname for the Fusion VM. |

Library     OperatingSystem
Library     RoboGalaxyLibrary
Library     FusionLibrary
Library     json
Resource    ../resources/fusion_api.txt
Resource    ../resources/defaults.txt
Variables   ../resources/variables.py    ${ENCLOSURE}  # Variables file
Resource    ../resources/enclosure_ui_shared_keywords.txt
Force Tags    DCS  HW

Suite Setup    Run Keywords
...    Login To Fusion Via REST
...    Open Browser And Login
Suite Teardown    Run Keywords
...    Exit Fusion Session
...    Logout Of Fusion Via REST

*** Variables ***

*** Keywords ***

Open Browser And Login
    # Initialize our truth data structure
    Set Library Search Order    OperatingSystem
    ${Schema}    Fusion Api Get Enclosures
    ${Schema}    Get From Dictionary    ${Schema}    members
    Set Suite Variable    ${All Enclosures}    ${Schema}

    # Now actually log into the system
    Open Browser    https://${FUSION_IP}/#/enclosure/show/general    firefox
    Wait Until Element Is Visible    id=hp-login-user
    ...                              timeout=300
    ...                              error=Not at Login page
    Input Text    hp-login-user    ${FUSION_USERNAME}
    Input Text    hp-login-password    ${FUSION_PASSWORD}
    Click Button    hp-login-button
    # Arrive at Details Page
    Wait Until Page Contains Element    id=cic-enclosure-more-panels

Exit Fusion Session
    Fusion UI Logout of Appliance
    Close Browser


### These used to be individual Test Cases until we decided to support multiple enclosures

Test Enclosure Name Is Present
    [Documentation]    Ensure enclosure name is properly represented throughout the page
    ${Serial_Number}    Get From Dictionary    ${Enc Object}    serialNumber
    Wait Until Element Is Visible    id=cic-enclosure-details-title
    Element Text Should Be    id=cic-enclosure-details-title    ${Serial_Number}
    ...    msg=Enclosure name not shown correctly in the Enclosure page heading
    Element Text Should Be    xpath=//td[text()="${Serial_Number}"]    ${Serial_Number}
    ...    msg=Enclosure name not shown correctly in the list of enclosures managed by Fusion
    Title Should Be    ${Serial_Number} - Enclosures - HP OneView

Test Actions Menu Items Are Present
    Run Keyword And Continue On Failure
    ...    Actions Menu Items Are Present

Test Edit Enclosure General Settings Allows Changing Correct Properties
    Run Keyword And Continue On Failure
    ...    Edit Enclosure General Settings Allows Changing Correct Properties
    ...    xpath=//li[@id="cic-enclosure-more-general"]/label/a

Test Fields In Enclosure Details - General Panel
    [Documentation]    Tests the fields in the "General" section of the Enclosure page
    [Documentation]    for presence, adherence to spec, & content.
    Test General Fields    more

Test Fields In Enclosure Details - Hardware Panel
    ${HW Settings 1}    Get Matching Xpath Count    //li[@id="cic-enclosure-more-hardware"]/form/div/fieldset[2]/ol/li
    ${HW Settings 2}    Get Matching Xpath Count    //li[@id="cic-enclosure-more-hardware"]/form/div/fieldset[3]/ol/li
    ${HW Elements}    Evaluate    ${HW Settings 1} + ${HW Settings 2}
    Should Be Equal    ${HW Elements}    ${5}
    ...    msg=There should be 5 items in Hardware: only Location, Powered by, Serial number, Part number, and Maximum power.

    # Test Presence
    Run Keyword And Continue On Failure
    ...    Element Should Be Visible    xpath=//li[@id="cic-enclosure-more-hardware"]//div[@id="cic-enclosure-more-location"]
    ...    msg="Location" field should exist in the "Hardware" section
    Run Keyword And Continue On Failure
    ...    Element Should Be Visible    xpath=//li[@id="cic-enclosure-more-hardware"]//div[@id="cic-enclosure-more-poweredBy"]
    ...    msg="Powered by" field should exist in the "Hardware" section
    Run Keyword And Continue On Failure
    ...    Element Should Be Visible    xpath=//li[@id="cic-enclosure-more-hardware"]//div[@id="cic-enclosure-more-serialNumber"]
    ...    msg="Serial number" field should exist in the "Hardware" section
    Run Keyword And Continue On Failure
    ...    Element Should Be Visible    xpath=//li[@id="cic-enclosure-more-hardware"]//div[@id="cic-enclosure-more-partNumber"]
    ...    msg="Part number" field should exist in the "Hardware" section
    Run Keyword And Continue On Failure
    ...    Element Should Be Visible    xpath=//li[@id="cic-enclosure-more-hardware"]//span[@id="cic-enclosure-maximumPower"]
    ...    msg="Maximum power" field should exist in the "Hardware" section

    # Test Contents
    # Fetch the rack the enclosure belongs to real quick
    ${UUID}    Get From Dictionary    ${Enc Object}    uuid
    ${Enc Env}    Fusion Api Get Server Hardware Environmental Config    /rest/enclosures/${UUID}
    ${Rack}    Get From Dictionary    ${Enc Env}    rackId
    ${Rack}    Set Variable If    ${Rack} == ${Null}    â€”    ${Rack}
    Run Keyword And Continue On Failure
    ...    Element Text Should Be    id=cic-enclosure-more-location    ${Rack}
    ...    msg=The enclosure location listed under "Hardware" should be set to ${Rack}.
    Run Keyword And Continue On Failure
    ...    Element Text Should Be    id=cic-enclosure-more-poweredBy    none
    ...    msg=The "Powered by" field under "Hardware" should be none.
    ${serialNumber}    Get From Dictionary    ${Enc Object}    serialNumber
    Run Keyword And Continue On Failure
    ...    Element Text Should Be    id=cic-enclosure-more-serialNumber    ${serialNumber}
    ...    msg=The serial number listed under "Hardware" should be ${serialNumber}
    ${partNumber}    Get From Dictionary    ${Enc Object}    partNumber
    Run Keyword And Continue On Failure
    ...    Element Text Should Be    id=cic-enclosure-more-partNumber    ${partNumber}
    ...    msg=The "Part number" field under "Hardware" should be ${partNumber}.
    Run Keyword And Continue On Failure
    ...    Element Text Should Be    id=cic-enclosure-maximumPower    not set
    ...    msg=The "Maximum power" listed under "Hardware" should be not set.

Test Fields In Enclosure Details - Firmware Panel
    Log    Awaiting developer user story completion
    #Run Keyword And Continue On Failure
    #...    Element Should Be Visible    xpath=//li[@id="cic-enclosure-more-firmware"]//div[@id="cic-enclosure-more-fw-no-data"]
    #...    msg=No data is expected to be displayed in the "Firmware" section. If this is no longer true, please update the test.
    #Run Keyword And Continue On Failure
    #...    Element Text Should Be    id=cic-enclosure-more-fw-no-data    Firmware information unavailable.
    #...    The information in the "Firmware" tab is not as expected.

Test Device Bays
    # Look for the correct number of columns in the data table
    ${Correct Column Count}    Run Keyword And Return Status
    ...    Xpath Should Match X Times    //table[@id="cic-enclosure-more-devbays-table"]/thead/tr/td    6
    Run Keyword And Continue On Failure
    ...    Should Be True    ${Correct Column Count}
    ...    There should be exactly 6 columns shown under Device Bays: Status, Bay number, Server Hardware, Model, and Server Profile.

    # Make sure the exact number of blades expected is shown in the table
    Run Keyword And Continue On Failure
    ...    Xpath Should Match X Times    //table[@id="cic-enclosure-more-devbays-table"]/tbody/tr    12
    ...    There should be exactly 12 bays shown under Device Bays.
    ${Device Test}    Set Variable    ${True}
    :FOR    ${Bay}    IN RANGE    1    13
    \    Run Keyword If    ${Correct Column Count}
    \    ...    Run Keyword And Continue On Failure
    \    ...        Xpath Should Match X Times
    \    ...        //table[@id="cic-enclosure-more-devbays-table"]/tbody/tr/td[text()="${Bay}"]/../td    6
    \    ...        msg=There should be exactly 6 columns shown for each Device Bay; bay ${Bay} has a different number of columns.
    \    ${Test Result}    Fusion UI Verify Tbird Device Bay    ${Bay}
    #    Keep a running tab of whether everything has passed or not
    \    ${Device Test}    Evaluate    ${Device Test} & ${Test Result}
    Run Keyword And Continue On Failure
    ...    Should Be True    ${Device Test}    msg=Failures were observed with the display of one or more Device Bays; see warnings for details.

Test Interconnect Bays
    # Look for the correct number of columns in the data table
    ${Correct Column Count}    Run Keyword And Return Status
    ...    Xpath Should Match X Times    //table[@id="cic-enclosure-icbays-table"]/thead/tr/td    5
    Run Keyword And Continue On Failure
    ...    Should Be True    ${Correct Column Count}
    ...    There should be exactly 5 columns shown under Interconnect Bays: Status, Bay number, Interconnect, and Installed Module.

    # Make sure the exact number of interconnects expected is shown in the table
    Run Keyword And Continue On Failure
    ...    Xpath Should Match X Times    //table[@id="cic-enclosure-icbays-table"]/tbody/tr    6
    ...    There should be exactly 6 bays shown under Interconnect Bays.
    ${Interconnect Test}    Set Variable    ${True}
    :FOR    ${Bay}    IN RANGE    1    7
    \    Run Keyword If    ${Correct Column Count}
    \    ...    Run Keyword And Continue On Failure
    \    ...        Xpath Should Match X Times
    \    ...        //table[@id="cic-enclosure-icbays-table"]/tbody/tr/td[text()="${Bay}"]/../td    5
    \    ...        msg=There should be exactly 5 columns shown for each Interconnect Bay; bay ${Bay} has a different number of columns.
    \    ${Test Result}    Fusion UI Verify Tbird Interconnect Bay    ${Bay}
    #    Keep a running tab of whether everything has passed or not
    \    ${Interconnect Test}    Evaluate    ${Interconnect Test} & ${Test Result}
    Run Keyword And Continue On Failure
    ...    Should Be True    ${Interconnect Test}    msg=Failures were observed with the display of one or more Interconnect Bays; see warnings for details.

Test CIM Bays To Spec F56 040
    # Look for the correct number of columns in the data table
    ${Correct Column Count}    Run Keyword And Return Status
    ...    Xpath Should Match X Times    //table[@id="cic-enclosure-more-ci-mgr-bays-table"]/thead/tr/td    6
    Run Keyword And Continue On Failure
    ...    Should Be True    ${Correct Column Count}
    ...    msg=There should be exactly 6 columns shown under CIM Bays: Status, Bay number, Model, Serial Number, Part Number, and Spare Part Number.

    # Make sure the exact number of CIM bays expected is shown in the table
    Wait Until Element Is Visible    xpath=//table[@id="cic-enclosure-more-ci-mgr-bays-table"]/tbody/tr[2]
    Run Keyword And Continue On Failure
    ...    Xpath Should Match X Times    //table[@id="cic-enclosure-more-ci-mgr-bays-table"]/tbody/tr    2
    ...    msg=There should be exactly 2 CIM bays in the system.
    ${CIM Test}    Set Variable    ${True}
    :FOR    ${Bay}    IN RANGE    1    3
    \    Get Matching Xpath Count    //table[@id="cic-enclosure-more-ci-mgr-bays-table"]/tbody/tr/td[text()="${Bay}"]/../td
    \    Run Keyword If    ${Correct Column Count}
    \    ...    Run Keyword And Continue On Failure
    \    ...        Xpath Should Match X Times    //table[@id="cic-enclosure-more-ci-mgr-bays-table"]/tbody/tr/td[text()="${Bay}"]/../td    6
    \    ...    msg=There should be exactly 6 columns shown for each CIM Bay; bay ${Bay} has a different number of columns.
    \    ${Test Result}    Fusion UI Verify Tbird CIM Bay    ${Bay}
    #    Keep a running tab of whether everything has passed or not
    \    ${CIM Test}    Evaluate    ${CIM Test} & ${Test Result}
    Run Keyword And Continue On Failure
    ...    Should Be True    ${CIM Test}
    ...    msg=Failures were observed with the display of one or more CIM Bay rows; see warnings for details.

Test EM Bays To Spec F56 030
    # Look for the correct number of columns in the data table
    ${Correct Column Count}    Run Keyword And Return Status
    ...    Xpath Should Match X Times    //table[@id="cic-enclosure-more-em-bays-table"]/thead/tr/td    7
    Run Keyword And Continue On Failure
    ...    Should Be True    ${Correct Column Count}
    ...    msg=There should be exactly 7 columns shown under EM Bays: the dropdown arrow, Status, Bay number, Model, State, MGMT Port State, and Link Port State.

    # Make sure the exact number of EM bays expected is shown in the table
    :FOR    ${Bay}    IN RANGE    1    3
    \    Wait Until Element Is Visible    xpath=//table[@id="cic-enclosure-more-em-bays-table"]/tbody/tr/td[text()="${Bay}"]/../td
    \    Run Keyword If    ${Correct Column Count}
    \    ...    Run Keyword And Continue On Failure
    \    ...        Xpath Should Match X Times    //table[@id="cic-enclosure-more-em-bays-table"]/tbody/tr/td[text()="${Bay}"]/../td    7
    \    ...        msg=There should be exactly 7 columns shown for each EM Bay; bay ${Bay} has a different number of columns.
    \    ${EM Test}    Fusion UI Verify Tbird EM Bay    ${Bay}
    \    Run Keyword And Continue On Failure
    \    ...    Should Be True    ${EM Test}
    \    ...    msg=Failures were observed with the display of EM Bay ${Bay}; see warnings for details.

    # Do this *after* you've unfurled all the EM rows, or else the results may vary
    # The table contains one row per EM bay, and then one additional row per populated EM bay
    ${managers}    Get From Dictionary    ${Enc Object}    managers
    ${Bay Count}    Set Variable    2
    :FOR    ${manager}    IN    @{managers}
    \    ${devicePresence}    Get From Dictionary    ${manager}    devicePresence
    \    ${devicePresence}    Convert To Lowercase    ${devicePresence}
    \    ${Bay Count Inc}    Evaluate    ${Bay Count} + 1
    \    ${Bay Count}    Set Variable If    '${devicePresence}' == 'present'    ${Bay Count Inc}    ${Bay Count}
    Run Keyword And Continue On Failure
    ...    Xpath Should Match X Times    //table[@id="cic-enclosure-more-em-bays-table"]/tbody/tr    ${Bay Count}
    ...    msg=There should be exactly ${Bay Count} rows (two for each EM) in the Enclosure Manager table.


*** Test Cases ***

Test Enclosure Details UI Page For All Tbirds
    ${Ran On Tbird}    Set Variable    ${False}
    # Find out what enclosures have been added to Fusion
    :FOR    ${Enc Object}    IN    @{All Enclosures}
    \    Set Suite Variable    ${Enclosure}    ${Enc Object}
    \    ${enclosureName}    Get From Dictionary    ${Enclosure}    name
    \    ${enclosureType}    Get From Dictionary    ${Enclosure}    enclosureType
    #    Ugly way to make sure the enclosure name is not null
    #    Run a test to see if it is null and if it passes, then report a problem
    \    ${status}    ${ignored}    Run Keyword And Ignore Error
    \    ...    Should Be Equal    ${enclosureName}    ${null}
    \    Run Keyword If    "${status}" == "PASS"
    \    ...    Run Keyword And Continue On Failure
    \    ...        Fail    msg=The enclosure name was found to be NULL from Fusion API.
    \    Run Keyword If    "${status}" == "PASS"    Continue For Loop
    #    Make sure they appear in the UI
    \    Wait Until Element Is Visible    xpath=//tbody/tr/td[2 and contains(text(),"${enclosureName}")]
    # If it's not a Tbird enclosure, skip the following keywords
    \    Continue For Loop If    '${enclosureType}' != 'Thunderbird'
    # If we're here, this is a Tbird enclosure; click on its name in the menu
    \    Click Element    xpath=//tbody/tr/td[2 and contains(text(),"${enclosureName}")]
    # Run the prescribed tests for this enclosure
    \    ${Ran On Tbird}    Set Variable    ${True}
    \    Test Enclosure Name Is Present
    \    Test Actions Menu Items Are Present
    \    Test Edit Enclosure General Settings Allows Changing Correct Properties
    \    Test Fields In Enclosure Details - General Panel
    \    Test Fields In Enclosure Details - Hardware Panel
    \    Test Fields In Enclosure Details - Firmware Panel
    \    Test Device Bays
    \    Test Interconnect Bays
    \    Test CIM Bays To Spec F56 040
    \    Test EM Bays To Spec F56 030
    # Make sure we actually ran something useful
    Should Be True    ${Ran On Tbird}    msg=No Thunderbird enclosures were found in this Fusion appliance. Make sure the test data is correct and that they are properly appearing in the UI.
