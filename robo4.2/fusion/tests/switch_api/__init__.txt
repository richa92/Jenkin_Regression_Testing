*** Settings ***
Documentation     Comet-DVT CRM Switch Test Suite
...    = Usage =
...    | pybot | -v FUSION_IP:<Your-IP> | -v SWITCH_IP:<ip> | -i <tags> | -e <tags> | . |
...    = Variables =
...    | FUSION IP | Required; IP address of the FusionVM to use | 
...    | SWITCH IP | Optional; IP address or name of the switch to use; Default is *unknown* | 
...    | CLAIM     | Optional; Set to True to Claim Switch; Default is *False* |
...    | RESET     | Optional; Set to False to inhibit Switch and Fusion reset; Default is *True* |

Library           SSHLibrary
Library           OperatingSystem
Library           RoboGalaxyLibrary           # DVTs Robot Framework extensions
Variables         resources/variables.py    ${FUSION_IP}    ${SWITCH_IP}    # Comet-DVT defaults
Resource          resources/fusion.txt        # Comet-DVT fusion API keyword extensions
Resource          resources/fusion_gui.txt    # Comet-DVT fusion GUI keyword extensions
Resource          resources/switch.txt        # Comet-DVT switch CLI keyword extensions

Suite Setup       Suite Setup
Suite Teardown    Suite Teardown

*** Variables ***
# Variables defined here will not be carried into sub-suites
${RESET}          ${True}                     # Default reset both the Switch and Fusion
${LOG_LEVEL}      TRACE                       # Default Loglevel
${CLAIM}          ${False}                    # Default omit claiming a switch
@{InterconnectNamesList}    ${I_Name}    ${null}    ${null}    ${null}    ${null}    ${null}    ${null}    ${null}
 
#*** Test Cases ***
# Initialization files cannot contain Test Cases

*** Keywords ***
Suite Setup
    [Documentation]    Perform common checks/verification/setup functions
    Log                Executing Comet-DVT Switch Setup keyword
    
    # Display default variables record current subversion ident
    Log Variables
    Set Log Level    ${LOG_LEVEL}
   
    # Reset 5900 Switch and Fusion VM
    Run Keyword If	'${RESET}'=='${True}'	Reset Fusion VM and Switch

    # Claim 5900 Switch
    Run Keyword If	'${CLAIM}'=='${True}'	Claim Switch   


Suite Teardown
    [Documentation]    Perform common checks/verifications/cleanup functions  
    Log                Executing Comet-DVT Switch Teardown keyword
    
    # Fusion Cleanup
    Run Keyword If	'${RESET}'=='${True}'	CleanUp FusionVM
    
    # Fusion and Switch Reset
    Run Keyword If    '${CLAIM}'=='${True}'		Reset Fusion VM and Switch
    
    
Reset Fusion VM and Switch
	# Reset 5900 Switch
    Run Keyword If    '${SWITCH_IP}'!='unknown'    Reset 5900 DL    ${SERIAL_DL_IP}    ${SERIAL_DL_USERNAME}    ${SERIAL_DL_PASSWORD}
    Run Keyword If    '${SWITCH_IP}'!='unknown'    Set Switch Version Metadata

    # Reset FusionVM 
    Run Keyword If    '${FUSION_IP}'!='unknown'    Reset Fusion Appliance to Factory Default
    Run Keyword If    '${FUSION_IP}'!='unknown'    First Time Setup for FusionVM
    Run Keyword If    '${FUSION_IP}'!='unknown'    Set Fusion Version Metadata
	Sleep	300			# FIXME: Sleep for 5 minutes (Workaround)
	
Claim Switch
	# Verify Switch is not already claimed
    ${rc}=	Verify OVuser is Not Present
    Should Be Equal		${rc}	${True}		msg=OVuser account still exists on 5900 switch.
    ${rc}=	Verify Unclaimed Switch Prompt
	Should Be Equal		${rc}	${True}		msg=Failed to verify <HP> prompt on 5900 switch.
    
    # Claim switch
    Setup Switch Gateway IP													      # FIXME: Set Gateway IP - Workaround
    Login to Fusion via REST
    Create Logical Interconnect Group    ${LigName}    @{InterconnectNamesList}   # Create LIG
    ${LigUri}    Get Logical Interconnect Group Uri    ${LigName}
    Create Logical Interconnect    ${LiName}    ${LigUri}                         # Create LI (Claim Switch)
    Logout of Fusion via REST
    
    # Verify Switch is claimed
    ${rc}=	Verify OVuser is Present
	Should Be Equal		${rc}	${True}		msg=Failed to verify OVuser account on 5900 switch.
	${rc}=	Verify Claimed Switch Prompt
	Should Be Equal		${rc}	${True}		msg=Failed to verify <MiliSwitch_1> prompt on 5900 switch.

