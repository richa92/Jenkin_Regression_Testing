*** Settings ***
Documentation     US21827: Create Logical Interconnect Group
Suite Setup       Log into Fusion As Administrator
Suite Teardown    Log out of Fusion appliance
Library           Collections
Library           String
Library           RoboGalaxyLibrary

*** Variables ***
${Appliance}      10.0.12.3
${User}           Administrator
${Password}       hpinvent
${enclosureuri}
${bay}            1
${port}
${logicalinterconnecturi}      /rest/logical-interconnects/cdb19ad1-cba8-405b-9234-978c33cff902


*** Test Cases ***

Make a Logical Interconnect Group Happily
    #
    # Happy Path - Completely Plain Vanilla
    #
    ${LIGdict}     Build Logical Interconnect Groups Dictionary    "LIG1"
    ${Response}    Fusion Api Add Logical Interconnect Groups     ${LIGdict}
    ${respStatus}  Get From Dictionary    ${Response}    status
    Should Be Equal    ${respStatus}    202    msg=Invalid response from Add Logical Interconnect Group
    ${Resource}    Get From Dictionary    ${Response}    associatedResource
    ${LIGid}       Get From Dictionary    ${Resource}    resourceUri
    ${LIG}      Fusion Api Get Logical Interconnect Groups Id    uri=${LIGid}
    # Combine our dictionary of defaults with the dictionary of custom values, which doesnâ€™t have every key in it
    ${completeLIGdict}    Make Complete Dictionary    LIG    ${LIG}    ${LIGdict}
    Dictionaries Should Be Equal   ${LIG}    ${completeLIGdict}    msg="The LIG info returned by the server didn't match the expected default + custom configuration."
    Fusion Api Delete Logical Interconnect Groups Id    uri=${LIGid}

Fail To Make a Logical Interconnect Group - 1
    #
    # Add a parameter that doesn't exist
    #
    ${LIGdict}     Build Logical Interconnect Groups Dictionary    "LIG2"
    Set To Dictionary    ${LIGdict}    MyBogusParameterOfDoom    "This doesn't really belong here and should cause the request to fail"
    ${Response}    Fusion Api Add Logical Interconnect Groups     ${LIGdict}
    ${respStatus}  Get From Dictionary    ${Response}    status
    Should Be Equal    ${respStatus}    400    msg=Invalid response from Add Logical Interconnect Group

Fail To Make a Logical Interconnect Group - 2
    #
    # Specify an invalid value for a parameter
    #
    ${LIGdict}     Build Logical Interconnect Groups Dictionary    "LIG3"
    # stackingMode should be None, Enclosure, SwitchPairs, or MultiEnclosure; here, we make it something else
    Set To Dictionary    ${LIGdict}    stackingMode    likeJengaBlocks
    ${Response}    Fusion Api Add Logical Interconnect Groups     ${LIGdict}
    ${respStatus}  Get From Dictionary    ${Response}    status
    Should Be Equal    ${respStatus}    400    msg=Invalid response from Add Logical Interconnect Group

*** Keywords ***

Log into Fusion As Administrator
    Should Not Be Equal    ${Appliance}    '10.0.12.10'    msg=Please specify a valid appliance name or address
    ${Response}    ${SessionId}=    Fusion Api Login Appliance    ${Appliance}    ${User}    ${Password}
    ${Status}    Get From Dictionary    ${Response}    status
    Should Be Equal    ${Status}    200    msg=Invalid response from Login
    Log    'Session ID: ${SessionId}'

Log out of Fusion appliance
    Fusion Api logout Appliance
