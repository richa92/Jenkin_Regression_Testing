*** Settings ***
Documentation     DL360 Server CLI keywords 
...               = Usage =
...               | Library | resources/server.txt |
Library           Collections
Library           String
Library           SSHLibrary
Library           RoboGalaxyLibrary         # DVTs Robot Framework extensions
Library           FusionLibrary                  # DVTs Robot Framework extensions
Variables         resources/variables.py    # Comet-DVT default variables

*** Variables ***
${ILO_PROMPT}     hpiLO->
${ILO_TIMEOUT}    300

*** Keywords ***
Login to Server iLO Via SSH
    [Documentation]    Connect to Server (defaults to iLO) via SSH
    [Documentation]    Example:\n| Login to Server ILO Via SSH | 10.0.12.106 | Administrator | hpvse123 |
    [Arguments]    ${IP}=${SERIAL_DL_ILO_IP}
    ...            ${USERNAME}=${SERIAL_DL_ILO_USERNAME}    
    ...            ${PASSWORD}=${SERIAL_DL_ILO_PASSWORD}
    ...            ${PROMPT}=${ILO_PROMPT}
    ...            ${TIMEOUT}=${ILO_TIMEOUT}
    Log Many    ${IP}    ${USERNAME}    ${PASSWORD}    ${PROMPT}    ${TIMEOUT}
    Set Default Configuration    prompt=${PROMPT}    timeout=${TIMEOUT}
    ${Id}=    Open Connection    ${IP}
    ${Output}=    Login    ${USERNAME}    ${PASSWORD}
    Log    ${Output}
    [Return]    ${Id}

Logout of Server iLO Via SSH
    [Documentation]    Exits the current SSH session
    [Documentation]    Example:\n| Logout Of Server Via SSH |
    #Write    Exit
    Close Connection

Execute iLO command
    [Documentation]    Return a bufer containing (raw) output from an iLO SMASH command.
    [Arguments]    ${Command}
    Write    ${Command}
    ${Output}=    Read Until Prompt
    # Check command status
    ${Match}    Should Match Regexp    ${Output}    status=0
    [Return]    ${Output}

Get ILO Version Information
    [Documentation]    Return a bufer containing (raw) output from a 'show /system1/firmware' command.
    ${Output}=    Execute iLO Command    show /system1/firmware1
    # Extract version string
    ${Match}    ${Version}    Should Match Regexp    ${Output}    version=(.*)\\r
    ${Match}    ${Date}    Should Match Regexp    ${Output}    date=(.*)\\r
    [Return]    ${Version}    ${Date}
    