
*** Settings ***

Documentation    SPT Regression Test Suite by Administrator and SBAC user
Variables                       ../../data_variables_files/SPT_SBAC_tBird_data_variable.py
Resource                        resource.txt
Suite Setup                     Setup Users and scopes
Suite Teardown                  clean up users and scopes

*** Variables ***
${isDataVariableFile}    True

*** Test Cases ***
OVTC31157
    [Documentation]    SPT_SBAC_OSDP_1
    ...    Single scope : Update SPT with newly added OS Deployment plans in scope check whether newly added OS Deployment plans can be accesed in SPT
    [Tags]    SPT_SBAC    OVF2940    PO
    [Template]    SPT SP by different users
        Administrator    opn1=Create SPT    opn1_payload=${SPT_SBAC_OSDP_1}    opn2=Update Scopes    opn2_payload=${SPT_SBAC_OSDP_1_EditScope}    isComplete=False
        User2_SPArch      opn1=Update SPT    opn1_payload=${SPT_SBAC_OSDP_1_Update_1}    isComplete=False
        User2_InfraAdm    opn1=Update SPT    opn1_payload=${SPT_SBAC_OSDP_1_Update_2}    isComplete=False
        User2_SAdm    opn1=Update SPT    opn1_payload=${SPT_SBAC_OSDP_1_Update_3}
    [Teardown]    run keywords    Fusion Api Login Appliance    ${APPLIANCE_IP}    ${user_credentials['Administrator']}    AND
    ...    Update Scopes    ${SPT_SBAC_OSDP_1_ScopeResetToOldResources}    False    AND
    ...    run keyword and Ignore Error    remove sp spt    ${SPT_SBAC_OSDP_1}

OVTC31158
    [Documentation]    SPT_SBAC_OSDP_2
    ...    Multi scope SPT: Update SPT (by same user) with newly added OS Deployment plans in scope check whether newly added OS Deployment plans can be accesed in SPT
    [Tags]    SPT_SBAC    OVF2940    PO
    [Template]    SPT SP by different users
        User2_SPArch      opn1=Create SPT       opn1_payload=${SPT_SBAC_OSDP_2}                isComplete=False
        Administrator    opn1=Update Scopes    opn1_payload=${SPT_SBAC_OSDP_2_EditScope}    isComplete=False
        User2_SPArch      opn1=Update SPT       opn1_payload=${SPT_SBAC_OSDP_2_Update_1}
    [Teardown]    run keywords    Fusion Api Login Appliance    ${APPLIANCE_IP}    ${user_credentials['Administrator']}    AND
    ...    Update Scopes    ${SPT_SBAC_OSDP_2_ScopeResetToOldResources}    False    AND
    ...    run keyword and Ignore Error    remove sp spt    ${SPT_SBAC_OSDP_2}

OVTC31159
    [Documentation]    SPT_SBAC_OSDP_3
    ...    Update SPT with newly (by different user) added OS Deployment plans in scope check whether newly added OS Deployment plans can be accesed in SPT
    [Tags]    SPT_SBAC    OVF2940    PO
    [Template]    SPT SP by different users
        User2_SPArch      opn1=Create SPT       opn1_payload=${SPT_SBAC_OSDP_3}                isComplete=False
        Administrator    opn1=Update Scopes    opn1_payload=${SPT_SBAC_OSDP_3_EditScope}      isComplete=False
        User2_InfraAdm    opn1=Update SPT       opn1_payload=${SPT_SBAC_OSDP_3_Update_1}       isComplete=False
        User2_SAdm    opn1=Update SPT       opn1_payload=${SPT_SBAC_OSDP_3_Update_2}
    [Teardown]    run keywords    Fusion Api Login Appliance    ${APPLIANCE_IP}    ${user_credentials['Administrator']}    AND
    ...    Update Scopes    ${SPT_SBAC_OSDP_3_ScopeResetToOldResources}    False    AND
    ...    run keyword and Ignore Error    remove sp spt    ${SPT_SBAC_OSDP_3}

OVTC31160
    [Documentation]    SPT_SBAC_OSDP_4
    ...    Create a SPT with OS Deployment plans where OS Deployment plans and SPT is assigned with same scope. Check whether SBAC allows to remove OS Deployment plans from scope
    [Tags]    SPT_SBAC    OVF2940    PO    Negative
    [Template]    SPT SP by different users
        User2_SPArch      opn1=Create SPT       opn1_payload=${SPT_SBAC_OSDP_4}                isComplete=False
        Administrator    opn1=Update Scopes    opn1_payload=${SPT_SBAC_OSDP_4_EditScope}      isComplete=False
        User2_SPArch      opn1=Get SPT          opn1_payload=${SPT_SBAC_OSDP_4}                isComplete=False
        User2_InfraAdm    opn1=Get SPT          opn1_payload=${SPT_SBAC_OSDP_4}                isComplete=False
        User2_SAdm    opn1=Get SPT          opn1_payload=${SPT_SBAC_OSDP_4}
    [Teardown]    run keywords    Fusion Api Login Appliance    ${APPLIANCE_IP}    ${user_credentials['Administrator']}    AND
    ...    Update Scopes    ${SPT_SBAC_OSDP_4_ScopeResetToOldResources}    False    AND
    ...    run keyword and Ignore Error    remove sp spt    ${SPT_SBAC_OSDP_4}

OVTC31161
    [Documentation]    SPT_SBAC_OSDP_5
    ...    Remove OS Deployment plans from scope "Deployment Plans" where same network is used by SPT. This SPT is created using same scope " production"
    [Tags]    SPT_SBAC    OVF2940    PO
    [Template]    SPT SP by different users
        Administrator    opn1=Update Scopes    opn1_payload=${SPT_SBAC_OSDP_5_EditScope_1}    isComplete=False
        User2_SPArch      opn1=Create SPT       opn1_payload=${SPT_SBAC_OSDP_5}                isComplete=False
        Administrator    opn1=Update Scopes    opn1_payload=${SPT_SBAC_OSDP_5_EditScope_2}    isComplete=False
        User2_SPArch      opn1=Get SPT          opn1_payload=${SPT_SBAC_OSDP_5}                isComplete=False
        User2_InfraAdm    opn1=Get SPT          opn1_payload=${SPT_SBAC_OSDP_5}                isComplete=False
        User2_SAdm    opn1=Get SPT          opn1_payload=${SPT_SBAC_OSDP_5}
    [Teardown]    run keywords    Fusion Api Login Appliance    ${APPLIANCE_IP}    ${user_credentials['Administrator']}    AND
    ...    Update Scopes    ${SPT_SBAC_OSDP_5_ScopeResetToOldResources}    False    AND
    ...    run keyword and Ignore Error    remove sp spt    ${SPT_SBAC_OSDP_5}

OVTC31162
    [Documentation]    SPT_SBAC_OSDP_6
    ...    Create SPT with mandatory resource by multi scope user. update spt with network from different user
    [Tags]    SPT_SBAC    OVF2940    PO
    [Template]    SPT SP by different users
        Administrator    opn1=Update Scopes    opn1_payload=${SPT_SBAC_OSDP_6_EditScope_1}    isComplete=False
        User2_SPArch      opn1=Create SPT       opn1_payload=${SPT_SBAC_OSDP_6}                isComplete=False
        Administrator    opn1=Update Scopes    opn1_payload=${SPT_SBAC_OSDP_6_EditScope_2}    isComplete=False
        User2_SPArch      opn1=Get SPT          opn1_payload=${SPT_SBAC_OSDP_6}                isComplete=False
        User2_InfraAdm    opn1=Get SPT          opn1_payload=${SPT_SBAC_OSDP_6}                isComplete=False
        User2_SAdm    opn1=Get SPT          opn1_payload=${SPT_SBAC_OSDP_6}                isComplete=False
        User2_SAdm    opn1=Update SPT       opn1_payload=${SPT_SBAC_OSDP_6_Update_1}
    [Teardown]    run keywords    Fusion Api Login Appliance    ${APPLIANCE_IP}    ${user_credentials['Administrator']}    AND
    ...    Update Scopes    ${SPT_SBAC_OSDP_6_ScopeResetToOldResources}    False    AND
    ...    run keyword and Ignore Error    remove sp spt    ${SPT_SBAC_OSDP_6}
