Overview
========
This document describes how to use `Create CPT Payload For OS Deployment` and
`Create CPT Payload For Bulk OS Deployment` keywords available in FusionLibrary.
The purpose of these keywords is to facilitate operating system deployments
using Cirrus Provisioning Tool.

Requirements
------------
- Details of the server hosting Cirrus Provisioning Tool
- OneView/Composer credentials... read-only permissions is sufficient
- The iLO of compute/blade servers must be configured with IPv4 addresses
- Data file should contain iLO credentials

Workflow
--------
With server and OS profile information, the keyword generates a JSON object that
is understood by RG's CPT keywords. The main sections of JSON object are

- OS Profile
    - Install Media
    - Drivers (storage and network) required for pre-installation
    - Host information like hostname and system password
- Server Info
    - iLO details
    - Boot Mode
    - Storage information
    - Network information

Details of the server are retrieved from OneView with the help of the Server
Profile URL. The OS Profile is hand-crafted by the user and available in the
test input file.

OS Profile
----------
The supported keys are as follows

Key                 |   Type    |   Mandatory   |       Description
================================================================================
os_name             |   String  |   Yes         | Any value of $(l2add -supportedos)
os_repo             |   String  |   Yes         | Location of the install media
deployment_network  |   String  |   Yes         | Deployment network name. It must match the networks in Fusion
ilo_user            |   String  |   Yes         | HPE iLO administrator privileged user account.
ilo_pass            |   String  |   Yes         | Credentials of the aforementioned user.
system_password     |   String  |   No          | System root/admin password to be configured
network_driver      |   String  |   No          | Location of the pre-installation network drivers
storage_driver      |   String  |   No          | Location of the pre-installation storage drivers
custom_script       |   String  |   No          | Post-installation custom script location local to the test head
kickstart           |   String  |   No          | Custom kickstart location local to the test head.

Cirrus Provisioning Tool
------------------------
Installer and details to setup the tool are available at http://csinfs.americas.hpqcorp.net/cpt/

Usage
=====

Sample Data file
----------------
ESXi65 = {"os_name": "ESXi65x64",
          "os_repo": "http://192.168.124.10/iso/esxi/65.iso",
          "deployment_network": "icsp",
          "ilo_user": "Administrator",
          "ilo_pass": "admin123"}

cpt_host = {"host": "192.168.124.20",
            "user": "root",
            "password": "hpvse123"}

Sample Test Suite
------------------
*** Settings ***
Library         RoboGalaxyLibrary
Library         FusionLibrary
Variables       data.py
Suite Setup     Prepare Test Env

*** Variables ***
${OV_IP}        10.1.2.107
&{OV_CRED}      userName=Administrator      password=admin123
${SRV_URI}      /rest/server-profiles/10b38748-0c44-4ec6-b6b2-81e0b8ac329b
${SRV_URI_1}    /rest/server-profiles/1c9e0329-ad24-47bf-8b70-a9a0714fe258

*** Keywords ***
Prepare Test Env
    [Documentation]     Prepares the testing environment by logging to the appliances
    Set Log Level   Trace
    CPT Login       ${cpt_host['host']}     ${cpt_host['user']}     ${cpt_host['password']}
    ${r}    ${s}=    Fusion Api Login Appliance  ${OV_IP}  ${OV_CRED}
    Run Keyword If  ${r['status_code']} is not 200    Fail      Unable to login

*** Test Cases ***
Test OS Deployment
    &{pay}=     Create CPT Payload For OS Deployment    ${SRV_URI}      ${ESXi65}
    ${fn}=      CPT Deploy OS      ${pay}
    Should Not Be Equal     ${fn}   ${None}     "Encountered an error during deploy"
    ${out}=     CPT Wait On Task    ${fn}
    Should Not Be Equal     ${out}  ${Empty}      "Unable to monitor task"
    Should Be Equal     "${out['status']}"      "Success"       "Provisioning task has failed."

Test Multiple OS Deployment
    @{I1}=      Create List     ${SRV_URI}      ${ESXi65}
    @{I2}=      Create List     ${SRV_URI_1}    ${ESXi65}
    @{SR1}=     Create List     ${I1}       ${I2}
    &{pay}=     Create CPT Payload For Bulk OS Deployment     ${SR1}
    ${fn}=      CPT Deploy OS      ${pay}
    Should Not Be Equal     ${fn}   ${None}     "Encountered an error during deploy"
    ${out}=     CPT Wait On Task    ${fn}
    Should Not Be Equal     ${out}  ${Empty}      "Unable to monitor task"
    Should Be Equal     "${out['status']}"      "Success"       "Provisioning task has failed."