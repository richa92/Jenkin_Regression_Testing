*** Settings ***
Documentation     Test to generate the Libdoc for all resource files in repo
Variables         ${GIT_REPO_ROOT}/mgmtfw/tests/thunderbird/variables/tBird_variables.py
Resource          ${GIT_REPO_ROOT}${RESOURCE_ROOT}/thunderbird_all.txt
Library           RoboGalaxyLibrary
Library           MgmtFWLibrary
Library           Selenium2Library
Library           Collections
Library           Dialogs
Library           OperatingSystem

*** Test cases ***
Generate the doc
    [Documentation]    This will generate Libdoc (High Level Resource documentation) for all the resources file in a given repository
    ...    If all your resource files are in one folder then you should set the FLAT variable to Yes. All the libdoc html files are copied into the libdoc folder and an index.html file is created. If your resource files are located in various folders underneath one common folder, such as fusion
    ...    resources, set the FLAT variable to Yes and the libdoc html files will be created in each folder, as well as under libdoc, with an index.html file.
    ...
    ...    Arguments
    ...
    ...    GIT_REPO_ROOT - path on local system where your repos are located
    ...
    ...    RESOURCES_ROOT_FOLDER - path to resource folder
    ...
    ...    FILETYPE: - either html or xml
    ...
    ...    Examles
    ...
    ...    --variable GIT_REPO_ROOT:c:robogalaxy --variable RESOURCES_ROOT_FOLDER:c:/robogalaxy/fusion
    ...    /resources --variable FLAT:No --variable \ FILETYPE:xml
    ...
    ...    --variable GIT_REPO_ROOT:c:robogalaxy --variable RESOURCES_ROOT_FOLDER:c:/robogalaxy/fusion
    ...    /resources --variable FLAT:No --variable \ FILETYPE:html
    OperatingSystem.Empty Directory    ${RESOURCES_ROOT_FOLDER}\\libdoc
    Run Keyword If    '${FLAT}'=='Yes'    Generate Flat Root Libdoc Files
    Run Keyword If    '${FLAT}'=='Yes'    Pass Execution    Libdoc files and Resource Keywords Index file created
    @{resource_file_list}=    OperatingSystem.List Files In Directory    ${RESOURCES_ROOT_FOLDER}    *.txt
    ${resources_root_folder_list}=    OperatingSystem.List Directories In Directory    ${RESOURCES_ROOT_FOLDER}
    : FOR    ${resource_folder}    IN    @{resources_root_folder_list}
    \    @{resource_file_list}=    OperatingSystem.List Files In Directory    ${RESOURCES_ROOT_FOLDER}/${resource_folder}    *.txt
    \    Generate Root Libdoc Files    ${resource_folder}    @{resource_file_list}
    \    Traverse to Lower Folders    ${resource_folder}
    ${rc}    ${output}=    Run Keyword If    '${FILETYPE}' == 'html'    Run And Return Rc And Output    python ${GIT_REPO_ROOT}/rg/robogalaxylibrary/utils/create_html_index.py ${RESOURCES_ROOT_FOLDER}/libdoc
    Run Keyword If    '${FILETYPE}' == 'html'    Should Be True    ${rc}==0    msg=Error creating index file
    Run Keyword If    '${FILETYPE}' == 'html'    Log To Console    \rLibdoc html files and Resource Keywords Index file created
    Run Keyword If    '${FILETYPE}' == 'xml'    Log To Console    \rLibdoc xml files created

*** Keywords ***
Generate Libdoc Files
    [Arguments]    ${resource_folder}    ${resource_folder_name}    @{resource_file_list}
    : FOR    ${file_name}    IN    @{resource_file_list}
    \    ${resource_name}    ${ext}=    Split Extension    ${file_name}
    \    ${resource_file_path}=    Join Path    ${RESOURCES_ROOT_FOLDER}/${resource_folder}/${resource_folder_name}    ${file_name}
    \    ${resource_file_folder}=    Set Variable    ${RESOURCES_ROOT_FOLDER}/${resource_folder}/${resource_folder_name}
    \    ${rc}=    Run Keyword If    '${FILETYPE}' == 'html'    Run And Return Rc    python -m robot.libdoc ${resource_file_path} ${resource_file_folder}/${resource_name}.html
    \    Run Keyword If    '${FILETYPE}' == 'html'    Should Be True    ${rc}==0    msg=Error creating libdoc files
    \    ${rc}=    Run Keyword If    '${FILETYPE}' == 'html'    Run And Return Rc    python -m robot.libdoc ${resource_file_path} \ ${RESOURCES_ROOT_FOLDER}/libdoc/${resource_name}.html
    \    Run Keyword If    '${FILETYPE}' == 'html'    Should Be True    ${rc}==0    msg=Error copying files to libdoc folder
    \    ${rc}=    Run Keyword If    '${FILETYPE}' == 'xml'    Run And Return Rc    python -m robot.libdoc ${resource_file_path} \ ${RESOURCES_ROOT_FOLDER}/libdoc/${resource_name}.xml
    \    Run Keyword If    '${FILETYPE}' == 'xml'    Should Be True    ${rc}==0    msg=Error copying files to libdoc folder

Traverse to Lower Folders
    [Arguments]    ${resource_folder}
    ${resources_folders_list}=    OperatingSystem.List Directories In Directory    ${RESOURCES_ROOT_FOLDER}/${resource_folder}
    : FOR    ${resource_folder_name}    IN    @{resources_folders_list}
    \    @{resource_file_list}=    OperatingSystem.List Files In Directory    ${RESOURCES_ROOT_FOLDER}/${resource_folder}/${resource_folder_name}    *.txt
    \    Generate Libdoc Files    ${resource_folder}    ${resource_folder_name}    @{resource_file_list}

Generate Root Libdoc Files
    [Arguments]    ${resource_folder}    @{resource_file_list}
    : FOR    ${file_name}    IN    @{resource_file_list}
    \    ${resource_name}    ${ext}=    Split Extension    ${file_name}
    \    ${resource_file_path}=    Join Path    ${RESOURCES_ROOT_FOLDER}/${resource_folder}    ${file_name}
    \    ${resource_file_folder}=    Set Variable    ${RESOURCES_ROOT_FOLDER}/${resource_folder}
    \    ${rc}=    Run Keyword If    '${FILETYPE}' == 'html'    Run And Return Rc    python -m robot.libdoc ${resource_file_path} ${resource_file_folder}/${resource_name}.html
    \    Run Keyword If    '${FILETYPE}' == 'html'    Should Be True    ${rc}==0    msg=Error creating root level libdoc files
    \    ${rc}=    Run Keyword If    '${FILETYPE}' == 'html'    Run And Return Rc    python -m robot.libdoc ${resource_file_path} ${RESOURCES_ROOT_FOLDER}/libdoc/${resource_name}.html
    \    Run Keyword If    '${FILETYPE}' == 'html'    Should Be True    ${rc}==0    msg=Error copying files to libdoc folder
    \    ${rc}=    Run Keyword If    '${FILETYPE}' == 'xml'    Run And Return Rc    python -m robot.libdoc ${resource_file_path} ${RESOURCES_ROOT_FOLDER}/libdoc/${resource_name}.xml
    \    Run Keyword If    '${FILETYPE}' == 'xml'    Should Be True    ${rc}==0    msg=Error copying files to libdoc folder

Generate Flat Root Libdoc Files
    @{resource_file_list}=    OperatingSystem.List Files In Directory    ${RESOURCES_ROOT_FOLDER}    *.txt
    : FOR    ${file_name}    IN    @{resource_file_list}
    \    ${resource_name}    ${ext}=    Split Extension    ${file_name}
    \    ${resource_file_path}=    Join Path    ${RESOURCES_ROOT_FOLDER}    ${file_name}
    \    ${resource_file_folder}=    Set Variable    ${RESOURCES_ROOT_FOLDER}
    \    ${rc}=    Run Keyword If    '${FILETYPE}' == 'html'    Run And Return Rc    python -m robot.libdoc ${resource_file_path} ${RESOURCES_ROOT_FOLDER}/libdoc/${resource_name}.html
    \    Run Keyword If    '${FILETYPE}' == 'html'    Should Be True    ${rc}==0    msg=Error copying files to libdoc folder
    \    ${rc}=    Run Keyword If    '${FILETYPE}' == 'xml'    Run And Return Rc    python -m robot.libdoc ${resource_file_path} ${RESOURCES_ROOT_FOLDER}/libdoc/${resource_name}.xml
    \    Run Keyword If    '${FILETYPE}' == 'xml'    Should Be True    ${rc}==0    msg=Error copying files to libdoc folder
    ${rc}    ${output}=    Run Keyword If    '${FILETYPE}' == 'html'    Run And Return Rc And Output    python ${GIT_REPO_ROOT}/rg/robogalaxylibrary/utils/create_html_index.py ${RESOURCES_ROOT_FOLDER}/libdoc
    Run Keyword If    '${FILETYPE}' == 'html'    Should Be True    ${rc}==0    msg=Error creating index file
